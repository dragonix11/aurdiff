# Maintainer: Javier Torres <javitonino [at] gmail [dot] com>

pkgname=opendnssec
pkgver=1.4.2
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3)"
arch=('i686' 'x86_64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=()
depends=('libxml2>=2.6.16' 'ldns>=1.6.16' 'sqlite3>=3.3.9')
makedepends=()
optdepends=('softhsm: key storage')
provides=()
conflicts=()
replaces=()
backup=("etc/opendnssec/conf.xml"
        "etc/opendnssec/kasp.xml"
        "etc/opendnssec/zonelist.xml")
options=()
install="opendnssec.install"
changelog=
source=("http://www.opendnssec.org/files/source/${pkgname}-${pkgver}.tar.gz"
	"pid-path.patch"
        "ods-signer.service"
        "ods-enforcer.service")
noextract=()

md5sums=('4ac59962721632e07cfb03405fdd152c'
         '5dbef62063ddb08678d1509c660019f0'
         '70020c6ca2c9ce9b003450014ebd12b5'
         'c5464f7be7984bb9a925da0b157eafdf')
sha1sums=('82991f3110820ec0b12608fd3175bb70252a6f2b'
          '8b4d120c5965f1869d2689257245e6905372d1f9'
          'bf542497817af8cd0ad2b09602ca508d68b6da8d'
          'fa28111fdce06c389813ff6ed2d4cae136252488')
sha256sums=('b4bc70bfb54ede8ed657cc7f669b5f58bc5e20eabf9b01ca107a6876b08bed35'
            '487a4c05a07feb97c865ddc4c13d00eea6ce8b2b1e5031983c15484f4991ebed'
            '1a8cdec0e97a33048956268b766da570f8f7a90d05af59e547219f1381832071'
            '75cecbfb0ece13957a68a5bc39c20a1d69b95373e7473545d70621e1732733d8')
sha384sums=('beb49258194e92238316a6fb5344312ae3553e38f844bbffd4f01d0f795defff5e66cb2ee85ebf722be607f86657acae'
            'b990c56136f7d3759093e666ea9b3d33aa1861242cd301ab84cd20f6fc6fdb81d56f4cda84d02da3894f3d82cfcfe532'
            'ee45474ebb67f59e2c86aa4a9e2103f1e3a86216e71f7730aa697ea96cd7124344005d8fc65cfb4af8524e1550c0ebba'
            '0869168e8c5a5064cbac0d2f0afe71539a68785d548752c609759381648b04015fc7c3ed9684aec944a914143c777a66')
sha512sums=('e115dc31b69eaa476b182be0211e09e7bad950a0c3009a53556a16d7a242254fa748e8fd1264cd9565d04abe9d64f781e1af120c2363aa856e0e34f6260e381f'
            '74f386ec8db1ff3a1dd4faf8e1d7eaa6e04f1ab0f89d5289f440ed1c2fe7040d8688ce27796d04d8152c235369297ce829f5dec0aa205c3e62263ae759e15d4a'
            '28d868feedbee8251b0000556705ee094707782dad2072f4e37ebff3ac1658d09c884488dd6852e9734fa859218048daff50b66fe25930f6445619e3c89ca877'
            'a3700c82e6577bdacbce9cfd749e71e3c749814884ad4e9a1359e97105f9c045dc1472ba231ecb52c23855cacf67874623c8eef715955bfd41239b199d03a0db')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /var/lib/run -> /var/run
  patch -p0 < "${srcdir}/pid-path.patch"

  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --sbindir=/usr/bin

  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "${pkgdir}/var/run/opendnssec"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 "${srcdir}/ods-signer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-signer.service"
  install -Dm0644 "${srcdir}/ods-enforcer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-enforcer.service"
  install -Dm0644 "enforcer/utils/migrate_adapters_1.sqlite3" \
        "${pkgdir}/usr/share/opendnssec"

  chown 227:227 "${pkgdir}/etc/opendnssec" -R
  chown 227:227 "${pkgdir}/var/lib/opendnssec" -R
  chown 227:227 "${pkgdir}/var/run/opendnssec" -R

  chmod 750 "${pkgdir}/etc/opendnssec" \
            "${pkgdir}/var/lib/opendnssec" \
            "${pkgdir}/var/run/opendnssec"
}
