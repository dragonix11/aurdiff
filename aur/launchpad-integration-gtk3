# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=launchpad-integration-gtk3
pkgver=0.1.56.2
pkgrel=2
pkgdesc="A library to integrate launchpad urls into the menus"
arch=('i686' 'x86_64')
url="https://launchpad.net/launchpad-integration"
license=('GPL3')
depends=('hicolor-icon-theme' 'gtk3' 'pygtk')
makedepends=('bzr' 'gobject-introspection')
install="${pkgname%-*}.install"
source=("${pkgname%-*}-${pkgver}.tar.gz::http://bazaar.launchpad.net/~ubuntu-desktop/launchpad-integration/ubuntu/tarball/189")
sha256sums=('6795ec298505baf39d09dea7d9a7796bb3c3b06e2876d423bae5e2e276ab5620')

build() {
  cd ~ubuntu-desktop/${pkgname%-*}/ubuntu

  export PYTHON='/usr/bin/python2'
  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --libexecdir="/usr/lib/${pkgname%-*}" --disable-static --with-gtk='3'
  make
}

package() {
  cd ~ubuntu-desktop/${pkgname%-*}/ubuntu

  make DESTDIR="${pkgdir}" install

# Fix permissions
  chmod 644 "${pkgdir}"/usr/share/launchpad-integration/launchpadintegration/*
}

# vim: ts=2 sw=2 et:
