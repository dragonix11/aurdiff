# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-clp
pkgver=1.15.2
pkgrel=1
pkgdesc="Clp (Coin-or linear programming) is an open-source linear programming solver written in C++"
arch=('i686')
url="https://projects.coin-or.org/Clp"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.9.0') 
source=(http://www.coin-or.org/download/source/Clp/Clp-$pkgver.tgz)
md5sums=('47188cb667da53619c8452ae616987f8')

build() {
  cd "$srcdir/Clp-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="`pkg-config --libs osi`" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

check() {
  cd "$srcdir/Clp-$pkgver"
  make test
}

package() {
  cd "$srcdir/Clp-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
