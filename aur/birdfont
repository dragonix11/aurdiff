# Maintainer: Matej Lach <matej.lach@gmail.com>

pkgname=birdfont
pkgver=0.31
pkgrel=2
pkgdesc='A free font editor that lets you create vector graphics and export TTF, EOT & SVG fonts'
arch=(i686 x86_64)
url='http://birdfont.org/'
license=('GPL3')
depends=(
	'vala' 'libgee' 'glib2' 'gtk2' 'libxml2' 'webkitgtk2' 'python2' 'gettext' 'desktop-file-utils'
	'hicolor-icon-theme')
makedepends=('vala' 'gettext' 'libgee')
source=(
	'http://birdfont.org/releases/birdfont-0.31.tar.gz'
	'http://www.unicode.org/Public/UNIDATA/NamesList.txt')
sha512sums=('717738fb38238e1568c4392c0db0e2dfea98d7b7e69ab803bd705bb67825f88385026fad99555bd007f2f16141a9c4981baab7dea7f19f631e7b6f372678f68d'
            '7138b96f446c72261dafa26f6eec446ac7134ba09bba75b0fa258465dc43b45cab1d9a4d90ff7b508b654469789f76329279e192fa6de6111b80d8140e75794e')

install=birdfont.install

prepare() {
	cd "${srcdir}/$pkgname-$pkgver"
	sed -i -e 's/gee-1\.0/gee-0.8/g' ./scripts/build.py	
}

build() {
	cd "${srcdir}/$pkgname-$pkgver"

	python2 ./configure
	python2 ./scripts/linux_build.py
}

package() {
	cd "${srcdir}/$pkgname-$pkgver"
	
	install -d -m 0755 "${pkgdir}/usr/share/unicode"
	python2 ./install.py -d "${pkgdir}"
	mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
	install -m 0644 "${srcdir}/NamesList.txt" "${pkgdir}/usr/share/unicode"
}
