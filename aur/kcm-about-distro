# Maintainer: birdflesh <antkoul at gmail dot com>

pkgname=kcm-about-distro
_pkgname=about-distro
pkgver=1.0.0
pkgrel=1
pkgdesc="KCM displaying distribution and system information"
arch=('i686' 'x86_64')
url='https://projects.kde.org/about-distro'
license=('GPL3')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/$_pkgname/$pkgver/src/$_pkgname-$pkgver.tar.xz"
        "kcm-about-distrorc"
        "start-here-arch.png")
md5sums=('7ae962af0ffcb7dd2715d96dea3e17c3'
         '14738e76fa51b57b8523e203a8f7865d'
         '75f412b722b01d8911a5fb16c4ed3766')

build() {
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/usr/share/config/kcm-about-distrorc"
  install -Dm644 "$srcdir/start-here-arch.png" "$pkgdir/usr/share/icons/hicolor/128x128/places/start-here-arch.png"
}
