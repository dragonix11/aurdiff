# Maintainer: Alexander Hunziker <alex.hunziker@gmail.com>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=gimp-git
pkgver=20131103.g771293d
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org"
license=('GPL' 'LGPL')
groups=()
depends=('gtk2>2.24.10' 'lcms2>=2.2' 'libwmf>=0.2.8' 'webkitgtk2>=1.6.1' \
         'libgexiv2' 'librsvg>=2.16.1' 'libpng>=1.2.27' \
         'fontconfig>=2.2.0' 'desktop-file-utils' \
         'libexif>=0.6.15' 'libart-lgpl>=2.3.19' 'pygtk>=2.10.4' 'dbus-glib' 'gtk-doc' \
         'gegl-git' 'openexr>=1.6.1' 'cairo>=1.12.2' 'python2' 'hicolor-icon-theme')
makedepends=('gutenprint>=5.0.0' 'intltool>=0.40.1' 'gtkhtml>=2.6.3' \
             'gnome-python>=2.16.2' 'poppler>=0.12.4' 'pkgconfig>=0.21' \
	         'alsa-lib>=1.0.0' 'libtool>=2.2' 'autoconf>=2.54' 'automake=1.14' \
             'libxslt' 'pango>=1.29.4' 'curl>=7.15.1' 'gettext' 'git')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
options=('!libtool' '!makeflags')
provides=('gimp')
conflicts=('gimp')
replaces=()
backup=()
install='gimp-git.install'
source=(git://git.gnome.org/gimp
		linux.gpl)
noextract=()
md5sums=('SKIP' #generate with 'makepkg -g'
         'bb27bc214261d36484093e857f015f38')

_gitname=gimp

pkgver() {
	cd $_gitname
	GITREV=$(git describe --always|awk -F"-" '{print $NF}')
	echo `date +%Y%m%d`.${GITREV}
}

build() {
  cd "$srcdir/$_gitname"

	sed -i -e 's/automake-1.11/automake-1.14/g' \
			-e 's/aclocal-1.11/aclocal-1.14/g' autogen.sh

  PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw --with-libcurl \
    --without-aa --without-hal --without-gvfs --without-gnomevfs
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -s gimp-2.9 ${pkgdir}/usr/bin/gimp
  ln -s gimp-console-2.9 ${pkgdir}/usr/bin/gimp-console
}

