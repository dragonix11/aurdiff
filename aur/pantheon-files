# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=pantheon-files
pkgver=0.1.3
pkgrel=2
pkgdesc="Files is a simple, powerful, sexy file manager for the Pantheon desktop."
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
depends=('gconf' 'desktop-file-utils' 'granite' 'libnotify')
makedepends=('cmake' 'gnome-common' 'sqlite3' 'vala0.16')
conflicts=('marlin-bin' 'marlin-bzr')
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%?}x/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('3d0a717eae06038ae1304c11f410d64d88260ffe55c4814294203a4e5e42bfbe')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE=/usr/bin/valac-0.16 -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
