# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>
pkgname=libcdk
pkgver=5.0.20130901
pkgrel=1
pkgdesc='Curses Development Kit - widget library, enhanced version'
arch=('x86_64' 'i686')
url='http://invisible-island.net/cdk/'
license=('custom')
options=('!emptydirs')
source=("ftp://invisible-island.net/cdk/cdk.tar.gz")
sha256sums=('c3cd5ae7a375c09f8480ba76c0fd06b795c2dd09569adaa94133a7c5654463d9')

build() {
  cd "$srcdir/cdk-${pkgver/.2/-2}"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}
  
package() {
  cd "$srcdir/cdk-${pkgver/.2/-2}"

  make \
    DOCUMENT_DIR="$pkgdir/usr/share/doc/cdk" \
    DESTDIR="$pkgdir" \
    install
  make \
    DESTDIR="$pkgdir" \
    installCDKSHLibrary
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
