# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-b-c
_lastauthor=R/RU/RURBAN
_pkgname=B-C
pkgver=1.42.git.1981.a18841f
pkgrel=1
pkgdesc="Perl compiler's C backend"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-ipc-run' 'perl-opcodes' 'perl-b-flags' 'perl-time-hires')
url="http://search.cpan.org/dist/${_pkgname}/"
#source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
#sha256sums=('d6a9080c18c9d96ee97da1529374cc5fa5dc1c7da59493ae7477ef399732a76b')
source=(${_pkgname}::git+https://code.google.com/p/perl-compiler/)
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo 1.42.git.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${_pkgname}

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL
  make
}
check() {
  cd ${_pkgname}
  make test
}
package() {
  cd ${_pkgname}
  make install
}

