# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-dragonegg-plugin
pkgver=3.3
_gccver=4.8.0
pkgrel=1
pkgdesc="integrates the LLVM optimizers and code generator with the GCC parsers"
arch=('i686' 'x86_64')
url="http://dragonegg.llvm.org/"
license=('GPL')
depends=("gcc>=${_gccver}" 'llvm>=3.3')
source=(http://llvm.org/releases/3.3/dragonegg-3.3.src.tar.gz{,.sig})
md5sums=('797df8d1af8355b556e8b6060946a17e'
         'ee97947d84560789b180f3a1007327d9')

_plugindir=/usr/lib/gcc/${CHOST}/${_gccver}/plugin

build() {
  cd $srcdir/dragonegg-${pkgver}.src/
  sed -i "s#\$(shell svnversion -n \$(TOP_DIR))#$pkgver#" Makefile
  make
}

package() {
  cd $srcdir/dragonegg-${pkgver}.src/
  install -Dm755 dragonegg.so $pkgdir/${_plugindir}/dragonegg.so
}
