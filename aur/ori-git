# Maintainer: David Mazieres <http://www.scs.stanford.edu/~dm/>
pkgname=ori-git
pkgver=20130817
pkgrel=1
pkgdesc="Peer-to-peer distributed file system with history"
arch=(i686 x86_64)
url="http://ori.scs.stanford.edu/"
license=('MIT')
#depends=(openssl libevent fuse xz libxml2)
depends=(libevent fuse)
makedepends=(git scons boost)
provides=(ori)
conflicts=(ori)
source=()
md5sums=()

_gitroot=http://bitbucket.org/orifs/ori.git
_gitname=ori

prepare() {
  srcdest=${SRCDEST:-$srcdir}
  cd "$srcdest" || return 1
  if [[ -L "$_gitname.git" && ! -w "$_gitname.git/" ]]; then
      local orig=$(readlink "$_gitname.git")
      rm -f "$_gitname.git"
      git clone --bare "$orig" "$_gitname.git"
  fi

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname.git" ]]; then
    cd "$_gitname.git" && git fetch origin +master:master
    msg "The local files are updated."
  else
    git clone --bare "$_gitroot" "$_gitname.git"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir"
  rm -rf "$_gitname"
  git clone -s "$srcdest/$_gitname.git" "$_gitname"
  echo WITH_MDNS=0 >> "$_gitname/Local.sc"
}

build() {
  cd "$srcdir/$_gitname"
  scons
}

package() {
  cd "$srcdir/$_gitname"
  scons PREFIX="$pkgdir/usr" install
}
