# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-tools-git
pkgver=v5.1.1.93.ga865832
pkgrel=1
pkgdesc="A cross-platform application and UI framework (Tools)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base' 'qt5-xmlpatterns' 'qt5-declarative' 'qt5-jsbackend')
makedepends=('git')
provides=('qt5-tools')
conflicts=('qt5-tools')
source=("git://gitorious.org/qt/qttools.git#branch=dev")
sha256sums=('SKIP')
_gitname='qttools'

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	qmake
	make
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
}
