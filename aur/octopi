# Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=octopi
pkgver=0.2.0
pkgrel=1
pkgdesc="a powerful Pacman frontend using Qt libs"
arch=('i686' 'x86_64')
url="https://github.com/aarnt/octopi"
license=('GPL2')
install=$pkgname.install
makedepends=('automoc4' 'cmake')
depends=('qt4')
optdepends=('kdesu: for KDE'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'yaourt: for AUR support')
conflicts=('oktopi-git')
source="https://github.com/aarnt/${pkgname}/archive/v${pkgver}.tar.gz"
sha256sums=('9f0f2a3815a858262c6b991c6d89a08ff836a51a29b5338cd3c28dec6815cead')

build() {
  _cpucount=$(grep -c processor /proc/cpuinfo 2>/dev/null)
  _jc=$((${_cpucount:-1}))
   
  cd "$srcdir/$pkgname-$pkgver"
  qmake-qt4 $pkgname.pro
    
  make -j $_jc
  cd notifier/pacmanhelper
  msg "Building pacmanhelper..."
  qmake-qt4 pacmanhelper.pro
  make -j $_jc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
 
  install -D -m755 bin/$pkgname ${pkgdir}/usr/bin/$pkgname
  install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
  install -D -m644 resources/images/${pkgname}_red.png ${pkgdir}/usr/share/icons/$pkgname.png
  
  #services
  install -D -m755 notifier/bin/pacmanhelper ${pkgdir}/lib/octopi/pacmanhelper

  install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacman.policy ${pkgdir}/usr/share/polkit-1/actions/org.octopi.pacman.policy
  install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.conf ${pkgdir}/etc/dbus-1/system.d/org.octopi.pacmanhelper.conf
  install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.xml ${pkgdir}/usr/share/dbus-1/interfaces/org.octopi.pacmanhelper.xml
  install -D -m644 notifier/pacmanhelper/polkit/org.octopi.pacmanhelper.service ${pkgdir}/usr/share/dbus-1/system-services/org.octopi.pacmanhelper.service
}

# vim: set ts=2 sw=2 ft=sh noet:
