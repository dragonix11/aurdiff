# Maintainer: naelstrof <naelstrof@gmail.com>

pkgname=mingw-w64-physfs
pkgver=2.0.3
pkgrel=2
pkgdesc="A library to provide abstract access to various archives (mingw-w64)"
arch=('any')
url="http://icculus.org/physfs/"
license=('ZLIB')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake')
depends=('mingw-w64-zlib')
options=('!strip' '!buildflags')
source=(http://icculus.org/physfs/downloads/physfs-${pkgver}.tar.bz2)
md5sums=('c2c727a8a8deb623b521b52d0080f613')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/physfs-${pkgver}"
  # fix compiler error due to an unused variable
  sed -i "/lastDirectory/d" archivers/wad.c
  unset LDFLAGS
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -fno-strict-aliasing"
  #Unstrict warnings are required as 64 bit compiler complains about an int->pointer cast (which should be safe)
  sed -i 's/-Werror//g' CMakeLists.txt
  for _arch in ${_architectures}; do
        mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}
        ${_arch}-cmake  -DPHYSFS_BUILD_TEST=OFF \
                        -DPHYSFS_BUILD_WX_TEST=OFF \
                        ../physfs-${pkgver}
        make all
  done
}

package() {
    for _arch in ${_architectures}; do
        cd ${srcdir}/build-${_arch}
        make DESTDIR=${pkgdir} install
        cd ${srcdir}/physfs-${pkgver}
        install -D -m644 LICENSE.txt "${pkgdir}/usr/${_arch}/share/licenses/${pkgname}/LICENSE"
    done
}
