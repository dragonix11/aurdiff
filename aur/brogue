# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Joao Cordeiro <jlcordeiro at gmail dot com>

pkgname=brogue
pkgver=1.7.3
pkgrel=1
pkgdesc="A 26-level dungeon crawl to the Amulet of Yendor."
arch=('i686' 'x86_64')
url="http://sites.google.com/site/broguegame/"
license=('AGPL3')
depends=('sdl' 'libtcod')
install=brogue.install
source=(http://sites.google.com/site/broguegame/$pkgname-linux-$pkgver.tar.gz
      'brogue.sh')
md5sums=('f03028b7eaf1db704b756eb698feeed0'
         '306b9301cc1ddf158c4ef0c5da2a1784')

build() {
  cd "$srcdir/$pkgname-linux-$pkgver/$pkgname-$pkgver"

  sed -i '/^CFLAGS=*/a CFLAGS+=-I/usr/include/libtcod' Makefile
  sed -i 's/-ltcod/-ltcod -lSDL -lm/g' Makefile

  # todo, fix sources to use /usr/share directly and remove brogue.sh

  make clean
  make
}

package() {
  cd "$srcdir/$pkgname-linux-$pkgver/$pkgname-$pkgver"

  _dest_dir="$pkgdir/usr/share/$pkgname"
  mkdir -p "$_dest_dir/fonts"
  chown -R :games "$_dest_dir"
  chmod -R g+rw "$_dest_dir"

  install -Dm755 bin/brogue "$_dest_dir"
  install -Dm755 bin/keymap "$_dest_dir"
  install -Dm644 bin/fonts/*.png "$_dest_dir"/fonts/ 
  #install -Dm644 brogue.desktop "$pkgdir/usr/share/applications/brogue.desktop"
  install -T -D -m 755 "$srcdir/brogue.sh" "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
