# Maintainer: Eivind Uggedal <eivind@uggedal.com>
# Based on the mpv-git PKGBUILD

pkgname=mpv
pkgver=0.1.4
pkgrel=2
pkgdesc="A movie player and encoder for linux"
arch=('i686' 'x86_64')
license=('GPL')
url="http://mpv.io"
depends=('portaudio' 'ffmpeg' 'lcms2' 'libxinerama' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva')
makedepends=('mesa' 'python2-docutils')
conflicts=('mpv-git')
options=(!emptydirs)
source=("https://github.com/mpv-player/$pkgname/archive/v${pkgver}.tar.gz")
md5sums=('7a0ff10fb0c6a2a8c176d40883eb09d4')

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS

  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
      --disable-lircc \
      --disable-joystick \
      --disable-radio \
      --disable-radio-capture \
      --disable-radio-v4l2 \
      --disable-rpath \
      --disable-libbs2b \
      --disable-direct3d \
      --disable-corevideo \
      --disable-cocoa \
      --disable-coreaudio
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 ${srcdir}/$pkgname-$pkgver/etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 ${srcdir}/$pkgname-$pkgver/DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
  install -m755 ${srcdir}/$pkgname-$pkgver/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin"
}
