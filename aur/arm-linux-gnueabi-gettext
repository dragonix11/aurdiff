_target=arm-linux-gnueabi
_sysroot=/usr/${_target}

pkgname=${_target}-gettext
pkgver=0.18.3.1
pkgrel=1
arch=(any)
pkgdesc="GNU internationalization library (${_target})"
depends=(${_target}-libiconv)
makedepends=(${_target}-gcc)
options=(!strip !buildflags !libtool)
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz"{,.sig})
md5sums=('3fc808f7d25487fc72b5759df7419e02' 'SKIP')

build()
{
  cd "$srcdir/gettext-$pkgver"

  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export LD=${_target}-ld
  export RANLIB=${_target}-ranlib

  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig:${_sysroot}/share/pkgconfig"
  
  unset LDFLAGS
  ./configure \
    --prefix=${_sysroot} \
    --host=${_target} \
    --enable-shared \
    --enable-static \
    --enable-threads \
    --without-included-regex \
    --cache-file=arm.cache \
    --disable-java --disable-openmp --without-libiconv-prefix --without-libintl-prefix --without-libglib-2.0-prefix --without-libcroco-0.6-prefix --with-included-libxml --without-libncurses-prefix --without-libtermcap-prefix --without-libcurses-prefix --without-libexpat-prefix --without-emacs
  make
}


package() {
  cd "$srcdir/gettext-$pkgver"
  make DESTDIR="${pkgdir}" install
}
