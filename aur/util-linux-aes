# Maintainer: xerc <aur[at]xerc.de>
# Contributor: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>
# Contributor: falconindy <d[at]falconindy.com>

_basename=util-linux
pkgname=${_basename}-aes
_basever=2.24
pkgver=${_basever}
pkgrel=1
aneurysma=-20131026

pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://sourceforge.net/projects/loop-aes/"

license=('GPL2')
arch=('any')
groups=('base')

depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem' 'pam' 'shadow' 'coreutils' 'glibc')
makedepends=('systemd' 'autoconf' 'automake')

provides=('eject' "${_basename}=${pkgver}" "${_basename}-ng=${pkgver}")
conflicts=('eject' "${_basename}" "${_basename}-ng")
replaces=('eject' "${_basename}" "${_basename}-ng", "${_basename}-ng-aes")
options=('!libtool')

install=${pkgname}.install
source=(https://www.kernel.org/pub/linux/utils/${_basename}/v${_basever}/${_basename}-${pkgver}.tar.xz
        pam-common::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/pam-common?h=packages/${_basename}
        pam-login::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/pam-login?h=packages/${_basename}
        pam-su::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/pam-su?h=packages/${_basename}
        uuidd.tmpfiles::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/uuidd.tmpfiles?h=packages/${_basename}
        http://loop-aes.sourceforge.net/updates/${_basename}-${pkgver}${aneurysma}.diff.bz2
        ${pkgname}.modules)

build() {
  cd "${srcdir}/${_basename}-${pkgver}"

  msg "Patching with loop-AES"
  patch -Np1 -i "${srcdir}/${_basename}-${pkgver}${aneurysma}.diff"

  msg "Starting autogen"
  ./autogen.sh

  msg "Starting configure"
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --localstatedir=/run \
    --enable-fs-paths-extra=/usr/bin \
    --enable-raw \
    --enable-vipw \
    --enable-newgrp \
    --enable-chfn-chsh \
    --enable-write \
    --enable-mesg \
    --enable-partx \
    --enable-socket-activation \
    --with-python=3

  msg "Starting make"
  make
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  
  # enable modules
  [ -d /etc/modules-load.d ] && install -Dm644 "${srcdir}/${pkgname}.modules" "${pkgdir}/etc/modules-load.d/${pkgname}.conf"

  # setuid chfn and chsh
  chmod 4755 "${pkgdir}"/usr/bin/{newgrp,ch{sh,fn}}

  # install PAM files for login-utils
  install -Dm644 "${srcdir}/pam-common" "${pkgdir}/etc/pam.d/chfn"
  install -m644 "${srcdir}/pam-common" "${pkgdir}/etc/pam.d/chsh"
  install -m644 "${srcdir}/pam-login" "${pkgdir}/etc/pam.d/login"
  install -m644 "${srcdir}/pam-su" "${pkgdir}/etc/pam.d/su"
  install -m644 "${srcdir}/pam-su" "${pkgdir}/etc/pam.d/su-l"

  # include tmpfiles fragment for uuidd
  # TODO(dreisner): offer this upstream?
  install -Dm644 "${srcdir}/uuidd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/uuidd.conf"

  # usrmove
  cd "$pkgdir"
  mv {,usr/}sbin/* usr/bin
  rmdir sbin usr/sbin
}

md5sums=('4fac6443427f575fc5f3531a4ad2ca01'
         'a31374fef2cba0ca34dfc7078e2969e4'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'fa85e5cce5d723275b14365ba71a8aad'
         'a39554bfd65cccfd8254bb46922f4a67'
         '98e686c7431c29f2b5f28a8cc2ad39dc'
         'a10bfcaff767a4cb3066106bfd4ea30b')
