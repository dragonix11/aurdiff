# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Gour <Gour <gour@-nitai.com>

pkgname=mkgmap-svn
_pkgname='mkgmap'
pkgver=2785
pkgrel=1
pkgdesc='Create maps for Garmin GPS devices from OpenStreetMap (OSM) data.'
url='http://www.mkgmap.org.uk'
license=('GPL2')
arch=('any')
depends=('java-environment' 'sh')
makedepends=('subversion' 'apache-ant')
conflicts=("$_pkgname")
source=("$_pkgname" "svn+http://svn.mkgmap.org.uk/$_pkgname/trunk")

md5sums=(
  'b65d4b09e29c526c33d76cdddb6ff4e2'
  'SKIP'
)
sha256sums=(
  '842312f61c9602ec1427afd024c09e66e9d15304ea4ccf2e395d078c5812563c'
  'SKIP'
)

pkgver() {
  # -c makes svnversion show the version of "trunk", not the version of the
  # repository as a whole. Unfortunately, -c also makes svnversion show the full
  # range of revisions that comprise "trunk". For example:
  #
  # svnversion => 2756
  # svnversion -c => 53:2754
  #
  # Use parameter expansion trickery to strip off the leading number and colon.
  cd trunk
  local readonly ver="$(svnversion -c)"
  echo "${ver#*:}"
} 

build() {
  cd "$srcdir/trunk"
  ant
}

package() {
  cd "$srcdir/trunk/dist"

  # Install the executable, jarfile, dependencies and man page.
  install -Dm755 "$srcdir/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 \
    "${_pkgname}.jar" \
    "${pkgdir}/usr/share/java/${_pkgname}/${_pkgname}.jar"
  cp -r lib/ "${pkgdir}/usr/share/java/${_pkgname}/"
  install -Dm644 "doc/${_pkgname}.1" "${pkgdir}/usr/share/man/man1/${_pkgname}.1"
}

# vim:set ts=2 sw=2 et:
