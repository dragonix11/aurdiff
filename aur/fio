# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=fio
pkgver=2.1
pkgrel=2
pkgdesc="Scriptable I/O tool for storage benchmarks and drive testing"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/fio"
license=('GPL2')
depends=('bash' 'libaio')
optdepends=('gfio: GTK2 frontend'
	    'gnuplot: generating plots using fio_generate_plots'
	    'numactl: NUMA support (must also be present at build time)')
source=("http://brick.kernel.dk/snaps/fio-${pkgver}.${pkgrel}.tar.bz2")
sha256sums=('ebebbdfb6035369a6e5c960527a3ec77e96cd6c35cd3cfda058d76b7f15d2729')

build() {
	cd $srcdir/fio-${pkgver}.${pkgrel}
	./configure
	make
}

package() {
	cd $srcdir/fio-${pkgver}.${pkgrel}
	make DESTDIR="${pkgdir}" prefix="/usr" mandir="/usr/share/man" install

	# documentation
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
	install -m644 \
		HOWTO \
		LICENSE \
		README \
		REPORTING-BUGS \
		SERVER-TODO \
		"${pkgdir}/usr/share/doc/${pkgname}"

	# examples
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples"
	install -m644 examples/* "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
