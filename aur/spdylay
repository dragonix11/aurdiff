# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=spdylay
pkgver=1.1.0
pkgrel=1
pkgdesc="An experimental implementation of Googleâ€™s SPDY protocol"
arch=('i686' 'x86_64')
url="http://spdylay.sourceforge.net/"
license=('MIT')
depends=('gcc-libs' 'glibc' 'libevent' 'libxml2' 'openssl' 'zlib')
makedepends=('python-sphinx')
checkdepends=('cunit')
provides=('libspdylay.so')
options=('!libtool')
source=("https://github.com/tatsuhiro-t/$pkgname/archive/release-$pkgver.tar.gz")
md5sums=('762ad84fd5129bdc02f8ae63709bcf9d')

prepare() {
  cd "$pkgname-release-$pkgver"

  # can't currently (sanely) build documentation locally due to python mess.
  # online docs available at: http://tatsuhiro-t.github.io/spdylay/
  # TODO: this is a hack which may someday fail. fix it properly upstream.
  2to3 -w --no-diffs doc/mkapiref.py

  # 2to3 can't handle all of the conversion here, so take the lame route
  # and enforce a python2 interpreter in the shebang
  sed -i '1s/python$/python2/' tests/end_to_end.py
}

build() {
  cd "$pkgname-release-$pkgver"

  ./configure --prefix=/usr

  make
  make html
}

check() {
  cd "$pkgname-release-$pkgver"

  make check
}

package() {
  cd "$pkgname-release-$pkgver"

  make DESTDIR="$pkgdir" install

  # add MIT license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/spdylay/COPYING"

  # drop in wholesale generated documentation
  install -dm755 "$pkgdir/usr/share/doc"
  cp -dr --no-preserve=ownership "doc/manual" "$pkgdir/usr/share/doc/spdylay"
}

# vim: ft=sh syn=sh et
