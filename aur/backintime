# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Matias Korhonen <webadmin@draco-vulgaris.net>
# Contributor: Dylon Edwards <deltaecho@archlinux.us>

pkgname=backintime
pkgver=1.0.26
pkgrel=6
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault."
arch=('any')
url="http://backintime.le-web.org/"
license=('GPL')
depends=( 'python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring' )
optdepends=( 'openssh: Support for remote backups over SFTP' )
conflicts=( 'backintime-bzr' 'backintime-kde4' )
source=("http://backintime.le-web.org/wp-content/uploads/2009/03/$pkgname-$pkgver.tar.gz"
'fix_spaces_in_anacron_jobs.patch'
'fix_AttributeError_with_python-keyring_gt1.6.1.patch'
'fix_kde4.patch')
sha256sums=('65ec812eacc0f5ff4095c3eb6004249554b916e4a76bc787849d6d7dbcb31003'
            '93dabc0ab5f6320099e5cd83a4a14cf2afc0e8a993938de7d0b712a9517e25d5'
            '980c4642fe3dae3f1cf97e545256f6fb5bb9e48b21645f8b7d1632bf7ed7490a'
            'be777d4d99587fb4c54646a4b074caa3c34aedb2bee466aa6613d06ac23a0c15')

BUILD_GUI=1 # set to 0 if you don't want a GUI at all

# Build a custom array of depends based on what DE is installed, if user
# actually wants a GUI
if [[ $BUILD_GUI != 0 ]] ; then
  _type=
  if [[ -e '/usr/lib/libkonq.so' ]] ; then
    # KDE is installed; Build the QT GUI
    depends=( ${depends[@]} xorg-utils python2-pyqt kdebindings-python2)
    _type=( ${_type[@]} kde4 )
  fi
  if [[ -n "$(which gnome-session 2> /dev/null)" ]] ; then
    # GNOME is installed; build the GTK GUI
    depends=( ${depends[@]} pygtk python2-notify gnome-python meld gksu gnome-session )
    _type=( ${_type[@]} gnome )
  fi
  if [[ -z "${_type[@]}" ]] ; then
    # other DE like Xfce, Enlightenment etc; Build GTK GUI with less deps
    depends=( ${depends[@]} pygtk python2-notify gnome-python meld gksu )
    _type=( ${_type[@]} gnome )
  fi
fi

prepare () {
  patch -Np1 -i "${srcdir}/fix_spaces_in_anacron_jobs.patch"
  patch -Np1 -i "${srcdir}/fix_kde4.patch"
  patch -Np1 -i "${srcdir}/fix_AttributeError_with_python-keyring_gt1.6.1.patch"
}

build() {
  cd "$srcdir"/

  msg "Compiling COMMON components..."
  {
    cd "${srcdir}"/common
    ./configure
    make
  }

  for t in ${_type[@]}; do
    msg "Building the "${t}" UI"
    {
      cd "${srcdir}"/${t}
      ./configure --no-check
      make
    }
  done
}

package() {
  msg "Packaging COMMON components..."
  {
    cd "${srcdir}"/common
    make DESTDIR="$pkgdir" install
  }

  for t in ${_type[@]}; do
    msg "Packaging the "${t}" UI"
    {
      cd "${srcdir}"/${t}
      make DESTDIR="$pkgdir" install
    }
  done

  # Patch for python2
  sed -e 's|^python |python2 |g' \
    -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

# vim:set ts=2 sw=2 et:
