# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

pkgname=mingw32-libpng
pkgver=1.6.6
pkgrel=2
arch=('any')
pkgdesc="A collection of routines used to create PNG format graphics (mingw32)"
depends=('mingw32-runtime' 'mingw32-zlib')
makedepends=('mingw32-gcc')
license=('custom')
url="http://www.libpng.org/pub/png/libpng.html"
options=(!strip !buildflags staticlibs)
source=("http://downloads.sourceforge.net/project/libpng/libpng16/$pkgver/libpng-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/libpng-apng/libpng16/$pkgver/libpng-$pkgver-apng.patch.gz")
md5sums=('2d8ec4b8251e0a343abda7d38594d557'
         '8af242769611daf62a8a17349b6059f7')

build() {
  cd "$srcdir/libpng-$pkgver"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i "${srcdir}/libpng-$pkgver-apng.patch" || return 1

  ./configure --host=i486-mingw32 --prefix=/usr/i486-mingw32

  make
}

package() {
  cd "$srcdir/libpng-$pkgver"

  make install DESTDIR=$pkgdir

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
