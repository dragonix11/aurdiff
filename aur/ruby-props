# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
_gemname=props
pkgname=ruby-props
pkgver=1.0.2
pkgrel=1
pkgdesc="Manage Settings Hierachies (Commandline, User, Home, Defaults, etc.)"
arch=(any)
url="http://geraldb.github.com/props"
license=('custom')
depends=('ruby')
makedepends=('rubygems')
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem
        'LICENSE')
noextract=(${_gemname}-${pkgver}.gem)
md5sums=('b637090891b7806f2836ee7a8673897a'
         '838084f669adc77da719a976a11e28ca')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "${pkgdir}${_gemdir}" -n "$pkgdir/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
