# $Id$
# Maintainer: Mladen Pejakovic <pejakm at gmail dot com>

pkgname=qtmobility
pkgver=1.2.0
pkgrel=9
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/qt-addons/mobility/'
pkgdesc='A cross-platform application and UI framework'
license=('LGPL2.1' 'GPL3')
depends=('qt4' 'udev')
makedepends=('mesa' 'gstreamer0.10-base' 'gstreamer0.10-bad' 'networkmanager' 'bluez-libs')
optdepends=('networkmanager: for Bearer Management API'
            'gstreamer0.10-base: for Multimedia mediaservice plugins'
            'gstreamer0.10-bad: for Multimedia mediaservice plugins')
source=(
#"http://get.qt.nokia.com/qt/add-ons/qt-mobility-opensource-src-${pkgver}.tar.gz"
        "http://pkgs.fedoraproject.org/repo/pkgs/qt-mobility/qt-mobility-opensource-src-1.2.0.tar.gz/ea5db5a8d3dd4709c2926dceda646bd8/qt-mobility-opensource-src-1.2.0.tar.gz"
        "qt48.patch"
        "qsensor_fix.patch"
        "pa-header_fix.patch")
md5sums=('ea5db5a8d3dd4709c2926dceda646bd8'
         '5a8cd86a7840f7bef2037c17e3c4d7af'
         'f6cb1c2c739d47f46b4fc7eeea49af69'
         '5d91bfaf50c21e1e3a4eb7f12c6d7174')

build() {
  cd "${srcdir}"/qt-mobility-opensource-src-${pkgver}
#   Qt 4.8 patch
  patch -Np1 -i "${srcdir}"/qt48.patch
#   Fix compilation of qtwebkit with gcc 4.7
  patch -Np0 -i "${srcdir}"/qsensor_fix.patch
  patch -Np0 -i "${srcdir}"/pa-header_fix.patch

  ./configure -prefix /usr -release \
      -plugindir /usr/lib/qt4/plugins \
      -qmake-exec qmake-qt4
  make
}

package() {
  cd "${srcdir}"/qt-mobility-opensource-src-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}
