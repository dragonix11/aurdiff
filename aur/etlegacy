# Maintainer: Spyhawk
# Contributor: Babets

pkgname=etlegacy
pkgver=2.71rc1
pkgrel=2
pkgdesc="Fully compatible client and server for the popular online FPS game Wolfenstein: Enemy Territory"
arch=('i686' 'x86_64')
url="http://www.etlegacy.com/"
license=('GPL3')
if [[ "$CARCH" == "i686" ]]; then
  depends=('enemy-territory-data' 'alsa-plugins' 'sdl' 'libjpeg' 'glu' 'libsm' 'curl' 'libvorbis' 'gcc-libs' 'lua')
  optdepends=('libtxc_dxtn: S3 Texture Compression (S3TC) library for Mesa')
else
  depends=('enemy-territory-data' 'lib32-alsa-plugins' 'lib32-sdl' 'lib32-libjpeg' 'lib32-glu' 'lib32-libsm' 'lib32-curl' 'lib32-libvorbis' 'lib32-gcc-libs' 'lib32-lua')
  optdepends=('lib32-libtxc_dxtn: S3 Texture Compression (S3TC) library for Mesa')
fi
makedepends=('cmake' 'zip')
source=("http://mirror.etlegacy.com/$pkgname-linux-$pkgver.tar.gz"
        "$pkgname.desktop"
        "etl.sh"
        "etlded.sh")
md5sums=('9547660cb362e9708e2f27287167abca'
         '444dbc890f1782aa6f2b9ecf319e326c'
         'e0fc0b5ff9d9b39e99c99e78a3b5f4b0'
         'ff8bebddbd23ca90bb9dc8fba2e39ee6')

prepare() {
    # extract
    tar -zxvf $pkgbase-linux-$pkgver.tar.gz

    # ugly hack to bypass hardcoded fs_basepath
    sed -i -e "s/.\/legacy\/omni-bot/\/usr\/share\/etlegacy\/legacy\/omni-bot/" $pkgbase-linux-$pkgver/{etl_bot.sh,etlded_bot.sh}
}

package() {
    # files
    mkdir -p $pkgdir/usr/bin
    mkdir -p $pkgdir/usr/share/$pkgbase/etmain
    install -Dm 755 {etl.sh,etlded.sh} $pkgdir/usr/bin/
    install -Dm 755 $pkgbase-linux-$pkgver/{etl,etl_bot.sh,etlded,etlded_bot.sh} $pkgdir/usr/bin/
    install -Dm 644 $pkgbase-linux-$pkgver/etmain/* $pkgdir/usr/share/$pkgbase/etmain/
    mkdir -p $pkgdir/usr/share/$pkgbase/legacy
    mv $pkgbase-linux-$pkgver/legacy/ $pkgdir/usr/share/$pkgbase/

    # data files
    ln -s /usr/share/enemy-territory/etmain/{mp_bin,pak0,pak1,pak2}.pk3 $pkgdir/usr/share/$pkgbase/etmain/

    # doc
    mkdir -p $pkgdir/usr/share/doc/$pkgbase
    install -Dm 644 $pkgbase-linux-$pkgver/README.md $pkgdir/usr/share/doc/$pkgbase/

    # desktop file and icon
    mkdir -p $pkgdir/usr/share/{pixmaps,applications}
    install -Dm 644 $pkgname-linux-$pkgver/ETL.xpm $pkgdir/usr/share/pixmaps/$pkgname.xpm
    install -Dm 644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}

