# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# based on the unetbootin PKGBUILD
pkgname=tuxboot
pkgver=0.6
pkgrel=1
pkgdesc="A tool that helps you to create a bootable Live USB drive for Clonezilla live, DRBL live, GParted live and Tux2live"
url="http://www.tuxboot.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgver}/source/${pkgname}-${pkgver}.src.tar.gz)
depends=('syslinux' 'p7zip' 'qt4' 'mtools' 'guisu' 'polkit')
md5sums=('bcde268dbf0683feb84e57adf488bb7f')
build() {
   cd $pkgname-$pkgver
# Fix desktop file
   sed -i '/\[en_US\]/d' ${pkgname}.desktop
   sed -i 's|/usr/bin/tuxboot|tuxboot|g' ${pkgname}.desktop
   sed -i '/^RESOURCES/d' ${pkgname}.pro
   lupdate-qt4 ${pkgname}.pro
   lrelease-qt4 ${pkgname}.pro
   qmake-qt4 "DEFINES += NOSTATIC" "RESOURCES -= tuxboot.qrc"
   make
}

package() {
   cd $pkgname-$pkgver
   install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
   mkdir -p ${pkgdir}/usr/share/${pkgname}
   install -m644 i18n/${pkgname}_*.qm ${pkgdir}/usr/share/${pkgname}/
   install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
   install -Dm644 ${pkgname}.xpm  ${pkgdir}/usr/share/pixmaps/${pkgname}.xpm
}
