# Maintainer: Rob McCathie aka korrode <archaur at rmcc dot com dot au>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

_name=bison
pkgname=bison27
pkgver=2.7.1
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('glibc' 'm4' 'sh')
groups=('base-devel')
provides="bison=$pkgver"
conflicts='bison'
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${_name}-${pkgver}.tar.xz{,.sig})
md5sums=('7be02eb973eccf388f1ae750fc09eed0'
         'SKIP')

build() {
  cd ${srcdir}/${_name}-${pkgver}  
  ./configure --prefix=/usr --datadir=/usr/share
  make
}

check() {
  cd ${srcdir}/${_name}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${_name}-${pkgver}
  make DESTDIR=${pkgdir} install
}
