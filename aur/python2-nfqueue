# Maintainer: Evan Teitelman <teitelmanevan@gmail.com>
# Contributor: Mitchel Humpherys <mitch.special@gmail.com>

pkgname=python2-nfqueue
pkgver=0.4
pkgrel=1
pkgdesc="Python bindings for libnetfilter_queue by Pierre Chifflier"
url="https://www.wzdftpd.net/redmine/projects/nfqueue-bindings/wiki/"
arch=('any')
license=('GPL')
depends=('python2' 'libnetfilter_queue')
makedepends=('python2-distribute' 'cmake' 'swig')
source=("http://ftp.de.debian.org/debian/pool/main/n/nfqueue-bindings/nfqueue-bindings_$pkgver.orig.tar.gz"
        python-support.patch)
md5sums=('2ac7cca1c2f41dfee4a600374ef86292'
         '21c3cdd78fbc6fa2c261dcd53beb36b2')

prepare() {
  cd "$srcdir/nfqueue-bindings-$pkgver"

  patch -p1 -i ../python-support.patch

  # I don't feel like editing the patch.
  sed -i 'i\#include <stdint.h>' nfq.h
}

package() {
  cd "$srcdir/nfqueue-bindings-$pkgver"

  # Examples.
  install -dm755 -p "$pkgdir/usr/share/doc/nfqueue-bindings/examples"
  install -m644 examples/*.py "$pkgdir/usr/share/doc/nfqueue-bindings/examples"

  # Library.
  make PREFIX="$pkgdir/usr" install
}
