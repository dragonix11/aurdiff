# Maintainer: Michal Lisowski <lisu at riseup dot net>

pkgname=exfalso
pkgver=3.0.2
pkgrel=1
pkgdesc="GTK+ audio tag editor"
arch=('any')
url="http://code.google.com/p/quodlibet/"
license=('GPL2')
depends=('mutagen' 'desktop-file-utils' 'hicolor-icon-theme' 'python2-gobject')
makedepends=('intltool')
conflicts=('quodlibet')
install=exfalso.install
source=(http://quodlibet.googlecode.com/files/quodlibet-$pkgver.tar.gz)
md5sums=('896148be29f6d483816a0bdab9d936ae')

build () {
  cd "$srcdir"/quodlibet-$pkgver

  # Use Python 2
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' exfalso.py quodlibet/util/fmps.py

  python2 setup.py build
}

package() {
  cd "$srcdir"/quodlibet-$pkgver

  python2 setup.py install --root="$pkgdir"

  # Remove Quod Libet
  rm -f "$pkgdir"/usr/bin/quodlibet
  rm -f "$pkgdir"/usr/share/applications/quodlibet.desktop
  rm -f "$pkgdir"/usr/share/icons/hicolor/*/apps/quodlibet.*
  rm -f "$pkgdir"/usr/share/pixmaps/quodlibet.png
  rm -f "$pkgdir"/usr/share/man/man1/quodlibet.1
}
