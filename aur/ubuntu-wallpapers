# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
_actual_ver=13.04.0
_extra_ver=+13.10.20130924.1
_ubuntu_rel=0ubuntu1
# AUR is broken once again. How is ${_actual_ver}.${_extra_ver} greater than
# 32 characters?
pkgver=13.10
pkgrel=3
pkgdesc="The default wallpapers for Ubuntu (from 9.10 Karmic Koala to 13.10 Saucy Salamander)"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python2-distutils-extra')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('8fd5817a00bbc600364b94ef69823855a1a4db04e5045e72ca6d4b4d80979970ae46a10f2ba344933b3f8c4688a41f94c892cb3192d5dea20f2419e221d4c142'
            '9c11a250e2baabab34550b408f7f2c9a50c07c10a05e7d93ebe8ab89c1970e03e3d6bbc979964272d828fc53b3b0e741d858fdbe1d3c5379b74bc31075fc90d4')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # The setup.py script reads the package version from debian/*
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
