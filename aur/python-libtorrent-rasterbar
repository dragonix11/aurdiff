# Maintainer : Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
        
pkgname=python-libtorrent-rasterbar
pkgver=0.16.12
pkgrel=1
pkgdesc="Python 3 bindings for the libtorrent library"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python' 'libtorrent-rasterbar')
makedepends=('boost')
options=('!emptydirs')
source=(http://libtorrent.googlecode.com/files/libtorrent-rasterbar-$pkgver.tar.gz)
sha512sums=('91c7511d02a94d9a89852e28dd201d22c5211fe9f3e0c07e5ca2db084e5673534ddafaeb7c222b0c96ceeadef4c495e5db21102b3736d40490246b57e65a3ee6')
        
build() {
  cd libtorrent-rasterbar-$pkgver
  ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system \
      --with-boost-python=boost_python3
  cd bindings/python
  make
}

package() {
  cd libtorrent-rasterbar-$pkgver/bindings/python
  make DESTDIR="$pkgdir" install
  install -D "${srcdir}/libtorrent-rasterbar-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
