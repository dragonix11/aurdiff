# $Id: 21155555adfdfa0847d0df16306f08764479e45c $
# Maintainer: Ido Rosen <ido@kernel.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
# 
# From the OpenOnload website, http://www.openonload.org/ :
#   ``OpenOnload(r) is a high performance network stack from Solarflare that
#   dramatically reduces latency and cpu utilisation, and increases message rate
#   and bandwidth.  OpenOnload runs on Linux and supports TCP/UDP/IP network
#   protocols with the standard BSD sockets API, and requires no modifications
#   to applications to use. It achieves performance improvements in part by
#   performing network processing at user-level, bypassing the OS kernel
#   entirely on the data path. Networking performance is improved without
#   sacrificing the security and multiplexing functions that the OS kernel
#   normally provides.'' (10/3/2013)

#
# NOTE REGARDING OpenOnload Linux kernel compatibility for kernel 3.10.x:
# I was granted permission to distribute the attached OpenOnload patch by
# Solarflare: ``You are welcome to distribute the patch onward.  We intend 
# to include it in the next Onload release.''  So that patch is included as
# ``openonload-201210-u2-bug37560-bug37722_3.10_merged.patch'' below.
#

pkgname='openonload'
pkgdesc="A high performance network stack from Solarflare."
pkgver='201210.u2'
_oover='201210-u2'
_kernelver="$(uname -r)"
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.openonload.org/'
license=('custom')
depends=('linux-lts')
makedepends=('linux-lts' 'linux-lts-headers')
options=('!libtool' '!strip' '!makeflags')
source=(http://www.openonload.org/download/openonload-201210-u2.tgz
        openonload.install
        openonload.service
        openonload-201210-u2-bug37560_bug37722_3.10_merged.patch)
sha512sums=('4e1c7376fbde9c33d044586dba975e83a71ba25df049f56e47d53244b65c2f79d08e6c3992ed183ac5793b01c6431ca61faf1e7c0ec70f75860364e70ad2e8d3'
            '0990593ba7f9845b2e89df4cc9cd178ffec774aea24649420abde653e8578c86e8d84da86bc3299be02e7d4541d90cf5b62a5c6f60c13b144a53c8e09b1c2933'
            '78b5d3dbe7625dcad892701f14e8bbc467d1c7e475837ad0c69476e6d69432d321a38e3007b681683bd3a0154ab4980935e23060e1924ce6bc70ef298603f047'
            'f8bf374c1afb0f6af45415c8dd45f81f9c554458415f2d035e2c59ea9ef5555a89ee9fabc42043d856af3c0a659fd89a683e4e3751ebb1969a5e996054acd74e')

install="openonload.install"

prepare() {
    cd "${srcdir}/${pkgname}-${_oover}"

    patch -p1 -i "${srcdir}/openonload-201210-u2-bug37560_bug37722_3.10_merged.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_oover}"
  
  case $CARCH in
    "i686")
      # x86 (32 bit) build
      ./scripts/onload_build --kernelver "$_kernelver" --user32 ${DEBUG:+--debug}
    ;;
    "x86_64")
      # x64 build
      ./scripts/onload_build --kernelver "$_kernelver" ${DEBUG:+--debug}
    ;;
    *)
      # unsupported
      echo "Unsupported architecture."
      exit 1
    ;;
  esac
}

package() {
  cd "${srcdir}/${pkgname}-${_oover}"

  install -dm755 "${pkgdir}/etc/modprobe.d"
  install -dm755 "${pkgdir}/etc/depmod.d"
  i_prefix="${pkgdir}" ./scripts/onload_install --verbose \
      --kernelver "$_kernelver" \
      ${DEBUG:+--debug} rpm_install
  rm -f "${pkgdir}/etc/modprobe.conf" # may be created by onload_install

  install -Dm755 scripts/onload_install \
                                  "${pkgdir}/lib/onload/onload_install"

  # Install documentation:
  install -dm755                  "${pkgdir}/usr/share/doc/openonload"
  install -m644 ChangeLog         "${pkgdir}/usr/share/doc/openonload"
  install -m644 LICENSE           "${pkgdir}/usr/share/doc/openonload"
  install -m644 README            "${pkgdir}/usr/share/doc/openonload"
  install -m644 README.affinity   "${pkgdir}/usr/share/doc/openonload"
  install -m644 README.ef_vi      "${pkgdir}/usr/share/doc/openonload"
  install -m644 ReleaseNotes      "${pkgdir}/usr/share/doc/openonload"
  install -m644 ReleaseNotes-sfc  "${pkgdir}/usr/share/doc/openonload"

  # Install ef_vi tests/samples:
  install -dm755                  "${pkgdir}/usr/share/doc/openonload/ef_vi"
  cp -r "src/tests/ef_vi/."       "${pkgdir}/usr/share/doc/openonload/ef_vi/."

  # Install LICENSE file:
  install -dm755                  "${pkgdir}/usr/share/licenses/openonload"
  ln -s "/usr/share/doc/openonload/LICENSE" \
                                  "${pkgdir}/usr/share/licenses/openonload"

}
