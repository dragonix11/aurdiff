# Maintainer: Jerome Lebleu <jerome dot lebleu at mailoo dot org>

pkgname=qlcplus
pkgver=4.4.1
pkgrel=2
pkgdesc="Q Light Controller Plus - The open DMX lighting desk software for controlling professional lighting fixtures."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qlcplus"
license=('GPL2')
depends=('qt4' 'liblo' 'libftdi' 'libsndfile' 'libmad' 'shared-mime-info' 'fftw')
optdepends=('qlcplus-plugin-ola: Open Lighting Architecture plugin')
conflicts=('qlc' 'qlc-svn')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/qlcplus/${pkgver}/qlcplus_${pkgver}.tar.gz")
md5sums=('3f061c0a1e30cd4d0c3a80b53f75bc0d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # don't build ola plugin
  sed -i "/ola/s/^/#/" plugins/plugins.pro

  qmake-qt4 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
