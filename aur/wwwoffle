# Maintainer: Dreieck
# Contributor: Vasco Costa <vasco dot costa at geekslot dot com>

pkgname=wwwoffle
pkgver=2.9i
pkgrel=1
pkgdesc="Simple proxy server with special features for use with dial-up internet links"
arch=('i686' 'x86_64' 'arm' 'arm64')
url="http://www.gedanken.org.uk/software/wwwoffle/"
license=('GPL')
depends=('zlib' 'gnutls' 'libgcrypt')
backup=('etc/wwwoffle/wwwoffle.conf')
install="${pkgname}.install"
source=("http://www.gedanken.org.uk/software/wwwoffle/download/${pkgname}-${pkgver}.tgz" "wwwoffle" "wwwoffle.conf.d" "${install}")

sha512sums=(
'c9e9d67df9a0e40b584916857221a9a728f36a677cba28527f8c2febea1a23379d3b112d96723bf01ec1718a0897d8980715864ac7ef898b2585dd3f01d9a456' 'af966e5e33586a6db06dd91f30b284272169150128eabed9d83a939394b69ced86d02091c9f870542849a3edf19cdea664bafe85431fa8c59dc5f53279191137' '8d268855f04d8a9804c8053f539f21d85342010655c699bd3ac83947fb0a69487f2620fc710dba8cae750c896e84408e1df2c5e144ed2a8c75513d9f2457c284' 'bdd6f384878eb4bc399f2715b626a0a0d12785bd141234a75e7b5816882fc0952b8e4d37b00b40501956c978fb8fdb8852d429c0a2f6475bbb7b13e68bcbba47'
)

build() {
  _unpackeddir="${srcdir}/${pkgname}-${pkgver}"
  cd "${_unpackeddir}"
  
  ./configure \
    --prefix=/usr \
    --with-zlib=/usr/include \
    --with-gnutls=/usr/include/gnutls \
    --with-gcrypt=/usr/include \
    --with-ipv6 \
    --with-spooldir=/var/spool/wwwoffle \
    --with-confdir=/etc/wwwoffle \
    --with-default-language=en
  
  make || return 1
}

package() {
  _unpackeddir="${srcdir}/${pkgname}-${pkgver}"
  cd "${_unpackeddir}"
  
  make DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/wwwoffle" "${pkgdir}/etc/rc.d/wwwoffle"
  install -D -m644 "${srcdir}/wwwoffle.conf.d" "${pkgdir}/etc/conf.d/wwwoffle"
  sed -i -e 's/^#run-uid.*/ run-uid           = wwwoffle/' \
    "${pkgdir}/etc/wwwoffle/wwwoffle.conf"
  sed -i -e 's/^#run-gid.*/ run-gid           = wwwoffle/' \
    "${pkgdir}/etc/wwwoffle/wwwoffle.conf"
}

