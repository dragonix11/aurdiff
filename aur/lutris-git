# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=lutris-git
pkgver=395.065bb6a
pkgrel=1
pkgdesc="Open Gaming Platform"
arch=('any')
url="http://lutris.net/"
license=('GPL3')
depends=('dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'polkit' 'python2-gobject' 'python2-xdg' 'python2-yaml')
makedepends=('git' 'python2-setuptools')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("git+https://github.com/lutris/lutris.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-*}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

package() {
  cd ${pkgname%-*}

  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim: ts=2 sw=2 et:
