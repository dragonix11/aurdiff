# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
# Contributor: JohannesSM64 <johannes.a.langoy@gmail.com>
pkgname=jupp
pkgver=3.1jupp26
pkgrel=1
pkgdesc="An enhanced, portable version of joe, forked in 2004."
url="https://mirbsd.org/jupp.htm"
license=('custom:GPLv1')
depends=('glibc' 'ncurses')
arch=('i686' 'x86_64')
conflicts=('joe')
source=("http://www.mirbsd.org/MirOS/dist/jupp/joe-$pkgver.tgz")

md5sums=('ae5d4cfe33e36e82c6e276576b7c8748')
sha256sums=('99985d6f0db5cc59ba45b30291ce6c3ef9626637f0cca039b2e03484cf5dda42')

backup=('etc/joe/jupprc' 
        'etc/joe/joerc' 
	'etc/joe/jmacsrc'
	'etc/joe/rjoerc'
	'etc/joe/jstarrc'
	'etc/joe/jpicorc')

build() {
	cd "$srcdir/$pkgname"
	
	sh configure --prefix=/usr --sysconfdir=/etc \
	             --disable-dependency-tracking --disable-termidx
	make
}

package() {
	cd "$srcdir/$pkgname"
	make "DESTDIR=$pkgdir" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
