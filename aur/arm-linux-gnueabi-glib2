_target=arm-linux-gnueabi
_sysroot=/usr/${_target}

pkgname=${_target}-glib2
pkgver=2.38.0
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ and other libs (${_target})"
depends=(${_target}-gettext ${_target}-zlib ${_target}-libffi)
makedepends=(${_target}-gcc python2 automake>=1.13)
license=(LGPL)
options=('!libtool' '!strip' '!buildflags')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        'revert-warn-glib-compile-schemas.patch'
	'http://svn.gobolinux.org/tools/branches/015/Compile/Resources/Defaults/Settings/Compile/Cross/arm_cache.conf')
md5sums=('c50d2805a76763e9b4cc4385d4ea215d'
	'cde8e1a9d18918be0d90af67a981f184'
	'6b6421d3113459432c10db333a09e77f')

prepare() {
  cd "$srcdir/glib-$pkgver"
  cp ../arm_cache.conf ./
  
cat >> ./arm_cache.conf << EOF

# glib fix
ac_cv_android_arm=yes
ac_cv_func_posix_getgrgid_r=no

# lucasvr
gt_cv_have_gettext=yes
gt_cv_func_ngettext_libc=yes
gt_cv_func_dgettext_libc=yes
gt_cv_func_dgettext_libintl=yes
ac_cv_func_bind_textdomain_codeset=yes

EOF
  
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
}

build()
{
  cd "$srcdir/glib-$pkgver"

  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig:${_sysroot}/share/pkgconfig"
  unset LDFLAGS
  unset PKG_CONFIG_PATH
  
  ./autogen.sh
  
  PYTHON="/usr/bin/python2" \
  ./configure \
    --prefix=${_sysroot} \
    --host=${_target} \
    --enable-static \
    --enable-shared \
    --cache-file=./arm_cache.conf
  
  make
}

package() {
  cd "$srcdir/glib-$pkgver"
  make DESTDIR="$pkgdir" install
}

