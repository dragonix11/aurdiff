# Maintainer: Johnny Halfmoon <jhalfmoon@milksnot.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-gdb-linaro
_relver=7.6
_reldate=2013.05
_relverdate=${_relver}-${_reldate}
pkgver=$_relver_$_reldate
pkgrel=1
pkgdesc="Linaro GDB is a branch of FSF GDB that contains ARM focused features and patches."
arch=(i686 x86_64)
url="https://launchpad.net/gdb-linaro/7.6"
license=('GPL3')
groups=('arm-none-eabi-toolchain')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
optdepends=('openocd: for debugging JTAG targets')
provides=('arm-none-eabi-gdb')
conflicts=('cross-arm-none-eabi-gdb')
options=(!libtool !emptydirs)
source=(https://launchpad.net/gdb-linaro/${_relver}/${_relverdate}/+download/gdb-linaro-${_relverdate}.tar.bz2)
sha256sums=('008bf4d9ce66c714eab1d324afa0f8220a6dc24fc5b6e68cbf3ee28c0e80b73a')

_basedir=gdb-linaro-${_relverdate}

build() {
  cd ${srcdir}/${_basedir}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir build
  cd build
  ../configure --target=arm-none-eabi \
               --prefix=/usr \
               --datarootdir=/usr/share/arm-none-eabi-gdb \
               --enable-multilib \
               --enable-interwork \
               --with-system-readline \
               --disable-nls
  make
}

package() {
  cd ${srcdir}/${_basedir}/build
  make DESTDIR=${pkgdir} install

  # libiberty.a conflicts with host version
  rm -f  ${pkgdir}/usr/lib/libiberty.a
}

# vim: set ts=2 sw=2 ft=sh noet:

