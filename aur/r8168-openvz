# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community]

pkgname=r8168-openvz
_realname=r8168
pkgver=8.037.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards for linux-openvz"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz")
makedepends=("linux-openvz-headers")
source=("http://r8168.googlecode.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("50478efd8027bd8ae83f27b9c1f08850")
sha256sums=("06aa1c6a58a878450798100eb5f9c3d87763c5f41e99b7d5f3b2fe78646d02b0")

build() {
  _kernver=$(pacman -Q linux-openvz | cut -d . -f 3 | cut -f 1 -d -)
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz/version)

	cd "${_realname}-${pkgver}"

  # avoid using the Makefile directly -- it doesn't understand
  # any kernel but the current.
  make -C "/usr/lib/modules/${KERNEL_VERSION}/build" \
      SUBDIRS="${srcdir}/${_realname}-${pkgver}/src" \
      EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
  _kernver=$(pacman -Q linux-openvz | cut -d . -f 3 | cut -f 1 -d -)
  depends=("linux-openvz>=2.6.${_kernver}" "linux-openvz<2.6.$(expr ${_kernver} + 1)")
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz/version)
  msg "Kernel = ${KERNEL_VERSION}"

	cd "${_realname}-${pkgver}"
	install -Dm644 "src/${_realname}.ko" "${pkgdir}/usr/lib/modules/extramodules-2.6.${_kernver}-openvz/${_realname}.ko"
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} +

  sed -i "s|extramodules-.*-openvz|extramodules-2.6.${_kernver}-openvz|" "${startdir}/${_realname}.install"
}
