# Maintainer: Xeross <contact@xeross.me>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=python2-amqp
pkgver=1.0.13
pkgrel=1
pkgdesc="Low-level AMQP client for Python (fork of amqplib)"
arch=('any')
url="https://github.com/celery/py-amqp/"
license=("LGPL")
depends=('python2>=2.6' 'setuptools')
source=(https://pypi.python.org/packages/source/a/amqp/amqp-${pkgver}.tar.gz)
sha256sums=('15fed1a2f0326f198a9d3d16ce1dc4296385d33859bd9f7c59705093fab8f53c')

prepare() {
  cd amqp-${pkgver}
  # replace /usr/bin/env python with python2
  find . -name '*.py' -exec sed -i -e '1 s/python\s*$/python2/' {} \;
}

package() {
  cd amqp-${pkgver}
  python2 ./setup.py install --root="${pkgdir}" --prefix=/usr
}

# vim: ft=sh syn=sh
