# $Id$
# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins-git
pkgver=2013.09.03
pkgrel=1
pkgdesc='Plugins for Audacious'
url='http://audacious-media-player.org/'
license=('custom:BSD' 'GPL3')
arch=('i686' 'x86_64')
provides=('audacious-plugins')
conflicts=('audacious-plugins' 'audacious-plugins-hg')

makedepends=("audacious-git>=${pkgver}"
             'glib2' 'python2' # for gdbus-codegen
             'oss' 'alsa-lib' 'pulseaudio' 'jack2' 'lame' 'libvorbis' 'flac'
             'mpg123' 'faad2' 'ffmpeg' 'libmodplug' 'fluidsynth'
             'libcdio-paranoia' 'libsidplayfp' 'wavpack' 'libnotify' 'lirc-utils'
             'curl' 'libmtp' 'neon' 'libmms' 'libcue' 'git')

optdepends=('oss: Open Sound System v4 output'
            'alsa-lib: Advanced Linux Sound Arch. output'
            'pulseaudio: PulseAudio output'
            'jack2: Jack Audio Connection Kit output'
            'lame: FileWriter MP3 output'
            'libvorbis: Vorbis input, FileWriter Vorbis output'
            'flac: FLAC input, FileWriter FLAC output'

            'mpg123: MP3 input'
            'faad2: AAC input'
            'ffmpeg: ffaudio input'
            'libmodplug: modplug input'
            'fluidsynth: MIDI FluidSynth backend input'
            'libcdio-paranoia: CD Digital Audio input'
            'libsidplayfp: Commodore 64 audio input'
            'wavpack: WavPack input'

            'libnotify: libnotify OSD'
            'lirc-utils: LIRC'
            'curl: AudioScrobbler Client'
            'libmtp: Upload to MTP device'

            'neon: neon-based http transport'
            'libmms: libmms-based mms transport'
            'libcue: CUE playlist format')

source=('git://github.com/audacious-media-player/audacious-plugins.git')
sha256sums=('SKIP')

pkgver() {
    date +%Y.%m.%d
}

build() {
    cd "$srcdir/audacious-plugins"

    autoreconf
    ./configure \
        --prefix=/usr \
        --enable-amidiplug \
        --disable-adplug \

    make
}

package() {
    cd "$srcdir/audacious-plugins"
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
