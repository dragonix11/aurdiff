# Maintainer: blackleg <blackleg@lavabit.com>
#Based in lua51 package
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=mingw-w64-lua51
pkgver=5.1.5
pkgrel=5
pkgdesc='LUA 5.1 Mingw Version (Only x86_64)'
arch=('x86_64')
url='http://www.lua.org/'
depends=("readline" "mingw-w64-crt")
makedepends=("mingw-w64-gcc")
license=('MIT')
options=(!strip !buildflags)
source=("http://www.lua.org/ftp/lua-$pkgver.tar.gz")

md5sums=('2e115fe26e435e33b0d5c022e4490567')

_arch=x86_64-w64-mingw32

build() {
    unset LDFLAGS
    cd lua-$pkgver
    sed -e 's:llua:llua5.1:' -e 's:/include:/include/lua5.1:' -i etc/lua.pc
    sed -r -e '/^LUA_(SO|A|T)=/ s/lua/lua5.1/' -e '/^LUAC_T=/ s/luac/luac5.1/' -i src/Makefile
    make -j1  AR="${_arch}-ar rcu" RANLIB="${_arch}-ranlib" \
        STRIP="${_arch}-strip" CC="${_arch}-gcc" mingw
}

package() {
    cd lua-$pkgver
    make \
        TO_BIN="lua.exe luac.exe lua51.dll" \
        TO_LIB="liblua5.1.a" \
        INSTALL_TOP="${pkgdir}/usr/${_arch}" \
        INSTALL_INC="${pkgdir}/usr/${_arch}/include/lua5.1" \
        INSTALL_MAN="${pkgdir}/usr/${_arch}/share/man/man1" \
        install
    install -D -m644 etc/lua.pc "${pkgdir}/usr/${_arch}/lib/pkgconfig/lua5.1.pc"
    # Install the documentation
    install -d "${pkgdir}/usr/${_arch}/share/doc/$pkgname"
    install -m644 doc/*.{gif,png,css,html} "${pkgdir}/usr/${_arch}/share/doc/$pkgname"
    # Install copyrigth file
    install -D -m644 COPYRIGHT "${pkgdir}/usr/${_arch}/share/licenses/$pkgname/COPYRIGHT"
    # fixups
    cd "${pkgdir}/usr/${_arch}/bin"
    cp -p lua51.dll "${pkgdir}/usr/${_arch}/lib/liblua5.1.dll"      
    cd "${pkgdir}/usr/${_arch}/share/man/man1"
    mv lua.1 lua5.1.1
    mv luac.1 luac5.1.1
}

# vim:set ts=4 sw=4 et:
