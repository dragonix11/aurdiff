# Maintainer: Bertrand Bonnefoy-Claudet <bertrandbc@gmail.com>

_gemname=veewee
pkgname=ruby-$_gemname
pkgver=0.3.7
pkgrel=1
pkgdesc="A tool for building custom Vagrant base boxes and virtual machine images."
arch=(any)
url="https://github.com/jedi4ever/veewee"
license=('MIT')
depends=(ruby 'ruby-ansi>=1.3.0' ruby-childprocess 'ruby-cucumber>=1.0.0'
         'ruby-fission>=0.4.0' 'ruby-fog>=1.8' ruby-grit ruby-highline ruby-i18n
         'ruby-net-ssh>=2.2.0' 'ruby-popen4>=0.1.2' ruby-progressbar 'ruby-rspec>=2.5'
         'ruby-vnc>=1.0.0' 'ruby-thor>=0.15' 'ruby-vagrant>=0.9')
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('424ce1d2f65b04c3cdf93c4928fa6b69')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"

  # veewee-0.3.7 net-ssh and ansi version requirements are too specific with
  # respect to those installed as vagrant dependencies
  sed -i '/net-ssh\|ansi/ s/~>/>=/' $pkgdir$_gemdir/specifications/$_gemname-$pkgver.gemspec
}

