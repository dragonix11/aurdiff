# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vmware-view-client
pkgver=2.1.0
pkgrel=1
pkgdesc="VMware Horizon View Client connects to virtual machines managed by VMware Horizon View (proprietary version with PCoIP support)"
conflicts=('vmware-view-open-client' 'vmware-view-open-client-beta')
arch=('i686' 'x86_64')
url="https://my.vmware.com/web/vmware/info/slug/desktop_end_user_computing/vmware_view/5_0"
license=('custom')
source=("http://archive.canonical.com/pool/partner/v/vmware-view-client/vmware-view-client_${pkgver}.orig.tar.gz"
	'tmpfiles.conf'
	'vmware-view.png'
	'vmware-view-usbd'
	'vmware-view-usbd.service'
	'vmware-view-client.desktop')
depends=()
optdepends=('rdesktop: for RDP remote desktop connections')
install=vmware-view-client.install

if [[ "${CARCH}" == 'x86_64' ]]; then
	_libprefix='lib32-'
	optdepends+=('lib32-systemd: for USB redirect support')
else
	_libprefix=''
fi
# no spaces here
depends+=(${_libprefix}{'openssl098','libpng12','gtk2','libxml2','libxtst'})
optdepends+=(${_libprefix}{'alsa-lib: for audio support via alsa','libpulse: for audio support via pulse sound server','pcsclite: for smartcard support'})

package() {
	cd "${srcdir}/vmware-view-client-${pkgver}"

	for FILE in $(find -type f); do
		install -D ${FILE} ${pkgdir}/${FILE}
	done

	# set up vmware-view-usbd
	chmod +s ${pkgdir}/usr/lib/vmware/vmware-{usbarbitrator,view-usbd}
	install -D -m0755 ${srcdir}/vmware-view-usbd ${pkgdir}/usr/lib/systemd/scripts/vmware-view-usbd
	install -D -m0644 ${srcdir}/vmware-view-usbd.service ${pkgdir}/usr/lib/systemd/system/vmware-view-usbd.service
	if [[ "${CARCH}" == 'x86_64' ]]; then
		ln -s ../../lib32/libudev.so.1 ${pkgdir}/usr/lib/vmware/libudev.so.0
	else
		ln -s ../libudev.so.1 ${pkgdir}/usr/lib/vmware/libudev.so.0
	fi

	# this needs to be writeable for USB redirection
	install -d ${pkgdir}/var/run/vmware
	install -D -m0644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/vmware-view-client.conf

	# desktop file for menu entries
	install -D -m0644 ${srcdir}/vmware-view-client.desktop ${pkgdir}/usr/share/applications/vmware-view-client.desktop

	# icon
	install -D -m0644 ${srcdir}/vmware-view.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/vmware-view.png
}



sha256sums=('111c1d753a42c3c0eb1829f7a1bcc4d654f23458830f4727883dc6516ef2bb12'
            'ec763930dd50d6e77a31c40c939909752cfb124cafb0a4ca4f76860375a14d75'
            '5c5ea6ff244b798fbe1d276b95c9f5f7c047924bbc59341792e63d2be6d3b749'
            'cc03137151aae050f7cdb965d02488089a9e0ef7e03c21a13ea44036a3762b08'
            '021ce563b9a672fc5027fd83821561b2e34ea25c51c1d9cb9e84d905d45003e9'
            'af6893b96e07e9e8023d64eaa4cece90c73890d744e4d91d80cfdcd3120b60bd')
