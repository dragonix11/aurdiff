#Maintainer: almostalive <almostalive at wemail dot me>
#Contributor <Themaister> <maister@archlinux.us>

pkgname=dinothawr
_gitname=Dinothawr
pkgver=358.6759f26
pkgrel=2
pkgdesc="Dinothawr is a block pushing puzzle game on slippery surfaces. Our hero is a dinosaur whose friends are trapped in ice. Through puzzles it is your task to free the dinos from their ice prison. Made for the libretro API"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/Themaister/Dinothawr"
license=('custom')
makedepends=('git')
install=dinothawr.install
source=("${_gitname}::git://github.com/Themaister/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/dinothawr_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-dinothawr.so"
  install -Dm644 "${_gitname}/dinothawr/dinothawr.game" "${pkgdir}/usr/share/libretro/libretro-dinothawr/dinothawr/dinothawr.game"
  cp -r "${_gitname}/dinothawr/" "${pkgdir}/usr/share/libretro/libretro-dinothawr/"
}