# Contributor: 'viperpaulo'
# Contributor: 'lazork'
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>

pkgname=freefem++
pkgver=3.25
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'suitesparse')
makedepends=('ed' 'gcc-fortran' 'wget')
options=('!makeflags')
source=("http://www.freefem.org/ff++/ftp/${pkgname}-${pkgver}.tar.gz"
        "disable-doc.patch")
sha256sums=('dd83f7958689db158ad6606e9126a7a74c6757d4b49c92628a82c24867c799c2'
            '4df7aaecfc53f1721415cd38fd1b3ab066fc07cb118a13d32353c2338ba2e13d')

prepare() {
  cd "$pkgname-$pkgver"

  # add header
  ed -s src/Graphics/getprog-unix.hpp <<< $'1i\n#include <unistd.h>\n.\nw'

  # disable doc
  patch -Np1 -i ../disable-doc.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-download \
    --disable-pdf \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd" \
  # --with-mpiinc="-I/usr/include/mpif.h"

  mkdir -p ${srcdir}/$pkgname-$pkgver/download/include/
  mkdir -p ${srcdir}/$pkgname-$pkgver/download/include/scotch/
  ln -sf /usr/include/mpif* ${srcdir}/$pkgname-$pkgver/download/include/scotch/

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et
