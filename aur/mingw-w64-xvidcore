# Maintainer: Karl-Felix Glatzer <karl.glatzer@gmx.de>

pkgname=mingw-w64-xvidcore
pkgver=1.3.2
pkgrel=3
pkgdesc="XviD is an open source MPEG-4 video codec (mingw-w64)"
arch=('any')
url="http://www.xvid.org/"
license=('GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'nasm' 'yasm')
options=(!libtool !strip staticlibs)
source=(http://downloads.xvid.org/downloads/xvidcore-${pkgver}.tar.bz2
        mingw.patch)
md5sums=('34389e980d4f849688f8b1e168f4e2f9'
         '0f9ee289a5b90d85b9778d601113c4cc')
sha1sums=('74f9ce9c1bebcc9806bf21cb53274b8102700309'
          'aaf8bfbcc9e37be10383a05630eb91a3eab95058')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir"/xvidcore

  patch -p1 -i "../mingw.patch"
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch}

    cp -r "${srcdir}"/xvidcore ${srcdir}/build-${_arch}/

    cd ${srcdir}/build-${_arch}/xvidcore/build/generic

    unset CFLAGS LDFLAGS CPPFLAGS
    ./configure \
      --prefix=/usr/${_arch} \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"/xvidcore/build/generic
    make DESTDIR="${pkgdir}" install

    #Fix dynamic libraries
    mkdir -p "${pkgdir}/usr/${_arch}/bin"
    mv "${pkgdir}/usr/${_arch}/"lib/*.dll "${pkgdir}/usr/${_arch}/bin/"
    cd "${pkgdir}/usr/${_arch}/bin"
    _mylib=$(basename xvidcore.dll)
    ln -sf ${_mylib} libxvidcore.dll.4
    ln -sf ${_mylib} libxvidcore.dll

    cp "${srcdir}/build-${_arch}"/xvidcore/build/generic/=build/*.dll.a "${pkgdir}/usr/${_arch}/lib/"

    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}"/bin/*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}"/lib/*.a
  done
}
