# Contributor: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>
pkgname=dofus-transition
pkgver=2.14.0
pkgrel=1
pkgdesc="A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR."
url="http://www.dofus.com/"
license=("custom:Dofus License")
arch=('i686' 'x86_64')

source=(http://dl.ak.ankama.com/games/dofus2/setup/DofusInstall.run
        air-generic-launcher.sh
        ankama-dofus.sh
        transition.conf
        dofus.ici)
md5sums=('6ab70b3109f7de548982b79dd5a409b1'
         'f179eaa5e6e6674b1853cf826fc33c3a'
         '253a47833fde4e1306a546b1e4d3e6b1'
         '44c24ec38930c5220719da607c924bfc'
         'be49d6607c77abd90b28f9764ca5738b')

depends=('adobe-air-sdk' 'ankama-transition')
if [ "$CARCH" == "x86_64" ];then
  depends+=('lib32-gtk2' 'lib32-libxt' 'lib32-libjpeg6' 'lib32-libpng12' 'lib32-openssl098'
            'lib32-gvfs' 'lib32-nss' 'lib32-libxslt' 'lib32-alsa-lib')
else
  depends+=('gtk2' 'libxt' 'libjpeg6' 'libpng12' 'openssl098' 'gvfs' 'nss' 'libxslt' 'alsa-lib')
fi

build() {
  cd "$srcdir"

  msg2 "Extracting installer..."
  sh ./DofusInstall.run --noexec --keep --target dofus_install
}

package() {
  cd "$srcdir/dofus_install"
  _install_dir="$pkgdir/opt/ankama/dofus"
  install -d "$_install_dir"

  msg2 "Installing main applications..."
  cp -R Dofus/* "$_install_dir"
  install "$srcdir/air-generic-launcher.sh" "$_install_dir/bin/air-generic-launcher.sh"
  ln -sf "$_install_dir/bin/air-generic-launcher.sh" "$_install_dir/bin/Dofus"
  cp -R Reg "$_install_dir/share/reg"
  install "$srcdir/air-generic-launcher.sh" "$_install_dir/share/reg/bin/Reg"

  msg2 "Installing additional content..."
  ln -s ../Reg.swf "$_install_dir/share/reg/share/Reg.swf"
  ln -s ../content "$_install_dir/share/reg/share/content"
  install -m 644 uplauncherComponents.xml "$_install_dir/share/uplauncherComponents.xml"

  chgrp -R games $_install_dir
  chmod -R g+w $_install_dir

  msg2 "Installing launcher script..."
  install "$srcdir/ankama-dofus.sh" "$_install_dir/ankama-dofus"
  install -d "$pkgdir/usr/bin"
  ln -s /opt/ankama/dofus/ankama-dofus "$pkgdir/usr/bin/ankama-dofus"
  install $srcdir/transition.conf "$_install_dir/transition.conf"
  install $srcdir/dofus.ici "$_install_dir/share/dofus.ici"

  msg2 "Installing menu icon..."
  mkdir -p "$pkgdir/usr/share/applications"
  cat > "$pkgdir/usr/share/applications/Dofus.desktop" <<EOF
[Desktop Entry]
Encoding=UTF-8
Type=Application
Name=Dofus
GenericName=Dofus
Comment=Dofus
Icon=/opt/ankama/dofus/share/icon/dofus-icon-48.png
Exec=/opt/ankama/dofus/ankama-dofus
Path=/opt/ankama/dofus/share/
Categories=Game;RolePlaying;
EOF

  msg2 "Installing licenses..."
  mkdir -p "$pkgdir/usr/share/licenses/dofus"
  cp -R licenses/*.txt "$pkgdir/usr/share/licenses/dofus"
  chmod 0644 "$pkgdir/usr/share/licenses/dofus"/*
}
# vim:set ts=2 sw=2 et:
