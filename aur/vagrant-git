pkgname=vagrant-git
pkgver=1.2.7.194.g2b4c956
pkgrel=1
pkgdesc="Virtualization tool"
arch=('i686' 'x86_64')
url="http://vagrantup.com"
license=('GPL')
depends=('ruby' 'ruby-rspec-mocks' 'ruby-ffi' 'ruby-childprocess' 'ruby-erubis'
        'ruby-i18n' 'ruby-log4r' 'ruby-net-ssh' 'ruby-net-scp')
makedepends=('git')
source=('git://github.com/mitchellh/vagrant.git')
md5sums=('SKIP')

pkgver(){
  cd vagrant
  git describe --tags --long | sed 's/-/./g;s/^v//'
}

_gemname=vagrant
_gemver(){
  $srcdir/$_gemname/bin/vagrant -v | sed 's/Vagrant v//'
}

build() {
  cd $srcdir/vagrant
  gem build vagrant.gemspec
}

package() {
  cd $srcdir/vagrant
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "./$_gemname-$(_gemver).gem"
}

# vim:set ts=2 sw=2 et:
