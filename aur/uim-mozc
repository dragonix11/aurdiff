# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

## NOTE: This doens't support kill_line feature of uim.

## Mozc compile option
_bldtype=Release
#_bldtype=Debug

_uimmozcrev=327
_protobuf_ver=2.5.0
_gyp_rev=1656

pkgname=uim-mozc
_pkgname=mozc
pkgver=1.11.1522.102
pkgrel=1
pkgdesc="uim plugin module for Mozc"
arch=('i686' 'x86_64')
url="http://code.google.com/p/macuim/"
license=('BSD')
groups=('mozc-im')
depends=('mozc' 'uim')
install=${pkgname}.install
makedepends=('pkg-config' 'python2')
changelog=ChangeLog
source=(http://mozc.googlecode.com/files/mozc-${pkgver}.tar.bz2
        http://protobuf.googlecode.com/files/protobuf-${_protobuf_ver}.tar.bz2
        http://downloads.sourceforge.net/project/pnsft-aur/mozc/gyp-${_gyp_rev}.tar.xz
        http://downloads.sourceforge.net/project/pnsft-aur/mozc/uim-mozc-${_uimmozcrev}.tar.xz
)
sha1sums=('0859efcbc6ddb93d9cf5a6e0d27ab8da0a72a1ee'
          '62c10dcdac4b69cc8c6bb19f73db40c264cb2726'
          '1ff96a2d93027b7581b517f0cbc111c5d2560da9'
          '72766b97a7aed523486e7812f4f76c9cc0a751ef')


build() {
  # Use Qt4
  _rcc_loc=`pkg-config QtCore --variable=rcc_location`
  _qt4dir=${_rcc_loc%%/bin/rcc}
  _qt4i=`pkg-config --cflags-only-I QtGui`
  CFLAGS+=" $_qt4i"
  CXXFLAGS+=" $_qt4i"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  # uim-mozc
  cp -rf "${srcdir}/uim-mozc-${_uimmozcrev}/uim" unix/
  # Extract license part of uim-mozc
  head -n 32 unix/uim/mozc.cc > unix/uim/LICENSE

  # Copy gyp
  cp -rf "${srcdir}"/gyp-${_gyp_rev}/* third_party/gyp
  # Copy protobuf to be linked statically
  cp -rf "${srcdir}/protobuf-${_protobuf_ver}" protobuf/files

  # Adjust to use python2
  find . -name  \*.py        -type f -exec sed -i -e "1s/python.*$/python2/"  {} +
  find . -regex '.*\.gypi?$' -type f -exec sed -i -e "s/'python'/'python2'/g" {} +

  msg "Starting make..."
  # Get make -j option from $MAKEFLAGS
  _jobs=`sed -n -e "s/.*--jobs=\([0-9]\+\).*/\1/p" -e "s/.*-j *\([0-9]\+\).*/\1/p" <<< "$MAKEFLAGS"`
  _jobs=${_jobs:-1}

  QTDIR=$_qt4dir python2 build_mozc.py gyp
  python2 build_mozc.py build_tools -c $_bldtype -j $_jobs
  python2 build_mozc.py build -c $_bldtype -j $_jobs unix/uim/uim.gyp:uim-mozc
}

package() {
  depends=("mozc=${pkgver}" 'uim')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -D -m 755 out_linux/${_bldtype}/libuim-mozc.so  "${pkgdir}/usr/lib/uim/plugin/libuim-mozc.so"
  install -d "${pkgdir}/usr/share/uim"
  install    -m 644 ${srcdir}/uim-mozc-${_uimmozcrev}/scm/*.scm       "${pkgdir}/usr/share/uim/"
  install -D -m 644 data/images/unix/ime_product_icon_opensource-32.png "${pkgdir}/usr/share/uim/pixmaps/mozc.png"
  install    -m 644 data/images/unix/ui-tool.png       "${pkgdir}/usr/share/uim/pixmaps/mozc_tool_selector.png"
  install    -m 644 data/images/unix/ui-properties.png "${pkgdir}/usr/share/uim/pixmaps/mozc_tool_config_dialog.png"
  install    -m 644 data/images/unix/ui-dictionary.png "${pkgdir}/usr/share/uim/pixmaps/mozc_tool_dictionary_tool.png"

  install -D -m 644 unix/uim/LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/uim-mozc/LICENSE"
}
