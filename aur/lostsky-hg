# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: megadriver <megadriver at gmx dot com>

pkgname=lostsky-hg
pkgver=1146
pkgrel=1
pkgdesc="Strategy RPG set in the Touhou universe"
arch=(any)
url="http://lostsky.featheredmelody.com/"
license=('BSD')
depends=('python2-pygame' 'python2-xdg')
makedepends=('mercurial')
options=(!emptydirs)
source=('hg+https://bitbucket.org/featheredmelody/lost-sky-project' 'lostsky.desktop' 'lostsky.png')
md5sums=('SKIP'
         '8a94cecf6a377770bfdecce4b4d3715b'
         '0c7493aad44dfd09dacca835ea6e3b6a')

pkgver() {
  cd "lost-sky-project"
  hg identify -n
}

build() {
  cd "lost-sky-project/Story of a Lost Sky"
  python2 setup.py build
}

package() {
  cd "lost-sky-project/Story of a Lost Sky"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p $pkgdir/usr/share/{lostsky,doc/lostsky,licenses/lostsky}
  mv "$pkgdir/usr/bin/srpg.py" "$pkgdir/usr/bin/lostsky"
  mv bsd_license.txt $pkgdir/usr/share/licenses/lostsky
  mv *.txt $pkgdir/usr/share/doc/lostsky
  install -D -m755 $srcdir/lostsky.desktop $pkgdir/usr/share/applications/lostsky.desktop
  install -D -m644 $srcdir/lostsky.png $pkgdir/usr/share/icons/lostsky.png
}
