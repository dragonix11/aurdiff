# Maintainer: SaultDon <sault.don gmail>
pkgname=whitebox
pkgver=3.0.5
pkgrel=2
_pkgname=Whitebox_${pkgver//./_}
pkgdesc="A GIS intended for advanced geospatial analysis and data visualization in research and education settings. Runs on JAVA."
arch=('any')
url="http://www.uoguelph.ca/~hydrogeo/Whitebox/index.html"
license=('GPL3')
depends=('java-environment' 'bash')
provides=("$pkgname=$pkgver")
conflicts=("$pkgname-svn")
install=$pkgname.install
changelog=$pkgname.changelog
source=("$pkgname-$pkgver.zip::https://whitebox-geospatial-analysis-tools.googlecode.com/files/$_pkgname.zip"
	"$pkgname.sh"
	"$pkgname.desktop"
	'gpl-3.0.txt')
noextract=($pkgname-$pkgver.zip)
sha1sums=('4d19a6082e5a9a04b7b350bdf62102e4d659dac9' #https://code.google.com/p/whitebox-geospatial-analysis-tools/downloads/detail?name=Whitebox_3_0_4.zip&can=2&q=
	  '1b12747ee6959a645aa5e7fa2b00c725fc9484c6'
          'fd3f308e69ce14ff62f7365a09bcf76e02b86be9'
          '8624bcdae55baeef00cd11d5dfcfa60f68710a02')

package(){

	msg2 "Installing license..."
	install -Dm644 $srcdir/gpl-3.0.txt \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	msg2 "Installing desktop entry..."
	install -Dm644 $srcdir/$pkgname.desktop \
	  "$pkgdir/usr/share/applications/$pkgname.desktop"

	msg2 "Installing package..."
	install -d $pkgdir/opt

	install -Dm755 $srcdir/$pkgname.sh \
	  "$pkgdir/opt/$pkgname/$pkgname.sh"

	bsdtar --no-same-owner \
	       -s /$_pkgname/$pkgname/ \
	       --exclude .DS_Store \
	       --directory $pkgdir/opt \
	       -xf $pkgname-$pkgver.zip $_pkgname

        msg2 "Installing icon..."
        install -Dm644 $pkgdir/opt/$pkgname/resources/Images/wbGAT.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
	
	msg2 "Making required folders/files writeable..."
	find $pkgdir/opt/$pkgname/resources -exec chmod a+w '{}' \;
	find $pkgdir/opt/$pkgname/resources -type f -name '*.jar' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname/resources -type f -name '*.java' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname/resources/Images -exec chmod go-w '{}' \;
}
