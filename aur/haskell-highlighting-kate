# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Contributor: Lex Black <autumn-wind at web dot de>

_hkgname=highlighting-kate
pkgname=haskell-highlighting-kate
pkgver=0.5.5
pkgrel=1
pkgdesc="Syntax highlighting"
url="http://hackage.haskell.org/package/$_hkgname"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-blaze-html>=0.4.2' 'haskell-containers' 'haskell-filepath' 'haskell-mtl' 'haskell-parsec' 'haskell-pcre-light')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=$pkgname.install
md5sums=('7c11a837292cc6f2d12b5bcb389bce48')

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
}
