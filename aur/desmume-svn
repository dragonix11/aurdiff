#! /bin/bash
# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Jorge Barroso <jorge.barroso.11 at gmail dot com>

pkgname=desmume-svn
pkgver=0.9.10+4721
pkgrel=1
pkgdesc='Nintendo DS emulator, svn version'
url='http://desmume.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('agg' 'gtkglext' 'libglade' 'libpcap' 'mesa' 'openal' 'sdl' 'soundtouch')
makedepends=('autoconf' 'automake' 'intltool' 'pkg-config' 'subversion')
conflicts=('desmume' 'desmume-cvs')
provides=('desmume')
source=(svn://svn.code.sf.net/p/desmume/code/trunk
        enable-jit.patch)
sha512sums=('SKIP'
            'f4d601f79b004aec502156fe500fc04dbdde489afc56251ee298694142f72629ef77bfdccd58df408fa6e45ee2da4e4707c6c13c08a13eb778c198f010b0fb4c')

pkgver() {
  cd trunk/desmume
  echo $(sed -nr '/define DESMUME_VERSION_STRING/s/.*"([0-9]+\.[^"]+)".*/\1/p' src/version.cpp)+$(svnversion)
}

prepare () {
  cd $srcdir
  mv -v ./trunk/* ./
  rm -r ./trunk
  cd desmume
  patch -Np0 -i ../../enable-jit.patch
}

build() {
  cd desmume
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-wifi \
    --enable-openal \
    --enable-osmesa \
    --enable-hud \
    --enable-glade
  make
}

package() {
  cd desmume
  make DESTDIR="$pkgdir" install
}
