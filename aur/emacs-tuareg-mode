# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=emacs-tuareg-mode
pkgver=2.0.6
pkgrel=2
pkgdesc="Emacs tuareg mode for ocaml"
arch=(any)
url=https://forge.ocamlcore.org/projects/tuareg/
license=(GPL2)
depends=(emacs)
DLAGENTS=('https::/usr/bin/curl -fkLC - --retry 3 --retry-delay 3 -o %o %u')
install=$pkgname.install
source=(https://forge.ocamlcore.org/frs/download.php/882/tuareg-$pkgver.tar.gz)
sha256sums=('ea79ac24623b82ab8047345f8504abca557a537e639d16ce1ac3e5b27f5b1189')
sha512sums=('a7162daea30baa38bf1673b62ac545936a24b87b5e4d65329304ce5da68e151af1ef9f6e7720067f1f741614b3b6e1dfeab7080c4e9e7ebf14af594f3a0b0d46')

build() {
    make -C tuareg-$pkgver
}

package() {
    cd tuareg-$pkgver/
    for i in *.el{,c}; do
        install -Dm644 $i "$pkgdir"/usr/share/emacs/site-lisp/tuareg/$i
    done
    install -Dm644 README "$pkgdir"/usr/share/doc/tuareg/README
}
