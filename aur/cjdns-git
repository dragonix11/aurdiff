# Maintainer: Prurigro
# Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.1618
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use."
url="https://github.com/cjdelisle/${_pkgname}"
license=('GPL3')
makedepends=('git')
optdepends=('cmake: speed up the build process by skipping the need to compile cmake'
            'libnacl: speed up the build process by skipping the need to compile cnacl'
            'cjdscript: provides the collection of working scripts included with cjdns'
            'cjdnsify: allows you to run programs bound exclusively to your cjdns connection'
            'cjdns-git-sysvinit: support for sysvinit style init (rc.d)')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
backup=("etc/default/cjdns")
install=${pkgname}.install

source=("git://github.com/cjdelisle/${_pkgname}.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

prepare() {
    cd "${srcdir}/${_pkgname}"

    ## Clean previous builds and resync
    ./clean
}

build() {
    cd "${srcdir}"/${_pkgname}

    ##  Disable Arch's generic makepkg optimizations (set via /etc/makepkg.conf) in favour of those specified by cjdns
    unset MAKEFLAGS
    unset CFLAGS
    unset CPPFLAGS

    ##  Build using the ./do script
    NO_DEBUG=1 ./do
}

## Package creation helper functions
_package_cjdns() {
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/cjdroute2 "${pkgdir}"/usr/bin/cjdroute
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/systemd/${_pkgname}.service "${pkgdir}"/usr/lib/systemd/system/${_pkgname}.service
}
_package_scripts() {
    install -D -m755 "${srcdir}"/${_pkgname}/scripts/${_pkgname}.sh "${pkgdir}"/usr/bin/${_pkgname}.sh
    install -D -m755 "${srcdir}"/${_pkgname}/scripts/${_pkgname} "${pkgdir}"/etc/default/${_pkgname}
}

## Package creation function: comment out a helper function to remove its associated functionality
package() {
    _package_cjdns ## Core binaries (cjdns and cjdroute) and its systemd service file
    _package_scripts ## The cjdns.sh run script and its config located @ /etc/default/cjdns
}
