# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=tuxfootball-git
pkgver=113.9ef800e
pkgrel=1
pkgdesc="Arcade-style 2D football game very reminiscent of Sensible Soccer and Kick Off."
url="http://tuxfootball.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('hicolor-icon-theme' 'sdl2_image' 'sdl2_mixer')
makedepends=('git')
conflicts=('tuxfootball')
source=('git+git://git.code.sf.net/p/tuxfootball/code')
md5sums=('SKIP')

prepare() {
  cd $srcdir/code
  libtoolize --force
  aclocal
  autoheader
  automake --add-missing
  autoconf
  ./configure --prefix=/usr
}

build() {
  cd $srcdir/code
  make
}

package() {
  cd $srcdir/code
  make DESTDIR=$pkgdir install
  
  # Full screen mode(-w for windowed mode)
  sed -i "s|Exec=tuxfootball|Exec=tuxfootball -f|" $pkgdir/usr/share/applications/tuxfootball.desktop
}

pkgver() {
  cd $srcdir/code
  echo $(git rev-list --count master).$(git rev-parse --short master)
}