# Maintainer: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20120410
pkgrel=2
pkgdesc="Static library for handling DWARF Debugging Information Format"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://reality.sgiweb.org/davea/dwarf.html"
depends=('elfutils' 'gcc-libs')
makedepends=('libelf')
source=(https://dl.dropboxusercontent.com/u/55625711/libdwarf/$pkgname-$pkgver.tar.gz)

build() {
  cd $srcdir/dwarf-$pkgver/libdwarf
  ./configure --enable-shared
  make

  # dwarfdump
  cd $srcdir/dwarf-$pkgver/dwarfdump2
  ./configure --prefix=/usr CFLAGS=-I../libdwarf CXXFLAGS=-I../libdwarf LDFLAGS=-L../libdwarf
  # set LD_LIBRARY_PATH to work around build failure, see dwarfdump2/README
  # disable parallelization; -j5 build fails ~20% of the time on my machine
  LD_LIBRARY_PATH=$srcdir/dwarf-$pkgver/libdwarf make -j1
}

package() {
  cd $srcdir/dwarf-$pkgver/libdwarf

  install -d $pkgdir/usr/lib
  install -m 644 libdwarf.a $pkgdir/usr/lib
  install -m 644 libdwarf.so $pkgdir/usr/lib

  install -d $pkgdir/usr/include/libdwarf
  install dwarf.h libdwarf.h $pkgdir/usr/include/libdwarf

  install -dm 755 $pkgdir/usr/share/doc/$pkgname
  install README NEWS *.pdf $pkgdir/usr/share/doc/$pkgname

  # dwarfdump
  cd $srcdir/dwarf-$pkgver/dwarfdump2
  install -Dm755 dwarfdump $pkgdir/usr/bin/dwarfdump
  install -Dm644 dwarfdump.1 $pkgdir/usr/share/man/man1/dwarfdump.1
  install -Dm644 dwarfdump.conf $pkgdir/usr/lib/dwarfdump.conf
}
md5sums=('77c8b351f11738bc9fa50474a69d5b36')
