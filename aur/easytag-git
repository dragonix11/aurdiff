## Maintainer: rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
## 
## enable/disable gnome help files; adds makedepends+='yelp-tools'
_build_gnome_help="no" # yes|no

##
## list of locales to build see also: po/LINGUAS
## bg ca cs da de el es fr he hu it ja lt nl pl pt_BR ro ru sl sr sr@latin sv tr uk zh_CN zh_TW
##
## eg. export LINGUAS="bg ru"
##
#export LINGUAS="ru" # uncomment to enable

pkgname=easytag-git
_gitname=easytag
pkgver=714.d932d03
pkgrel=1
pkgdesc="Utility for viewing and editing tags for most audio formats - git version"
arch=('i686' 'x86_64')
license=('GPL')
url='http://projects.gnome.org/easytag/'
depends=('id3lib' 'taglib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'speex' 'wavpack' 'desktop-file-utils' 'hicolor-icon-theme')
## depends on buiding gnome help files build or not
makedepends=('git' 'intltool')
if [ "$_build_gnome_help" = 'yes' ]; then
echo -e "\n>>>\n>>> Build with GNOME help files\n>>>\n"
makedepends+=('yelp-tools')
fi

provides=('easytag')
conflicts=('easytag')
install="$pkgname.install"
source=("git://git.gnome.org/easytag" 
        'easytag-2.1.8-fix_mime_directory_association.patch'
        "$pkgname.install")
md5sums=('SKIP'
         '6707b9212fe95bff079d79050f6efbfa'
         'a2583875aa894ac884a27531480fb28a')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
#  git describe --always | sed -e 's|-|.|g'
#  git describe --always | sed 's/^easytag.//;s/-/./g'
}

prepare() {
  cd "$_gitname"
  ## Patch from mainline package 
  patch -p0 -i "${srcdir}/easytag-2.1.8-fix_mime_directory_association.patch"
  ##
  ## disable-gnome-help-files
  ##
  if [ "$_build_gnome_help" != 'yes' ]; then
    echo -e "\n>>>\n>>> Build without GNOME help files\n>>>\n"
	sed -i 's@= help po@= po@' Makefile.am
	sed -i '/help/d' configure.ac
	rm -fr help/
  fi
  ./autogen.sh --prefix=/usr #--disable-nls
}

build() {
  cd "$_gitname"
#  ./configure --prefix=/usr #--disable-nls
  make
}

check() {
  cd "$_gitname"
  make -k check
}
package() {
  cd "$_gitname"
  make DESTDIR="${pkgdir}" install
  rm -fr "${pkgdir}"/usr/share/icons/hicolor/256x256/ \
		"${pkgdir}"/usr/share/icons/hicolor/128x128/
}
