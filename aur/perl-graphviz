# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: antoniolucas <antoniolucasnobar at gmail dot com>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

_perlmod=GraphViz
pkgname=perl-graphviz
pkgver=2.14
pkgrel=1
pkgdesc="GraphViz - Interface to AT&T's GraphViz. Deprecated. See GraphViz2"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('graphviz'
         'perl-file-which>=1.09'
         'perl-ipc-run>=0.6'
         'perl-parse-recdescent>=1.965001'
         'perl-xml-twig>=3.38'
         'perl-xml-xpath>=1.13')
makedepends=('perl-module-build>=0.3800' 'perl-test-pod>=1.44')
source=("http://cpan.perl.org/modules/by-module/$_perlmod/$_perlmod-${pkgver}.tgz")
md5sums=('c401c26a2695d82f3cb1878bbef2d7b1')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	perl Build.PL installdirs=vendor destdir="$pkgdir/"
	perl Build
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build install
}
