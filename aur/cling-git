# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
# based on cling-svn
# Contributor: Daniel Micay <danielmicay@gmail.com>
pkgname=cling-git
pkgver=0.0.977.faac165
pkgrel=1
pkgdesc="Interactive C++ interpreter and JIT compiler"
arch=('i686' 'x86_64')
url="http://root.cern.ch/drupal/content/cling"
license=('custom')
#license=('custom:University of Illinois/NCSA Open Source License')
depends=('gcc-libs' 'libffi' 'python2' 'gcc' 'clang=3.3')
makedepends=('git' 'svn' 'curl')
options=(!buildflags)

_clingrepo=git+http://root.cern.ch/git/cling.git
_llvmrepo=svn+http://llvm.org/svn/llvm-project

msg 'Determining with which LLVM SVN revision to build cling'
_llvmrevision=$(curl 'http://root.cern.ch/gitweb?p=cling.git;a=blob_plain;f=LastKnownGoodLLVMSVNRevision.txt')
msg2 "LLVM revision: $_llvmrevision"

source=(cling::"$_clingrepo"
llvm::"$_llvmrepo/llvm/trunk#revision=$_llvmrevision"
clang::"$_llvmrepo/cfe/trunk#revision=$_llvmrevision"
'enable-lto.patch')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '05f77e1e92ba79ac5e93aa68fd850fc3b1eac970bc5b8e46de91378cdbf74c8f')

pkgver() {
  cd $srcdir/cling
  echo 0.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  # use python2 for the build
  mkdir -p "$srcdir/python2-path"
  ln -sf /usr/bin/python2 "$srcdir/python2-path/python"
  export PATH="$srcdir/python2-path:$PATH"

  cd $srcdir/llvm
  mv $srcdir/clang tools
  mv $srcdir/cling tools

  # Use gold instead of default linker to make -flto work
  patch -d tools/clang -Np0 -i "$srcdir/enable-lto.patch"

  cat tools/cling/patches/*.diff | patch -p0
}

build() {
  cd $srcdir/llvm
  ./configure \
    --prefix=/usr \
    --enable-optimized \
    --disable-assertions \
    --enable-targets=host \
    --with-binutils-include=/usr/include \

  make cling-only
}

package() {
  cd $srcdir/llvm
  make DESTDIR="$pkgdir" install-cling

  rm -r "$pkgdir/usr/lib/clang"
}
