# Contributor: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: Thomas Kinnen <thomas.kinnen@gmail.com>
pkgname=fife-git
provides=("fife" "fife-svn=4091")
conflicts=("fife" "fife-svn")
pkgdesc="The mission of the FIFE project is to create a cross platform game creation framework"
url="http://www.fifengine.net/"
pkgver=0.3.4.170.g681cbec
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
depends=('sdl' 'boost' 'sdl_ttf' 'sdl_image' 'libvorbis' 'libogg' 'openal' 'guichan' 'swig' 'python2' 'zlib' 'libgl' 'libpng' 'tinyxml' 'fifechan-git')
makedepends=('scons' 'git')
source=(git+https://github.com/fifengine/fifengine.git)
md5sums=('SKIP')

pkgver() {
  cd $srcdir/fifengine
  local ver="$(git describe --tags --long)"
  printf "%s" "${ver//-/.}"
}

build() {
  cd $srcdir/fifengine
  scons
}

package() {
  cd $srcdir/fifengine
  scons install-all --prefix $pkgdir/usr --python-prefix $pkgdir/usr/lib/python2.7/site-packages/
}

