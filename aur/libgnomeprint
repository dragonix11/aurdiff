# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomeprint
pkgver=2.18.8
pkgrel=6
pkgdesc='Printing routines for GNOME'
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
depends=('pango' 'libart-lgpl' 'libxml2' 'libgnomecups' 'libgcrypt' 'bzip2')
makedepends=('intltool' 'pkgconfig' 'addinclude' 'bison27')
replaces=('libgnomeprint-cups')
conflicts=('libgnomeprint-cups')
url='http://www.gnome.org/'
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/2.18/$pkgname-$pkgver.tar.bz2")
sha256sums=('1034ec8651051f84d2424e7a1da61c530422cc20ce5b2d9e107e1e46778d9691')

build() {
  cd "$pkgname-$pkgver"

  addinclude "$pkgname/modules/cups/gnome-print-cups-transport.c" stdio
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
