# Contributor: saik0
# Maintainer: Ben R <thebenj88 *AT* gmail *DOT* com>
pkgname=boson-x
pkgver=1.0.1
_pkgver=${pkgver//\./_} # replace all '.' with '_'
pkgrel=3
pkgdesc="DRM-free rotational running game"
arch=('x86_64')
url="http://www.boson-x.com/"
license=('Custom')
source=("http://downloads.muandheyo.com/BosonX_v${_pkgver}_Linux.zip"
		'bosonx.sh'
		"${pkgname}.desktop")
depends=('glibc' 'libgl')
sha1sums=('ddf62263730cdac3289606d38d2f2f072ba5a15c'
          'a2ad55adce9bef502c0fcb83f1da7646d8822099'
          '97dadb126a3bf8a6868e3e398952997e31537767')

package() {
  cd "${srcdir}/BosonX_v${_pkgver}_Linux/"

  # Executable
  install -Dm755 bosonx \
  	"${pkgdir}/usr/share/${pkgname}/bosonx"

  # Desktop File
  install -Dm755 "${srcdir}/${pkgname}.desktop" \
	"${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Icon
  install -Dm644 data/bosonx_symbol.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

  # Game data
  install -d "${pkgdir}/usr/share/${pkgname}/"
  cp -rd --no-preserve=ownership data/ "${pkgdir}/usr/share/${pkgname}/data"

  # bosonx executable wants to be in the same directory as 'data/'
  # Symbolic links aren't currently working.
  install -Dm755 ${srcdir}/bosonx.sh "${pkgdir}/usr/bin/${pkgname}"
  

  # License
  install -Dm644 LICENSES.txt \
  	"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
