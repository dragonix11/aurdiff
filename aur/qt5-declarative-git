# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-declarative-git
pkgver=v5.1.1.2169.g87ccf53
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtQml, QtQuick)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-xmlpatterns' 'qt5-jsbackend' 'mesa')
makedepends=('git')
provides=('qt5-declarative')
conflicts=('qt5-declarative')
source=("git://gitorious.org/qt/qtdeclarative.git#branch=dev"
        'use-python2.patch')
md5sums=('SKIP'
         'd2897bedd03bc05f8aa110abc0b06840')
_gitname='qtdeclarative'

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

prepare() {
	cd "$srcdir/$_gitname"
	patch -p1 -i "$srcdir/use-python2.patch"
}

build() {
	cd "$srcdir/$_gitname"
	qmake
	make -j8
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
}
