# Maintainer: Sam S. <smls75@gmail.com>

pkgname=trine2-hib
pkgver=0_20130911
pkgrel=1
pkgdesc='Trine 2: Complete Story - a fantasy action-puzzle platformer (Humble Bundle version)'
url='http://www.trine2.com/'
arch=('i686' 'x86_64')
license=('custom: commercial')
if [ "$CARCH" == 'i686' ]; then
  depends=('glu' 'nvidia-cg-toolkit' 'gtk2' 'libpng12'
           'openal' 'alsa-lib' 'libogg'  'libvorbis xorg-xwininfo')
else
  depends=('lib32-glu' 'lib32-nvidia-cg-toolkit' 'lib32-gtk2' 'lib32-libpng12'
           'lib32-openal' 'lib32-alsa-lib' 'lib32-libogg'  'lib32-libvorbis'
           'xorg-xwininfo')
fi
conflicts=('trine2')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

source=('hib://trine2_linux.tar.gz'
        "$pkgname.launcher"
        "$pkgname.desktop")
md5sums=('783cde05d588435a352aaca1daa29f9e'
         '5166c155066bc0b1dccc68830030d11c'
         '80509ff6b10602acb2beeaa41a4847ec')

package() {
  cd "$srcdir"
  _target=/opt/Trine2
  
  # Install game files
  mkdir -p "$pkgdir"/$_target
  cp -TRl trine2_linux "$pkgdir"/$_target
  
  # Remove unneeded files
  rm "$pkgdir"/$_target/lib/lib32/libCg*
  
  # Install icon & desktop entry
  install -Dm644 trine2_linux/trine2.png \
                 "$pkgdir"/usr/share/pixmaps/trine2.png
  install -Dm644 $pkgname.desktop \
                 "$pkgdir"/usr/share/applications/$pkgname.desktop
  
  # Install launch script
  install -Dm755 $pkgname.launcher "$pkgdir"/usr/bin/trine2
  sed -i -e "s|__INSTALLDIR__|$_target|g" "$pkgdir"/usr/bin/trine2
}
