# Maintainer: Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>

pkgname=lazpaint-gtk2
_pkgname=lazpaint
pkgver=5.6
_inetver=0.6.6
_bgrabitmapver=7.2
_bgracontrolsver=2.2
pkgrel=1
pkgdesc='Image editor, like PaintBrush or Paint.Net, written in Lazarus (Free Pascal).'
arch=(i686 x86_64)
url='http://sourceforge.net/projects/lazpaint/'
license=(GPL3 LGPL)
depends=(gtk2 desktop-file-utils)
makedepends=(lazarus unzip)
provides=(${_pkgname})
conflicts=(${_pkgname}-qt4)
install=${_pkgname}.install
source=(http://downloads.sourceforge.net/project/${_pkgname}/src/${_pkgname}${pkgver}_src.zip
        http://downloads.sourceforge.net/project/${_pkgname}/src/inet${_inetver}.zip
        http://downloads.sourceforge.net/project/${_pkgname}/src/bgrabitmap${_bgrabitmapver}.zip
        http://downloads.sourceforge.net/project/bgracontrols/bgracontrols-${_bgracontrolsver}.zip
        ${_pkgname}.desktop
        ${_pkgname}.png
        language_path_fix.patch
        constref_update.patch)
md5sums=(3c8805ccf641e31a04d0124cc35adc29
         63e9ae30886fdd3ee309a12bdc0ff613
         29e496ae174d3b7f09802fea523468bb
         c34b93ddb087ca8e647447dce6a8176e
         e6f4f8c4d9456b21ed4a25d7b775fec4
         8f26062ad1e3222d375201d96341716c
         4938510815fa494acdf9104603c237a2
         d05b17d67506853ff1ec0d93803661da)
_ws=gtk2

prepare() {
  cd ${srcdir}

  patch -p0 < language_path_fix.patch
  patch -p0 < constref_update.patch

  mkdir config
}

build() {
  cd ${srcdir}/inet${_inetver}/lazaruspackage
  lazbuild lnetvisual.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/bgrabitmap${_bgrabitmapver}
  lazbuild bgrabitmappack.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/bgracontrols-${_bgracontrolsver}
  lazbuild bgracontrols.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/${_pkgname}${pkgver}
  lazbuild ${_pkgname}.lpi --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
}

package() {
  cd ${srcdir}/${_pkgname}${pkgver}/release

  install -D -m 755 ${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -d -m 755 ${pkgdir}/usr/share/${_pkgname}/i18n
  install -D -m 644 i18n/*.po ${pkgdir}/usr/share/${_pkgname}/i18n

  install -D -m 644 ${srcdir}/${_pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${_pkgname}.desktop
  install -D -m 644 ${srcdir}/${_pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${_pkgname}.png
}
