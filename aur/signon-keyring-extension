# Maintainer:
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-keyring-extension
_pkgname=keyring
pkgver=0.4
pkgrel=1
pkgdesc='GNOME keyring extension for signond'
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-keyring-extension"
license=('LGPL')
depends=('signon' 'libgnome-keyring')
source=(https://launchpad.net/online-accounts-keyring-extension/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.bz2)
md5sums=('4bbe5840cf0d5cfd142ba5ef262a38ed')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Do not install tests
  echo 'INSTALLS =' >>tests/tests.pro
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
