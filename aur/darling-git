# Maintainer: X0rg

pkgname=darling-git
_gitname=darling
pkgver=594.6407eba
pkgrel=8
pkgdesc="A Wine-like emulation layer that enables you to run Darwin/OS X apps on Linux"
arch=('any')
url="http://darling.dolezel.info/en/Darling"
license=('GPL3')
depends=('cairo' 'libbsd' 'systemd' 'lcms' 'bzip2' 'util-linux' 'ncurses' 'libxrandr' 'ffmpeg' 'libkqueue' 'gcc-libs' 'gnustep-gui-clang-svn' 'gnustep-corebase-clang-svn')
makedepends=('git' 'clang>=3.1' 'nasm' 'yasm')
optdepends=('dmg2dir-git: extract OS X application from .dmg file')
backup=(etc/darling/dylib.conf)
install=darling.install
options=('!buildflags')
source=('git://github.com/LubosD/darling.git'
	'unistd.h')
md5sums=('SKIP'
	'a7e3d23ac8b3fc0d930af1a66f312338')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_gitname"
  msg2 "Clone submodules..."
  git submodule init
  git submodule update

  msg2 "Make 'build' directory..."
  mkdir -pv ./build

  msg2 "Move patched file 'unistd.h' in '$srcdir/$_gitname/include' directory..."
  cp -v "$srcdir/unistd.h" "./include"
}
 
build() {
  cd "$_gitname/build"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"

  msg2 "Run 'cmake'..."
  CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" cmake .. -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev

  msg2 "Run 'make'..."
  make
}

package() {
  cd "$_gitname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install

  msg2 "Make new executables..."
  mkdir -pv "$pkgdir/usr/share/$pkgname"
  mv -v "$pkgdir/usr/bin/"* "$pkgdir/usr/share/$pkgname"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/dyld \$@"	> "$pkgdir/usr/bin/dyld"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/dyldd \$@"	> "$pkgdir/usr/bin/dyldd"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/motool \$@"	> "$pkgdir/usr/bin/motool"
  chmod -v --reference="$pkgdir/usr/share/$pkgname/dyld" "$pkgdir/usr/bin/"*
}
