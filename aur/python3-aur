#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-aur
pkgver=2013.10
pkgrel=1
pkgdesc='AUR-related modules and helper utilities (aurploader, aurquery, aurtomatic).'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/python3-aur"
depends=(python3 python-xdg)
optdepends=('curl: Aurploader support')
replaces=(aurploader)
conflicts=(aurploader)
provides=(aurploader)
source=(
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2013.10.tar.xz
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2013.10.tar.xz.sig
)
sha512sums=(
  eba15deeb52375607004a5d0d93acae08d78ecf8337fdbb4c8605dd1d88430efe865b25fa7f711c6b3cf282861b6d5e1f872f7f9b3c1e56df3cb91ba46b2809f
  8fe026156d2c9d128080d3b2a30cdd237b7daccae822961e37c93b0a66ebae565f6344f17feb2c8204bc939baa69bccc2116aeac5d81429ce744134d5645ba56
)
md5sums=(
  1d269ed6290ba888bad86f0eb28bdac9
  f5ca41837980ad6da860f205170b98ac
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  for aurfoo in aur*
  do
    if [[ -x "$aurfoo" ]]
    then
      install -Dm755 "$aurfoo" "$pkgdir/usr/bin/$aurfoo"
    fi
  done
}


# vim: set ts=2 sw=2 et:
