# Maintainer: Jan Dolinar <dolik.rce@gmail.com>
# Contributor: Abakus <java5 at arcor dot de>
pkgname=debootstrap
pkgver=1.0.53~bpo70+1
pkgrel=1
pkgdesc="A tool used to create a Debian base system from scratch, without requiring the availability of dpkg or apt"
arch=('any')
license=('GPL')
url="http://packages.qa.debian.org/d/debootstrap.html"
depends=('wget')
optdepends=( 'debian-archive-keyring: checking Debian release signatures'
             'gnupg1:  checking Debian release signatures' )
source=("http://ftp.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_${pkgver}_all.deb") 
install="debootstrap.install"
md5sums=('965a41f24cdb6c21bc86ed45180b92cc')

package(){
  tar -xzf "${srcdir}/data.tar.gz" -C "${pkgdir}/"
  # move the executables to /usr/bin
  mkdir -p ${pkgdir}/usr/bin
  mv "${pkgdir}/usr/sbin"/* "${pkgdir}/usr/bin/"
  rmdir "${pkgdir}/usr/sbin"
  # patch the path to use Debian defaults (prevents error in chrooted environment) 
  sed -i 's|export PATH|export PATH="/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin:/opt/java/jre/bin:/usr/bin/vendor_perl:/usr/bin/core_perl"|' ${pkgdir}/usr/bin/debootstrap
  # doesn't work with gpg 2.x, patch to point to gpg1v
  sed 's/gpgv/gpg1v/g;' -i "${pkgdir}/usr/bin/debootstrap" "${pkgdir}/usr/share/debootstrap/functions"
}
