# Maintainer: willemw <willemw12@gmail.com>

pkgname=webilder-gtk-patched
pkgver=0.7.2
pkgrel=3
pkgdesc="Deliver stunning wallpapers directly from Flickr and Webshots. Gtk-only patched version"
url="http://www.webilder.org"
arch=('any')
depends=('desktop-file-utils' 'python2-pillow')
makedepends=('python2-setuptools')
license=('BSD')
provides=('webilder')
conflicts=('webilder-applet' 'webilder-gnome')
install=$pkgname.install
source=(http://www.webilder.org/static/downloads/Webilder-$pkgver.tar.gz
        setup.py)
md5sums=('78fae36776820380dec71583f0544bba'
         'ba51145af9f3207ec131fb681f9f6046')

prepare() {
  # Patch setup script
  cp setup.py webilder-$pkgver

  # Patch Pillow
  cd webilder-$pkgver
  sed -i "s|^import Image$|from PIL import Image|" src/webilder/flickr/__init__.py
}

package() {
  cd webilder-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

