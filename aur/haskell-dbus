# Maintainer: Jochen Keil <jochen dot keil at gmail dot com>
_hkgname=dbus
pkgname=haskell-dbus
pkgver=0.10.5
pkgrel=1
pkgdesc="A client library for the D-Bus IPC system."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL-3')
arch=('i686' 'x86_64')
makedepends=()
conflicts=('haskell-dbus-core')
replaces=('haskell-dbus-core')
depends=('ghc' 'haskell-cereal<0.5' 'haskell-libxml-sax<0.8'
'haskell-network>=2.3.0.2' 'haskell-parsec>=3.1.1' 'haskell-random>=1.0.0.3'
'haskell-text' 'haskell-transformers>=0.2.2.0' 'haskell-vector'
'haskell-xml-types<0.4')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('779c61a4c25c1299f4e8e4596297b120')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
