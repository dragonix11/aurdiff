# Maintainer: Chun Yang <x@cyang.info>
pkgname=rstudio-server
_pkgname=rstudio
pkgver=0.98.309
pkgrel=1
pkgdesc="An integrated development environment (IDE) for R (server version)"
url="http://www.rstudio.com/"
license=('AGPL3')
arch=('i686' 'x86_64')
depends=('r>=2.11.1' 'boost-libs' 'gwt' 'hunspell' 'mathjax' 'openssl>=0.9.8' 'pam')
makedepends=('boost' 'cmake>=2.6' 'fakeroot' 'java-environment' 'google-guice' 'google-gin')
backup=(etc/pam.d/rstudio)
install=rstudio-server.install
source=(https://github.com/rstudio/rstudio/archive/v${pkgver}.tar.gz
        rstudio-server.install
        rstudio-server.service
        arch-build.patch)
noextract=()
md5sums=('66c4be4d25014ed078d9780ebd77c1b1'
         'afab28f5888ec78af93349c182993fa0'
         'ef7f96f0f8fbbd829646e46d96db1f6a'
         '3c4c4ff53948766fc65e03d34a6bf030')

prepare() {
    cd "$srcdir/$_pkgname-$pkgver/"
    patch -p1 -i "$srcdir/arch-build.patch"
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"

    mkdir -p build
    cd build

    cmake .. -DRSTUDIO_TARGET=Server -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio-server

    make
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/build"

	make DESTDIR="$pkgdir" install

    # Create config directory
    install -dm755 "$pkgdir/etc/rstudio"

    # Create var directories
    install -dm755 "$pkgdir/var/run/rstudio-server"
    install -dm755 "$pkgdir/var/lock/rstudio-server"
    install -dm755 "$pkgdir/var/log/rstudio-server"
    install -dm755 "$pkgdir/var/lib/rstudio-server"
    install -dm755 "$pkgdir/var/lib/rstudio-server/conf"
    install -dm755 "$pkgdir/var/lib/rstudio-server/body"
    install -dm755 "$pkgdir/var/lib/rstudio-server/proxy"

    # PAM config
    install -dm755 "$pkgdir/etc/pam.d"
    install -Dm644 "$pkgdir/usr/lib/$pkgname/extras/pam/rstudio" "$pkgdir/etc/pam.d/rstudio"

    # Systemd script
    install -Dm644 "$srcdir/rstudio-server.service" "${pkgdir}/usr/lib/systemd/system/rstudio-server.service"

    # Create symlink for admin script
    install -dm755 "$pkgdir/usr/bin"
    ln -sv "/usr/lib/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
