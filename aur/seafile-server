
# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seafile-server
pkgver=2.0.3
pkgrel=1
pkgdesc="Seafile is an open-source Dropbox replacement. (Server components, without seahub)"
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/haiwen/seafile/"
license=('GPL3')
depends=('python2-mako' 'python2-webpy' 'python2-simplejson' 'python2-imaging' 'python2-chardet' 'python2-pip' 'python2-virtualenv' 'gunicorn' 'python2-djblets' 'libevhtp' 'seafile-shared>=2.0.7')
makedepends=('vala')
optdepends=('python2: for seahub upgrade scripts')
provides=()
conflicts=('django' 'django-rest-framework', 'seafile')
options=('!libtool' '!emptydirs')
install=seafile.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}-server.tar.gz"
        "seafile-admin.patch"
        "seafile.install")
md5sums=('21845c947f96cbebab09f749adaeb922'
         '32dbcdd5f2273643be49c5c65389f0b2'
         'a25c00bee5502689ed5d7b0e2379401b')
sha256sums=('ec2391db1e6e270719aba6ce181a9e548bb5a4d5f8653401f41362c33854e938'
            '0d9578b85c00f3087a19f227b29c3fda13e83d093d024c68c1df5575d30d56dc'
            '44889af3e5dd861caa1bb0b0c63ad739856d9ac22b327b2c2b3d5e5929cf0896')

prepare ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	patch -p1 -i ${srcdir}/seafile-admin.patch
}

build ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	./autogen.sh
	./configure --enable-server --enable-httpserver --disable-client --disable-python --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	# Install library and header files
	cd "${srcdir}/seafile-${pkgver}-server"
	make DESTDIR="${pkgdir}/" install

	# Remove files already installed by seafile-shared
	# to maintain compatibility
	rm -rf "${pkgdir}/usr/lib"
	rm -rf "${pkgdir}/usr/include"

	# Install upgrade scripts
	mkdir -p "${pkgdir}/usr/share/$pkgname/scripts"
	cp -r -p "${srcdir}/seafile-${pkgver}-server/scripts/upgrade" "${pkgdir}/usr/share/$pkgname/scripts/upgrade"
	rm -rf "${pkgdir}/usr/share/$pkgname/scripts/upgrade/win32"
	# Fix upgrade script's  python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/db_update_1.3_1.4.py"

	# Workaround for strange behaviour in the Makefile, which
	# installs python bindings in the package root directory,
	# even though they were disabled
	rm -rf "${pkgdir}/seaserv"
	rm -rf "${pkgdir}/seafile"

	# Install license
	install -D -m644 ${srcdir}/seafile-${pkgver}-server/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}