# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=antimicro-git
pkgver=1.1.5.g2373858
pkgrel=1
pkgdesc="Map keyboard and mouse actions to gamepad buttons, inspired by qjoypad."
arch=('i686' 'x86_64')
url="https://github.com/Ryochan7/antimicro"
license=('GPL3')
depends=('libxtst' 'qt4' 'sdl')
makedepends=('git')
provides=('antimicro')
conflicts=('antimicro')
install=antimicro-git.install

source=("$pkgname"::'git+https://github.com/Ryochan7/antimicro')

md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    local ver="$(git describe)"
    printf "%s" "${ver//-/.}"
}

build() {
    cd "$pkgname"/src
    qmake-qt4 INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$pkgname"/src
    make INSTALL_ROOT="$pkgdir" install
}
