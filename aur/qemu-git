# Maintainer:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Frederic Bezies <fredbezies@gmail.com>

pkgname=qemu-git
_gitname=qemu
pkgver=1.7.r29198.gfc8ead7
pkgrel=1
pkgdesc="A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation. Git version."
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2')
url="http://wiki.qemu.org/Index.html"
depends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
         'gnutls>=2.4.1' 'bluez-libs' 'vde2' 'util-linux' 'curl' 'libsasl'
         'libgl' 'libpulse' 'seabios' 'libcap-ng' 'libaio' 'libseccomp'
         'libiscsi' 'libcacard' 'spice')
makedepends=('git' 'texi2html' 'perl' 'python2' 'dtc' 'spice-protocol')
optdepends=('ovmf-svn: UEFI Firmware (OVMF) with Secure Boot Support - for Virtual Machines (QEMU) ')
conflicts=('qemu' 'kvm' 'kvm-git' 'qemu-spice')
provides=('qemu' 'qemu-kvm' 'qemu-spice')
backup=('etc/qemu/target-x86_64.conf')
source=('git://git.qemu.org/qemu.git'
        '65-kvm.rules'
        'qemu.install')
sha256sums=('SKIP'
            '59b613f570db2bf6dac80cf93df2a356d691241f821c1c3503e012e2b3d6e95c'
            '78ae4c98ab868e6c4e2ba03dd7f1e9ae2f73996758ed19ca99384fbd81d9970c')
install='qemu.install'
options=(!strip)

pkgver() {
	cd "${srcdir}/$_gitname"
	echo "1.7.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

build()
{
	cd $_gitname
	# gtk gui is disabled for now (Keymapping problems)
        ./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list='pa alsa sdl' \
              --python=/usr/bin/python2 --smbd=/usr/bin/smbd \
              --enable-docs --libexecdir=/usr/lib/qemu \
              --disable-gtk --enable-linux-aio --enable-seccomp --localstatedir=/var
              make
}

package() {
 
  cd $_gitname

  make DESTDIR="${pkgdir}" libexecdir="/usr/lib/qemu" install
  # provided by seabios package
  rm "${pkgdir}/usr/share/qemu/bios.bin"
  rm "${pkgdir}/usr/share/qemu/acpi-dsdt.aml"
  rm "${pkgdir}/usr/share/qemu/q35-acpi-dsdt.aml"
  # remove conflicting /var/run directory
  rm -r "${pkgdir}/var"
  install -D -m644 "${srcdir}/65-kvm.rules" \
                   "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"
  # add sample config
  echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample
  # strip scripts directory
    find "${pkgdir}/usr/src/linux-${_kernver}/scripts"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
  # remove libcacard files
  rm -rf ${pkgdir}/usr/include/cacard
  rm -rf ${pkgdir}/usr/lib/libcacard*
  rm -rf ${pkgdir}/usr/lib/pkgconfig/libcacard.pc
  rm -rf ${pkgdir}/usr/bin/vscclient
}



