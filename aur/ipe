# Contributor: Dmitriy Morozov <archlinux@foxcub.org>

pkgname=ipe
_sfproject=ipe7
_dirver=7.1
pkgver=7.1.4
pkgrel=3
pkgdesc="The extensible drawing editor"
url="http://tclab.kaist.ac.kr/ipe/"
depends=('lua' 'qt4' 'freetype2' 'zlib' 'poppler' 'python2')
arch=('i686' 'x86_64')
license=("GPL")
conflicts=('ipe')
ftiversion_=20091205
ptiversion_=20130124
stiversion_=20100608
ipepresenter_version_=f29d75686e95
source=("http://downloads.sourceforge.net/project/$_sfproject/$pkgname/$_dirver/$pkgname-$pkgver-src.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/figtoipe-$ftiversion_.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/pdftoipe-$ptiversion_-src.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/svgtoipe-$stiversion_.tar.gz"
        "http://hg.mrzv.org/IpePresenter/archive/$ipepresenter_version_.tar.gz"
        "ipe.bash-completion"
        "svgtoipe-python2.patch"
        "config.patch"
        "pdftoipe.patch"
        )
md5sums=('8121a5b68fad0cf68beab75a0cc64942'
         'a19e0712df137939c37c194b551da6b8'
         '3e924a19c91f073150d53b088e24ad85'
         'da05b41f9ce7be0f0dbd14c5ffe08e45'
         '07dc9c39181b63c4bc30400c9a1b67e3'
         '694f0d5402655901be385647e5d8d6e3'
         '967a191ca673aec93546640002c794ab'
         '596c22776aa8dfb372c35b91ce19aba9'
         'fb8ab8d9ef949c3f905002a34bf000bc')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/src"
  patch config.mak < "$srcdir/config.patch"

  cd "$srcdir/svgtoipe-$stiversion_"
  patch svgtoipe < "$srcdir/svgtoipe-python2.patch"

  cd "$srcdir/pdftoipe-$ptiversion_-src"
  patch -p1 -i "$srcdir/pdftoipe.patch"
}

build() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  make IPEPREFIX=/usr

  # figtoipe
  cd "$srcdir/figtoipe-$ftiversion_"
  make

  # pdftoipe
  cd "$srcdir/pdftoipe-$ptiversion_-src"
  make

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  CPPFLAGS+=" -I ../$pkgname-$pkgver/src/include -I ../$pkgname-$pkgver/src/ipecanvas" \
    LIBS=" -L ../$pkgname-$pkgver/build/lib" \
    make
}

package() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  INSTALL_ROOT="$pkgdir" make install IPEPREFIX=/usr

  # figtoipe
  cd "$srcdir/figtoipe-$ftiversion_"
  install -m755 figtoipe "$pkgdir/usr/bin"

  # pdftoipe
  cd "$srcdir/pdftoipe-$ptiversion_-src"
  install -m755 pdftoipe "$pkgdir/usr/bin"

  # svgtoipe
  cd "$srcdir/svgtoipe-$stiversion_"
  install -m755 svgtoipe "$pkgdir/usr/bin"

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  install -m755 ipepresenter "$pkgdir/usr/bin"

  # Lua binding
  mkdir -p "$pkgdir/usr/lib/lua/5.1"
  ln -s /usr/lib/libipelua.so "$pkgdir/usr/lib/lua/5.1/ipe.so"

  # Bash completion
  mkdir -p "$pkgdir/etc/bash_completion.d"
  install "$srcdir/ipe.bash-completion" "$pkgdir/etc/bash_completion.d/ipe"
}

# vim: ft=sh syn=sh et ts=2 sw=2
