# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=scratch-text-editor
pkgver=2.0.1
pkgrel=1
pkgdesc='The text editor that works'
arch=('i686' 'x86_64')
url='https://launchpad.net/scratch'
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'gtksourceview3' 'libpeas' 'python2-chardet' 'libzeitgeist')
makedepends=('cmake' 'gobject-introspection' 'pantheon-files' 'vala' 'vte3' 'webkitgtk3')
optdepends=('vala: Outline plugin'
            'vte3: Terminal plugin'
            'webkitgtk3: Browser Preview plugin')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname%%-*}/2.x/${pkgver}/+download/${pkgname%%-*}-${pkgver}.tar.gz")
sha256sums=('002ab65364c0c51e21207fc5e6610be9b869f215df2434cca20b330537ddf242')

prepare() {
  cd ${pkgname%%-*}

  sed -i 's/gtkspell-3.0/gtkspell3-3.0/' plugins/spell/CMakeLists.txt
  sed -i '/add_subdirectory (spell)/d' plugins/CMakeLists.txt
  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname%%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname%%-*}/build

  make DESTDIR="${pkgdir}" install
  chmod +x "${pkgdir}"/usr/share/scratch/scripts/*.py
}

# vim: ts=2 sw=2 et:
