# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-svg-git
pkgver=v5.1.1.14.gbfda474
pkgrel=1
pkgdesc="A cross-platform application and UI framework (QtSvg)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-base')
provides=('qt5-svg')
conflicts=('qt5-svg')
makedepends=('git')
source=("git://gitorious.org/qt/qtsvg.git#branch=dev")
sha256sums=('SKIP')
_gitname='qtsvg'

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	qmake
	make
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
}
