# Maintainer: Dmytro Bagrii <dimich@softerra.com>

pkgname=neard
pkgver=0.13
pkgrel=1
pkgdesc="Intel's Near Field Communication manager"
arch=('i686' 'x86_64')
url="https://01.org/linux-nfc/"
license=('GPL2')
depends=('glibc' 'dbus>=1.2' 'libnl' 'glib2>=2.28')
makedepends=('automake' 'autoconf>=2.60')
backup=(etc/neard/main.conf)
source=(http://git.kernel.org/cgit/network/nfc/neard.git/snapshot/$pkgname-$pkgver.tar.gz
        bindir.patch
        config.patch
        neard.service)
md5sums=('a7d3e145559ae3f19d6d438475fe00f9'
         '8eaaf09636bad33e5771c45457c9c53b'
         '149d15e8b0a68db08f21ca1bd9d470be'
         '3968d529bd6d1604f7e2ee3ce18fd3b6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/bindir.patch

  patch -p1 -i $srcdir/config.patch

  ./bootstrap && \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug \
    --enable-tools

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -D -m644 "${srcdir}/neard.service" \
    "${pkgdir}/usr/lib/systemd/system/neard.service"

  install -D -m644 "${srcdir}/$pkgname-$pkgver/src/main.conf" \
    "${pkgdir}/etc/neard/main.conf"
}
