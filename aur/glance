# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=havana
pkgver=2013.2.rc1
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-jsonpointer python2-glanceclient python2-keystoneclient
    python2-pyopenssl python2-warlock)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-${pkgver##*.}/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('fe9dd4d07a13fa9b87df6494b7571b17b276c8d925ec2423eeb04fb9c77e1ff1')
sha512sums=('664a3c681d81c11d4b56f9c37f5574d3fb1644e371ba5894b64886dd322020ace591c15b4ba16df1bb0d766c3b6acea90c41b48b7eebc7c7385340be17678e43')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
