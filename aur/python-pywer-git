# Maintainer: William Giokas <1007380@gmail.com>

pkgname=python-pywer-git
_pkgname=pywer
pkgver=0.3.r1.g7ee1fb0
pkgrel=2
pkgdesc='A simple python-based AUR helper'
url='http://git.kaictl.net/wgiokas/pywer.git'
license=('MIT')
arch=('any')
provides=('python-pywer')
conflicts=('python-pywer')
makedepends=('python-setuptools')
depends=('python-requests' 'python-xdg')
source=("git+git://git.kaictl.net/pub/wgiokas/pywer.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g' 
}
prepare() {
  cd "$srcdir/$_pkgname"
  sed -i -E "s/(.*__version__ = ).*/\1'$pkgver'/g" libaur/__init__.py
}

package() {
  cd "$srcdir/$_pkgname"
  ./setup.py install --prefix=/usr --root="$pkgdir"
}
