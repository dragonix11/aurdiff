# Maintainer: X0rg

pkgname=darling-multilib-git
_gitname=darling
pkgver=594.6407eba
pkgrel=5
pkgdesc="A Wine-like emulation layer that enables you to run Darwin/OS X apps on Linux for multilib"
arch=('x86_64')
url="http://darling.dolezel.info/en/Darling"
license=('GPL3')
depends=('cairo'       'libbsd'       'systemd'       'lcms'       'bzip2'       'util-linux'       'ncurses'       'libxrandr'       'ffmpeg'       'libkqueue' \
         'lib32-cairo' 'lib32-libbsd' 'lib32-systemd' 'lib32-lcms' 'lib32-bzip2' 'lib32-util-linux' 'lib32-ncurses' 'lib32-libxrandr' 'lib32-ffmpeg' \
         'gcc-libs-multilib' 'gnustep-gui-multilib-clang-svn' 'gnustep-corebase-multilib-clang-svn')
makedepends=('git' 'clang>=3.1' 'nasm' 'yasm')
optdepends=('dmg2dir-git: extract OS X application from .dmg file')
provides=('darling-git')
conflicts=('darling-git')
backup=(etc/darling/dylib.conf)
install=darling.install
options=('!buildflags')
source=('git://github.com/LubosD/darling.git'
	'unistd.h'
	'dyld')
md5sums=('SKIP'
	'a7e3d23ac8b3fc0d930af1a66f312338'
	'2059cbba07ef34884b45b2afdf109474')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_gitname"
  msg2 "Clone submodules..."
  git submodule init
  git submodule update

  msg2 "Make 'build' directory..."
  mkdir -pv ./build/{32,64}

  msg2 "Move patched file 'unistd.h' in '$srcdir/$_gitname/include' directory..."
  cp -v "$srcdir/unistd.h" "./include"
}

build() {
  cd "$_gitname/build/32"
  source "/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"

  msg2 "Run 'cmake' (32-bit)..."
  CC="clang -m32" CXX="clang++ -m32" LDFLAGS="-L/usr/share/GNUstep32/lib32 -L/usr/lib32" cmake ../.. -DSUFFIX=32 -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev

  msg2 "Run 'make' (32-bit)..."
  make

  # 64-bit build
  cd ../64
  source "/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config"

  msg2 "Run 'cmake' (64-bit)..."
  CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" cmake ../.. -DSUFFIX=64 -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev

  msg2 "Run 'make' (64-bit)..."
  make
}

package() {
  cd "$_gitname/build/32"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install

  # 64-bit install
  cd ../64
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install
  mv -v "$pkgdir/usr/lib64" "$pkgdir/usr/lib"

  msg2 "Make new executables..."
  mkdir -pv "$pkgdir/usr/share/$pkgname"
  mv -v "$pkgdir/usr/bin/"* "$pkgdir/usr/share/$pkgname"
  sed -i "s|PKGNAME|$pkgname|g" "$srcdir/dyld"
  install -Dvm755 "$srcdir/dyld" "$pkgdir/usr/bin/dyld"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep32/lib32\n/usr/share/$pkgname/dyld32 \$@"	> "$pkgdir/usr/bin/dyld32"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/dyld64 \$@"	> "$pkgdir/usr/bin/dyld64"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/dyldd \$@"	> "$pkgdir/usr/bin/dyldd"
  echo -e "#!/usr/bin/sh\n\nexport LD_LIBRARY_PATH=/usr/share/GNUstep/lib\n/usr/share/$pkgname/motool \$@"	> "$pkgdir/usr/bin/motool"
  chmod -v --reference="$pkgdir/usr/share/$pkgname/dyld64" "$pkgdir/usr/bin/"*
}
