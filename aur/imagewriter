# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=imagewriter
pkgver=1.10
_opensuse_rel=3.4
pkgrel=9
pkgdesc="SUSE Studio Imagewriter - Graphical image writer application"
url="http://kiwi.berlios.de"
arch=('x86_64' 'i686')
license=('GPLv2')
depends=('qt4' 'xdg-su')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/imagewriter-${pkgver}-${_opensuse_rel}.src.rpm"
        'build_fix.patch'
        'xdg-su_arguments.patch')
sha512sums=('4d551f73cd9eb5cdb7fd1e4df4105973889873aca523a2183055671500cae57473df7fc17e4079c1f9bce1b5c974bb1d75c08f013e56eaaf0a0ce1b3b7131302'
            '4b487b0374cc03f1e9eb89a76c8da0b1aa5076082be51bd69bc0d02b434afd6a5cfd54fd7ac3c552566758acf1f4d0983177ef9d7d48dd43612eb96efba4740a'
            '934b2108acf9dc540d227c25bc6739babd0a3fcb70926684f67d46878d0ad193939a0b781f96b1e20723eb891467d23e6c23e3e857ddb1ba45e1e47c02b903aa')

prepare() {
  cd "${srcdir}"
  rm -vf imagewriter.spec
  tar zxvf imagewriter-${pkgver}.tar.gz
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/build_fix.patch"
  patch -p1 -i "${srcdir}/xdg-su_arguments.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake-qt4 PREFIX=/usr imagewriter.pro
  make CFLAGS="${CFLAGS} -DKIOSKHACK -Wno-unused-parameter"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
