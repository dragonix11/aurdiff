# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.9.1
_reldate=20130917-0100
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip' 'java-environment')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/mylyn/drops/${pkgver}/v${_reldate}/mylyn-${pkgver}.v${_reldate}.zip)
replaces=('eclipse-mylyn-extras')
changelog=ChangeLog
provides=("eclipse-mylyn-extras=${pkgver}")
md5sums=('bdcb56d52e10603e2d781b8093ce1fa8')
sha256sums=('3840fdbc179f6ade8314c12efdb24e9c4ebb0b56e5460bf6eb8da167cab1f69b')
sha384sums=('2f75999a35b6c217f85d21a842e932450bcb1860ce847697ae11d42a6aad09ff91c6719230950b22e29b999d4957dc2f')
sha512sums=('f922819c5f256f335cc6ca924939b7029e5ef02c341708ecf7c84fcc0279da6db73f31e389f6680f9371727954aa6a72d49e22a8b3d84972d3c6a0bd0880f1cc')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}
