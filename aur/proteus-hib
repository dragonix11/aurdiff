# Maintainer: Sam S. <smls75@gmail.com>

pkgname=proteus-hib
pkgver=1.1.1_20130608
_hibver=06082013
pkgrel=1
pkgdesc='An audio-visual exploration game (Humble Bundle version)'
url='http://www.visitproteus.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
if [ $CARCH == i686 ]; then
  depends=('sdl' 'sdl_image' 'glu' 'cairo' 'alsa-lib' 'libogg' 'libvorbis')
  optdepends=('alsa-plugins: PulseAudio support'
              'sdl-nokeyboardgrab: support for alt-tabbing out of fullscreen')
else
  depends=('lib32-sdl' 'lib32-sdl_image' 'lib32-glu' 'lib32-cairo'
           'lib32-alsa-lib' 'lib32-libogg' 'lib32-libvorbis')
  optdepends=('lib32-alsa-plugins: PulseAudio support'
              'lib32-libpulse: PulseAudio support'
              'lib32-sdl-nokeyboardgrab: support for alt-tabbing out of fullscreen')
fi
install='proteus-hib.install'
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

source=("hib://proteus-${_hibver}-bin"
        'proteus-hib.desktop')
md5sums=('9e8d105bd56157fd1f7cca3a9f814481'
         '4ddd7b894b490e3bbd32a83922e111b3')

package() {
  cd $srcdir
  _target="/opt/Proteus"
  
  # Remove bundled libraries (use distro versions instead)
  ( cd data/lib &&
    rm libogg.so.0 libvorbis.so.0 libvorbisfile.so.3 libSDL-1.2.so.0 \
       libSDL_image-1.2.so.0 libcairo.so.2 libpixman-1.so.0 \
       libfontconfig.so.1 libfreetype.so.6 )

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "data/Proteus.png" "${pkgdir}/usr/share/pixmaps/proteus.png"

  # Install game files
  mkdir -p "${pkgdir}${_target}"
  mv -T "data" "${pkgdir}${_target}"
  
  # Install launch script
  [ $CARCH == "i686" ] && _arch='x86' || _arch='x86_64'
  echo -e "#!/bin/sh\ncd ${_target} && ./Proteus" > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/proteus"
}
