# Maintainer: Richard Schwab <mail NOSPAM w.tf-w.tf>

pkgname=quiterss-hg
pkgver=2843.5331a91f5c18
pkgrel=1
pkgdesc="QuiteRSS is fast and light rss ,feed reader written in C++/Qt4 - mercurial checkout"
arch=('i686' 'x86_64')
url="http://code.google.com/p/quite-rss/"
license=('GPL')
depends=('qtwebkit')
makedepends=('cmake' 'gettext' 'automoc4' 'mercurial')
source=("quiterss.install" "quiterss::hg+https://code.google.com/p/quite-rss/")
sha512sums=('418a36dc447e78a82504821e0f0c00a4cf969d93d2d8285478fcbb79c92674051f1aba89399dbcc21e673897384203f568439e741453a7c21003eb61c99a1c24' 'SKIP')
install=quiterss.install
provides=('quiterss')
conflicts=('quiterss')

pkgver() {
  cd "${pkgname%-*}"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "${pkgname%-*}"

  qmake-qt4 QuiteRSS.pro PREFIX=/usr \
  "CONFIG+=LINUX_INTEGRATED" \
  "INSTALL_ROOT_PATH=${pkgdir}"

  make 
}

package() {
  cd "${pkgname%-*}"

  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 quiterss.desktop "${pkgdir}/usr/share/applications/"
}
