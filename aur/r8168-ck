# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-ck
_realname=r8168
pkgver=8.036.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-ck kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-ck>=3.11' 'linux-ck<3.12')
makedepends=('linux-ck-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2 linux-3.10.patch)
install=$_realname.install

_extramodules=extramodules-3.11-ck
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

prepare() {
   cd "$_realname-$pkgver"
   patch -p0 -i "$srcdir/linux-3.10.patch"
}

build() {
   cd "$srcdir"
   #patch -p0 -i "linux-3.8.patch"
   cd "$_realname-$pkgver"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('937fd5d7d4cbfc95cf2859d8c3beb15a8536dc4e41752f7a53f2a25f07cdcd33'
            '1fb115b4d1b6b058d23322b89fad52d29748ce75b2774815e72c679a235d0e87')
