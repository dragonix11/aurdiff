# Maintainer: twa022 <twa022 at gmail dot com>
# based on nautilus-dropbox package, contributors below...
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Maintainer: American_Jesus <american.jesus.pt AT gmail DOT com>
# based on nemo-dropbox-git package

pkgname=nemo-dropbox
pkgver=2.0.0
_mintrel=petra
pkgrel=1
pkgdesc="Dropbox for Linux - Nemo extension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('libnotify' 'nemo' 'dropbox' 'hicolor-icon-theme')
makedepends=('python2-docutils' 'python2' 'pygtk')
conflicts=('nautilus-dropbox' 'caja-dropbox')
install=${pkgname}.install
options=('!libtool' '!emptydirs')

source=("http://packages.linuxmint.com/pool/main/n/$pkgname/${pkgname}_${pkgver}%2b${_mintrel}.tar.gz")
md5sums=('202ca5612eb4c61221f7ef59c447118c')

build() {
  cd ${pkgname}-${pkgver}+${_mintrel}

  sed -i "s|AM_CONFIG_HEADER|AC_CONFIG_HEADERS|g" configure.in

  autoreconf -fi

  sed -i "s/python/python2/" configure dropbox.in Makefile.in rst2man.py
  
  # since python2-docutils, rst2man.py is named "rst2man2.py"
  sed -i "s:rst2man.py:/usr/bin/rst2man2.py:" configure

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}+${_mintrel}
  make DESTDIR="${pkgdir}" install
  # remove executables and depend on 'dropbox' package
  rm "${pkgdir}/usr/bin/dropbox"
  rm "${pkgdir}/usr/share/applications/dropbox.desktop"  
  rm "${pkgdir}/usr/share/man/man1/dropbox.1"
  # install the common license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
