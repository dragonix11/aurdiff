# Maintainer: shivik <>, PGP Key: 761E423C

pkgname=nvidia-lqx
pkgver=325.15
_extramodules=extramodules-3.11-lqx
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=2
pkgdesc="NVIDIA drivers for linux-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lqx>=3.11' 'linux-lqx<3.12' "nvidia-utils=${pkgver}")
conflicts=('nvidia-173xx-lqx')
license=('custom')
options=(!strip)
install=nvidia-lqx.install
__mainserver="ftp://download.nvidia.com"
__usserver="http://us.download.nvidia.com"
__currentserver=${__mainserver}
source=('nvidia-linux-3.11.patch')
md5sums=('5c962760de069ed4ad34e64d5988686a')

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('528fc538584de6446384b9cec3099d6c')	
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('63b2caa0cb128efa1a7808d7bddb2074')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${srcdir}"
	sh "${_pkg}.run" --extract-only
	cd "${_pkg}/kernel"
	patch -Np2 -i ${srcdir}/nvidia-linux-3.11.patch
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lqx.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-lqx.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
