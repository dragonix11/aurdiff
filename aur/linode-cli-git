# Maintainer: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli-git
pkgver=2.4f112fc
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay')
makedepends=('git')
source=("$pkgname"::'git+https://github.com/linode/cli.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"
  sed -i '/use warnings;/a no warnings qw(experimental::smartmatch);' $(grep -Rl ~~ lib linode linode-linode)
}

package() {
  cd "$pkgname"
  install -D linode "$pkgdir/usr/bin/linode"
  install -D linode-linode "$pkgdir/usr/bin/linode-linode"
  for file in $(find lib -type f -name "*.pm"); do
    install -Dm644 "$file" "$pkgdir/usr/lib/perl5/vendor_perl/${file/lib\//}"
  done
}
