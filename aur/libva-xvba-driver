# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=libva-xvba-driver
pkgver=0.8.0
pkgrel=4
pkgdesc='XvBA backend for VA-API'
arch=(i686 x86_64)
url=http://freedesktop.org/wiki/Software/vaapi
license=(GPL2)
depends=(catalyst-utils libva)
makedepends=(mesa python2 xvba-sdk)
options=(!libtool)
source=(
http://cgit.freedesktop.org/vaapi/xvba-driver/snapshot/xvba-driver-$pkgver.zip
'http://git.frugalware.org/gitweb/gitweb.cgi?p=frugalware-current.git;a=blob_plain;f=source/x11-extra/libva-xvba-driver/fixes.patch'
https://build.opensuse.org/package/rawsourcefile/home:maxge73/xvba-video/xvba-video-0.8.0-glx-fix.patch
https://build.opensuse.org/package/rawsourcefile/home:maxge73/xvba-video/xvba-video-h264-level51.patch
https://build.opensuse.org/package/rawsourcefile/home:maxge73/xvba-video/xvba-video-remove-installation.patch
)
md5sums=(2784142d7275348409cfe5d149129ae1
         6f4096f8b2b8abb837e05827109bacc7
         5dc283eab46418eeef8e8be7c028a2ba
         bce1de0a8b274049568453a53e8fce6a
         9330d0af491d139748d4c861423edc5f)
sha1sums=(b9352b8b67cc7dcea3fbb838eba90bc28297fe55
          d17f16e859acdf3ce57a92c2208ff591c328bd16
          d06da6b1b9d84f38a7ad0051e2c91168529d1def
          3653de094a3561fc97dc1950d0cca5bac08c4a30
          38136d5107c7d57593910fa2dbddd36180eceaa1)

build() {
  cd xvba-driver-$pkgver

  # patch from opensuse (thanks vi0l0!)
  patch -Np1 -i ../xvba-video-remove-installation.patch
  patch -Np1 -i ../xvba-video-h264-level51.patch
  patch -Np1 -i ../xvba-video-0.8.0-glx-fix.patch

  patch -Np1 -i ../fixes.patch

  export CPPFLAGS="$CPPFLAGS -I/usr/include/amd"

  ./autogen.sh
  ./configure --disable-debug --disable-tracer --disable-valgrind --prefix=/usr

  make PYTHON=python2
}

package() {
  cd xvba-driver-$pkgver

  make DESTDIR=$pkgdir install
}
