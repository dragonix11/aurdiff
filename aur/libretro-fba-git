# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-fba-git
pkgver=707.c571301
pkgrel=2
pkgdesc="libretro implementation of Final Burn Alpha (FBA). (Arcade)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/fba-libretro"
license=('custom:FB Alpha License')
makedepends=('git')

_gitname=fba-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/fb_alpha_libretro.info")
md5sums=('SKIP'
         '1a6fb616f9ed9213071ef2cdbf3fe6c0')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/svn-current/trunk"
  make -f makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/svn-current/trunk/src/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/svn-current/trunk/fb_alpha_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-fba.so"
  install -Dm644 "fb_alpha_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-fba.info"
}
