# Maintainer: Loic Bidoux <loic.bidoux [at] owndata.org>

pkgname=servicemix
pkgver=4.5.3
pkgrel=1
pkgdesc='Apache ESB that unifies features and functionality of ActiveMQ, Camel, CXF, ODE and Karaf.'
arch=(any)
url=http://servicemix.apache.org
license=('Apache', 'custom')
depends=('bash' 'java-runtime')
install='.install'
changelog='.Changelog'
source=("http://mir2.ovh.net/ftp.apache.org/dist/servicemix/servicemix-4/$pkgver/apache-servicemix-full-$pkgver.tar.gz")
sha256sums=('336eec27ec1706e3b9e702235ad3ceaebe105903b4584173e23d2d05adcdaa4b')

package() {
  install -d $pkgdir/usr/share/{java/servicemix/{lib,system},licenses/servicemix,servicemix/{bin,examples}}
  install -d $pkgdir/var/lib/servicemix/{data,deploy,instances}
  install -d $pkgdir/etc/servicemix

  cp -r $srcdir/apache-servicemix-$pkgver/{bin,examples,NOTICE,README} $pkgdir/usr/share/servicemix
  cp -r $srcdir/apache-servicemix-$pkgver/{data,deploy} $pkgdir/var/lib/servicemix
  cp -r $srcdir/apache-servicemix-$pkgver/etc/* $pkgdir/etc/servicemix
  cp -r $srcdir/apache-servicemix-$pkgver/{lib,system} $pkgdir/usr/share/java/servicemix
  cp -r $srcdir/apache-servicemix-$pkgver/{licenses/*,LICENSE} $pkgdir/usr/share/licenses/servicemix

  chmod -R g+w $pkgdir/var/lib/servicemix
  chmod -R g+w $pkgdir/etc/servicemix
  
  ln -s /var/lib/servicemix/data $pkgdir/usr/share/servicemix/data
  ln -s /var/lib/servicemix/deploy $pkgdir/usr/share/servicemix/deploy
  ln -s /etc/servicemix $pkgdir/usr/share/servicemix/etc
  ln -s /var/lib/servicemix/instances $pkgdir/usr/share/servicemix/instances
  ln -s /usr/share/java/servicemix/lib $pkgdir/usr/share/servicemix/lib
  ln -s /usr/share/licenses/servicemix/LICENSE $pkgdir/usr/share/servicemix/LICENSE
  ln -s /usr/share/licenses/servicemix $pkgdir/usr/share/servicemix/licenses
  ln -s /var/lib/servicemix/lock $pkgdir/usr/share/servicemix/lock
  ln -s /usr/share/java/servicemix/system $pkgdir/usr/share/servicemix/system
}
