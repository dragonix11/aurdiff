# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower-nosystemd
pkgver=0.9.22
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('libusb' 'polkit-consolekit' 'pm-utils' 'dbus-glib' 'libimobiledevice' 'udev')
backup=('etc/UPower/UPower.conf')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection')
provides=("upower=${pkgver}")
conflicts=('upower')
replaces=('upower')
options=('!libtool')
source=($url/releases/upower-$pkgver.tar.xz
        git-fixes.patch)
md5sums=('22b0b13c16ceb1516b5edf5cf0e99d6d'
         'd47377f5f8a02e38f81b6a63d8a19ef3')

prepare() {
  cd "upower-$pkgver"

  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd "upower-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/upower \
    --disable-static \
    --enable-deprecated
  make
}

package() {
  cd "upower-$pkgver"
  make DESTDIR="$pkgdir" install
}
