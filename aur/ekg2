# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=ekg2
pkgver=0.3.1
pkgrel=8
pkgdesc='Ncurses based Jabber, Gadu-Gadu, Tlen and IRC client'
arch=('i686' 'x86_64')
url='http://ekg2.org/'
license=('GPL')
depends=('aspell' 'libjpeg' 'python2' 'libgadu' 'gnutls' 'gpm' 'libidn' 'giflib')
optdepends=('xosd: xosd support'
	    'sqlite: sqlite support'
            'gtk2: gtk support')
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.gz
        ekg2-0.3.1-giflib5.patch)
md5sums=('68fc05b432c34622df6561eaabef5a40'
         'e5855316bf7e9b456ebabb7cb9340e98')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|ncursesw/ncurses.h|ncurses.h|g' configure plugins/ncurses/ecurses.h
  patch -Np1 -i ../ekg2-0.3.1-giflib5.patch
}

build() {
  cd $pkgname-$pkgver

  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/ekg2 \
    --with-libgadu \
    --with-xosd \
    --enable-unicode \
    --enable-shared \
    --without-gpg \

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/lib/perl5/core_perl/perllocal.pod
  rm -rf "$pkgdir"/usr/libexec
  chmod -R 755 "$pkgdir"/usr/lib/perl5
  chmod +s "$pkgdir"/usr/lib/ekg2/ioctld
}

# vim:set ts=2 sw=2 et:
