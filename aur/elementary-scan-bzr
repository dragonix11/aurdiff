# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=elementary-scan-bzr
pkgver=607
pkgrel=1
pkgdesc="Simple Scanning Utility"
arch=('i686' 'x86_64')
url="https://launchpad.net/elementary-scan"
license=('GPL2')
depends=('granite' 'launchpad-integration-gtk3' 'sane' 'scour')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala')
provides=("${pkgname%-*}" 'simple-scan')
conflicts=("${pkgname%-*}" 'simple-scan')
install=${pkgname%-*}.install
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr'
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
