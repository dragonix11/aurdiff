# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=4
_bookver=2.3.1
pkgrel=1
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://stockfishchess.org/"
license=('GPL3')
install=${pkgname}.install
makedepends=('git')
_pkgcommit=4d120ee02edff250a6661e63d913e70efc37e2b6
_pkgfilever=${pkgver//./}
_bookfilever=${_bookver//./}
source=("${pkgname}::git+https://github.com/mcostalba/Stockfish#commit=${_pkgcommit}"
        "http://cl.ly/3x333m0G173F/download/${pkgname}-${_bookfilever}-book.zip")
md5sums=('SKIP'
         '9e51c2e57d8b55bbc588150033e4b133')

prepare()
{
    cd "$srcdir/${pkgname}/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp
}

build()
{
    cd "$srcdir/${pkgname}/src"

    if [[ "$CARCH" == "i686" ]];
    then
        _arch=x86-32
    elif grep popcnt /proc/cpuinfo 2>&1
    then
        _arch=x86-64-modern
    else
        _arch=x86-64
    fi
    make build ARCH=$_arch
}

package()
{
    cd "$srcdir"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/${pkgname}/src"
    make PREFIX="$pkgdir/usr" install
}
