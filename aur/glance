# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=havana
pkgver=2013.2
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-jsonpointer python2-glanceclient python2-keystoneclient
    python2-pyopenssl python2-warlock)
makedepends=(python2-setuptools)
source=($url/$_relname/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('135fb1b6fe8410894b9d070071e1b7d7ddd37fb7c7267e2644e91e2b5448d6e5')
sha512sums=('38f9f38a79c18b8ea32c36052434c48b6558cbc0a92e3855d4e6f6961b160e22b78d7c89ff52f877ce93f312c6e3f87573e4545c44d1ce20dddb626f7a747017')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
