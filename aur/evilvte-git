# Maintainer: ledti <antegist at gmail.com>

pkgname=evilvte-git
pkgver=143.9abeb9a
pkgrel=1
pkgdesc='VTE based, highly customizable terminal emulator, devel version.'
arch=('i686' 'x86_64')
url="http://www.calno.com/evilvte/"
license=('GPL2')
depends=('vte3' 'hicolor-icon-theme')
makedepends=('pkg-config' 'git')
provides=('evilvte')
conflicts=('evilvte')
install=evilvte.install
source=('evilvte::git://github.com/caleb-/evilvte.git#branch=master')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/evilvte"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  # copy config.h to startdir to ease configuration:
  cd "$startdir"
  if [[ -e config.h ]]; then
    cp -f config.h "$srcdir/evilvte/src/"
    echo "To reset your configuration, remove $startdir/config.h"
  else
    cp "$srcdir/evilvte/src/config.h" config.h
    echo "To change your evilvte settings, edit $startdir/config.h"
  fi
}

build(){
  cd "$srcdir/evilvte"
  ./configure --prefix=/usr --with-gtk=3.0
  make
}

package(){
  cd "$srcdir/evilvte"
  make DESTDIR="$pkgdir/" install
}
