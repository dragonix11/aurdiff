# $Id: PKGBUILD 48930 2011-06-07 18:55:04Z angvp $ 
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
pkgname="python-sqlalchemy-doc"
pkgver=0.8.2
pkgdesc="Python SQL toolkit and Object Relational Mapper"
pkgrel=1
arch=('any')
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-sphinx' 'python-mako')
source=("http://downloads.sourceforge.net/sourceforge/sqlalchemy/SQLAlchemy-$pkgver.tar.gz"
        md5-to-hashlib.patch)
sha256sums=(fb0814b1198331164d1b12c37c0cced3c84350a4279f9ef43c613fb2d2fb9d86
            81d5b60b04b5ed8e0c128f2ced7bdf4f5aa664b8f016e32f2cc36e5375e08b0d)

build() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  sed -i -r "s@^(templates_path = \[')templates@\1doc/build/templates@" doc/build/conf.py
  python2 sa2to3.py -w lib
  cd doc/build
  patch -Np2 <"${srcdir}/md5-to-hashlib.patch"
  make html
}

package() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  while read -d ''; do
    install -Dm644 "$REPLY" "$pkgdir/usr/share/doc/$pkgname/${REPLY#build/sphinx/html/}"
  done < <(find doc/build/output/html -type f -print0)
}
