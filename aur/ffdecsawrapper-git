# Submitter:   Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Tycho Lursen "bas-t" (responsible for hosting and development of ffdecsawrapper)
# Contributor: Petr Vacek "vaca" (contributing patch and code from sascng-linux3-patch and providing cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (coding workaround for procfs changes on kernels >= 3.10)

pkgname=ffdecsawrapper-git
pkgver=kernel_gitver
pkgrel=1
pkgdesc="FFdecsa empowered softcam (newest generation of open-sasc-ng)"
url=https://github.com/bas-t/ffdecsawrapper.git
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux-headers')
makedepends=('git' 'linux-headers>=3.10' 'linux-headers<3.11')
conflicts=('ffdecsawrapper' 'sasc-ng')
provides=('ffdecsawrapper')
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/ffdecsawrapper' 'etc/camdir/cardslot.conf')
install='ffdecsawrapper.install'

_basekernel=3.10

source=('git://github.com/bas-t/ffdecsawrapper.git#branch=stable'
	"http://www.kernel.org/pub/linux/kernel/v3.x/linux-$_basekernel.tar.bz2" \
	'cardclient.conf' 'ffdecsawrapper.rc' 'ffdecsawrapper.conf' \
	'ffdecsawrapper.lr' 'cardslot.conf' 'ffdecsawrapper.service' \
	'ffdecsawrapper.install' 'kernel-mutex.patch')

sha256sums=('SKIP'
            '46c9e55e1fddf40813b8d697d5645037a8e2af5c1a8dff52b3fe82b5021582b8'
            '7caba03e515fe55aced4aad831e72ef3c0e59a3cdcea7bcdf79f7bfff6fcec75'
            'f20b9a95247c827efb661546e06fae8a4565b2d95d1e8a577d11e9ddb590cfe8'
            '6f64a8a94178c075c793d2b8bbd36f221d5956bfa3eadc8352e78076a4d65de4'
            'f435344dc9f1c0ed7c2e0de74ec434cd73e2130a0d7589a4d38338e45925d8db'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '8e10e2306772427a31cba905374ab9c4616b2089b21b57df5ea058053414a84d'
            'e1f2e935a2e3d847e85c93ae5367ba199cc79bc9daa43edf06317a7a96793822'
            '7546b2822b604bc79deca4d04c33bbf17956cd1e9f4f74e212704b6152967691')

pkgver() {

	cd $srcdir/ffdecsawrapper
 	_gitver=`git describe --always | sed 's|-|.|g'`
	_kernel=`uname -r | sed -r 's/-/_/g'`
	echo $_kernel_$_gitver
}

prepare() {

	msg "Applying patches..."
	cd $srcdir/linux-$_basekernel
	patch -p1 < $srcdir/kernel-mutex.patch
}

build() {

	cd $srcdir/ffdecsawrapper/contrib/ffdecsawrapper      
	./configure --compiletype=release
  
	cd $srcdir/linux-$_basekernel/drivers/media/dvb-core
	make -C /usr/lib/modules/`uname -r`/build M=$(pwd) modules
}

package() {

	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/extra
	mkdir -p $pkgdir/etc/conf.d
	mkdir -p $pkgdir/etc/rc.d
	mkdir -p $pkgdir/etc/camdir
	mkdir -p $pkgdir/etc/logrotate.d/
	mkdir -p $pkgdir/usr/lib/systemd/system/
	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/kernel/drivers/media/dvb-core
      
	install -m0755 $srcdir/ffdecsawrapper/contrib/ffdecsawrapper/ffdecsawrapper  $pkgdir/usr/bin/
	install -m0644 $srcdir/ffdecsawrapper/contrib/ffdecsawrapper/dvbloopback.ko  $pkgdir/usr/lib/modules/`uname -r`/extra
	install -m0755 $srcdir/ffdecsawrapper.rc  $pkgdir/etc/rc.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.conf  $pkgdir/etc/conf.d/ffdecsawrapper
	install -m0644 $srcdir/cardclient.conf  $pkgdir/etc/camdir/cardclient.conf
	install -m0644 $srcdir/cardslot.conf  $pkgdir/etc/camdir/cardslot.conf
	install -m0644 $srcdir/ffdecsawrapper.lr  $pkgdir/etc/logrotate.d/ffdecsawrapper-git.lr
	install -m0644 $srcdir/ffdecsawrapper.service  $pkgdir/usr/lib/systemd/system/ffdecsawrapper.service
	install -m0644 $srcdir/linux-$_basekernel/drivers/media/dvb-core/dvb-core.ko  $pkgdir/usr/lib/modules/`uname -r`/kernel/drivers/media/dvb-core
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
