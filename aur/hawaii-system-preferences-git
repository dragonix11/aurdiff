# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-system-preferences-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii: System preferences"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL')
depends=('kf5-polkit-qt-git' 'kde-kconfig-git' 'qt-accountsservice-addon-git' 'qtxdg-git')
makedepends=('git' 'cmake' 'qttools-git')

_gitroot="git://github.com/hawaii-desktop/system-preferences.git"
_gitbranch=master
_gitname=system-preferences
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DPolkitQt-1_DIR=/opt/kf5/lib/cmake/PolkitQt-1 \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
