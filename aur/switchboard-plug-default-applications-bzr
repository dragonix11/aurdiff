# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=switchboard-plug-default-applications-bzr
pkgver=63
pkgrel=1
pkgdesc="Default Applications plug for Switchboard"
arch=('i686' 'x86_64')
url="https://launchpad.net/switchboard-plug-default-applications"
license=('GPL3')
depends=('libpantheon' 'switchboard')
makedepends=('bzr' 'cmake' 'vala0.16')
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE=/usr/bin/valac-0.16
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
