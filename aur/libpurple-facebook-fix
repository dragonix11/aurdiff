pkgname='libpurple-facebook-fix'
pkgver=2.10.7
pkgrel=1
arch=('i686' 'x86_64')
url="https://developer.pidgin.im/ticket/15706"
license=('GPL')
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm' 'libidn' 'python2' 'hicolor-icon-theme' 'farstream-0.1' 'avahi' 'tk' 'ca-certificates' 'intltool' 'networkmanager')
options=('!libtool')
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2
        pidgin-2.10.7-link-libirc-to-libsasl2.patch
        pidgin-2.10.7-fix-empty-names-in-roster.patch)

pkgdesc="IM library extracted from Pidgin, fix facebooks username bug"
depends=('farstream-0.1' 'libsasl' 'libidn' 'dbus-glib' 'nss')
optdepends=('avahi: Bonjour protocol support'
            'ca-certificates: SSL CA certificates'
            'python2-dbus: for purple-remote and purple-url-handler'
            'tk: Tcl/Tk scripting support')
provides=('libpurple=2.10.7')

build() {
  cd "$srcdir/pidgin-$pkgver"

  # https://developer.pidgin.im/ticket/15517
  patch -Np1 -i "$srcdir/pidgin-2.10.7-link-libirc-to-libsasl2.patch"
  patch -Np1 -i "$srcdir/pidgin-2.10.7-fix-empty-names-in-roster.patch"
  autoreconf -vi

  # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-meanwhile \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  for _dir in libpurple share/sounds share/ca-certs m4macros po; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}

# vim:set ts=2 sw=2 et:

sha256sums=('eba32994eca20d1cf24a4261b059b2de71a1ec2dd0926e904074b0db49f7f192'
            '063723d5dc5726c43137b4b383c9d07c2c008391f6a626faaf6cedd31a2f1e8f'
            'eb3b701cc06c4825bdb892eb9c66ecbc92da30ef951666a090a8b054f47526de')
