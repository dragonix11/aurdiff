# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Filip Dvorak <fila@pruda.com>
# Contributor: 3ED_0 (AUR)
# Maintainer: SanskritFritz (gmail)

pkgname=firehol
pkgver=1.296
pkgrel=5
pkgdesc="The iptables stateful packet filtering firewall builder"
url="http://firehol.sourceforge.net"
arch=('any')
license=('GPL')
depends=('iptables' 'gawk' 'iproute')
makedepends=('cvs')
backup=('etc/firehol/firehol.conf')
install='firehol.install'
source=("https://github.com/ktsaou/firehol/archive/v1.296.tar.gz"
	"disable-kernel-vercheck.patch"
        "firehol.service")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # the sed command for minimum kernel version is broken, and since it
  # only ensures we are using a kernel > 2.3, we can safely skip it entirely
  # https://bugs.archlinux.org/task/25917
  patch -p1 < "$srcdir/disable-kernel-vercheck.patch"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/etc/firehol/examples"

  install -D -m744 firehol.sh "$pkgdir/usr/bin/firehol"
  install -D -m744 adblock.sh "$pkgdir/etc/firehol/adblock.sh"

  install -D -m644 examples/*.conf "$pkgdir/etc/firehol/examples/"

  install -D -m644 man/firehol.1 "$pkgdir/usr/share/man/man1/firehol.1"
  install -D -m644 man/firehol.conf.5 "$pkgdir/usr/share/man/man5/firehol.conf.5"

  install -D -m644 $srcdir/firehol.service "$pkgdir/usr/lib/systemd/system/firehol.service"

  # backup does not work if the file is not contained in the package
  # plus, creating it in post_install will set 777 permissions and we dont want that

  touch "$pkgdir/etc/firehol/firehol.conf"
  chmod 600 "$pkgdir/etc/firehol/firehol.conf"
}

md5sums=('7b634a0e1a338acfa782e6dca1adf5b7'
         '73a8ae701f82af98e56a4b2f436fb399'
         'd87f844ac0ef319fd0ea0adcb0a66905')
