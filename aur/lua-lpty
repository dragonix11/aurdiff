# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname=lua-lpty
_pkgname=${pkgname#lua-}
pkgver=1.0.1
_pkgver=1.0.1-1
pkgrel=1
pkgdesc="A simple facility for lua to control other programs via PTYs."
url="http://www.tset.de/lpty/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('lua')
source=("http://www.tset.de/downloads/$_pkgname-$_pkgver.tar.gz")
sha256sums=('8cf5f33dbb402507bc02cb26d66475b4452f855c090309595592e2ccf4e14ee8')

build() {
	cd "$srcdir/$_pkgname-$_pkgver"
	make
}

package() {
	cd "$srcdir/$_pkgname-$_pkgver"
	# NOTE: Makefile's install rule is broken
	# make INSTALL_ROOT="$pkgdir" install

	_luaver=$(lua -v 2>&1|awk '{split($2, a, "."); print a[1] "." a[2]}')

	install -m755 -d "$pkgdir/usr/lib/lua/$_luaver"
	install -m755 -t "$pkgdir/usr/lib/lua/$_luaver" lpty.so
}