# Maintainer: Splex
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
pkgname=secondlife-bin
pkgver=3.6.9.282535
_pngver=1.5.1
pkgrel=1
pkgdesc="Second Life is a 3-D virtual world entirely built and owned by its residents (Binary version.)"
url="http://www.secondlife.com/"
license=('GPL')
[ "$CARCH" = "i686"   ] && depends=('alsa-lib' 'apr-util' 'freealut' 'gtk2' 'libgl' 'libidn' 'libjpeg6' 'mesa' 'nss' 'sdl' 'glu')
[ "$CARCH" = "x86_64" ] && depends=('lib32-freealut' 'lib32-gtk2' 'lib32-libidn' 'lib32-mesa' 'lib32-sdl' 'lib32-libxml2' 'lib32-glu' 'lib32-pangox-compat')
[ "$CARCH" = "x86_64"   ] && optdepends=('lib32-libpulse: for PulseAudio support' 'lib32-alsa-lib: for ALSA support' 'lib32-nvidia-utils: for NVIDIA support' 'lib32-flashplugin: for inworld Flash support')
arch=('i686' 'x86_64')
conflicts=('secondlife' 'secondlife-svn')
install=secondlife.install
source=('secondlife.desktop'
        'secondlife.launcher'
        "http://d378iqxsyg0s3u.cloudfront.net/Viewer_3/SecondLife-i686-${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz")
sha256sums=('187d4b7ceffddbf80a9074d2401d4ea7c85d9a5b18e3cb154684ca8a3b5a318e'
            'd958f7f18e78fddf552b1a9782427332889e80c118b5df520d27c04a1bafe6a7'
            'e9167a8743c97a5bb65014e8b7486eebc7c2c2f61c1b021c27b276e00afe3d1d'
            'd230837ad959b395bf7e2937e0136e6037208a29cda83000bf8b88a3692e8c69')

build() {
  # build compatible lib32-libpng
  msg "Building libpng..."

  if [ "$CARCH" = "x86_64" ] ; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  cd "${srcdir}/libpng-${_pngver}"

  ./configure
  make
}

package() {
  cd $srcdir
  
  # Rename Data Directory
  mv SecondLife-i686-${pkgver} secondlife
  
  # Install Desktop File
  install -D -m644 $srcdir/secondlife.desktop \
    $pkgdir/usr/share/applications/secondlife.desktop
  
  # Install Icon File
  install -D -m644 $srcdir/secondlife/secondlife_icon.png \
    $pkgdir/usr/share/pixmaps/secondlife.png
  
  # Install Launcher
  install -D -m755 $srcdir/secondlife.launcher \
    $pkgdir/usr/bin/secondlife

  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv secondlife $pkgdir/opt/
  
  # Change Permissions of files to root:games
  chown -R root:games $pkgdir/opt/secondlife
  chmod -R g+rw $pkgdir/opt/secondlife
  
  # Make Binary Group-Executable
  chmod g+x $pkgdir/opt/secondlife/secondlife

  # Install libpng we built
  install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
    $pkgdir/opt/secondlife/lib

  cd $pkgdir/opt/secondlife/lib
  ln -s libpng15.so.15.1.0 libpng15.so.15
  ln -s libpng15.so.15.1.0 libpng15.so
  ln -s libfontconfig.so.1.4.4 libfontconfig.so.1
}

# vim:set ts=2 sw=2 et:
