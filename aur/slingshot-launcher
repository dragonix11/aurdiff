# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=slingshot-launcher
pkgver=0.7.6
pkgrel=1
pkgdesc='Slingshot application launcher'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%-*}"
license=('GPL3')
depends=('gnome-menus2' 'granite' 'libunity' 'libwnck3' 'libzeitgeist')
makedepends=('cmake' 'vala')
install="${pkgname}.install"
source=("${url}/0.x/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('52a3ec3d913e1b1a590fb159c1862e10a70f1029792eacfd16a1ad1c2c4b85e1')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_EXE_LINKER_FLAGS='-lm' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
