# Maintainer: David Verelst <david dott verelst hat gmail com>
pkgname=python2-cartopy
pkgver=0.9.0
pkgrel=1
pkgdesc="a cartographic python library with matplotlib support"
arch=('i686' 'x86_64')
url="https://github.com/SciTools/cartopy"
license=('GPLv3')
depends=('cython' 'proj' 'geos' 'python2' 'python2-shapely' 'python2-pyshp-svn'
         'python2-scipy' 'python2-matplotlib')
makedepends=()
optdepends=('pep8-python2' 'python2-nose')
source=("https://github.com/SciTools/cartopy/archive/v${pkgver}.tar.gz")
md5sums=('762abba4765710af89f42c16803f6796')

build() {
  cd "$srcdir"/cartopy-$pkgver

  python2 setup.py build_ext --inplace
  python2 setup.py build
}

package() {
  cd "$srcdir"/cartopy-$pkgver
  python2 setup.py install --root="$pkgdir"/ 

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}


