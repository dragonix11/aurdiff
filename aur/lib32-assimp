# Contributor: Daniel Kirchner <daniel at ekpyron dot org>
# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

pkgname=lib32-assimp
_basename=assimp
pkgver=3.0.1270
pkgrel=1
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner"
arch=('x86_64')
license=('BSD')
depends=('lib32-gcc-libs' 'lib32-zlib')
makedepends=('cmake')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/project/assimp/assimp-3.0/assimp--${pkgver}-source-only.zip")
sha1sums=('e80a3a4326b649ed6585c0ce312ed6dd68942834')

build() 
{
  cd ${srcdir}

  rm -rf build && mkdir build && cd build

  cmake \
    -D BUILD_ASSIMP_TOOLS=OFF \
    -D ENABLE_BOOST_WORKAROUND=ON \
    -D CMAKE_INSTALL_PREFIX="/usr" \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="${CFLAGS/-march=x86_64} -march=i686 -m32" \
    -D CMAKE_CXX_FLAGS="${CXXFLAGS/-march=x86_64} -march=i686 -m32" \
    ../${_basename}--${pkgver}-source-only
  make 
}

package() 
{
  cd ${srcdir}/build

  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/include
  mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32

  install -Dm644 ${srcdir}/${_basename}--${pkgver}-source-only/LICENSE \
		 ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
