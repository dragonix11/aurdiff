# Maintainer: xylosper <darklin20@gmail.com>
# Contributor: willemw <willemw12@gmail.com>

pkgname=cmplayer
pkgver=0.7.8
pkgrel=2
pkgdesc="Powerful and easy-to-use multimedia player"
arch=('i686' 'x86_64')
url="http://$pkgname.github.io"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'icu' 'libdvdread' 'libcdio-paranoia' 'libcdio' 'libquvi' 'alsa-lib' 'mpg123' 'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('mesa' 'gcc')
source=(https://github.com/xylosper/cmplayer/releases/download/$pkgver/cmplayer-$pkgver-source.tar.gz)
md5sums=('388c16c09c320fe4db3c5d59dba1e780')

build() {
  export MAKE="make -j5"
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/--open=/--open /' cmplayer-opendvd.desktop
  sed -i 's/--open=/--open /' cmplayer.desktop
  ./build-libchardet
  ./build-mpv
  make PREFIX=/usr cmplayer
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DEST_DIR=$pkgdir PREFIX=/usr install
}
