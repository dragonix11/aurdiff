# Contributor: Aaron 'venisonslurpee' Laursen <venisonslurpee@gmail.com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
# Contributor: lh <jarryson@gmail.com>
# Contributor: Sebastian Schwarz <seschwar@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Xilon <xilon@gmail.com>

pkgname=xmms2-git
pkgver=0.8DrO_o.759.g73a4ab8
pkgrel=1
pkgdesc="X-platform Music Multiplexing System 2"
arch=(i686 x86_64)
url="https://xmms2.org/"
license=(GPL LGPL)
depends=(glib2)
optdepends=('alsa-lib: ALSA output'
            'avahi: announce xmms2d via bonjour/mDNS/zeroconf'
            'boost: C++ bindings'
            'curl: play HTTP streams'
            'cython: Python bindings'
            'faad2: AAC support'
            'ffmpeg: WMA, avcodec & avformat support'
            'fftw: visualization'
            'flac: FLAC support'
            'fluidsynth: MIDI support'
            'jack: JACK output'
            'libao: libao output'
            'libcdio-paranoia: CDDA support'
            'libdiscid: CDDA support'
            'libgme: support for various video game music formats'
            'libmad: MP3 support'
            'libmms: play MMS streams'
            'libmodplug: to play MOD files'
            'libmpcdec: Musepack support'
            'libofa: MusicDNS fingerprinting'
            'libsamplerate: vocoder support'
            'libshout: Icecast output'
            'libvorbis: Ogg Vorbis support'
            'libxml2: XSPF and podcast support'
            'mac: APE support'
            'mpg123: alternative MP3 support'
            'opusfile: Opus support'
            'oss: OSS output'
            'perl: Perl bindings'
            'pulseaudio: PulseAudio output'
            'ruby: Ruby bindings'
            'sidplay2-libs: support for C64 music files'
            'smbclient: direct CIFS/SMB access'
            'speex: Speex support'
            'wavpack: WavPack support')
makedepends=(python2 "${optdepends[@]%%:*}")
conflicts=(xmms2 xmms2-devel)
provides=(xmms2)
source=(xmms2::git://git.xmms2.org/xmms2/xmms2-devel.git
        xmms2d.service)
md5sums=('SKIP'
         'a1e670e7ce3f21d9a35b3ec305d71e86')

pkgver() {
    cd "$srcdir/xmms2"
    git describe --always | sed 's/-/./g'
}

prepare() {
    cd "$srcdir/xmms2"
    git submodule update --init
    sed -i '$a#define AVCODEC_MAX_AUDIO_FRAME_SIZE 192000 /* 1 second of 48kHz 32bit audio */' src/plugins/avcodec/avcodec_compat.h
    sed -i 's,#include <cdio/cdda.h>,#include <cdio/paranoia/cdda.h>,' src/plugins/cdda/cdda.c
    sed -i 's,libsmbclient.h,samba-4.0/libsmbclient.h,' src/plugins/samba/{samba.c,wscript}
}

build() {
    cd "$srcdir/xmms2"
    ./waf configure --prefix=/usr \
            --with-ruby-archdir=`ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]'` \
            --with-ruby-libdir=`ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]'`
    ./waf build
}

package() {
    cd "$srcdir/xmms2"
    ./waf --destdir="$pkgdir" install
    install -Dm0644 "$srcdir/xmms2d.service" \
            "$pkgdir/usr/lib/systemd/user/xmms2d.service"

    ## also install python2 bindings
    PYTHON=/usr/bin/python2 ./waf configure --prefix=/usr \
            --with-optionals=python --without-xmms2d
    ./waf build
    ./waf --destdir="$pkgdir" install
}
