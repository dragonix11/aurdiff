# Maintainer: adytzu2007 <adybac "at" gmail {dot} com>

pkgname=thinlinc-server
pkgver=4.1.0
pkgrel=3
pkgdesc="Cendio ThinLinc Linux Remote Desktop Server"
arch=('i686' 'x86_64')
url="http://www.cendio.com/"
license=('custom')
install=${pkgname}.install

depends=('python2' 'net-tools' 'procps-ng' 'xorg-xauth' 'pcsclite'
         'java-environment')

# to get download link, register here
_download_link="http://www.cendio.com/downloads/server/register.py"
_archive_name=tl-${pkgver}-server

source=("${_archive_name}.zip::${_download_link}"
        'tlwebaccess.service'
        'tlwebadm.service'
        'vsmagent.service'
        'vsmserver.service'
        'service.patch')
sha256sums=('d0c25bacac43d161aef10ac3109b40c1e1573eafc7d98b985ba11e0298f4c09f'
            '430bcbc959ab363a270fd830c9db8caa057dfbfde69beb6193958c282bd03f7d'
            'cbbf364b9303ff55a7fef434bddab7533f95b8228f045e232fd1c83b78a9a842'
            'b64dcb2ecfb38120a3314b14c114fbf79ecdf699984db7addadd3aec644165da'
            '3e0fdaeca38f4750c9b369a65b7b3c84dff996e9997dbb02dbfe16dc78a09849'
            'a172c721c81fb2da46bc9255550b6c795fdc7583c9999a44ece29261290ce7d0')

_extract_dir="extract"

build()
{
    cd "${srcdir}/${_archive_name}"

    cd serverkit-linux
    mkdir -p "${_extract_dir}"

    for rpm in *${CARCH}*rpm *noarch*rpm; do
        bsdtar -C "${_extract_dir}" -xf "${rpm}"
    done

    cd "${_extract_dir}"

    # Patch thinlinc-server to allow installing on a system with systemd
    cd "opt/thinlinc/libexec"
    patch -p1 < ${srcdir}/service.patch

    cd "${srcdir}/${_archive_name}/serverkit-linux/${_extract_dir}"

    mkdir -p "usr"
    mv "lib64" "usr/lib"

    rm -Rf "etc/init.d"
}

package()
{
    cd "${srcdir}/${_archive_name}/serverkit-linux/${_extract_dir}"

    cp -aR * "${pkgdir}"

    install -D -m0644 ${srcdir}/tlwebaccess.service ${pkgdir}/usr/lib/systemd/system/tlwebaccess.service
    install -D -m0644 ${srcdir}/tlwebadm.service ${pkgdir}/usr/lib/systemd/system/tlwebadm.service
    install -D -m0644 ${srcdir}/vsmagent.service ${pkgdir}/usr/lib/systemd/system/vsmagent.service
    install -D -m0644 ${srcdir}/vsmserver.service ${pkgdir}/usr/lib/systemd/system/vsmserver.service
}
