# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=switchboard-plug-elementary-tweaks-bzr
pkgver=126
pkgrel=1
pkgdesc="Elementary Tweaks plug for Switchboard"
arch=('i686' 'x86_64')
url="https://code.launchpad.net/elementary-community/elementary-tweaks"
license=('GPL3')
depends=('libpantheon-bzr' 'gconf' 'switchboard')
makedepends=('bzr' 'cmake' 'vala0.16')
source=("bzr+http://bazaar.launchpad.net/~versable/elementary-community/elementary-tweaks/")
sha256sums=('SKIP')

pkgver() {
  cd elementary-tweaks

  bzr revno
}

build() {
  cd elementary-tweaks

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE=/usr/bin/valac-0.16
  make
}

package() {
  cd elementary-tweaks/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
