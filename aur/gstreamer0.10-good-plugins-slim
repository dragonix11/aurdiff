# Maintainer: birdflesh <antkoul at gmail dot com>
# Contributor: Ananda Samaddar <ananda@samaddar.co.uk>
# Contributor: Alessandro Nakamuta <alessandro dot ufms at gmail dot com>
# Contributor: Pauline Martin <321eniluap@gmail.com>

pkgname=gstreamer0.10-good-plugins-slim
pkgver=0.10.31
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good) with reduced and no GNOME dependecies"
arch=('i686' 'x86_64')
url="http://gstreamer.freedesktop.org/"
license=('LGPL')
groups=('gstreamer0.10-plugins')
makedepends=('gstreamer0.10-base>=0.10.34')
depends=('cairo' 'gdk-pixbuf2' "gstreamer0.10-good=${pkgver}" 'jack' 'libavc1394' 'libdv' 'libiec61883' 'libjpeg' 'libpng' 'libpulse' 'libshout' 'libxdamage' 'taglib' 'v4l-utils' 'wavpack')
provides=("gstreamer0.10-good-plugins=${pkgver}")
conflicts=('gstreamer0.10-good-plugins')
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.gz)
md5sums=('c6aa1ef921d485a28d0703b791963fc9')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  sed -i '/GST_LOG_OBJECT (pool->v4l2elem, "  input:     %u", ret->vbuffer.input);/d' sys/v4l2/gstv4l2bufferpool.c
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
    --disable-hal \
    --disable-esd \
    --disable-oss \
    --disable-aalib \
    --disable-gconf \
    --disable-libcaca \
     --disable-gst_v4l2 \
    --with-package-name="GStreamer Good Plugins Slim (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
}

