# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=isse
pkgver=0.2.0
pkgrel=1
pkgdesc="An Interactive Source Separation Editor"
arch=('i686' 'x86_64')
url="http://isse.sourceforge.net/"
license=('GPL3')
depends=('gcc-libs' 'freetype2' 'fftw' 'alsa-lib' 'libxext')
optdepends=('jack: JACK audio backend')
source=("http://sourceforge.net/code-snapshots/git/i/is/isse/code.git/isse-code-a1a9c5229e288009e467d88a0baf6d53cb072f4f.zip"
        "https://github.com/julianstorer/JUCE/archive/2.1.5.tar.gz"
        "http://bitbucket.org/eigen/eigen/get/3.2.0.tar.gz")
noextract=("${source[1]##*/}" "${source[2]##*/}")
md5sums=('768fe3d75ef87588ceefb31657130119'
         '852dd584cb772f34ffaa67b8910536c8'
         '9559c34af203dde5f3f1d976d859c5b3')

prepare() {
  cd $pkgname-*

  # extract JUCE
  tar zxvf ../${source[1]##*/} -C SDKs/juce --strip=1

  # extract Eigen
  tar zxvf ../${source[2]##*/} -C SDKs/eigen --strip=1

  # set right JUCE source relative path
  sed -i '/juce\/modules/s/\.\.\/\.\./SDKs/2' builds/Linux/Makefile
}

build() {
  cd $pkgname-*/builds/Linux
  make CONFIG=Release
}

package() {
  cd $pkgname-*

  # bin
  install -Dm755 builds/Linux/build/ISSE "$pkgdir/usr/bin/isse"

  # samples
  find resources -type d -exec install -d {} ${pkgdir}/usr/share/isse/{} \;
  find resources -type f -exec install -Dm644 {} ${pkgdir}/usr/share/isse/{} \;

  # manual
  install -Dm644 docs/manual/manual.pdf "$pkgdir/usr/share/doc/isse/manual.pdf"
}
