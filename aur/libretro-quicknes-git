# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-quicknes-git
pkgver=214.bc2846d
pkgrel=2
pkgdesc="libretro implementation of QuickNES. (Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/QuickNES_Core"
license=('GPL')
makedepends=('git')

_gitname=QuickNES_Core
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/quicknes_libretro.info")
md5sums=('SKIP'
         'adb2c517ed539f44398fea1cd6847234')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/libretro"
  make
}

package() {
  install -Dm644 "${_gitname}/libretro/quicknes_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-quicknes.so"
  install -Dm644 "quicknes_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-quicknes.info"
}
