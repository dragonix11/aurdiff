#Maintainer : Olivier Le Moal <mail@olivierlemoal.fr>
#Contributor: Alexandre Chataignon <alexandre@chataignon.net>
#Contributor: Sebastien Duquette <ekse.0x@gmail.com>
#Contributor: Marc Poiroud <marci1@archlinux.fr>
pkgname=verbiste
pkgver=0.1.40
pkgrel=1
pkgdesc="Generates conjugations of french and italian verbs"
url="http://sarrazip.com/dev/verbiste.html"
license="GPL"
depends=('libxml2' 'gtk2')
makedepends=('perlxml' 'libgnomeui')
arch=('i686' 'x86_64')
install=${pkgname}.install
source=("http://perso.b2b2c.ca/sarrazip/dev/${pkgname}-${pkgver}.tar.gz")
md5sums=('e5801736ba08818dbb6edc61b7e214db')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-gtk-app
# ./configure --prefix=/usr # console support only
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Modify location for .desktop file
  install -Dm 644 "$srcdir/$pkgname-$pkgver/src/gnome/verbiste.desktop" \
		  "$pkgdir/usr/share/applications/verbiste.desktop"
  sed -e 's/Exec=verbiste/Exec=verbiste-gtk/g' -i "$pkgdir/usr/share/applications/verbiste.desktop"
}
