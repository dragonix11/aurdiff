# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=flash3kyuu_deband
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130919.6b10545
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/SAPikachu/${_plug}"
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
license=('GPL3')
depends=('vapoursynth')
makedepends=('git' 'waf')
source=("${_plug}::git://github.com/SAPikachu/${_plug}.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  waf configure --prefix=/usr --libdir=/usr/lib/vapoursynth

}

build() {
  cd "${_gitname}"

  waf build ${MAKEFLAGS}
}

package(){
  cd "${_gitname}"
  waf install --destdir="${pkgdir}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
