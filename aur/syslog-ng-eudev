# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

 _name="syslog-ng"

pkgname=${_name}-eudev
pkgver=3.4.3
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities with disabled systemd"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('eudev-base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('awk' 'eventlog' 'glib2' 'libcap' 'openssl' 'eudev')
makedepends=('flex' 'pkg-config' 'python2')
optdepends=('logrotate: for rotating log files'
	    'syslog-ng-openrc: syslog-ng openrc initscript')
provides=("${_name}")
conflicts=("${_name}")
options=('!libtool')
backup=('etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/logrotate.d/syslog-ng')
#install=syslog-ng.install
source=("http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${_name}_$pkgver.tar.gz"
        'syslog-ng.conf'
        'syslog-ng.logrotate')

sha256sums=('8a00c342036109e6cc1d441aaed12438134bc884ca3492c36902ff9120a107ee'
            '0be3d8b5b5de79392a67ed6a074e562b542beab117cd608529a6a56af678b6f8'
            '93c935eca56854011ea9e353b7a1da662ad40b2e8452954c5b4b5a1d5b2d5317')

build() {
  cd "${_name}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --localstatedir=/var/lib/syslog-ng \
    --datadir=/usr/share/syslog-ng \
    --with-pidfile-dir=/run \
    --disable-spoof-source \
    --enable-ipv6 \
    --disable-systemd

  make
}

package() {
  make -C "${_name}-$pkgver" DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"

}

