# Maintainer: Jens Adam <jra@byte.cx>

pkgname=icli
pkgver=0.46
pkgrel=1
pkgdesc='Icinga Command Line Interface'
url='http://finalrewind.org/projects/icli/'
license=('custom:WTFPL')
arch=('any')
depends=('perl-list-moreutils' 'perl-term-size')
makedepends=('perl-test-compile' 'perl-test-pod')
optdepends=('icinga')
options=('!emptydirs')
source=("http://finalrewind.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cf63d74fe99288b8b6c77e9e5fe258fc9d7c6546a7fbc5c0cae04fe1ed4d5106')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  perl Build.PL installdirs=vendor destdir="${pkgdir}"
  ./Build
  sed -i 's,/var/.*/icinga/,/var/spool/icinga/,' blib/script/icli
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./Build test
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./Build install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
