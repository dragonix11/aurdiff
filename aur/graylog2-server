# Maintainer: Spider.007 <aur@spider007.net>

pkgname=graylog2-server
pkgver=0.13.0_rc.1
pkgrel=2
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in ElasticSearch and MongoDB"
arch=(any)
url="http://graylog2.org/"
license=('GPL')
depends=(java-runtime elasticsearch)
optdepends=(
	"mongodb"
	"zeromq"
	"apache-activemq"
	"rabbitmq"
)
backup=(
	etc/graylog2.conf
	etc/graylog2.d/rules/graylog2.drl
	etc/graylog2-elasticsearch.yml
)
source=(
	https://github.com/Graylog2/graylog2-server/releases/download/${pkgver/_/-}/$pkgname-${pkgver/_/-}.tar.gz
	https://github.com/Graylog2/graylog2-server/raw/aa3ab41671cea39545b499f03b2c454546e3f0de/misc/graylog2.drl
	graylog2-server.sh
	graylog2.service
)

package() {
	cd "$srcdir/$pkgname-${pkgver/_/-}"

	install -Dm755 "$srcdir/graylog2-server.sh" "$pkgdir/usr/bin/graylog2-server"
	install -Dm644 "$srcdir/graylog2.drl" "$pkgdir/etc/graylog2.d/rules/graylog2.drl"
	install -Dm644 graylog2-server.jar "$pkgdir/usr/lib/graylog2-server.jar"
	install -Dm644 "$srcdir/graylog2.service" "$pkgdir/usr/lib/systemd/system/graylog2.service"
	
	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	cp README.markdown COPYING "$pkgdir/usr/share/doc/$pkgname"

	sed -e 's|mongodb_host.*|mongodb_host = 127.0.0.1|' \
		-e 's|mongodb_useauth.*|mongodb_useauth = false|' \
		graylog2.conf.example > "$pkgdir/etc/graylog2.conf"
	install -Dm644 elasticsearch.yml.example "$pkgdir/etc/graylog2-elasticsearch.yml"
}

sha256sums=('fbbb4e9e4314c921008661822f7c8965d3d453b449d968dd54000a400343fb02'
            '837062ba2ca00f3c93855fbae9d49d7b90ce21b63383a25b77a519d181c1c7fc'
            '445e76c32b33e0fed069dd1a92f686fc105293d6ac112e761ed43b21a55e32d8'
            '44a0ef51c608a50126bc78cf74e6580d2b5ca82997d2a7eebf9fb0e90cef15b9')

