# Maintainer: Bogdan Szczurek <thebodzio@gmail.com>

_gitname=udis86

pkgname=$_gitname-git
pkgver=v1.7.2.45.g5a7e0fb
pkgrel=1
pkgdesc="Disassembler library for x86"
arch=('i686' 'x86_64')
url="http://udis86.sourceforge.net/"
license=('BSD')
depends=('python2')
makedepends=('git')
provides=('udis86')
conflicts=('udis86' 'udis86-bin')
source=(git://github.com/vmt/$_gitname.git)
md5sums=(SKIP)

pkgver() {
  cd $srcdir/$_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  # Building...
  ./autogen.sh
  ./configure --prefix=/usr --with-python=/usr/bin/python2
  make
}

package() {
  cd $_gitname
  make DESTDIR=$pkgdir install

  # Taking care of the license
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
