# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2013.08
pkgrel=1
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2013.08')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/rakudo-star-$pkgver.tar.gz)
sha512sums=('34c1750a3e1c71c5d88512ae49495ebe3c5848cba59b56a9883a1d6d10b4420f295f4e7ab41994cdf94c6986fb582e8c97e7da58b0c7e9ec4e1e67d34353f288')

build() {
  cd "$pkgname-star-$pkgver"
  perl Configure.pl --prefix=/usr \
                    --with-nqp=/usr/bin/nqp
  make
}

package() {
  cd "$pkgname-star-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}
