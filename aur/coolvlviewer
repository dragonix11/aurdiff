# Maintainer: GordonGR <gordongr@freemail.gr>

pkgname=coolvlviewer
pkgver=1.26.8.35
pkgrel=1
_pngver=1.5.1
_pcrever=7.6
pkgdesc="A third-party viewer for Second Life (C) (secondlife) and OpenSim (opensimulator) grids. ('VL' stands for virtual life, formerly known as 'Cool SL Viewer'; native 32bit, binary)"
url="http://sldev.free.fr"
license=('custom')
[ "$CARCH" = "i686"   ] && depends=('apr-util' 'gtk2' 'libgl' 'libidn' 'libjpeg-turbo' 'mesa' 'nss' 'sdl' 'glu' 'pangox-compat')
[ "$CARCH" = "i686"   ] && optdepends=('libpulse: for PulseAudio support' 'alsa-lib: for ALSA support' 'nvidia-utils: for NVIDIA support' 'flashplugin: for inworld Flash support' 'gstreamer0.10: for video support, may need good, bad and ugly plugins' 'lib32-freealut: for OpenAL support')
[ "$CARCH" = "x86_64" ] && depends=('apr-util' 'lib32-gtk2' 'lib32-libgl' 'lib32-libidn' 'lib32-libjpeg-turbo' 'lib32-mesa' 'lib32-nss' 'lib32-sdl' 'lib32-glu' 'lib32-pangox-compat')
[ "$CARCH" = "x86_64"   ] && optdepends=('lib32-libpulse: for PulseAudio support' 'lib32-alsa-lib: for ALSA support' 'lib32-nvidia-utils: for NVIDIA support' 'lib32-flashplugin: for inworld Flash support' 'lib32-gstreamer0.10: for video support, may need good, bad and ugly plugins' 'lib32-freealut: for OpenAL support')
arch=('i686' 'x86_64')
conflicts=('coolvlviewer-legacy' 'coolvlviewer-experimental')
install=coolvlviewer.install
source=("http://sldev.free.fr/binaries/CoolVLViewer-${pkgver}-Linux-x86-Setup"
		"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz"
		"http://sourceforge.net/projects/pcre/files/pcre/${_pcrever}/pcre-${_pcrever}.zip"
        "coolvlviewer.desktop"
        "coolvlviewer.launcher")
        
md5sums=('6d5c10bef42cc0b675e2db358d5ba8d4'
         '35455234375f1adff8083f408a099e3a'
         '2ed6326a64dcf138582939fcb46e2aa3'
         '7c5063c21bbbe918020e03ded2bed19a'
         'fd78de1f6c1333a5120ece89873515e0')

build() {
cd $srcdir
# Run the installer
chmod +x CoolVLViewer-${pkgver}-Linux-x86-Setup
./CoolVLViewer-${pkgver}-Linux-x86-Setup --mode silent --destination $srcdir/coolvlviewer/
    
# build compatible lib32-libpng
msg "Building libpng..."

if [ "$CARCH" = "x86_64" ] ; then
	export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
fi

cd "${srcdir}/libpng-${_pngver}"
./configure
make

#build compatible lib32-pcre
msg "Building pcre..."

if [ "$CARCH" = "x86_64" ] ; then
	export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
fi

cd "${srcdir}/pcre-${_pcrever}"
./configure --enable-utf --enable-unicode-properties --enable-pcre16 --enable-pcre32 --enable-jit
make
  
}

package(){
# Install Desktop File
install -D -m644 $srcdir/coolvlviewer.desktop \
	$pkgdir/usr/share/applications/coolvlviewer.desktop
 
# Install Icon File
install -D -m755 $srcdir/coolvlviewer/cvlv_icon.png \
	$pkgdir/usr/share/pixmaps/clvl_icon.png

# Install Launcher
install -D -m755 $srcdir/../coolvlviewer.launcher \
	$pkgdir/usr/bin/coolvlviewer
    
# Install License
install -D -m644 $srcdir/coolvlviewer/licenses.txt \
	$pkgdir/usr/share/licenses/$pkgname/LISENSE
    
# Move Data to Destination Directory
install -d $pkgdir/opt/
mv coolvlviewer/ $pkgdir/opt/
    
# Change Permissions of files to root:games
chown -R root:games $pkgdir/opt/coolvlviewer
chmod -R g+rw $pkgdir/opt/coolvlviewer

# Make Binary Group-Executable
chmod g+x $pkgdir/opt/coolvlviewer/cool_vl_viewer
    
# Install libpng we built
install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
	$pkgdir/opt/coolvlviewer/lib
cd $pkgdir/opt/coolvlviewer/lib
ln -s libpng15.so.15.1.0 libpng15.so.15
ln -s libpng15.so.15.1.0 libpng15.so

# Install pcre we built

rm $pkgdir/opt/coolvlviewer/lib/libpcre.so*
install -D -m755 $srcdir/pcre-${_pcrever}/.libs/libpcre.so.0.0.1 \
	$pkgdir/opt/coolvlviewer/lib
install -D -m755 $srcdir/pcre-${_pcrever}/.libs/libpcrecpp.so.0.0.0 \
	$pkgdir/opt/coolvlviewer/lib
cd $pkgdir/opt/coolvlviewer/lib
ln -s libpcre.so.0.0.1 libpcre.so.0
ln -s libpcre.so.0.0.1 libpcre.so
ln -s libpcrecpp.so.0.0.0 libpcrecpp.so.0
ln -s libpcrecpp.so.0.0.0 libpcrecpp.so

}
