# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Patch by Procyon

_pkgbasename=libx11
pkgname=lib32-${_pkgbasename}-nokeyboardgrab
pkgver=1.6.1
pkgrel=2
pkgdesc="X11 client-side library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=("${_pkgbasename}" 'lib32-libxcb')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto' 'gcc-multilib')
provides=("lib32-${_pkgbasename}=${pkgver}")
options=('!libtool')
license=('custom:XFREE86')
source=("${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2")
sha256sums=('10a54fc16b58b4f5a5eed4d080c357a82fd2f42d09af625c1f5df50650701892')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libX11-${pkgver}"

  # PATCH KEYBOARD GRAB PREVENTION
  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c

  # Mouse grab prevention. Doesn't work with some games (e. g., Urban Terror).
  #sed -i '/xGrabPointerReply/i return (GrabSuccess);' src/GrPointer.c

  ./configure --prefix=/usr --disable-static --disable-xf86bigfont \
      --libdir=/usr/lib32 --disable-specs
  make
}

check() {
  cd "${srcdir}/libX11-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}/usr/"{include,share}

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
