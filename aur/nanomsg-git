# Maintainer: KokaKiwi <admin@kokaelkiwi.eu>

_pkgname=nanomsg
pkgname=nanomsg-git
pkgver=0.1.alpha.13.g82bb45b
pkgrel=13
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=('MIT')
source=('git://github.com/250bpm/nanomsg.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('git')
provides=('nanomsg')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr ..
  make
}

check() {
  cd "${srcdir}/${_pkgname}/build"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
