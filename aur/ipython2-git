# $Id$
# Maintainer: Joschka Thurner <Joschka.Thurner@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=ipython2-git
_gitname="ipython"
pkgver=11756.a886d6e
pkgrel=1
pkgdesc="An enhanced Interactive Python 2 shell in its current version from github."
arch=('any')
url="http://ipython.org"
license=('Modified BSD')
makedepends=('python2' 'git') # for setup.py
depends=('python2' 'python2-distribute')
optdepends=("python2-nose: if you want to run IPython's test suite"
            "python2-pyqt: for ipython qtconsole"
            "python2-sip: for ipython qtconsole"
            "python2-pygments: for ipython qtconsole"
            "python2-pyzmq: for ipython qtconsole / parallel computing"
            "python2-tornado: for ipython notebook"
            "python2-jinja: for ipython notebook")              
provides=('ipython2')
conflicts=('ipython2')
source="git://github.com/ipython/ipython.git"
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_gitname

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  find "$pkgdir/" -name '*.pyc' -delete
  
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  mv ipcluster.1    ipcluster2.1
  mv ipcontroller.1 ipcontroller2.1
  mv ipengine.1     ipengine2.1
  mv iplogger.1     iplogger2.1
  mv ipython.1      ipython2.1
  mv irunner.1      irunner2.1
  mv pycolor.1      pycolor2.1
  cd "$pkgdir/usr/bin/"
  mv ipcluster         ipcluster2
  mv ipcontroller      ipcontroller2
  mv ipengine          ipengine2
  mv iplogger          iplogger2
  mv iptest            iptest2
  mv ipython           ipython2
  mv irunner           irunner2
  mv pycolor           pycolor2
}
