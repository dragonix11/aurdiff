# Contributor: Jussi Timperi <gmail.com: jussi.timperi>
# Maintainer:  Jesse Jaara   <gmail.com: jesse.jaara>

pkgname=voikko-libreoffice
pkgver=3.4.1
pkgrel=4
pkgdesc="Finnish spellchecker, grammar checker and hyphenator extension for libreoffice"
arch=('i686' 'x86_64')
url="http://voikko.sourceforge.net"
license=('GPL3')
depends=('libvoikko' 'libreoffice-common')
makedepends=('zip' 'libreoffice-sdk')
install=voikko-libreoffice.install
source=("http://www.puimula.org/voikko-sources/libreoffice-voikko/libreoffice-voikko-${pkgver}.tar.gz"
libreoffice-4.1.patch::https://github.com/voikko/libreoffice-voikko/commit/b7e100bea5bcea3549ac87cd96c95ee4833757b3.patch)

prepare() {
  cd "${srcdir}/libreoffice-voikko-${pkgver}"

  patch -Np1 -i ../libreoffice-4.1.patch
  sed 's|LINK_LIBS += $(STLPORTLIB)||g' -i Makefile
}

build() {
  cd "${srcdir}/libreoffice-voikko-${pkgver}"

  . /usr/lib/libreoffice/sdk/setsdkenv_unix.sh

  make oxt
}

package() {
  cd "${srcdir}/libreoffice-voikko-${pkgver}/build" 

  mkdir -p "${pkgdir}/usr/lib/libreoffice/share/extensions/install"
  cp voikko.oxt "${pkgdir}/usr/lib/libreoffice/share/extensions/install/"
}

md5sums=('8fa9517366250e3316a7639567e78e35'
         '8407cbb0c7fc68732203a3719c2d6d82')
