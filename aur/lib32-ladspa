# Maintainer: Lara Maia <lara@craft.net.br>

pkgname=lib32-ladspa
pkgver=1.13
pkgrel=5
pkgdesc="Linux Audio Developer's Simple Plugin API (LADSPA) (32 bit)"
arch=("x86_64")
license=('LGPL')
url="http://www.ladspa.org/"

depends=('lib32-gcc-libs' "${pkgname##*-}")
makedepends=('gcc-multilib')

source=("http://www.ladspa.org/download/${pkgname##*-}_sdk_$pkgver.tgz"
	'hardcode-path.patch'
	'fix-lib32-makefile.patch')
	
md5sums=('671be3e1021d0722cadc7fb27054628e'
         '499a75673d1ea34df1b30452bd5fc346'
         '5ebaa919fdcaa07e039c0d31abca670b')

build() {
  cd "$srcdir"/${pkgname##*-}_sdk/src

  # Set lib32 needs in search.c and makefile
  patch -Np1 -i "$srcdir"/hardcode-path.patch
  patch -Np1 -i "$srcdir"/fix-lib32-makefile.patch

  make targets
}

package() {
  cd "$srcdir"/${pkgname##*-}_sdk/src
  
  make INSTALL_PLUGINS_DIR="$pkgdir"/usr/lib32/${pkgname##*-}/ \
       INSTALL_INCLUDE_DIR="$pkgdir"/usr/include/ \
       INSTALL_BINARY_DIR="$pkgdir"/usr/bin/ install
       
  rm -rf "$pkgdir"/usr/{bin,include}
}
