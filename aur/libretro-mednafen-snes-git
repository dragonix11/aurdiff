# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-mednafen-snes-git
pkgver=334.01fdcee
pkgrel=2
pkgdesc="libretro implementation of Mednafen SNES. (Super Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mednafen-libretro"
license=('GPL')
makedepends=('git')

_gitname=mednafen-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mednafen_snes_libretro.info")
md5sums=('SKIP'
         'c08f70c617d7313d745c1d84cb6d7203')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make core=snes
}

package() {
  install -Dm644 "${_gitname}/mednafen_snes_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mednafen-snes.so"
  install -Dm644 "mednafen_snes_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mednafen-snes.info"
}
