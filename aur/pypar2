# Maintainer: ValHue <vhuelamo at gmail dot com>
# Contributor: shamrok <szamrok at gmail dot com>

pkgname=pypar2
pkgver=1.4
pkgrel=4
pkgdesc="Graphical frontend (PyGTK) for the Linux par2 utility"
url="http://pypar2.silent-blade.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('pygtk' 'libglade' 'vte' 'par2cmdline')
source=("http://pypar2.silent-blade.org/uploads/Main/${pkgname}-${pkgver}.tar.gz")
md5sums=('d77ea7c0ff88209d994c5723c98d00a5')

build() {
    cd "${srcdir}/PyPar2-${pkgver}"

    # python2 fix
    sed -i 's_PYTHON ?= python_PYTHON ?= python2_' Makefile
    for file in $(find . -name '*.py' -print); do
        sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
    done
}

package() {
    cd "${srcdir}/PyPar2-${pkgver}"
    make prefix=${pkgdir}/usr install
    rm ${pkgdir}/usr/bin/pypar2
    ln -s /usr/share/pypar2/src/main.py ${pkgdir}/usr/bin/pypar2
} 
