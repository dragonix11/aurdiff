# Maintainer:  mortdeus <mortdeus@gocos2d.org>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>


pkgname=htmldoc
pkgver=1.8.27
pkgrel=2
pkgdesc="Produce PDF or Postscript from HTML documents including TOCs and Indices"
arch=(i686 x86_64)
url="http://www.htmldoc.org"
license=('GPL2')
depends=('libjpeg' 'shared-mime-info'  'libpng' 'openssl' 'fltk' 'libxpm')
conflicts=('htmldoc-patched' 'htmldoc-svn')
source=("http://www.msweet.org/files/project1/htmldoc-1.8.27-source.tar.gz"
 	'cve-2009-3050.patch'
        'fortify-fail.patch'
	'libpng15.patch'
	'manpage-fixes.patch'
        'useful-build-info.patch')

md5sums=('b284888cc2285b32e96cbce620b927c1'
         '90d0439be051df4b24dbe436d779559c'
	 '2001b7f539d530bf222378bd181b3085'
	 '222f7578af51fca45c15b28af6c917f5'
	 '1ce15f3f11e4dd2d7738c8c210484083'
	 'c82ef7afb9a03dbeb42971e466c4d1e3')
changelog=Changelog
install=$pkgname.install
prepare(){
	cd $srcdir/$pkgname-$pkgver
	 # apply patches
	 for patch in ../*.patch ; do
		 patch -Np1 -i $patch
	 done
}
build() {
  cd "$srcdir"
     
 rm -rf $srcdir/build

 cp -ar "$srcdir/$pkgname-$pkgver" $srcdir/build

  #
  # BUILD
  #
  
  cd $srcdir/build
  [ -x configure ] || autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/build"

  make install \
	prefix=${pkgdir}/usr \
	bindir=$pkgdir/usr/bin \
	datadir=$pkgdir/usr/share \
	datarootdir=$pkgdir/usr/share \
	docdir=$pkgdir/usr/share/doc/htmldoc \
	exec_prefix=$pkgdir/usr \
	mandir=$pkgdir/usr/share/man \
	prefix=$pkgdir/usr \
	sysconfdir=$pkgdir/etc

 # fix freedesktop stuff
  install -Dm 644 desktop/htmldoc-128.png \
    ${pkgdir}/usr/share/pixmaps/htmldoc.png
  echo "MimeType=application/vnd.htmldoc-book;" >> desktop/htmldoc.desktop
  sed -i 's|X-Red-Hat.*$||' desktop/htmldoc.desktop
  install -Dm 644 desktop/htmldoc.desktop \
    ${pkgdir}/usr/share/applications/htmldoc.desktop
}
