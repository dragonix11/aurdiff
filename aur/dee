# Maintainer: 
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=dee
pkgver=1.2.7
pkgrel=1
pkgdesc='Library to provide objects allowing to create Model-View-Controller type programs across DBus'
arch=('i686' 'x86_64')
url='https://launchpad.net/dee'
license=('LGPL')
depends=('glib2' 'icu')
makedepends=('python2' 'gobject-introspection' 'vala')
options=('!libtool')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('b92f27f0a99cac24c2128880601bb7d7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Dee.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Dee.py"
}

# vim:set ts=2 sw=2 et:
