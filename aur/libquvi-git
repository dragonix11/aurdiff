# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

# NOTE: Build will fail if old headers are present on the system

pkgname=libquvi-git
pkgver=0.9.3
pkgrel=2
pkgdesc='Cross-platform library for parsing flash media stream URLs. (GIT version)'
arch=('i686' 'x86_64')
url="http://quvi.sourceforge.net/"
license=('LGPL')
depends=('libquvi-scripts-git' 'curl' 'lua51' 'libproxy' 'libgcrypt' 'glib2')
makedepends=('git' 'asciidoc')
provides=('libquvi')
conflicts=('libquvi')
options=('!libtool')
source=('git://repo.or.cz/libquvi.git#branch=next'
        'am_subdir_objects.patch')
sha256sums=('SKIP'
            '28c2a9464017a93f00010ad1efcd20fe31ddc3687ad3b09f9785b8ffdacf7b1c')

pkgver() {
  cd 'libquvi'
  git describe | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd 'libquvi'
  patch -p1 < "$srcdir/am_subdir_objects.patch"
}

build() {
  cd 'libquvi'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd 'libquvi'

  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh sw=2 ts=2 et
