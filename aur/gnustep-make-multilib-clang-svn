# Maintainer: X0rg

pkgname=gnustep-make-multilib-clang-svn
_svnname=gnustep-make
pkgver=37254
pkgrel=2
pkgdesc="The GNUstep make package for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('GPL3')
groups=('gnustep-multilib-clang-svn')
depends=('lib32-llvm')
makedepends=('svn' 'clang' 'gcc-multilib')
conflicts=('gnustep-make' 'gnustep-make-clang-svn' 'lib32-gnustep-make-clang-svn')
provides=('gnustep-make-clang-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/tools/make/trunk/"
	'arch32'
	'arch64')
md5sums=('SKIP'
         '4f27e47b8b302d8f4e27f2c0e83892b1'
         '729010406aaee9c16f8163edba56a19a')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Make a clone of $_svnname"
  cp -Rv "$_svnname" "$_svnname-32"

  msg2 "Copy new file system layouts for Arch..."
  cp -v "$srcdir/arch32" "$_svnname-32/FilesystemLayouts/"
  cp -v "$srcdir/arch64" "$_svnname/FilesystemLayouts/"
}

build() {
  cd "$_svnname-32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"
  msg2 "Run 'configure' (32-bit)..."
  CC="clang -m32" CXX="clang++ -m32" ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc/GNUstep --with-layout=arch32

  # 64-bit build
  cd "$srcdir/$_svnname"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config"
  msg2 "Run 'configure' (64-bit)..."
  CC="clang" CXX="clang++" ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc/GNUstep --with-layout=arch64
}

package() {
  cd "$_svnname-32"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install

  msg2 "Use new file system layouts (32-bit)..."
  sed -i 's|GNUSTEP_CONFIG_FILE=/etc/GNUstep/GNUstep.conf|GNUSTEP_CONFIG_FILE=/etc/GNUstep/GNUstep32.conf|g' "$pkgdir/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  sed -i 's|GNUSTEP_CONFIG_FILE=/etc/GNUstep/GNUstep.conf|GNUSTEP_CONFIG_FILE=/etc/GNUstep/GNUstep32.conf|g' "$pkgdir/usr/bin/linux_386/"*
  mv -v "$pkgdir/etc/GNUstep/GNUstep.conf" "$pkgdir/etc/GNUstep/GNUstep32.conf"
  echo -e "# Added by GNUstep Make package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"

  # 64-bit install
  cd "$srcdir/$_svnname"
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install
  echo -e "# Added by GNUstep Make package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
}
