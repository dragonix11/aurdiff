# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=fabla-git
pkgver=207.5f2cb26
pkgrel=1
pkgdesc="A sampler LV2 plugin"
arch=('i686' 'x86_64')
url="http://openavproductions.com/fabla/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'cairomm' 'libsndfile' 'ntk-git')
provides=('fabla')
conflicts=('fabla')
source=("$pkgname::git+https://github.com/harryhaaren/openAV-Fabla.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname

  # plugin
  install -Dm644 dsp/fabla.ttl \
    "$pkgdir/usr/lib/lv2/fabla.lv2/fabla.ttl"
  install -Dm644 fabla.lv2/* "$pkgdir/usr/lib/lv2/fabla.lv2"

  # presets
  cp -a presets/* "$pkgdir/usr/lib/lv2"
}
