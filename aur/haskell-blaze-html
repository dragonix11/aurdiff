# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Aleksandar Micovic <metaleks@gmail.com>
_hkgname=blaze-html
pkgname=haskell-blaze-html
pkgver=0.6.1.1
pkgrel=2
pkgdesc="A blazingly fast HTML combinator library for Haskell"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=()
depends=('ghc' 'haskell-blaze-builder<0.4' 'haskell-blaze-markup<0.6' 'haskell-bytestring-show' 'haskell-text>=0.11.0.5')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=$pkgname.install
md5sums=('f9eda0a990cda190538b9e6ff73b05f3')

build() {
  cd "${srcdir}/${_hkgname}-${pkgver}"
  runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
  cd "${srcdir}/${_hkgname}-${pkgver}"
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/${_hkgname}"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
