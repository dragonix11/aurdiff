# Contributor: Christian Sturm <reezer@reezer.org>
pkgname=perl-test-tcp
pkgver=2.00
pkgrel=1
pkgdesc="testing TCP program"
arch=(any)
url="https://metacpan.org/release/Test-TCP"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.8.0' 'perl-test-sharedfork>=0.19')
options=(!emptydirs)
install=
source=(http://cpan.metacpan.org/authors/id/T/TO/TOKUHIROM/Test-TCP-$pkgver.tar.gz)
md5sums=('48e496d9550e6f78eb0d622d6eea909b')

build() {
  cd "$srcdir/Test-TCP-$pkgver"

  # install module in vendor directories.
  export  PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
          PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
          PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
          MODULEBUILDRC=/dev/null
  perl Build.PL
  ./Build || return 1

}

check() {
  cd "$srcdir/Test-TCP-$pkgver"
  ./Build test
}

package() {
  cd "$srcdir/Test-TCP-$pkgver"

  ./Build install DESTDIR="$pkgdir/" || return 1

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

# vim:set ts=2 sw=2 et:
