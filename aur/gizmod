# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=gizmod
pkgver=3.5
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Daemon for controlling input devices"
url="http://gizmod.sourceforge.net"
license="APACHE"
depends=('alsa-lib' 'boost' 'libvisual' 'libxext' 'libsm' 'python2')
makedepends=('gawk' 'boost-build' 'cmake' 'gcc')
install=gizmod.install
source=("http://sourceforge.net/projects/gizmod/files/$pkgname/3.5/$pkgname-$pkgver.tar.bz2")
md5sums=('10e88f8d54fed326ed0e6b8614d99793')


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir -p build
	cd build
	
	cmake -D CMAKE_BUILD_TYPE=Release \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D DATA_INSTALL_DIR=/usr/share/$pkgname \
		-D PLUGIN_INSTALL_DIR=/usr/lib/$pkgname \
		-D SYSCONF_INSTALL_DIR=/etc ..
	
	# Manually change paths since cmake doesn't seem to read the env vars
	find -type f -exec sed -ie 's|/usr/etc|/etc|g' {} \;
	
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
	
	install -d "${pkgdir}"/etc/udev/rules.d
	install -m644 "${srcdir}"/10-input-gizmod.rules \
		"${pkgdir}"/etc/udev/rules.d
}
