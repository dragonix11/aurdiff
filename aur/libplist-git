# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>
# Contributor: Adam Eberlin < ae at adameberlin dot com >

pkgname=libplist-git
epoch=1
pkgver=1.10.3.g95d1d0c
pkgrel=1
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="http://www.libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'libxml2')
makedepends=('gcc' 'git' 'make' 'cmake' 'swig')
provides=('libplist')
conflicts=('libplist')

source=("git://git.sukimashita.com/libplist.git")
md5sums=('SKIP')

pkgver() {
	cd libplist

	git describe --long | sed -r -e 's|-|.|g'
}

build() {
	cd libplist

	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DPYTHON_EXECUTABLE=/usr/bin/python2 \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd libplist

	make DESTDIR="${pkgdir}" install
}
