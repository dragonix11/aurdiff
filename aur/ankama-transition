# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=ankama-transition
pkgver=3.7.20131013
pkgrel=1
pkgdesc="Updater for Ankama games Dofus and Wakfu"
arch=('i686' 'x86_64')
url="http://www.ankama.com"
license=('custom')
depends=('qt4' 'polkit')
source=(http://dl.ak.ankama.com/games/linux/ankama-transition.tar.gz
        LATEST_REVISION)
md5sums=('d2092db0916d784f53905187e6eff9ba'
         'beaeabaac700dc80646cece47d8eb896')

touch LATEST_REVISION

pkgver() {
  cd "$srcdir/"
  _archdir=amd64/opt/ankama/transition/

  # find the revision string of the packaged binary
  strings "$_archdir/liblibupdater.so.1" \
    | grep '^[0-9a-f]\{7\}$' \
    | head -n 1 \
    > REVISION

  if cmp REVISION LATEST_REVISION
  then
    # if the revision is the same as the latest known revision, use the existing pkgver
    printf "%s" "$pkgver"
  else
    # otherwise update pkgver
    cat REVISION > LATEST_REVISION
    printf '%s' "$(cat "$_archdir/VERSION").$(date +%Y%m%d)"
  fi
}

prepare() {
  cd "$srcdir/"

  ln -sf x86 i686
  ln -sf amd64 x86_64
  sed -i 's:$$${installation_path}:/opt/ankama:' 'usr/share/polkit-1/actions/com.ankama.transitionupdateservice.policy'
}

package() {
  cd "$srcdir/"
  cp -r etc usr $CARCH/opt "$pkgdir/"
  install -D opt/ankama/transition/licences/README.txt $pkgdir/usr/share/licenses/ankama-transition/licenses.txt
}
