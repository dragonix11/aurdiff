# Maintainer: kfgz <kfgz at interia pl>
# Contributor: Kaspar Bumke <kaspar dot bumke at gmail dot com>
# Contributor: maleadt <tim dot besard at gmail dot com>

pkgname="turbovnc"
pkgdesc="An optimized version of TightVNC"
pkgver=1.2
pkgrel=3
arch=('i686' 'x86_64')
url="http://virtualgl.org"
license=('GPL')
makedepends=('libjpeg-turbo' 'cmake' 'rsync')
depends=('libxaw' 'libxt' 'libxcursor')
md5sums=('b8900785792f8b0d025c1f8b52fff14b'
         'a8a81422da5bfdf4b0a6f63fd7dfe18b')
conflicts=('turbovnc-bin' 'tigervnc' 'tigervnc-svn' 'tightvnc')
replaces=('turbovnc-bin')
source=(${pkgname}-${pkgver}.tar.gz::http://sourceforge.net/projects/virtualgl/files/TurboVNC/${pkgver}/${pkgname}-${pkgver}.tar.gz/download
        vncserver.service)

build() {
  mkdir "${srcdir}"/${pkgname}-${pkgver}/build
  cd "${srcdir}"/${pkgname}-${pkgver}/build
  cmake -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so -DCMAKE_INSTALL_PREFIX=/usr -DTVNC_DOCDIR=/usr/share/doc/${pkgname} ../
  make all xserver
}
 
package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install xserver-install
  mv "${pkgdir}"/usr/README.txt "${pkgdir}"/usr/share/doc/turbovnc/README.txt
  install -dm755 "${pkgdir}"/etc
  mv "${pkgdir}"/usr/etc "${pkgdir}"/
  rm -r "${pkgdir}"/etc/init.d
  install -D -m644 "${srcdir}"/vncserver.service "${pkgdir}"/usr/lib/systemd/system/vncserver.service
}
