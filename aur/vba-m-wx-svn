# Maintainer: Tilman Blumenbach <tilman+aur AT ax86 DOT net>
pkgname=vba-m-wx-svn
_svndir=vba-m-trunk
pkgver=1217
pkgrel=1
pkgdesc="Improved (Super) Game Boy Color/Advance emulator - wxWidgets GUI, SVN version"
arch=(i686 x86_64)
url="http://vba-m.com/"
license=('GPL2')
depends=(zlib libpng mesa sdl sfml1.6 openal wxgtk
  hicolor-icon-theme desktop-file-utils xdg-utils)
makedepends=(cmake subversion nasm imagemagick)
conflicts=(vba-m-wx)
install=vba-m.install
source=("${_svndir}::svn://svn.code.sf.net/p/vbam/code/trunk"
  sfml-build-fixes.patch)
md5sums=(SKIP
  8aebbfc0dc49b87f88912b539792aa80)

pkgver() {
  cd "$srcdir/$_svndir"
  svnversion | tr -d '[A-z]'
}

prepare() {
  cd "$srcdir/$_svndir"
  patch -Np0 -i "$srcdir/sfml-build-fixes.patch"
}

build() {
  cd "$srcdir/$_svndir"

  # ENABLE_ASM_CORE breaks GBA colors (at least with Pokemon Fire Red USA 1.0).
  # It also only works with 32 bit CPUs.
  cmake -D CMAKE_INSTALL_PREFIX:string=/usr -D ENABLE_ASM_SCALERS:bool=ON \
    -D ENABLE_GTK:bool=OFF -D ENABLE_WX:bool=ON -D ENABLE_SDL:bool=OFF .
  make
}

package() {
  cd "$srcdir/$_svndir"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
