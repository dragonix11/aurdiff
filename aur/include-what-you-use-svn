# Maintainer: Christoph Gysin <christoph.gysin@gmail.com>
pkgname=include-what-you-use-svn
pkgver=485
pkgrel=1
pkgdesc="A tool for use with clang to analyze includes in C and C++ source files"
_pkgname=include-what-you-use
arch=('i686' 'x86_64')
url='http://code.google.com/p/include-what-you-use'
license=('LLVM Release License')
makedepends=('python2')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
source=(include-what-you-use-llvm.patch)
noextract=()
md5sums=('4815757f4fc99a3c43957d916c97eca1')

_svntrunk="http://$_pkgname.googlecode.com/svn/trunk"
_svnmod="$_pkgname"

build() {
  cd "$srcdir"

  msg "Connecting to $_svnmod SVN server...."
  if [[ -d "$_svnmod/.svn" ]]; then
    (
        cd "$_svnmod"
        svn up -r "$pkgver"
        svn status | sed -n 's/M \+//p' | xargs -r svn revert
    )
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi
  msg "SVN checkout done or server timeout"

  _svntrunk_llvm="http://llvm.org/svn/llvm-project/llvm/trunk"
  _svnmod_llvm="llvm"

  msg "Connecting to llvm SVN server...."
  if [[ -d "$_svnmod_llvm/.svn" ]]; then
    (
        cd "$_svnmod_llvm"
        svn up
        svn status | sed -n 's/M \+//p' | xargs -r svn revert
    )
  else
    svn co "$_svntrunk_llvm" --config-dir ./ "$_svnmod_llvm"
  fi
  msg "SVN checkout done or server timeout"

  _svntrunk_clang="http://llvm.org/svn/llvm-project/cfe/trunk"
  _svnmod_clang="clang"

  msg "Connecting to clang SVN server...."
  if [[ -d "$_svnmod_clang/.svn" ]]; then
    (
        cd "$_svnmod_clang"
        svn up
        svn status | sed -n 's/M \+//p' | xargs -r svn revert
    )
  else
    svn co "$_svntrunk_clang" --config-dir ./ "$_svnmod_clang"
  fi
  msg "SVN checkout done or server timeout"

  msg "Starting build..."

  ln -snf ../../$_svnmod $_svnmod_clang/tools/include-what-you-use
  ln -snf ../../$_svnmod_clang $_svnmod_llvm/tools/clang

  msg "Patching..."
  patch -Np0 -i include-what-you-use-llvm.patch

  builddir="$srcdir/$_svnmod-build"
  mkdir -p "$builddir"
  cd "$builddir"
  cmake \
    -D CMAKE_BUILD_TYPE=Release \
    -D PYTHON_EXECUTABLE=/usr/bin/python2 \
    "$srcdir/$_svnmod_llvm"
  make -j$(grep -c proc /proc/cpuinfo)
}

package() {
    install -Dm755 \
        "$srcdir/$_svnmod-build/bin/include-what-you-use" \
        "$pkgdir/usr/bin/include-what-you-use"
    ln -s "include-what-you-use" \
        "$pkgdir/usr/bin/iwyu"
    install -Dm755 \
        "$srcdir/$_pkgname/fix_includes.py" \
        "$pkgdir/usr/bin/iwyu-fix_includes.py"
    install -d "$pkgdir/usr/share/doc/$_pkgname"
    install -Dm644 \
        "$srcdir/$_pkgname/README.txt" \
        "$pkgdir/usr/share/doc/$_pkgname"
    install -d "$pkgdir/usr/share/$_pkgname"
    install -Dm644 \
        "$srcdir/$_pkgname"/*.imp \
        "$pkgdir/usr/share/$_pkgname"
}
