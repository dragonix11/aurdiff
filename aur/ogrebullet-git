# Maintainer: Sandy Carter (bwrsandman) <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/ogrebullet-git

pkgname=ogrebullet-git
_gitname="ogrebullet"
pkgver=17.10bdcf0
pkgrel=1
pkgdesc="Bullet Physics wrapper for OGRE."
arch=('i686' 'x86_64')
url="http://www.ogre3d.org/tikiwiki/OgreBullet"
license=('lgpl')

depends=('ogre' 'bullet')
makedepends=('git' 'cmake' 'boost')
provides=('ogrebullet')
conflicts=('ogrebullet-svn')

source=('git+https://bitbucket.org/alexeyknyshev/ogrebullet.git')
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_gitname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DOgreBullet_BUILD_DEMOS=OFF
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
