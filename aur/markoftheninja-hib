# Maintainer: Sam S. <smls75@gmail.com>

pkgname=markoftheninja-hib
pkgver=1.21+h20130912
_hibver=21_1379021395
pkgrel=1
pkgdesc='Mark of the Ninja, a side-scrolling stealth game (Humble Bundle version)'
url='http://www.markoftheninja.com/'
arch=('i686' 'x86_64')
license=('custom: commercial')
depends=('sdl' 'libgl' 'gcc-libs' 'xdg-utils')
makedepends=('imagemagick')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_archive="markoftheninja_linux_${_hibver}.zip"
source=("hib://$_archive"
        'markoftheninja-hib.desktop')
md5sums=('3a6390cb709ca4e43bccbaf2a24999bb'
         '51019af45678e517235a50bb23eb98b4')

package() {
  cd "$srcdir"
  [ $CARCH == "i686" ] && _arch=32 && _other=64 || _arch=64 && _other=32
  _target=/opt/MarkOfTheNinja
  
  # Install game files
  mkdir -p "$pkgdir/$_target"
  cp -Rl {bin,data,data-pc} "$pkgdir/$_target"
  
  # Remove unneeded files
  rm "$pkgdir/$_target"/bin/lib$_arch/libSDL*
  rm -r "$pkgdir/$_target"/bin/{lib,ninja-bin}$_other
  
  # Fix permissions
  find "$pkgdir" -type f -exec chmod 644 "{}" +
  chmod 755 "$pkgdir/$_target"/bin/ninja-bin*
  
  # Install icon & desktop entry
  convert bin/motn_icon.xpm markoftheninja.png
  install -Dm644 markoftheninja.png "$pkgdir"/usr/share/pixmaps/markoftheninja.png
  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  
  # Install launch script
  echo -e "#!/bin/sh\ncd $_target/bin && exec ./ninja-bin$_arch \$*" > launcher.sh
  install -Dm755 launcher.sh "$pkgdir"/usr/bin/markoftheninja
}
