# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-ykfde
pkgver=0.2.0.r15.g68a220b
pkgrel=1
pkgdesc="Full disk encryption with Yubikey (Yubico key)"
arch=('any')
url="https://github.com/eworm-de/mkinitcpio-ykfde"
license=('GPL')
depends=('makepasswd' 'ykpers' 'cryptsetup' 'inotify-tools')
makedepends=('git')
install=mkinitcpio-ykfde.install
source=('git://github.com/eworm-de/mkinitcpio-ykfde.git')
backup=('etc/ykfde.conf')
    
pkgver() {
	cd mkinitcpio-ykfde/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd mkinitcpio-ykfde/

	install -D -m0755 bin/ykfde ${pkgdir}/usr/bin/ykfde
	install -D -m0644 conf/ykfde.conf ${pkgdir}/etc/ykfde.conf
	install -D -m0644 install/ykfde ${pkgdir}/usr/lib/initcpio/install/ykfde
	install -D -m0755 udev/ykfde ${pkgdir}/usr/lib/udev/ykfde
	install -D -m0644 udev/20-ykfde.rules ${pkgdir}/usr/lib/initcpio/udev/20-ykfde.rules
}

sha256sums=('SKIP')
