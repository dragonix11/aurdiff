# Maintainer: Jameson Pugh <imntreal@gmail.com>

_pkgbase=libdbusmenu
pkgname=lib32-libdbusmenu-glib
pkgver=12.10.2
pkgrel=1
pkgdesc="A library for passing menus over DBus (32-bit)"
arch=('x86_64')
url="https://launchpad.net/libdbusmenu"
license=('GPL3')
conflicts=('lib32-libdbusmenu')
provides=('lib32-libdbusmenu')
depends=('libdbusmenu-glib' 'lib32-glibc')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'gtk3' 'intltool' 'vala')
options=('!emptydirs')
source=("http://launchpad.net/dbusmenu/${pkgver%.?}/${pkgver}/+download/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('9d6ad4a0b918b342ad2ee9230cce8a095eb601cb0cee6ddc1122d0481f9d04c9')

build() {
  cd ${_pkgbase}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  export HAVE_VALGRIND_TRUE='#'
  export HAVE_VALGRIND_FALSE=''
  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --libdir=/usr/lib32 --disable-{dumper,static,tests,gtk}
  make
}

package() {
  cd ${_pkgbase}-${pkgver}

  make -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

# vim: ts=2 sw=2 et:
