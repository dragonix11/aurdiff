# Maintainer: Zachary Huff <zach.huff.386@gmail.com>

pkgname=pritunl
pkgver=0.0.5
pkgrel=1
pkgdesc="Simple openvpn server"
arch=("any")
license=("AGPL3")
url="https://github.com/zachhuff386/${pkgname}"
depends=(
    "python2"
    "python2-flask"
    "python2-cherrypy"
    "openvpn"
)
makedepends=(
    "python2-distribute"
)
provides=("${pkgname}")
conflicts=("${pkgname}")
install=${pkgname}.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=("d2acc0b4d4fbfded6d7c83fdcf656c89519985d9960e7f2f2b3acbae4e1f8c6e")
options=("emptydirs")
backup=(
    "etc/${pkgname}.conf"
    "var/lib/${pkgname}/${pkgname}.db"
    "var/log/${pkgname}.log"
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "${pkgdir}/var/lib/${pkgname}"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart
}
