# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Shizeeg Unadequatov <shizeeque@gmail.com>

pkgname=lua52-socket
_pkgname=luasocket
pkgver=3.0.rc1
_pkgver=3.0-rc1
pkgrel=1
pkgdesc='A comprehensive networking support library for Lua, with IPv6 support. Compatible with Lua 5.1 and 5.2'
arch=('i686' 'x86_64')
url="https://github.com/diegonehab/luasocket"
license=('MIT')
depends=('lua')
provides=('lua51-socket' 'luasocket-git')
conflicts=('lua51-socket' 'luasocket-git')
source=("https://github.com/diegonehab/luasocket/archive/v${_pkgver}.tar.gz")
sha256sums=('8b67d9b5b545e1b694753dab7bd6cdbc24c290f2b21ba1e14c77b32817ea1249')

build() {
  cd "$_pkgname-$_pkgver"

  make PLAT=linux prefix=/usr
}

package() {
  cd "$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir" prefix=/usr install-both
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

# vim: ft=sh syn=sh et
