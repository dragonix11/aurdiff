# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Tong Chunli <piggy1983@163.com>

pkgname=feynhiggs
pkgver=2.9.5
pkgrel=1
pkgdesc="A package for the calculation of the masses, mixings and much more of the Higgs bosons in the MSSM"
arch=('i686' 'x86_64')
url="http://wwwth.mpp.mpg.de/members/heinemey/feynhiggs/"
license=('GPL3')
depends=('gcc-libs')
makedepends=('gcc-fortran')
source=("http://wwwth.mpp.mpg.de/members/heinemey/feynhiggs/newversion/FeynHiggs-${pkgver}.tar.gz")
md5sums=('7ed8fae0cfa32136d27d36a3508d52be')


build() {
  cd "${srcdir}/FeynHiggs-${pkgver}"

  # Fix for x86_64
  sed -i 's_$(LIBDIRSUFFIX)__' makefile.in

  ./configure --prefix=/usr \
              --enable-slhapara
#              --enable-full-g-2 # Disabled due to compilation error

  # If you want to build the Mathematica frontend, uncomment 'all'
  make # all
}

package() {
  cd "${srcdir}/FeynHiggs-${pkgver}"
  make PREFIX="${pkgdir}/usr" install

  # Install man files
  mkdir -p "${pkgdir}/usr/share/man/man1"
  install -m644 man/man1/*.1 "${pkgdir}/usr/share/man/man1"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 man/api.* "${pkgdir}/usr/share/doc/${pkgname}"
}
