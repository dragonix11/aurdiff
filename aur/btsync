# Maintainer: ava1ar <mail(at)ava1ar(dot)info>
# Contributor: Dalton Miller
# Contributor: Kilian Lackhove kilian@lackhove.de
# Contributor: Justin Patera serialhex@gmail.com

pkgname=btsync
pkgver=1.2.67
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'armv6' 'armv7')
url="http://labs.bittorrent.com/experiments/sync.html"
install=btsync.install
backup=('etc/btsync.conf')
optdepends=("btsync-autoconfig: Auto-create users' config files if needed") 
source=(btsync.service btsync@.service)
sha1sums=('9187bafe0cc30895d55b8a8a69074ebdca325e4c' '0e9a7888776958d160f2b719e61257efc60d312a')
if [ "$CARCH" == x86_64 ]; then
	source+=("${pkgname}_x64-${pkgver}.tar.gz::http://download-lb.utorrent.com/endpoint/btsync/os/linux-x64/track/stable")
	sha1sums+=('ace8f37e3c1d0bb3ebfba0fb9f106f9db9b76756')
elif [ "$CARCH" == i686 ]; then
	source+=("${pkgname}_i386-${pkgver}.tar.gz::http://download-lb.utorrent.com/endpoint/btsync/os/linux-i386/track/stable")
	sha1sums+=('4987216e6f5e00f4dc118fb178770025697853a8')
elif [[ "$CARCH" == armv6 || "$CARCH" == armv7 ]]; then
	source+=("${pkgname}_arm-${pkgver}.tar.gz::http://download-lb.utorrent.com/endpoint/btsync/os/linux-arm/track/stable")
	sha1sums+=('97c25f17335014d312f22859516ffdbd954a2bea')
fi

package() {
	cd ${srcdir}
	install -Dm755 btsync ${pkgdir}/usr/bin/btsync
	# generate and install system-wide configand systemd unit
	mkdir -p ${pkgdir}/etc
	./btsync --dump-sample-config | sed 's:/home/user/\.sync:/var/lib/btsync:g' > ${pkgdir}/etc/btsync.conf
	# install systemd units
	install -Dm644 btsync.service ${pkgdir}/usr/lib/systemd/system/btsync.service
	install -Dm644 btsync@.service ${pkgdir}/usr/lib/systemd/system/btsync@.service
	# install license
	install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
