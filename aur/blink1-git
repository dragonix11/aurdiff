# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=blink1-git
pkgver=0.r527.gabf857b
pkgrel=1
pkgdesc="software for blink(1) USB RGB LED - git checkout"
arch=('i686' 'x86_64')
url="http://rzl.so/wiki/RZLblink"
license=('GPL')
makedepends=('git')
provides=('blink1')
conflicts=('blink1')
source=('git+https://github.com/todbot/blink1.git'
	'0001-commandline-update-build-for-linux-and-add-pkgconfig.patch')

pkgver() {
	cd blink1/

	# last tag is just text, so this does not work
	#if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
	#	echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	#else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	#fi
}

prepare() {
	cd blink1/

	# fix build, names, ...
	patch -Np1 < ${srcdir}/0001-commandline-update-build-for-linux-and-add-pkgconfig.patch
}

build() {
	cd blink1/commandline/

	make
}

package() {
	cd blink1/commandline/

	# lib
	install -D -m0755 libblink1.so ${pkgdir}/usr/lib/libblink1.so

	# bin
	install -D -m0755 blink1-tool ${pkgdir}/usr/bin/blink1-tool

	# include and pkgconfig
	install -D -m0644 blink1-lib.h ${pkgdir}/usr/include/blink1.h
	install -D -m0644 blink1-lib.pc ${pkgdir}/usr/lib/pkgconfig/blink1.pc
}

sha256sums=('SKIP'
            '306078d6cda5fded08147395cba2007e33bc0db3a1e636a43011667110d2780c')
