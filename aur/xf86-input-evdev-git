# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_pkgname=xf86-input-evdev
pkgname=$_pkgname-git
pkgver=2.8.1.3.g0f16065
pkgrel=1
pkgdesc="X.org evdev input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/$_pkgname/"
license=('custom')
groups=('xorg' 'xorg-drivers')
depends=('glibc' 'systemd-tools' 'mtdev')
makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto' 'git')
provides=("$_pkgname=$pkgver")
conflicts=('xorg-server<1.14.0' 'X-ABI-XINPUT_VERSION!=19' "$_pkgname")
options=('!libtool' '!makeflags')
source=("git://anongit.freedesktop.org/xorg/driver/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | cut -d "-" -f4- | tr - .
}

build() {
  cd $_pkgname
  msg2 "Starting autogen.sh..."
  ./autogen.sh --prefix=/usr
  msg2 "Starting make..."
  make
}

package() {
  cd $_pkgname
  msg2 "Starting make install..."
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}