# Maintainer: Chionsas <aur@chionsas.lt>

pkgrel=1
pkgver=current
pkgname=prezto-git
pkgdesc="Instantly Awesome Zsh. A configuration framework for Zsh that comes with modules, enriching the command line interface environment with sane defaults, aliases, functions, auto completion, and prompt themes."
url="https://github.com/sorin-ionescu/prezto/"
arch=('any')
license=('MIT')
depends=('zsh>=4.3.10')
makedepends=('git' 'coreutils' 'sed')
source=('git://github.com/sorin-ionescu/prezto.git' 'install')
install='install'
md5sums=('SKIP' '32c84ec04a6fa7bde49da86aec822906')
_gitname='prezto'

backup=('etc/zsh/zlogin' 'etc/zsh/zlogout' 'etc/zsh/zpreztorc' 'etc/zsh/zshenv' 'etc/zsh/zshrc')

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
	cd $srcdir/$_gitname
	git submodule update --init --recursive

	mkdir -p $srcdir/etc/zsh

	rm -f $srcdir/etc/zsh/*

	echo "source /etc/zsh/zpreztorc" > "$srcdir/etc/zsh/zshrc"
	echo "source /usr/lib/prezto/init.zsh" >> "$srcdir/etc/zsh/zshrc"


	sed -i 's#\${ZDOTDIR:-\$HOME}/\.zprezto/#/usr/lib/prezto/#g' $srcdir/$_gitname/init.zsh

	for entry in ${backup[@]}; do
		rcfile=$(basename $entry)
		if [ -f $srcdir/$_gitname/runcoms/$rcfile ]; then
			echo "source /usr/lib/prezto/runcoms/$rcfile" >> "$srcdir/etc/zsh/$rcfile"
		fi
	done	
}

package() {
	mkdir -p $pkgdir/usr/lib/prezto
	cp -r $srcdir/$_gitname/* $pkgdir/usr/lib/prezto

	mkdir -p $pkgdir/usr/share/doc/prezto
	mv $pkgdir/usr/lib/prezto/*.md $pkgdir/usr/share/doc/prezto

	mkdir -p $pkgdir/etc
	cp -r $srcdir/etc/zsh $pkgdir/etc
}
