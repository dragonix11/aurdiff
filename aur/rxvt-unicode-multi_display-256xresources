#  Maintainer: moetunes <moetunes111@gmail.com>
# Contributor: Jonas Palm <jpal@informatik.uni-kiel.de>
# Contributor: Christian Brassat <crshd@mail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributer: Magnus Woldrich <magnus@trapd00r.se>

pkgname=rxvt-unicode-multi_display-256xresources
_pkgname=rxvt-unicode
pkgver=9.19
pkgrel=1
pkgdesc="Unicode enabled rxvt-clone terminal emulator (urxvt) with patch for running a separate daemon on each DISPLAY, layout/font-width patches, secondary scroll wheel support and support for 256 Xresource colors"
arch=('i686' 'x86_64' 'armv6h')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=('GPL')
depends=('gcc-libs' 'libxft' 'gdk-pixbuf2')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
source=(http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2
        $_pkgname.desktop
        font-width-fix.patch
        256color.patch
        rxvt-unicode-urxvtd_socket.patch
        secondaryscrollWheel.patch)
provides=(rxvt-unicode)
conflicts=(rxvt-unicode)
md5sums=('45ad197eb9b5a840c0f65e3a8131921c'
         '3de6c13126a45bc3bc9f6bba077a1311'
         'df0c3a8b6bb0578d1b91e4081c47881c'
         'fb78c2ecf87626962734320cc2bb7ab1'
         '4b08c47eb36f9720baa9caa2ac012351'
         'ea5a4875e39ca52ed913665122e20b6e')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -p0 -i ../font-width-fix.patch
  patch -p0 -i ../256color.patch
  patch -p0 -i ../rxvt-unicode-urxvtd_socket.patch
  patch -p0 -i ../secondaryscrollWheel.patch
  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-256-color \
    --enable-font-styles \
    --enable-xim \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-smart-resize \
    --enable-pixbuf \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog \
    --enable-frills
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -d "$pkgdir/usr/share/terminfo"
  export TERMINFO="$pkgdir/usr/share/terminfo"
  make DESTDIR="$pkgdir" install
 # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed "$pkgdir/usr/bin/urxvt-tabbed"
 # install freedesktop menu
  install -Dm644 ../$_pkgname.desktop \
    "$pkgdir/usr/share/applications/$_pkgname.desktop"
}
