# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=phalanx
pkgver=23
_mypkgver=XXIII
pkgrel=1
pkgdesc="A chess engine which understands the xboard protocol. It's suitable for beginner and intermediate players"
arch=(i686 x86_64)
url="http://phalanx.sourceforge.net/"
license=('GPL')

source=(http://downloads.sourceforge.net/sourceforge/$pkgname/phalanx-${_mypkgver}-beta.tgz)
md5sums=('74393533dab14b95ce8414072c913bc0')
sha512sums=('39e09a8ddee85b60b79628e20268cafce28596ea47fdf7c61d396a7da4ef0dce6060168eb98a4794c4bc96484c0d6d82475dd1c20e8dc3546dd2ca857873af2c')

prepare () {
	# Use the CFLAGS set for makepkg
	cd "${srcdir}"/phalanx-${_mypkgver}
	sed -i "s/CFLAGS = -O0/# CFLAGS = -O0/g" makefile
	# Set directories for books
	sed -i "s/_DIR \"\/usr\/local\/lib\/phalanx/_DIR \"\/usr\/share\/phalanx/g;s/LEARN_DIR \"\/var\/local\/lib/LEARN_DIR \"\/var\/games/g" phalanx.h
}

build() {
echo $LEARN_DIR
	cd "${srcdir}"/phalanx-${_mypkgver}
	make
	dd if=/dev/zero of=./learn.phalanx count=512
}

package () {
	cd "${srcdir}"/phalanx-${_mypkgver}
	mkdir -p "${pkgdir}"/usr/share/phalanx
	mkdir -p -m 0775 "${pkgdir}"/var/games/phalanx
	install -D -m0644 eco.phalanx "${pkgdir}"/usr/share/phalanx/eco.phalanx
	install -D -m0644 pbook.phalanx "${pkgdir}"/usr/share/phalanx/pbook.phalanx
	install -D -m0644 sbook.phalanx "${pkgdir}"/usr/share/phalanx/sbook.phalanx
	install -D -g games -m0664 learn.phalanx "${pkgdir}"/var/games/phalanx
	install -D phalanx "${pkgdir}"/usr/bin/phalanx
	chmod 0775 "${pkgdir}"/var/games
	chgrp games "${pkgdir}"/var/games/phalanx
}
