# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Christoph Brill <egore911-at-gmail-dot-com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=android-studio
pkgver=0.2.11
pkgrel=1
_build=130.737825
_patch=132.855830
pkgdesc="A new Android development environment based on IntelliJ IDEA."
arch=(i686 x86_64)
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('python' 'fontconfig' 'mesa' 'libxrender' 'android-sdk')
makedepends=('unzip' 'xorg-server-xvfb' 'ttf-dejavu')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
options=('!strip')
install=${pkgname}.install
source=(https://dl.google.com/android/studio/android-studio-bundle-${_build}-linux.tgz
        ${pkgname}.desktop)
test ! -z ${_patch} && source+=("https://dl.google.com/android/studio/patches/AI-${_build}-${_patch}-patch-unix.jar")
noextract=(AI-${_build}-${_patch}-patch-unix.jar)
sha256sums=('f7b2c9a9b04984c688d68de6b6da17cce3b46dfc71a14cd4a7a41128a26212e1'
            '2bba1530d217d055441b1075f53f09b172bdb56ae3908ef80201fd2ba766b33d'
            'e312e0c05062705bb07dbb9341ce2f8b908a677853be41d2477f2ad30ac20936')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
    echo "Performing update..."

    if [ ! -z "${_patch}" ]
    then
        xvfb-run java -classpath "${srcdir}/AI-${_build}-${_patch}-patch-unix.jar" com.intellij.updater.Runner install "${srcdir}/${pkgname}" || ret="${?}"

        if [ "${ret}" != "42" ]; then
            echo "Update process failed with code: ${ret}"
            exit 1
        fi
    fi

    cd "${srcdir}/${pkgname}"

    # remove pause after open jdk warning
    sed -i 's/read IGNORE//' bin/studio.sh

    # extract the application icon
    unzip -o lib/resources.jar artwork/icon_AS_128.png

    # enable anti aliasing
    for f in bin/*.vmoptions; do
      echo "-Dawt.useSystemAAFontSettings=lcd" >> ${f}
      echo "-Dswing.aatext=true" >> ${f}
    done
}

package() {
    cd "${srcdir}/${pkgname}"

    # application stuff
    mkdir -p "${pkgdir}/opt/${pkgname}"
    cp -a bin lib plugins "${pkgdir}/opt/${pkgname}"

    mkdir -p "${pkgdir}/usr/bin"
    ln -s /opt/android-studio/bin/studio.sh "${pkgdir}/usr/bin/android-studio"

    # starter stuff
    install -Dm655 artwork/icon_AS_128.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm655 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    chmod -R ugo+rX "${pkgdir}/opt"
}
