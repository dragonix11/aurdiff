# Maintainer: Kelvin Ng (qpalz) <kelvin9302104 at gmail dot com>

# Thanks graysky. Sync with linux-ck: https://aur.archlinux.org/packages/linux-ck

pkgname=nvidia-uksm-ck
pkgver=325.15
_extramodules=extramodules-3.11-uksm-ck
pkgrel=2
_pkgdesc="NVIDIA drivers for linux-uksm-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-uksm-ck>=3.11' 'linux-uksm-ck<3.12' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-uksm-ck-headers>=3.11' 'linux-uksm-ck-headers<3.12')
license=('custom')
install=nvidia-uksm-ck.install
options=(!strip)
source=('nvidia-linux-3.11.patch')
md5sums=('5c962760de069ed4ad34e64d5988686a')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
		source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
		md5sums+=('528fc538584de6446384b9cec3099d6c')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
		source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
		md5sums+=('63b2caa0cb128efa1a7808d7bddb2074')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	patch -Np2 -i ${srcdir}/nvidia-linux-3.11.patch
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
		"${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -dm755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-uksm-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-uksm-ck.install"
	gzip -9 "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
