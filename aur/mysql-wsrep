# MySQL server with write set replication patches(by Codership Oy)
# This is a part of MySQL/Galera cluster
#
# Maintainer: Rustam Tsurik <rustam.tsurik@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mysql-wsrep
_pkgname=mysql
true && pkgname=('libmysqlclient-wsrep' 'mysql-clients-wsrep' 'mysql-wsrep')
pkgbase=mysql-wsrep
pkgver=5.5.33
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url=("https://launchpad.net/codership-mysql")
makedepends=('cmake' 'openssl' 'zlib')
options=('!libtool')
source=(http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.5/${_pkgname}-${pkgver}.tar.gz{,.asc}
	https://launchpad.net/codership-mysql/5.5/5.5.33-23.7.6/+download/mysql-5.5.33_wsrep_23.7.6.patch
        mysqld-post.sh
        mysqld-tmpfile.conf
        mysqld.service
        my.cnf
	bzrignore)
sha256sums=('6ad9af38de1f3595c5fb81bc24bdb62fa39b2ce1c690d87ac7d7a21ca71fceba'
            'SKIP'
            'df778868221195bc342e94a8e0553571d03dde18346f75e26286d0c0beb09bcd'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd'
            '6ae8739873ebb15a8aa60378850eeec54137db1e3f719e4bbc36b2951c626568'
            'SKIP'
            '47351c602a37c3f5163b54ecad588d90125a29152420f610d4e0b549e5fa7493')

build() {

  # Apply the wsrep patch 
  cd "${srcdir}"/mysql-${pkgver}
  cp ../bzrignore ./.bzrignore
  patch -p1 -N < ../mysql-5.5.33_wsrep_23.7.6.patch

  cd ..
  mkdir build
  cd build

  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}" \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=0 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITH_WSREP=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti -fpermissive"

  make
}

package_libmysqlclient-wsrep(){
  pkgdesc="MySQL client libraries"
  depends=('openssl')
  conflicts=('libmariadbclient')
  provides=("libmariadbclient=$pkgver" "libmysqlclient=$pkgver")
  
  cd build
  for dir in include libmysql libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}"/usr/bin
  install -m755 scripts/mysql_config "${pkgdir}"/usr/bin/
  install -d "${pkgdir}"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "${srcdir}"/${_pkgname}-${pkgver}/man/$man.1 "${pkgdir}"/usr/share/man/man1/$man.1
  done
}

package_mysql-clients-wsrep(){
  pkgdesc="MySQL client tools"
  depends=('libmysqlclient-wsrep')
  conflicts=('mariadb-clients')
  provides=("mariadb-clients=$pkgver" "mysql-clients=$pkgver")
  
  cd build
  make -C client DESTDIR="${pkgdir}" install

  # install man pages
  install -d "${pkgdir}"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "${srcdir}"/${_pkgname}-${pkgver}/man/$man.1 "${pkgdir}"/usr/share/man/man1/$man.1
  done

  # provided by mysql
  rm "${pkgdir}"/usr/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mysql-wsrep(){
  pkgdesc="wsrep-enabled MySQL server"
  backup=('etc/mysql/my.cnf')
  install=mysql.install
  depends=('mysql-clients-wsrep' 'systemd-tools')
  conflicts=('mariadb')
  provides=("mariadb=$pkgver" "mysql=$pkgver")
  options=('emptydirs')
  optdepends=('galera: Galera is a replication plugin utilizing wsrep API');

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/my.cnf "${pkgdir}"/etc/mysql/my.cnf
  install -Dm755 "${srcdir}"/mysqld-post.sh "${pkgdir}"/usr/bin/mysqld-post
  install -Dm644 "${srcdir}"/mysqld-tmpfile.conf "${pkgdir}"/usr/lib/tmpfiles.d/mysqld.conf
  install -d "${pkgdir}"/usr/lib/systemd/system
  install -Dm644 "${srcdir}"/mysqld.service "${pkgdir}"/usr/lib/systemd/system/

  # provided by libmysqlclient
  rm "${pkgdir}"/usr/bin/mysql_config
  rm "${pkgdir}"/usr/lib/libmysql*
  rm -r "${pkgdir}"/usr/include/
  rm "${pkgdir}"/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  
  # provided by mysql-clients
  rm "${pkgdir}"/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm "${pkgdir}"/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r "${pkgdir}"/usr/{data,mysql-test,sql-bench}
  rm "${pkgdir}"/usr/share/man/man1/mysql-test-run.pl.1

  # rsync already runs from the mysql user and these options won't work
  sed -i "${pkgdir}"/usr/bin/wsrep_sst_rsync -e "/^uid = $MYUID/d;/^gid = $MYGID/d" 

  #install -dm700 "${pkgdir}"/var/lib/mysql
}

pkgdesc="wsrep-enabled MySQL server"
