pkgname=zabbix-server-mysql
_pkgname=zabbix-server
pkgver=2.0.9
pkgrel=2
pkgdesc="Zabbix is an enterprise-class open source distributed monitoring solution."
arch=("i686"
      "x86_64"
     )
url="http://www.zabbix.com/"
license=("GPL")
depends=("apache>=2"
         "mariadb>=5.5"
         "php"
         "php-apache"
         "php-gd"
         "fping"
         "traceroute"
         "net-snmp"
         "nmap"
         "sudo"
         "curl"
         "iksemel"
         "libssh2"
        )
optdepends=("shellinabox: web-based ssh/telnet client")
conflicts=("${_pkgname}"
           "zabbix-agent"
          )
backup=("etc/zabbix/zabbix_server.conf"
        "etc/zabbix/zabbix_agentd.conf"
       )
install="${_pkgname}.install"
options=("emptydirs")
source=("http://downloads.sourceforge.net/sourceforge/zabbix/zabbix-${pkgver}.tar.gz"
        "zabbix-server.install"
        "config.diff"
       )
md5sums=("edf00241cce2302e0b65f620e83c7e41" # zabbix-$pkgver.tar.gz
         "ce928f0b9ddfc9d45c484d18eaf19f25" # zabbix-server.install
         "651e284397532f500b92c34bb0a2feda" # config.diff
        )
sha1sums=("858b52ca1769086f4302e431c28d4ad458464c62" # zabbix-$pkgver.tar.gz
          "fb72cdc63f11298a7e34a7edf197978c1625d877" # zabbix-server.install
          "82239b23cfb4f8f43d9a5a33f77e58103e567af4" # config.diff
         )

prepare() {
cat << EOL > "${srcdir}/${_pkgname}.sudoers"
# Defaults specification
Defaults visiblepw

# User privilege specification
zabbix ALL=(root) NOPASSWD: /usr/bin/nmap
EOL

cat << EOL > "${srcdir}/${_pkgname}.service"
[Unit]
Description=Zabbix Server
After=syslog.target network.target mysqld.service

[Service]
Type=oneshot
ExecStart=/usr/bin/zabbix_server
ExecReload=/usr/bin/zabbix_server -R config_cache_reload
RemainAfterExit=yes
PIDFile=/run/zabbix/zabbix_server.pid

[Install]
WantedBy=multi-user.target
EOL

cat << EOL > "${srcdir}/zabbix-agentd.service"
[Unit]
Description=Zabbix Agent
After=syslog.target network.target

[Service]
Type=oneshot
ExecStart=/usr/bin/zabbix_agentd
RemainAfterExit=yes
PIDFile=/run/zabbix/zabbix_agentd.pid

[Install]
WantedBy=multi-user.target
EOL

cat << EOL > "${srcdir}/${_pkgname}.conf"
d /run/zabbix 0755 zabbix zabbix
EOL
}

build() {
  cd "${srcdir}/zabbix-${pkgver}"
  patch -p1 < "${srcdir}/config.diff"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/zabbix \
    --enable-ipv6 \
    --enable-server \
    --enable-agent \
    --with-mysql \
    --with-net-snmp \
    --with-jabber \
    --with-libcurl \
    --with-ssh2

  make || return 1
}

package() {
  cd "${srcdir}/zabbix-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1

  install -dm 0755 "${pkgdir}/usr/share/webapps/zabbix"
  install -dm 0755 "${pkgdir}/etc/zabbix/database/mysql/upgrade/2.0"
  install -dm 0755 "${pkgdir}/etc/zabbix/database/mysql/setup/2.0"

  cp -r ${srcdir}/zabbix-${pkgver}/frontends/php/* ${pkgdir}/usr/share/webapps/zabbix/
  chown -R 33:33 "${pkgdir}/usr/share/webapps/zabbix/"
  find ${pkgdir}/usr/share/webapps/zabbix/ -type f -exec chmod 644 {} \;
  find ${pkgdir}/usr/share/webapps/zabbix/ -type d -exec chmod 755 {} \;

  for _UPGFILE in patch.sql rc4_rc5.sql upgrade
  do
    install -D -m 0444 "${srcdir}/zabbix-${pkgver}/upgrades/dbpatches/2.0/mysql/${_UPGFILE}" \
                       "${pkgdir}/etc/zabbix/database/mysql/upgrade/2.0/${_UPGFILE}"
  done
  for _SQLFILE in {data,images,schema}.sql
  do
    install -D -m 0444 "${srcdir}/zabbix-${pkgver}/database/mysql/${_SQLFILE}" \
                       "${pkgdir}/etc/zabbix/database/mysql/setup/2.0/${_SQLFILE}"
  done
  install -dm 0750                                    "${pkgdir}/etc/sudoers.d/"
  install -dm 0755                                    "${pkgdir}/run/zabbix"
  install -dm 0750                                    "${pkgdir}/var/log/zabbix"
  install -Dm 0640 "${srcdir}/${_pkgname}.sudoers"    "${pkgdir}/etc/sudoers.d/${_pkgname}"
  install -Dm 0644 "${srcdir}/${_pkgname}.conf"       "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
  install -Dm 0644 "${srcdir}/${_pkgname}.service"    "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
  install -Dm 0644 "${srcdir}/zabbix-agentd.service"  "${pkgdir}/usr/lib/systemd/system/zabbix-agentd.service"
}