# $Id: 21155555adfdfa0847d0df16306f08764479e45c $
# Maintainer: Ido Rosen <ido@kernel.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
# 

pkgname='lmctfy'
pkgdesc="The open source version of Googleâ€™s container stack, which provides Linux application containers."
pkgver=0.1.0
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/google/lmctfy'
license=('Apache')
depends=()
makedepends=('protobuf') # these are only available on AUR: 'gflags' 're2'
options=()
source=(https://github.com/google/lmctfy/archive/0.1.0.tar.gz
        https://gflags.googlecode.com/files/gflags-2.0.tar.gz
        https://re2.googlecode.com/files/re2-20130802.tgz)
sha512sums=('9cfa523fe04ef210f0700b8796700447aad839eceb448aafc3aff0a073b62d438fb6d3b8dee9b168358e3e1b7c2958f6db360dd78e20809b22b218cc14f555e7'
            'ba7061d5d57fcadb53ef70f846caacc44c319552e7dd6b05def38f961d3d89f4023e4eda95845efb399f3e94f24f75bdadfc67b22e29bccc94e46de8db501b83'
            '7a6181911219159ab31f91ebe0faf29e1722ab7134c9c1933ed99ba44415d2a8f2663438f1b5445fc95f064cbb1717e3031db48a8c17212f959e463b4b862769')

build() {
  cd "${srcdir}/gflags-2.0"
  ./configure
  make

  cd "${srcdir}/re2"
  make

  cd "${srcdir}/${pkgname}-${pkgver}"
  CXXFLAGS="${CXXFLAGS} -L../gflags-2.0/.libs -L../re2/obj -I../re2 -I../gflags-2.0/src -Wno-deprecated"
  make all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Install binaries:
  install -dm755                      "${pkgdir}/usr/bin"
  install -m755 bin/lmctfy/cli/lmctfy "${pkgdir}/usr/bin"

  # Install static library:
  install -dm755                  "${pkgdir}/usr/lib"
  install -m644 bin/liblmctfy.a   "${pkgdir}/usr/lib"
  install -m644 bin/lmctfy_cli.a  "${pkgdir}/usr/lib"

  # Install header files:
  install -dm755                      "${pkgdir}/usr/include"
  install -m644 include/lmctfy.h      "${pkgdir}/usr/include"
  install -m644 include/lmctfy.proto  "${pkgdir}/usr/include"

  # Install LICENSE file:
  install -dm755                  "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE           "${pkgdir}/usr/share/licenses/${pkgname}"

  # Install documentation:
  install -dm755                  "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 AUTHORS           "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 CONTRIBUTORS      "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 LICENSE           "${pkgdir}/usr/share/doc/${pkgname}"
}
