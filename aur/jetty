# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Yegorius <yegorius@domic.us>

pkgname=jetty
pkgver=9.0.6
_timestamp=v20130930
pkgrel=1
pkgdesc="Jetty is a pure Java-based HTTP server and Java Servlet container"
arch=('any')
url="http://www.eclipse.org/jetty/"
license=('Apache' 'EPL')
depends=('java-environment')
options=('!strip')
_distname=jetty-distribution-$pkgver.$_timestamp
source=(http://download.eclipse.org/jetty/stable-9/dist/$_distname.tar.gz
		jetty.default
		jetty.logrotate
		jetty.service
		rundir.patch)
install=$pkgname.install

sha1sums=('7525ee46900aabda2fb043887be66924dab3ab5f'
          'b79ee2e795bbfcc7af6918fd5ac8084e85208e77'
          '6da2dafb061db3fab9770abe16fef331850c1f3b'
          'ef629f374e985ce53ed384780f2f1a745a9c9f16'
          'd4213fc4b61450edc24993970104b71a9d4a8271')

package() {
	cd "$srcdir/$_distname"

	install -dm755 "$pkgdir/etc/jetty"
	install -dm755 "$pkgdir/usr/bin"
	install -dm755 "$pkgdir/var/log/jetty"
	install -dm755 "$pkgdir/var/lib/jetty/webapps"

	install -Dm755 bin/jetty.sh "$pkgdir/usr/share/jetty/bin/jetty.sh"
	cp -r etc/* "$pkgdir/etc/jetty"
	cp -r {resources,start.ini,start.d} "$pkgdir/etc/jetty/"
	cp -r {lib,start.jar} "$pkgdir/usr/share/jetty/"
	cp -r webapps.demo/ROOT "$pkgdir/var/lib/jetty/webapps"

	ln -s /etc/jetty "$pkgdir/usr/share/jetty/etc"
	ln -s /etc/jetty/start.d "$pkgdir/usr/share/jetty/start.d"
	ln -s /etc/jetty/start.ini "$pkgdir/usr/share/jetty/start.ini"
	ln -s /etc/jetty/resources "$pkgdir/usr/share/jetty/resources"
	ln -s /usr/share/jetty/bin/jetty.sh "$pkgdir/usr/bin/jetty"
	ln -s /var/lib/jetty/webapps "$pkgdir/usr/share/jetty/webapps"

	install -Dm755 "$srcdir/jetty.default" "$pkgdir/etc/default/jetty"
	install -Dm644 "$srcdir/jetty.logrotate" "$pkgdir/etc/logrotate.d/jetty"
	install -Dm644 "$srcdir/jetty.service" "$pkgdir/usr/lib/systemd/system/jetty.service"

	patch -Np1 -i "$srcdir/rundir.patch" "$pkgdir/usr/share/jetty/bin/jetty.sh"
	sed -i 's|su - |su -s /bin/sh - |' "$pkgdir/usr/share/jetty/bin/jetty.sh"

	rm "$pkgdir/etc/jetty/jetty-demo.xml"
	rm "$pkgdir/etc/jetty/start.d/900-demo.ini"
	rm -r "$pkgdir/usr/share/jetty/lib/jndi.demo"
	rm "$pkgdir/usr/share/jetty/lib/setuid/libsetuid-osx.so"
}
