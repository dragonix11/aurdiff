# Maintainer: Stefano Bergamini <bergs at live dot it>
# Contributor: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>

pkgname=epson-inkjet-printer-escpr
pkgver=1.3.0
pkgrel=1
pkgdesc="Epson Inkjet Printer Driver (ESC/P-R) for Linux"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2')
depends=('cups' 'gutenprint')
options=('!libtool')
source=(http://download2.ebz.epson.net/dsc/f/03/00/02/36/49/83c08cbb739bd5bfb2529ad4d18696f212ec177e/$pkgname-$pkgver-1lsb3.2.src.rpm)
md5sums=('90e6637aa928bfe35ea29f5c8951a812')
 
prepare() {
  tar zxf "$pkgname-$pkgver-1lsb3.2.tar.gz"
}
 
build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-cupsfilterdir=/usr/lib/cups/filter \
              --with-cupsppddir=/usr/share/ppd
  make
}
 
package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
