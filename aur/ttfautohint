# Maintainer: Arthur Țițeică arthur.titeica/gmail/com

pkgname=ttfautohint
pkgver=0.96
pkgrel=1
pkgdesc="Provides automated hinting process for web fonts"
arch=('i686' 'x86_64')
url="http://www.freetype.org/ttfautohint/"
license=('GPL')
depends=('freetype2')
makedepends=('qt4')
optdepends=('texlive-bin: generate docs'
            'pandoc: generate docs'
            'ghc: pandoc filter'
            'qt4: ttfautohintGUI')
source=(http://download.savannah.gnu.org/releases/freetype/$pkgname-$pkgver.tar.gz{,.sig})
sha1sums=('007188d3003355b9823e708d973b8c829c5dc8a3'
         'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export QMAKE='/usr/bin/qmake-qt4'
  export MOC='/usr/bin/moc-qt4'
  export UIC='/usr/bin/uic-qt4'
  export RCC='/usr/bin/rcc-qt4'
  # export QTDIR='/usr/lib/qt4'
  CFLAGS="-I/usr/include/qt4" ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
