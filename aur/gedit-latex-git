# Maintainer: Francesco Marella <francesco.marella@anche.no>
# Contributor: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>

pkgname=gedit-latex-git
_gitname=gedit-latex
pkgver=20130913
pkgrel=1
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python-gobject python-dbus)
makedepends=(git cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gedit-latex.install
source=('git://git.gnome.org/gedit-latex')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format='%cd' --date=short | tr -d -- '-'
}


build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
}

