_target=arm-linux-gnueabi
_sysroot=/usr/${_target}
_pkgname=libiconv

pkgname="${_target}-${_pkgname}"
pkgver=1.14
pkgrel=1
pkgdesc="Provides libiconv.so and libcharset.so (${_target})"
arch=('any')
url='http://www.gnu.org/software/libiconv/'
license=('LGPL')
source=("http://ftp.gnu.org/pub/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('e34509b1623cec449dfeb73d7ce9c6c6')
options=(!strip !buildflags !libtool)

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed '/LD_RUN_PATH/d' -i Makefile.in
  cp -f "${_sysroot}/include/stdio.h" srclib/stdio.in.h
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export LD=${_target}-ld
  export RANLIB=${_target}-ranlib

  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig:${_sysroot}/share/pkgconfig"
  
  unset LDFLAGS
  ./configure --prefix=${_sysroot} --host=${_target} --enable-shared
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/${_sysroot}/share/man/man3"
  rm "$pkgdir/${_sysroot}/include/iconv.h"
  rm "$pkgdir/${_sysroot}/bin/iconv"
}
