# Maintainer: Steven Allen <steven {at} stebalien {dot} com>

_pkgname="xssstate"
pkgname="$_pkgname-git"
pkgver=10
pkgrel=1
epoch=1
pkgdesc="A simple tool to retrieve the X screensaver state"
arch=('i686' 'x86_64' 'armv7h')
url="http://tools.suckless.org/$_pkgname"
license=('MIT')
depends=('libxss' 'sh')
makedepends=('git')

source=("git://git.suckless.org/$_pkgname")

pkgver() {
  cd "$srcdir/$_pkgname"
  git rev-list --count HEAD
}

build() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_pkgname"

  make PREFIX="/usr" DESTDIR="$pkgdir" install

  # install readme/example script
  install -dm755 "$pkgdir/usr/share/doc/xssstate"
  install -m644 "README" "xsidle.sh" "$pkgdir/usr/share/doc/xssstate/"

  # install license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('SKIP')
