# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Simon Malinge <tapi@syskall.net>
pkgname=rats
pkgver=2.3
pkgrel=3
pkgdesc="A tool for scanning C, C++, Perl, PHP and Python source code and flagging common security related programming errors"
url="http://www.fortify.com/security-resources/rats.jsp"
license=('GPL2')
depends=('expat')
arch=('i686' 'x86_64')
source=("https://www.fortify.com/downloads2/public/$pkgname-$pkgver.tar.gz")
sha256sums=('36872ab4ed55058821d49cd0d1f2c36f1505dae129a697083717a95924cd1478')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/$(BINDIR)/$(DESTDIR)$(BINDIR)/g' -i Makefile.in
  sed -e 's/$(MANDIR)/$(DESTDIR)$(MANDIR)/g' -i Makefile.in
  sed -e 's/ $(LIBDIR)/ $(DESTDIR)$(LIBDIR)/g' -i Makefile.in
  sed -e 's/ $(SHAREDIR)/ $(DESTDIR)$(SHAREDIR)/g' -i Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make SHAREDIR=/usr/share/${pkgname}
}

package() {
  cd ${pkgname}-${pkgver}
  make MANDIR=/usr/share/man SHAREDIR=/usr/share/${pkgname} DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/usr/lib
  install -Dm 644 README ${pkgdir}/usr/share/doc/${pkgname}/README
}

# vim: ts=2 sw=2 et:
