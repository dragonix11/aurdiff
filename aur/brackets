# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: mac <poczciwiec at gmail dot com>

pkgname=brackets
_build=31
pkgver=sprint$_build
pkgrel=1
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("brackets-lib" "gconf" "nodejs" "nspr" "systemd")
makedepends=("prelink")
optdepends=(
	"chromium: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
provides=("brackets" "adobe-brackets")
conflicts=("brackets-git")
replaces=()
backup=()
options=()
install=$pkgname.install

if [[ "$CARCH" == "i686" ]]; then
	source=("https://www.dropbox.com/s/ksq6v6atf3oxfsf/brackets-sprint-31-LINUX32.deb")
	sha512sums=("0829692a8d361cf0bedaf703462f135488d6117e1e63ed2ce7e49b49a36dbf3d6565c0e2313efb86a2aef97518269a1b2feebe4a40c51fda98f1e857b4c993b4")
elif [[ "$CARCH" == "x86_64" ]]; then
	source=("https://www.dropbox.com/s/yi3b9zzx1y8bmzi/brackets-sprint-31-LINUX64.deb")
	sha512sums=("d4206b0fa9564681a9f32edd8a04c646b9d6208f6fd69efd917f83f04e960142fb2f3972e4b719784498579bb28a99294b77c4f2c8a8912154ffe1fc80aa7f84")
fi

prepare() {
	cd $srcdir

	echo "  -> Extracting files..."
	tar -xf data.tar.gz

	echo "  -> Fixing executable stack..."
	execstack -c opt/$pkgname/Brackets
}

package() {

	echo "  -> Installing program..."

	mkdir $pkgdir/opt
	cp -r opt/$pkgname $pkgdir/opt/$pkgname

	chmod -R a+rw $pkgdir/opt/$pkgname/samples

	install -d $pkgdir/usr/bin
	ln -s /opt/brackets/brackets $pkgdir/usr/bin/$pkgname

	echo "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -d $pkgdir/$_icon_dir/scalable/apps
	install -Dm644 $_icon_dir/scalable/apps/$pkgname.svg $pkgdir/$_icon_dir/scalable/apps/$pkgname.svg
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size=${_icon##*/appshell}
		install -d $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps
		install -Dm644 $_icon $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps/$pkgname.png
	done

	echo "  -> Installing .desktop file..."
	install -Dm755 opt/$pkgname/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop


	echo "  -> Installing license..."
	install -Dm755 usr/share/doc/$pkgname/copyright $pkgdir/usr/share/licenses/$pkgname/copyright

}