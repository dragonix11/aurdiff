# Maintainer: Jan Pacner
# Contributor: dumblob <dumblob@gmail.com>

# TODO make this PKGBUILD modular (
#   maybe by detaching the biggest Dao modules to separate PKGBUILDS?
#   or just by using optdepends wherever possible?)

_basename=dao
pkgname=${_basename}-lang-git
pkgver=1012.804a758
pkgrel=1
pkgdesc='A lightweight, portable, optionally typed programming language and VM written in C featuring blazingly fast real concurrency, deferring, panicking, OOP, LLVM JIT, bytecode generation, syntax customization using BNF, many modules (e.g. graphics) etc.'
url='http://www.daovm.net/'
license=('BSD')
arch=('i686' 'x86_64')
source=(
  "$_basename::git+https://github.com/daokoder/dao"
  "$_basename-modules::git+https://github.com/daokoder/dao-modules#branch=Compatibility_Clang_3_3"
  "DaoGraphics::git+https://github.com/daokoder/DaoGraphics"
  "DaoSQL::git+https://github.com/daokoder/DaoSQL"
  "DaoSDL::git+https://github.com/daokoder/DaoSDL"
  "DaoGObject::git+https://github.com/daokoder/DaoGObject"
  "$_basename-tools::git+https://github.com/daokoder/dao-tools#branch=Compatibility_Clang_3_3")
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
depends=('gcc-libs-multilib' 'mesa' 'glu' 'freeglut')
# FIXME emscripten-git (wait until clang 3.3 become supported)
makedepends=('patchelf' 'sqlite' 'mariadb' 'libffi' 'sdl' 'clang' 'cblas')
#optdepends=()
optdepends=(
  #'emscripten-git: support for compilation to JavaScript'
  'sqlite:         support for the DaoSQLite module'
  'mariadb:        support for the DaoMySQL module'
  'libffi:         support for the clinker module (allowing to call C functions directly from Dao)'
  'sdl:            support for the DaoSDL module'
  'cblas:          support for the cblas module'  # FIXME not a good idea to let it being optional!
  'clang:          dependency for the cblas module'
  'clang:          support for C/C++ library binding tool')
provides=('dao')
conflicts=()

build() {
  mv "$_basename-modules/"* "$_basename/modules/"
  mv "DaoGraphics/"         "$_basename/modules/"
  mv "DaoSQL"               "$_basename/modules/"
  mv "DaoSDL"               "$_basename/modules/"
  mv "DaoGObject"           "$_basename/modules/"
  mv "$_basename-tools/"*   "$_basename/tools/"
  cd "$_basename"

  # the "graph" module is deprecated in favor of the "DaoGraphics" module
  rm -rf tools/graph/

  # FIXME remove because of some bugs
  rm -rf modules/clinker/
  rm -rf tools/clangdao/

  # enable JIT and set CPU count
  sed -i -r -e "s/# *(cpu *=).*/\1$(grep '^processor' /proc/cpuinfo | wc -l)/" \
            -e 's/# *(jit *=).*/\1yes/' dao.conf

  msg "Starting make..."
  _opts="       --option-INSTALL-PATH '$pkgdir/usr/lib/dao/'"
  _opts="$_opts --option-BIN-PATH     '$pkgdir/usr/bin/'"
  _opts="$_opts --option-LIB-PATH     '$pkgdir/usr/lib/dao/'"
  _opts="$_opts --option-INC-PATH     '$pkgdir/usr/include/dao/'"
  _opts="$_opts --option-MOD-PATH     '$pkgdir/usr/lib/dao/modules/'"
  _opts="$_opts --option-DOC-PATH     '$pkgdir/usr/share/doc/dao/'"
  make -f Makefile.daomake linux RESET='--reset' OPTIONS="$_opts"
    #JAVASCRIPT='ON'
}

pkgver() {
  cd "$_basename"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  cd "$_basename"
  make install
  #FIXME try to hack the Dao build system and change rpath of dao_libdao.so
  #  to /usr/lib/
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/dao"
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/daomake"
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/daotest"
  install -D -m644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
