# Maintainer: Daniel Micay <danielmicay@gmail.com>
_hkgname=tagsoup
pkgname=haskell-tagsoup
pkgver=0.12.8
pkgrel=3
pkgdesc="Parsing and extracting information from (possibly malformed) HTML/XML documents"
url="http://community.haskell.org/~ndm/tagsoup/"
license=(custom:BSD3)
arch=('i686' 'x86_64')
depends=("ghc" "haskell-text")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install=$pkgname.install
sha256sums=('a31e49133690b546a6b948a9ab9f924aaf5bd94f9e3e0afb99bb570c104c9515')

build() {
  cd $srcdir/${_hkgname}-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/${_hkgname}-${pkgver}
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
