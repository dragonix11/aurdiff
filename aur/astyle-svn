# Maintainer: Armin Fasold <mail@bebna.de>
# Based on astyle PKGBUILD from archlinux/community

pkgver=409  
pkgrel=3

pkgname=astyle-svn
pkgdesc="A free, fast and small automatic formatter for C, C++, C-Sharp, and Java source code."
url="http://sourceforge.net/projects/astyle/"
arch=("i686" "x86_64")
license=("LGPL")
depends=("gcc-libs")
makedepends=("subversion")
conflicts=("astyle")
provides=("astyle")

source=("svn+http://svn.code.sf.net/p/astyle/code/trunk/AStyle")
md5sums=("SKIP")

build() {
	cd "AStyle/build/gcc"
	make
}

package() {
	cd "AStyle/build/gcc"

  install -Dm0755 bin/astyle "$pkgdir/usr/bin/astyle"

  # install documentation (implements FS#23290)
  install -d "$pkgdir/usr/share/doc/astyle/"
  for f in ../../doc/*.html; do
  	install -m0644 "$f" "$pkgdir/usr/share/doc/astyle/"
  done
}

pkgver() {
	cd AStyle
	svnversion | tr -d [A-z]
}
