# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Jimmy Tang <jtang@tchpc.tcd.ie>

pkgname=pristine-tar
pkgver=1.28
pkgrel=2
pkgdesc="Tool to regenerate a pristine upstream tarball using only a small binary delta file and a copy of the source which can be a revision control checkout."
arch=('i686' 'x86_64')
url="http://joeyh.name/code/pristine-tar/"
license=('GPL')
depends=('perl' 'xdelta')
conflicts=('pristine-tar-git')
source=(http://ftp.debian.org/debian/pool/main/p/${pkgname}/${pkgname}_${pkgver}.tar.gz
        0001-implement-tar-format-guessing.patch
        0002-Use-posix-tar-format-by-default.patch
        0003-Mangle-PAX-headers-when-using-posix-tar-format.patch
        0004-Remove-all-timestamps-from-extended-PAX-headers.patch)
sha256sums=('67c48238bc39173bd0ee50d35e468c0d556eedc2ee61fd0271c4f9a1d12f2238'
            '0bebe5749abd0784c06e34c07402514db3027c7694ed5b686c7f37631d97609a'
            'a41617db19c85b18b9b157b2cb3e523f5484472b434cc9f69717499e268e7fcf'
            'c8848744d934eebe9d672f92f57b05929edc13f608835c7da367479ce1bdd992'
            'a6ddb2602d536fa1f4a2e6f748ad8bac2aa49d8f533e3b5993f2f77716bcdb73')

prepare() {
  cd "$srcdir/$pkgname"

  # upstream commit c0e427ad7ccaefbb81e99fe58fadb0110eab27c4
  patch -Np1 -i "$srcdir"/0001-implement-tar-format-guessing.patch

  # three patches used in the Tizen tools package (improvements on the previous patch)
  patch -Np1 -i "$srcdir"/0002-Use-posix-tar-format-by-default.patch
  patch -Np1 -i "$srcdir"/0003-Mangle-PAX-headers-when-using-posix-tar-format.patch
  patch -Np1 -i "$srcdir"/0004-Remove-all-timestamps-from-extended-PAX-headers.patch
}

build() {
  cd "$srcdir/$pkgname"

  export PATH=/usr/bin/core_perl:$PATH

  perl Makefile.PL
  make
}

package() {
  cd "$srcdir/$pkgname"

  make install DESTDIR=$pkgdir PREFIX=/usr INSTALLSITESCRIPT=/usr/bin
  install -d $pkgdir/usr/share/doc/pristine-tar
  install -D GPL TODO delta-format.txt $pkgdir/usr/share/doc/pristine-tar
}

# vim:set ts=2 sw=2 et:
