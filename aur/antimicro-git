# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=antimicro-git
pkgver=1.0.22.gc19b80b
pkgrel=2
pkgdesc="Graphical program used to map keyboard and mouse buttons to gamepad buttons"
arch=('i686' 'x86_64')
url="https://github.com/Ryochan7/antimicro"
license=('GPL3')
depends=('libxtst' 'qt4' 'sdl')
provides=('antimicro')
conflicts=('antimicro')
install=antimicro-git.install

source=("$pkgname"::'git+https://github.com/Ryochan7/antimicro')

md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    local ver="$(git describe)"
    printf "%s" "${ver//-/.}"
}

build() {
    cd "$pkgname"/src
    qmake-qt4 INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$pkgname"/src
    make INSTALL_ROOT="$pkgdir" install
}
