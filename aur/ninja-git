# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja-git
epoch=1
pkgver=1658.a3c823c
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='http://martine.github.com/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'python2' 're2c')
# makedepends+=('emacs')
optdepends=('emacs: for Emacs mode')
provides=('ninja')
conflicts=('ninja')
install=ninja-git.install

source=('git://github.com/martine/ninja.git')
md5sums=('SKIP')

_python="python2"

pkgver() {
	cd ninja

	printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd ninja

	"${_python}" bootstrap.py
	if [[ "${makedepends[@]}" =~ "emacs" ]]; then
		emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
	fi
	asciidoc doc/manual.asciidoc
}

package() {
	cd ninja

	# Main binary
	install -m755 -D ninja \
						"${pkgdir}/usr/bin/ninja"

	# Manual
	install -m644 -D doc/manual.asciidoc \
						"${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
	install -m644 -D doc/manual.html \
						"${pkgdir}/usr/share/doc/ninja/manual.html"

	# Emacs mode
	install -m644 -D misc/ninja-mode.el \
						"${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
	if [[ "${makedepends[@]}" =~ "emacs" ]]; then
		install -m644 -D misc/ninja-mode.elc \
						"${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.elc"
	fi
}
