# Maintainer: Attila Bukor <r1pp3rj4ck[at]w4it[dot]eu>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Slava Volkov <sv99sv[at]gmail[dot]com>

pkgname=phpstorm
_pkgname=PhpStorm # Directory name in the tar file
pkgbuild=131.374
pkgver=7.0
pkgrel=2
pkgdesc="Lightweight and Smart PHP IDE. 30-day free trial."
arch=('i686' 'x86_64')
options=(!strip)
url="http://www.jetbrains.com/phpstorm/"
license=('custom')
depends=('java-environment>=6' 'bash' 'lib32-glibc' 'intellij-idea-libs')
source=(http://download.jetbrains.com/webide/PhpStorm-$pkgver.tar.gz
        phpstorm.desktop
        phpstorm.sh)
md5sums=('58772330b38eca7609d6a0cdd6336429'
         'fd8a6e3388991818c1f0a64cb5ec4631'
         '2d31cd6e4f3dbbe84ffe5ecdf3771634')

package() {
  install -d -m 755 ${pkgdir}/usr/share
  install -d -m 755 ${pkgdir}/usr/lib
  cp -a ${srcdir}/${_pkgname}-${pkgbuild} $pkgdir/usr/share/${pkgname}

  [ $CARCH == "x86_64" ] && SUFFIX=64
  install -m 644 ${pkgdir}/usr/share/${pkgname}/bin/libyjpagent-linux${SUFFIX}.so ${pkgdir}/usr/lib

  [ $CARCH == "x86_64" ] && SUFFIX="_64"
  install -m 644 ${pkgdir}/usr/share/${pkgname}/plugins/terminal/lib/linux/x86${SUFFIX}/libpty.so ${pkgdir}/usr/lib

  rm -rf ${pkgdir}/usr/share/${pkgname}/bin/{fsnotifier,libyjpagent-linux}*
  rm -rf ${pkgdir}/usr/share/${pkgname}/plugins/terminal/lib/{linux,macosx,win}

  install -D -m 755 ${pkgdir}/usr/share/${pkgname}/bin/phpstorm.sh ${pkgdir}/usr/bin/phpstorm
  install -D -m 755 ${srcdir}/phpstorm.sh ${pkgdir}/usr/bin/phpstorm.sh
  install -D -m 644 ${srcdir}/phpstorm.desktop ${pkgdir}/usr/share/applications/phpstorm.desktop
  install -D -m 644 ${pkgdir}/usr/share/${pkgname}/bin/webide.png ${pkgdir}/usr/share/pixmaps/phpstorm.png
  install -D -m 644 ${srcdir}/${_pkgname}-${pkgbuild}/license/${_pkgname}_license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
