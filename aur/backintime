# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Matias Korhonen <webadmin@draco-vulgaris.net>
# Contributor: Dylon Edwards <deltaecho@archlinux.us>

pkgname=backintime
true && pkgname=(backintime backintime-gtk backintime-gnome backintime-qt4)
pkgver=1.0.28
pkgrel=3
arch=('any')
url="http://backintime.le-web.org/"
license=('GPL')
depends=('python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring' 'python2-secretstorage')
optdepends=('openssh: Support for remote backups over SFTP')
source=("http://backintime.le-web.org/download/backintime/backintime-$pkgver.tar.gz")
sha256sums=('d5c6785f2c9efa2d50242847b0dbaff9cc9c4a3c5f3c48c7b6844c56f591d5ba')

build() {
  cd common
  ./configure
  make

  for t in gnome kde4; do
    cd "$srcdir/$t"
    ./configure --no-check
    make
  done
}

package_backintime() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. CLI version."
  depends=('python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring'
  'python2-secretstorage')
  optdepends=('openssh: Support for remote backups over SFTP')
  conflicts=('backintime-bzr' 'backintime-kde4'
  'backintime-gnome' 'backintime-gtk' 'backintime-qt4')

  cd common
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-gnome() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. Gnome GUI version."
  depends=('python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring'
  'python2-secretstorage' 'pygtk' 'python2-notify' 'gnome-python' 'meld' 'gksu' 'gnome-session')
  optdepends=('openssh: Support for remote backups over SFTP')
  provides=('backintime')
  conflicts=('backintime-bzr' 'backintime-kde4'
  'backintime' 'backintime-gtk' 'backintime-qt4')

  cd common
  make DESTDIR="$pkgdir" install

  cd "$srcdir/gnome"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-gtk() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. GTK GUI version."
  depends=('python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring'
  'python2-secretstorage' 'pygtk' 'python2-notify' 'gnome-python' 'meld' 'gksu')
  optdepends=('openssh: Support for remote backups over SFTP')
  provides=('backintime')
  conflicts=('backintime-bzr' 'backintime-kde4'
  'backintime-gnome' 'backintime' 'backintime-qt4')

  cd common
  make DESTDIR="$pkgdir" install

  cd "$srcdir/gnome"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-qt4() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. QT4 GUI version."
  depends=('python2' 'rsync' 'cron' 'python2-gnomekeyring' 'python2-keyring'
  'python2-secretstorage' 'xorg-utils' 'python2-pyqt' 'kdebindings-python2')
  optdepends=('openssh: Support for remote backups over SFTP')
  provides=('backintime')
  replaces=('backintime-kde4')
  conflicts=('backintime-bzr' 'backintime-kde4'
  'backintime-gnome' 'backintime-gtk' 'backintime')

  cd common
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kde4"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

# Global pkgdesc for the AUR to pick up
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault."

# vim:set ts=2 sw=2 et:
