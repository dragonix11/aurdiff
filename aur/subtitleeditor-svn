pkgname=subtitleeditor-svn
pkgver=0.41.0.r848 
pkgrel=1
pkgdesc="GTK+2 tool to create and edit subtitles."
arch=('i686' 'x86_64')
url="http://home.gna.org/subtitleeditor/"
license=('GPL3')
depends=('desktop-file-utils' 'enchant' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 
         'gstreamermm' 'gtk2' 'gtkmm' 'hicolor-icon-theme' 'libsigc++' 'libxml++' 'xdg-utils')
makedepends=('svn' 'intltool')
optdepends=('gstreamer0.10-ffmpeg: to be able to view movies inside the subtitleeditor'
            'gstreamer0.10-ugly-plugins: to be able to view movies inside the subtitleeditor')
provides=(${pkgname%-*})
conflicts=(${pkgname%-*})
install=${pkgname%-*}.install
source=('subtitleeditor-svn::svn+http://svn.gna.org/svn/subtitleeditor/trunk')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  local ver="$(svnversion)"
  printf "0.41.0.r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd $pkgname

  sh autogen.sh --prefix=/usr
  make
}

package () {
  cd $pkgname

  make DESTDIR=${pkgdir} install
}
