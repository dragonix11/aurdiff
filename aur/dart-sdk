# Maintainer : T. Jameson Little <t.jameson.little at gmail dot com>
# Maintainer : Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
pkgname=dart-sdk
pkgver=0.8.7.0
pkgrel=1
pkgdesc="the Dart language SDK. (integration version as tested)"
arch=('i686' 'x86_64')
url="http://www.dartlang.org/docs/sdk/"
license=('BSD')
depends=('unzip' 'wget')
optdepends=('java-runtime: for dartanalyzer')
makedepends=()
provides=()
conflicts=()
replaces=()
install=dart-sdk.install
changelog=dart-sdk.changelog
noextract=()

if test "$CARCH" == x86_64; then
	source=("https://storage.googleapis.com/dart-editor-archive-integration/29341/dartsdk-linux-64.tar.gz")
	md5sums=('59d2af72f67f62a789409d2945f4552a')
else
	source=("https://storage.googleapis.com/dart-editor-archive-integration/29341/dartsdk-linux-32.tar.gz")
	md5sums=('ebd5a4bc5d21de3b9ca8caa7f8dc2721')
fi

source+=(dart2js.patch dartanalyzer.patch dartdoc.patch pub.patch)
md5sums+=('88ed196db15741f160c8c4cf3bec476d' '2260eac691474c1f0c37a27955ff96e5' '8ac872057860951d29d7241339f4fde0' 'b4fe0e30b181805e8fa43108e706203a')

package(){
	patch -p1 -i ${srcdir}/dart2js.patch
	patch -p1 -i ${srcdir}/dartanalyzer.patch
	patch -p1 -i ${srcdir}/dartdoc.patch
	patch -p1 -i ${srcdir}/pub.patch

	mkdir -p ${pkgdir}/opt
	find ${srcdir}/dart-sdk -perm -u+x -exec chmod +x '{}' +
	find ${srcdir}/dart-sdk -perm -u+r -exec chmod +r '{}' +
	cp -r ${srcdir}/dart-sdk ${pkgdir}/opt/

	mkdir -p ${pkgdir}/usr/bin

	# make sure dart is executable
	chmod +x ${srcdir}/dart-sdk/bin/dart

	# add symlinks
	for _f in dart dartanalyzer dartdoc dart2js pub; do
		ln -s /opt/dart-sdk/bin/$_f ${pkgdir}/usr/bin/$_f
	done
}
