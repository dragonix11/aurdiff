# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libquvi-git
pkgver=0.9.2+1+gf02cc3a
pkgrel=1
pkgdesc='Cross-platform library for parsing flash media stream URLs. (GIT version)'
arch=('i686' 'x86_64')
url="http://quvi.sourceforge.net/"
license=('LGPL')
depends=('libquvi-scripts' 'curl' 'lua51' 'libproxy' 'libgcrypt' 'glib2')
makedepends=('git' 'asciidoc')
provides=('libquvi')
conflicts=('libquvi')
options=('!libtool')
source=('git://repo.or.cz/libquvi.git#branch=next'
        'am_subdir_objects.patch')
sha256sums=('SKIP'
            '85745fb92412b40b5cdd1a0718b8d264b67f289ddb7bf582626e18b4e9d7e677')

pkgver() {
  cd 'libquvi'
  git describe | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd 'libquvi'
  patch -p1 < "$srcdir/am_subdir_objects.patch"
}

build() {
  cd 'libquvi'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd 'libquvi'

  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh sw=2 ts=2 et
