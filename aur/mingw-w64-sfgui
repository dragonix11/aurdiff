pkgname=mingw-w64-sfgui
pkgver=0.1.1
pkgrel=1
pkgdesc="SFGUI (Simple and Fast Graphical User Interface), a C++ GUI library for SFML. (mingw-w64)"
arch=(any)
url="http://sfgui.sfml-dev.de"
license=("zlib")
makedepends=(mingw-w64-gcc mingw-w64-cmake sfml dos2unix)
depends=(mingw-w64-crt
"mingw-w64-sfml>=2.0")
options=(!libtool !strip !buildflags)
source=("SFGUI-$pkgver-source.zip::http://sfgui.sfml-dev.de/download/21")
md5sums=('c1fbaaaaa20eb4ed05790d8c73e597af')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/SFGUI-$pkgver-source"
  find . -type f -exec dos2unix {} \;
}

build() {
  cd "$srcdir/SFGUI-$pkgver-source"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ${_arch}-cmake \
      -DSFML_SYSTEM_LIBRARY_RELEASE=/usr/${_arch}/lib/libsfml-system.a \
      -DSFML_WINDOW_LIBRARY_RELEASE=/usr/${_arch}/lib/libsfml-window.a \
      -DSFML_GRAPHICS_LIBRARY_RELEASE=/usr/${_arch}/lib/libsfml-graphics.a \
      -DSFGUI_BUILD_EXAMPLES=OFF \
      -DSFGUI_INCLUDE_FONT=OFF \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/SFGUI-$pkgver-source/build-$_arch"
    make DESTDIR=$pkgdir install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/"{AUTHORS,LICENSE,README}
  done
}