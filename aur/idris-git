# Maintainer: Leif Warner <abimelech@gmail.com>
pkgname=idris-git
pkgver=20130223
pkgrel=1
pkgdesc="Functional programming language with dependent types."
url="http://www.idris-lang.org"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-cabal' 'haskell-binary' 'haskell-bytestring' 'haskell-containers' 'haskell-directory' 'haskell-filepath' 'haskell-haskeline' 'haskell-mtl' 'haskell-parsec' 'haskell-pretty' 'haskell-process' 'haskell-transformers' 'haskell-split' 'haskell-libffi' 'haskell-language-java' 'haskell-llvm-general' 'haskell-vector-binary-instances' 'gc' 'haskell-ansi-terminal' 'haskell-trifecta>=1.1')
depends=('gmp' 'libffi' 'gcc')
optdepends=(
  'gc: dynamically linked from the output of the llvm backend'
  'maven: downloads runtime when using Java backend'
)
options=('strip')
conflicts=('idris')
_gitroot=https://github.com/edwinb/Idris-dev.git
_gitname=Idris-dev
build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #
  runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
  runhaskell Setup build
}
package() {
    cd "$srcdir/$_gitname-build"
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
    rmdir ${pkgdir}/usr/share/doc/${pkgname}
}
