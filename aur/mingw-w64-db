pkgname=mingw-w64-db
pkgver=5.3.28
pkgrel=1
pkgdesc="The Berkeley DB embedded database system (mingw-w64)"
arch=(any)
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=("custom")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz")
sha1sums=('fa3f8a41ad5101f43d08bc0efb6241c9b6fc1ae9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
_exes="archive checkpoint deadlock dump hotbackup load log_verify printlog recover replicate stat tuner upgrade verify"

prepare() {
	cd "$srcdir/db-$pkgver"
  sed -i "s,WinIoCtl.h,winioctl.h,g" "src/dbinc/win_db.h"
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "$srcdir/db-$pkgver/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}/build_windows"
    ../dist/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-compat185 \
      --enable-cxx \
      --enable-mingw \
      --enable-stl \
      --enable-localization \
      --enable-dbm
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}/build_windows/.libs"
    for _exe in ${_exes}; do
			mv "db_${_exe}" "db_${_exe}.exe"
		done
		cd ..
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/docs"
  done
}