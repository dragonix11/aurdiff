# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena
pkgver=0.2.69
pkgrel=1
pkgdesc="Milena is a Polish speech synthesizer for Linux"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=('milena-data' 'mbrola-voices-pl1' 'enca' 'antiword' 'odt2txt' 'poppler' 'libao' 'sox')
install=milena.install
optdepends=('milena-basewords: for milena_book'
            'lame: for milena_book'
            'vorbis-tools: for milena_book'
            'faac: for milena_book'
            'amrwb: for milena_nokia'
            'ImageMagick: for milena_nokia')
source=(http://tts.polip.com/files/$pkgname-dist-$pkgver.tar.gz
        10-makefile-fix.patch
        20-build_without_data_archive.patch
        40-archlinux_package.patch)
sha256sums=('225591a3c1aa644d10d73271503497df21e9f6e3bd9feb18f82a44e580a43e34'
            '959c032ff5cde1a0a0999aee27ddc7c81c6292c353dc3ae22b3495d4f2d59676'
            '4c99227e07a7a81f377f9ce4706f6e33a366a9ce32f0c50a4995b4f81ac2c871'
            '29944b5894544f48aab8886cc6bc7cac5d720235b277b4d2dbb47252bd3534f0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying patch: ${source[i]##*/}"
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" prefix="/usr" install
}

# vim:set ts=2 sw=2 et:
