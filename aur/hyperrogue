# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=hyperrogue
pkgver=3.8
_pkgver=38
pkgrel=1
pkgdesc="You are a lone outsider in a strange, non-Euclidean hyperbolic world."
arch=('i686' 'x86_64')
url="http://www.roguetemple.com/z/hyper.php"
license=('GPL2')
depends=('sdl_gfx' 'sdl_ttf')
makedepends=()
source=(http://www.roguetemple.com/z/$pkgname-$_pkgver.zip)
md5sums=('cc594f027a317f58d9f8b1b545e3e677')

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    chmod -x *
    sed -i 's|VeraBd.ttf|/usr/share/hyperrogue/&|' graph.cpp
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    install -Dm755 hyper      "$pkgdir/usr/bin/hyperrogue"
    install -Dm644 VeraBd.ttf "$pkgdir/usr/share/hyperrogue/VeraBd.ttf"
}

