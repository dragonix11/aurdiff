# Maintainer: Buce <dmbuce@gmail.com>

pkgname=pacrepo-git
pkgver=0.r31.g7a25d28
pkgver() {
  cd "$srcdir/$pkgname"
  if ! git describe --tags 2>/dev/null; then
    echo "0.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
  fi | sed 's/-/.r/; s/-/./g'
}
pkgrel=1
pkgdesc="A simple pacman repository management script"
arch=(any)
url="https://github.com/DMBuce/pacrepo"
license=('BSD')
groups=()
depends=('openssh' 'pacman' 'bash')
makedepends=('git')
provides=(pacrepo)
conflicts=(pacrepo)
replaces=()
backup=('etc/pacrepo.conf')
options=()
install=
source=("$pkgname::git://github.com/DMBuce/pacrepo.git")
md5sums=('SKIP')

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"
  make prefix=/usr sysconfdir=/etc localstatedir=/var DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
