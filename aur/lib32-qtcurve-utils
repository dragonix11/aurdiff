# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: xduugu

pkgname=lib32-qtcurve-utils
pkgver=1.8.17
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk. Multilib.'
arch=('x86_64')
url="https://github.com/QtCurve/qtcurve"
license=('GPL')
groups=('qtcurve')
depends=('lib32-libx11')
makedepends=('cmake' 'gcc-multilib' 'lib32-xcb-util-image')
source=($url/archive/$pkgver.tar.gz)
md5sums=('c4f2e9bc296398b42bfc132b88a3b989')

build() {
  cd qtcurve-$pkgver

  # export multilib parameters
  # and QT_BUILD_KEY workaround
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  [ -d bld ] || mkdir bld && cd bld

  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DLIB_INSTALL_DIR=/usr/lib32 \
           -DQTC_QT4_ENABLE_KDE=false \
           -DENABLE_GTK2=false \
           -DENABLE_QT5=false

  cd qtcurve-utils
  make
}

package() {
  cd qtcurve-$pkgver/bld/qtcurve-utils
  make DESTDIR="$pkgdir" install
}
