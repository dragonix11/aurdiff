# Maintainer: carstene1ns <carsten.1 gmx de>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com>
# Contributor: Larryhaja  <larryhaja at gmail.com>

pkgname=methane
pkgver=1.5.1
pkgrel=3
pkgdesc="Use your gas gun to capture baddies (aka Super Methane Brothers)"
arch=('i686' 'x86_64')
url="http://methane.sourceforge.net"
license=('GPL')
depends=('clanlib')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tgz"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname-clanlib23.patch"
        "$pkgname-nogl1.patch")
md5sums=('7e4258e326478678c2332388eab2ed14'
         'ee0ebbf6138142365f5c1a287d1691a6'
         'a9ec845554b4987c02723b87d308e573'
         'ca3b7c6d7e959914e086882d40f8d810'
         'c6237f8fc6ebaa95b0ee7ff1caabe69f')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # clanlib related changes
  patch -Np1 -i $srcdir/$pkgname-clanlib23.patch
  # remove opengl1 renderer
  patch -Np1 -i $srcdir/$pkgname-nogl1.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver

  # remove leftover build dir
  rm -fR build

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr/share/$pkgname
  mkdir $pkgdir/usr/bin

  # install desktop entry
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png

  # install data and executable
  cp -R {resources,docs} $pkgdir/usr/share/$pkgname
  install -m755 $pkgname $pkgdir/usr/bin
}
