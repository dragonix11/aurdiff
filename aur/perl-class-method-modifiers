# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-class-method-modifiers'
pkgver='2.08'
pkgrel='1'
pkgdesc="provides Moose-like method modifiers"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-build-tiny>=0.028' 'perl-test-fatal') # 'perl-extutils-installpaths' 'perl-test-checkdeps' 'perl-test-warnings')
url='http://search.cpan.org/dist/Class-Method-Modifiers'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Class-Method-Modifiers-${pkgver}.tar.gz")
md5sums=('32e8bf0e68524866cbc8ae763eb08e74')
sha512sums=('f987ed498591b9b89e3dd5375d0eb17700800e5e4b943a594ce9487e9af1ac8c537b1a8fe09e837732eaff236d7ae7e2c9273402cd0f8317cc587ca4cc62eb5e')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Class-Method-Modifiers-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Build.PL
  ./Build
}

check() {
  prepare_environment
  ./Build test
}

package() {
  prepare_environment
  ./Build install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
