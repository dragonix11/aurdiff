# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2013.09
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=5.5.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(https://github.com/perl6/$pkgname/archive/${pkgver}.tar.gz)
sha512sums=('21d41656584976787234201b16d264e4016b136e490b2cd83630c557842ec41a5402049498b8f231aba88b7484e8e0bc8dcdf590292406ed9d3f4e125af4be3d')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    perl Configure.pl --with-parrot=/usr/bin/parrot --prefix=/usr
    make
}
package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

