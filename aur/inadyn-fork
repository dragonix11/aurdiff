# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=inadyn-fork
pkgver=1.99.4
pkgrel=2
pkgdesc="Simple dynamic DNS client - fork of the original INADYN implementation from Narcis Ilisei"
url=http://troglobit.com/inadyn.html
arch=(x86_64 i686 armv6h)
license=(GPL)
backup=(etc/inadyn.conf)
conflicts=(inadyn-opendns inadyn)
provides=(inadyn)
source=(https://github.com/troglobit/inadyn/archive/$pkgver.tar.gz
        inadyn.conf
        inadyn.service)
sha256sums=('3d10299e51c1c569aecd78704eb162efb0df60b948e7dc68a13993ac1c23610d'
            '9196481a3c6875a0ead3a318608877ef49402c968f162f13a2f92d6882008c41'
            '0504e087e1c8c3216312cbdec961cf31ec383c56010cbab307cc3d696030918c')


prepare(){
  cd inadyn-$pkgver
  sed -i -e 's/sbin/bin/g' Makefile
}

build(){
  make -C inadyn-$pkgver
}

package(){
  make -C inadyn-$pkgver prefix=/usr DESTDIR=$pkgdir install
  install -Dm644 inadyn.service $pkgdir/usr/lib/systemd/system/inadyn.service
  install -Dm644 inadyn.conf $pkgdir/etc/inadyn.conf
}
