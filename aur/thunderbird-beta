# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Enable PGO?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-beta
pkgver=24.0
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="https://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libnotify' 'libvpx' 'libxt' 'mime-types' 'mozilla-common' 'nss' 'sqlite' 'startup-notification')
makedepends=('autoconf2.13' 'mesa' 'python2' 'unzip' 'wireless_tools' 'yasm' 'zip')
[ "$_pgo" = "1" ] && makedepends+=('imake' 'xorg-server-xvfb')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
options=('!emptydirs' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2"
        'mozconfig'
        "$pkgname.desktop"
        'vendor.js')
md5sums=(`curl -sL ${source/\/so*}/MD5SUMS | grep source | sed -n 3p | cut -d " " -f1` # thunderbird-$pkgver.source.tar.bz2
         '41511a6bcdd04cf56706218e4670e631'   # mozconfig
         'f8a9b91202e8c85ad7b8f23fb82a4d7b'   # $pkgname.desktop
         '5a53179d14ae9631b7afe5e4d0fc0b25')  # vendor.js
# # Alternative mirror:
# source[0]="http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2"

prepare() {
  cd comm-*

  cp "$srcdir"/mozconfig .mozconfig

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd comm-*

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname-$pkgver"
  export PYTHON="/usr/bin/python2"

  if [ "$_pgo" = "1" ]; then
    # Tweak .mozconfig
    _options="mk_add_options PROFILE_GEN_SCRIPT='\$(PYTHON) \$(MOZ_OBJDIR)/_profile/pgo/profileserver.py 10'"
    echo -e "# PGO\n$_options" >> .mozconfig

    # Prepare
    export DISPLAY=:99
    Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
  fi

  # Build
  make -f client.mk build MOZ_PGO=$_pgo || kill $!
}

package() {
  cd comm-*/obj-*
  make package

  cd mozilla/dist/
  tar -xf thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir"/opt/$pkgname-$pkgver/
  ln -sf /opt/$pkgname-$pkgver/thunderbird "$pkgdir"/usr/bin/$pkgname

  install -Dm644 "$srcdir"/vendor.js "$pkgdir"/opt/$pkgname-$pkgver/defaults/preferences/vendor.js

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir"/comm-*/other-licenses/branding/thunderbird/mailicon${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/$pkgname.png
  done

  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  rm -rf "$pkgdir"/opt/$pkgname-$pkgver/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir"/opt/$pkgname-$pkgver/dictionaries
  ln -s /usr/share/hyphen "$pkgdir"/opt/$pkgname-$pkgver/hyphenation

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir"/opt/$pkgname-$pkgver/defaults/pref/channel-prefs.js
}