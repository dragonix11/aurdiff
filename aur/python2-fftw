# Maintainer: Rich Lindsley <rich at dranek dot com>
pkgname=python2-fftw
_py_pkgname=pyFFTW # The pypi package name
pkgver=0.9.2
pkgrel=1
pkgdesc="A pythonic wrapper around FFTW"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyFFTW/"
license=('GPL3')
depends=('python2-numpy' 'fftw')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/p/$_py_pkgname/$_py_pkgname-$pkgver.tar.gz)
md5sums=('809df137afcf86641718da087f130a30')
sha1sums=('534f013186f3815fb51055132b47129fba9dfe13')
sha256sums=('1124af785d3e08b242318c6dfede0c5b0d263e40e5c06d73cbbe6cceb20ae823')

build() {
  cd "$srcdir/$_py_pkgname-$pkgver"

  # Enforce this to be a strictly python2 package
  sed -i -e "s|^#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|^#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|^#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${srcdir} -name '*.py')
}

package() {
  cd "$srcdir/$_py_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
