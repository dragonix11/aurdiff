# Maintainer : alucryd <alucryd at gmail dot com>

pkgname=beat
pkgver=03
pkgrel=1
pkgdesc="Delta patcher for the higan emulator"
arch=('i686' 'x86_64')
url="http://byuu.org/programming/beat/"
license=('GPL3')
depends=('gtk2')
makedepends=('gendesk')
source=("http://byuu.org/files/beat_v${pkgver}.tar.xz")
sha256sums=('0f4a33cbbe54af28077433ca9f855e5d80ae942d5d976a0d654a8e1a528a1a1a')

prepare() {
  gendesk -n --pkgname "beat" --pkgdesc "${pkgdesc}" --name='beat' --exec='beat'
}

build() {
  make compiler=g++ platform=x phoenix=gtk flags="$CXXFLAGS -I.. -fomit-frame-pointer -std=gnu++11" link='-s -lX11' -C beat
}

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,share/applications}
  install -m 755 beat/beat "${pkgdir}"/usr/bin/beat
  install -m 644 beat.desktop "${pkgdir}"/usr/share/applications/beat.desktop
}

# vim: ts=2 sw=2 et:
