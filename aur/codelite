# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: nem <nem@ikitten.co.uk>
# Contributor: agvares <agvares13@gmail.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>

pkgname=codelite
pkgver=5.3
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
arch=('i686' 'x86_64')
url="http://www.codelite.org/"
license=('GPL')
depends=('wxgtk2.9>=2.9.4' 'curl' 'webkitgtk2' 'libssh')
makedepends=('pkgconfig' 'cmake')
optdepends=('graphviz: callgraph visualization')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-gtk.src.tar.gz
	http://repos.codelite.org/wxCrafterLibs/wxgui.zip
        codelite.patch)
md5sums=('ca4da7644ce58c5730647a0b5549d0f4'
         '645b066d5927586361ab042d9b7f5eab'
         'd0d3831d383cdfe793daf04497c689c6')
if [[ "$CARCH" == 'i686' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/32/wxCrafter.so)
  md5sums+=('32cfaffcf6e67607d7a75bc2466a52ea')
elif [[ "$CARCH" == 'x86_64' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/64/wxCrafter.so)
  md5sums+=('f58453ee73920eb9d4a1ba8773b4f3ec')
fi
noextract=('wxgui.zip')


build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -p1 -i ../codelite.patch
    mkdir -p build
    cd build
    sed -i 's/wx-config/wx-config-2.9/g' ../CMakeLists.txt
    cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9 ..
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make -j1 DESTDIR=${pkgdir} install
    install -m 755 -D ${srcdir}/wxCrafter.so ${pkgdir}/usr/lib/codelite/wxCrafter.so
    install -m 644 -D ${srcdir}/wxgui.zip ${pkgdir}/usr/share/codelite/wxgui.zip
    install -m 644 -D ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
