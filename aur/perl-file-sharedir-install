# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: AUR Perl <aurperl@juster.info>

_perlmod=File-ShareDir-Install
_modnamespace=File
pkgname=perl-file-sharedir-install
pkgver=0.05
pkgrel=1
pkgdesc="File::ShareDir::Install - Install shared files"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-${pkgver}.tar.gz")
sha512sums=('7191cbca7fbc7e9f81b9a81d38bfab571e645200098c2842d9811db56e9989b9ca17807e3a3c6ffd22b7d1882ccacd24cc5bce7444c9b58dc78c3b6e5dfdf625')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
