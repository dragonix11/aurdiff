# Maintainer: X0rg

pkgname=gnustep-base-clang-svn
_svnname=gnustep-base
pkgver=37254
pkgrel=3
pkgdesc="The GNUstep base package, using Clang"
arch=('any')
url="http://www.gnustep.org/"
license=('GPL2' 'GPL3' 'LGPL3')
groups=('gnustep-clang-svn')
depends=('libffi' 'libxml2' 'libxslt' 'openssl' 'gnutls' 'icu' 'avahi' 'xz' 'gnustep-libobjc2-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
optdepends=('iconv: only if you don`t have glibc'
	'ffcall: alternative for libffi')
conflicts=('gnustep-base' 'gnustep-base-svn')
options=('!emptydirs' '!buildflags')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/base/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

build() {
  cd "$_svnname"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"

  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" ./configure --prefix=/usr --libdir=/usr/lib \
    --with-default-config=/etc/GNUstep/GNUstep.conf --with-config-file=/etc/GNUstep/GNUstep.conf \
    --disable-unicodeconstants --with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
# }

package() {
  cd "$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install

  msg2 "Fix permissions..."
  chown -Rc root:root "$pkgdir/"
}
