# Maintainer: graysky <graysky AT archlinux DOT org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )

pkgname=broadcom-wl-ck
pkgver=6.30.223.141
pkgrel=4
_pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver for linux-ck.'
_extramodules="extramodules-3.11-ck"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-ck>=3.11' 'linux-ck<3.12')
makedepends=('linux-ck-headers>=3.11' 'linux-ck-headers<3.12')
conflicts=('broadcom-wl-ck-atom' 'broadcom-wl-ck-barcelona' 'broadcom-wl-ck-bulldozer' 'broadcom-wl-ck-corex' 'broadcom-wl-ck-core2' 'broadcom-wl-ck-haswell' 'broadcom-wl-ck-ivybridge' 'broadcom-wl-ck-kx' 'broadcom-wl-ck-k10' 'broadcom-wl-ck-nehalem' 'broadcom-wl-ck-p4' 'broadcom-wl-ck-piledriver' 'broadcom-wl-ck-pentm' 'broadcom-wl-ck-sandybridge')
#replaces=()
#groups=('ck-generic')

[[ $CARCH = x86_64 ]] && _arch=_64 || _arch=
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35${_arch}-nodebug-pcoem-${pkgver//./_}.tar.gz"
'modprobe.d'
'license.patch'
'linux-recent.patch')
sha256sums=('d57c33f6bf4ebe68cac67ffe39c2260b8990bb0f07413dfd021dd4db845199a7'
            'f660ad9379950c61586892e04488be4138f8471b6c0a8cc09aba10a210e22539'
            '24d02018e4e5b90516ba3e98bd957ad91e758944c8a21e22d56a516f2361c6f9'
            'a57e409b3ef9d7b3d630dd2d55e2383cc57ca8f3dc5d2c4854a882ffeaf77bcd')
[[ $CARCH = x86_64 ]] && sha256sums[0]='5f37b2b879e29b220dc64ce2e93d922dc231d4241da03bcbab15ced10e649b4a'
backup=('etc/modprobe.d/broadcom-wl_ck.conf')
install=broadcom-wl-ck.install

prepare() {
  patch -p1 -i linux-recent.patch
  patch -p1 -i license.patch
}


build() {
	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

	# makepkg does not do this automatically for this pkg so do it here
	gzip -9 "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl_ck.conf
}
