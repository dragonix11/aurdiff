# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Robert Kornmesser <Robert.Kornmesser@gfz-potsdam.de>

pkgname=libbluedevil-bluez5-git
_gitname=libbluedevil
pkgver=v1.9.2.18.g6216cea
pkgrel=1
pkgdesc='A Qt wrapper for bluez5 used in the new KDE bluetooth stack - Git build'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/libs/libbluedevil"
license=('GPL')
depends=('qt4' 'bluez')
makedepends=('cmake' 'git')
provides=("libbluedevil")
conflicts=("libbluedevil")
source=('git://anongit.kde.org/libbluedevil.git#branch=bluez5')
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  # Use the tag of the last commit
  echo "$(git describe --always | sed 's|-|.|g')"
}

build() {
  cd "${_gitname}"
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}