# Maintainer: bjoern lindig (bjoern _dot_ lindig _at_ google.com)
pkgname=faust2-git
pkgver=20130405
pkgrel=1
pkgdesc="A compiled language for real-time audio signal processing. This package provides a new backend for Faust, that can be used to directly produce LLVM Intermediate Representation."
arch=('i686' 'x86_64')
url="http://faust.grame.fr/"
license=('GPL')
depends=('llvm' 'ruby')
optdepends=('oscpack: osc support')
makedepends=('git')
provides=('faust')
conflicts=('faust')
source=("$pkgname.patch")

_gitroot="git://faudiostream.git.sourceforge.net/gitroot/faudiostream/faudiostream"
_gitname="faudiostream"
_providesname="faust"

md5sums=('53b26c2160e38f93e3861aad445244eb')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $srcdir/$_gitname ] ; then
    cd $srcdir/$_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  cd $srcdir/$_gitname && git checkout faust2

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build" && git checkout faust2
  msg "Checked out faust2-branch"

  patch -p2 < ../$pkgname.patch

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr/ DESTDIR="$pkgdir/" install

  # docs
  install -d "$pkgdir/usr/share/doc/$_providesname"
  install -Dm644 documentation/*.{pdf,txt} \
    "$pkgdir/usr/share/doc/$_providesname"

  # examples
  install -d "$pkgdir/usr/share/$_providesname/examples"
  cp -R "examples/"* \
    "$pkgdir/usr/share/$_providesname/examples/"

  ## syntax highlighting files
  cd syntax-highlighting

  # kate
  install -Dm644 $_providesname.xml \
    "$pkgdir/usr/share/apps/katepart/syntax/$_providesname.xml"
  # gedit
  install -Dm644 $_providesname.lang \
    "$pkgdir/usr/share/gtksourceview-2.0/language-specs/$_providesname.lang"
  install -Dm644 $_providesname.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/$_providesname.lang"

  # highlight 
  install -Dm644 dsp.lang \
    "$pkgdir/usr/share/highlight/langDefs/dsp.lang"

  # vim
  install -Dm644 $_providesname.vim \
    "$pkgdir/usr/share/vim/vimfiles/syntax/$_providesname.vim"
}
