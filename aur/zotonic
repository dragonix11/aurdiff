# Maintainer: jdarch <jda.cloud plus archlinux at gmail.com>

pkgname=zotonic
pkgver=0.9.4
pkgrel=2
pkgdesc="Content management system (web-CMS) server written in Erlang"
arch=('any')
url="http://zotonic.com/"
license=("Apache")
depends=('erlang-nox'
         'postgresql'
         'imagemagick')
optdepends=('perl-image-exiftool: Enable image autorotation'
            'nginx: Proxy Zotonic with nginx'
            'varnish: Speed up Zotonic with Varnish')
backup=('etc/zotonic/config'
        'etc/zotonic/erlang.config')
install=${pkgname}.install
source=("http://zotonic.googlecode.com/files/${pkgname}-${pkgver}.zip"
	"${pkgname}.service"
        "config"
        "erlang.config")
md5sums=('ea08f76604a0cb7bf80194fa809c1589'
         '51b5e672c3925d644dcc46b39529c21e'
         '7904af22bf674471c212bde82a2cdcb6'
         '4f09c0c994f5a17844b8ed999d3e7e39')
sha512sums=('b5e09f585c26e07797a8b21f7ea1baf201da05a90a016d3522e649244969cb33a2814b494a746179cd1423284e2689fda50ff1e01a3fe0108b8d127ce90c67a3'
            '70ef1f738f9045d4035a73b14e42846fc95190f346a38f2b604be91a6b872186f99f30c4947e4356ca13ca769cc620e481c878505659a812181a39277e4e96fa'
            '62213581acaadcefacf15253175e27135bcc964768a061624a04ebcb3dc6e761ce3629017ce01286b741884fb0b23c4ebfcb6508f14200d99ecdb4f4860bd000'
            'a6e5069e087a314053dab3774ecb123b5d5888c561430ecf02be34b72b3917c513d076323667188fde0ee6c2014e44f41b3a0a4b8fd79dd17c5523425f6979d8')

# Zotonic will build binary-code on startup
#build() {
#   cd "${srcdir}/${pkgname}"
#   make
#}

package() {
   mkdir -p "${pkgdir}/etc/${pkgname}"
   mkdir -p "${pkgdir}/usr/lib/systemd/system"
   mkdir -p "${pkgdir}/var/lib/${pkgname}"
   mkdir -p "${pkgdir}/var/log/${pkgname}"

   cp -r -t "${pkgdir}/var/lib/${pkgname}" "${srcdir}/${pkgname}"
   install -m 660 "${srcdir}/config" "${pkgdir}/etc/${pkgname}"
   install -m 660 "${srcdir}/erlang.config" "${pkgdir}/etc/${pkgname}"
   install -m 644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system"

   ln -s "/etc/${pkgname}/config" "${pkgdir}/var/lib/${pkgname}/${pkgname}/priv/config"
   ln -s "/etc/${pkgname}/erlang.config" "${pkgdir}/var/lib/${pkgname}/${pkgname}/priv/erlang.config"
}
