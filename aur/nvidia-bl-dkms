# Maintainer: Clément Démoulins <clement@archivel.fr>

pkgname=nvidia-bl-dkms
_modname=nvidia_bl
pkgver=0.17.3
pkgrel=3
pkgdesc="Driver to adjust display backlight on modern mobile NVidia graphics adapters. This version has to be used in conjunction with DKMS."
arch=('any')
license=('GPL')
depends=('dkms' 'linux')
makedepends=('linux-headers')
conflicts=('nvidia-bl')
install=nvidia-bl-dkms.install
url="https://launchpad.net/~mactel-support/+archive/ppa"
_pkg="${pkgname}_${pkgver}%7Emaverick"

source=('https://launchpad.net/~mactel-support/+archive/ppa/+files/'${_pkg}'.tar.gz')
md5sums=('dfe4abfad94e2b6c918a691dbe57e013')

prepare() {
  sed -i 's/__devinitconst//g' "$srcdir/$pkgname-$pkgver/usr/src/dkms_source_tree/nvidia_bl.c"
}

package() {
  mkdir -p "$pkgdir/usr/src/${_modname}-$pkgver"
  cp -a "$srcdir/nvidia-bl-dkms-${pkgver}/usr/src/dkms_source_tree"/* "$pkgdir/usr/src/${_modname}-$pkgver"
  install -d -m755 "$pkgdir/etc/modprobe.d"
  echo "blacklist mbp_nvidia_bl" >> $pkgdir/etc/modprobe.d/nvidia-bl-dkms.conf
}

# vim:set ts=2 sw=2 et:
