# Maintainer: Ner0

pkgname=nemo-git
pkgver=617.25292c9
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
         'cinnamon-desktop-git' 'gnome-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('git' 'gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common')
optdepends=('nemo-dropbox-git: Dropbox integration'
            'nemo-fileroller-git: File Roller integration'
            'gksu: Open as Root'
            'cinnamon-translations-git: Translations')
provides=('nemo' 'nemo-fm' 'nautilus')
conflicts=('nemo' 'nemo-fm')
options=('!emptydirs' '!libtool')
install=nemo.install
source=('git://github.com/linuxmint/nemo.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/nemo"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/nemo"

  # Python2 fix
  sed -i 's/bin\/python/bin\/python2/g' files/usr/share/nemo/actions/myaction.py

  # Fix build
  sed -i '/AC_SUBST(DISABLE_DEPRECATED_CFLAGS)/d' configure.in

  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
}

build() {
  cd "$srcdir/nemo"

  AUTOMAKE=automake \
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-gtk-doc-html \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/nemo"
  make DESTDIR="$pkgdir/" install

  # Autostart only in Cinnamon to avoid conflict with GNOME Classic session
  cp "$pkgdir/etc/xdg/autostart/nemo-autostart.desktop" \
    "$pkgdir/etc/xdg/autostart/nemo-autostart2d.desktop"
  sed -i 's/^AutostartCondition=.*/AutostartCondition=GNOME3 if-session cinnamon/' \
    "$pkgdir/etc/xdg/autostart/nemo-autostart.desktop"
  sed -i 's/^AutostartCondition=.*/AutostartCondition=GNOME3 if-session cinnamon2d/' \
    "$pkgdir/etc/xdg/autostart/nemo-autostart2d.desktop"
}
