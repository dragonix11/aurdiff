# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: anubis2591 <anubis2591 at gmail dot com>

pkgname=kid3-qt
pkgver=2.3
pkgrel=2
pkgdesc="An efficient ID3 tag editor - Non KDE version"
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net"
license=('GPL2')
depends=('chromaprint' 'id3lib' 'qt4' 'taglib')
makedepends=('cmake' 'docbook-xsl')
install=$pkgname.install
source=(http://downloads.sourceforge.net/kid3/kid3-$pkgver.tar.gz)
sha1sums=('59c5e1aab126ca9ff591918500f0b1d90aa9dcea')

prepare() {
 cd kid3-"$pkgver"
 sed -i 's/AVCODEC_MAX_AUDIO_FRAME_SIZE/192000/' src/core/import/ffmpegfingerprintdecoder.cpp
}

build() {
  rm -rf build
  mkdir build && cd build

  cmake ../kid3-"$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DWITH_KDE=OFF \
    -DWITH_PHONON=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
