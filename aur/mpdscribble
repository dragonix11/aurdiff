# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor:  Bart≈Çomiej Piotrowski <nospam@bpiotrowwski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: evr <evanroman at gmail>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.22
pkgrel=7
pkgdesc='MPD client which submits track info to Last.fm'
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'glib2' 'libmpdclient')
install=$pkgname.install
source=(http://www.musicpd.org/download/mpdscribble/${pkgver}/mpdscribble-${pkgver}.tar.bz2
        service)
md5sums=('652ee927b797e9a4cef45494e77047db'
         '6c2407b32e272084ec41e12f79c27b80')

build() {
  cd mpdscribble-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-http-client=soup

  make
}

package() {
  cd mpdscribble-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/service \
    "${pkgdir}"/usr/lib/systemd/system/mpdscribble.service

  # default config is really an example
  install -D -m644 "${pkgdir}"/etc/mpdscribble.conf \
    "${pkgdir}"/usr/share/mpdscribble/mpdscribble.conf.example
  rm -f "${pkgdir}"/etc/mpdscribble.conf

  install -d "${pkgdir}"/var/cache/mpdscribble
  touch "${pkgdir}"/var/cache/mpdscribble/mpdscribble.cache
}

# vim:set ts=2 sw=2 et:
