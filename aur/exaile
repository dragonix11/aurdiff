#! /bin/bash
# Maintainer: Jorge Barroso <jorge.barroso.11 at gmail dot com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Joshua Stiefer <facedelajunk@gmail.com>

pkgname=exaile
pkgver=3.3.1
pkgrel=3
pkgdesc="A full-featured Amarok-style media player for GTK+"
arch=('any')
url="http://www.exaile.org"
license=('GPL')
depends=('python2'
	       	'gstreamer0.10-python'
	       	'gstreamer0.10-good-plugins'
		'gstreamer0.10-base-plugins'
	       	'mutagen'
	       	'python2-dbus'
	       	'pygtk>=2.10'
	       	'librsvg')
makedepends=('make' 'help2man')
optdepends=('pycddb: CD metadata retrieval'
            'python2-bsddb: music collection support'
	          'gstreamer0.10-bad-plugins: support for more formats'
	          'gstreamer0.10-ugly-plugins: support for more formats'
	          'gstreamer0.10-ffmpeg: support for more formats'
            'python2-beautifulsoup3: lyricwiki plugin'
            'python2-notify: notifyosd plugin'
            'notify-osd: notifyosd plugin'
            'webkitgtk2: contextinfo plugin'
            'pywebkitgtk: contextinfo plugin'
            'python2-imaging: contextinfo plugin'
            'streamripper: streamripper plugin'
            'moodbar: moodbar plugin')
source=(https://www.launchpad.net/${pkgname}/3.3.x/${pkgver}/+download/exaile-${pkgver}.tar.gz)
install=$pkgname.install
sha512sums=('995283dab5101562f869a87ed955d56b093ffb9e8e3c6dcc55b830c6d762936843af77dfb84010e175ed5be37b3b75bbf231ea761333c109875dc3e84772ed69')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install

	# fix for clicking files with spaces in names from nautilus
	sed -i "s#%u#%f#" "${pkgdir}/usr/share/applications/exaile.desktop"
  sed -i "s|Exec=$pkgdir/*|Exec=/|" "${pkgdir}/usr/share/dbus-1/services/org.exaile.Exaile.service"
}
