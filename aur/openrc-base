# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: williamh <williamh@gentoo.org>

_cat="sys-apps"
_name="openrc"
_gentoo_uri=("http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86")
_src_uri="http://dev.gentoo.org/~williamh/dist"

_udev="udev-init-scripts"
_uver=26

_net="netifrc"
_nver=0.1

pkgname=${_name}-base
pkgver=0.12.2
pkgrel=1
pkgdesc="Gentoo's universal init system."
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD')
groups=('openrc' 'openrc-base')
depends=('inetutils' 'net-tools' 'pkg-config' 'psmisc' 'sysvinit' 'udev')
optdepends=('dhcpcd-openrc: dhcpcd initscript')
conflicts=('initscripts' 'systemd-sysvcompat' 'openrc' 'openrc-git')
backup=('etc/rc.conf' 'etc/conf.d/consolefont' 'etc/conf.d/keymaps'
	'etc/conf.d/hostname' 'etc/conf.d/modules' 'etc/conf.d/net')
install=${_name}.install
source=("${_src_uri}/${_name}-${pkgver}.tar.bz2"
	"${_src_uri}/${_udev}-${_uver}.tar.bz2"
	"${_gentoo_uri}/${_cat}/${_name}/files/${_name}.logrotate"
	"${_gentoo_uri}/${_cat}/kmod/files/kmod-static-nodes"
	"${_src_uri}/${_net}-${_nver}.tar.bz2"
	"${_gentoo_uri}/${_cat}/sysvinit/files/reboot.sh"
	"${_gentoo_uri}/${_cat}/sysvinit/files/shutdown.sh")

sha256sums=('514b86b52bc468a1ad083fbe084c029e923d3e93467fdd212ab0891682d5b2b9'
            'eff4cd581cb5d57a1fa09772f9f82a37d9a33a3828f0b7e988278a974864d203'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '078b07581e63b5113cef9e85da7d5b53e1fbad91d793f7fa8b7188b364689b0e'
            '65eb507bda9ed51fdac2dcf70de3428229758257039ea4ce8360edb0903e39e9'
            '967fa572014ac6dd69f5e7e24d5250abad9c20c644563b927b295778608cafef'
            '2cc84a5194a949f16a82c6215459f4bf6d8156e50d8d9130d310f085bb208d4c')

package() {
      cd "${srcdir}/${_name}-${pkgver}"
	make \
	  SYSCONFDIR='/etc' \
	  OS='Linux' \
	  BRANDING='Arch Linux' \
	  PREFIX='/usr' \
	  LIBEXECDIR='/usr/lib/rc' \
	  SBINDIR='/usr/bin' \
	  MKSELINUX='no' \
	  MKPAM='pam' \
	  MKTERMCAP='ncurses' \
	  MKNET='oldnet' \
	  DESTDIR="${pkgdir}" \
	install

      install -m 644 "${srcdir}/${_name}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"

      local _binpath='s|/sbin/|/usr/bin/|g'

      sed -e "${_binpath}" -i "${pkgdir}/etc/inittab"

      install -Dm 0644 "${srcdir}/${_name}.logrotate" "${pkgdir}/etc/logrotate.d/${_name}"

      # shutdown & reboot scripts for certain console setups
      install -Dm755 ${srcdir}/reboot.sh ${pkgdir}/etc/init.d/reboot.sh
      install -Dm755 ${srcdir}/shutdown.sh ${pkgdir}/etc/init.d/shutdown.sh

      sed -e "${_binpath}" -i ${pkgdir}/etc/init.d/reboot.sh
      sed -e "${_binpath}" -i ${pkgdir}/etc/init.d/shutdown.sh

      # udev-init-scripts merge
      cd "${srcdir}/${_udev}-${_uver}"

      make DESTDIR="${pkgdir}" install

      local _shebang='s|#!/sbin/runscript|#!/usr/bin/runscript|'

      _binpath='s|/sbin/udevd|/usr/bin/udevd|g'

      sed -e "${_shebang}" -e "${_binpath}" -i "${pkgdir}/etc/init.d/udev"
      sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/udev-mount"

      # kmod-static-nodes
      install -Dm755 "${srcdir}/kmod-static-nodes" "${pkgdir}/etc/init.d/kmod-static-nodes"

      sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/kmod-static-nodes"

      # create runlevels
      ln -sf "/etc/init.d/udev" "${pkgdir}/etc/runlevels/sysinit/udev"
      ln -sf "/etc/init.d/udev-mount" "${pkgdir}/etc/runlevels/sysinit/udev-mount"
      ln -sf "/etc/init.d/kmod-static-nodes" "${pkgdir}/etc/runlevels/sysinit/kmod-static-nodes"

      # netifrc merge; comment out for openrc MKNET='newnet'
      cd "${srcdir}/${_net}-${_nver}"
	make \
	  SYSCONFDIR='/etc' \
	  OS='Linux' \
	  BRANDING='Arch Linux' \
	  PREFIX='/usr' \
	  SBINDIR='/usr/bin' \
	  LIBEXECDIR="/usr/lib/${_net}" \
	  DESTDIR="${pkgdir}" \
	install

      install -Dm 644 "${srcdir}/${_net}-${_nver}/doc/net.example" "${pkgdir}/etc/conf.d/net"
      ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"

}
