# Maintainer: X0rg

pkgname=gnustep-corebase-clang-svn
_svnname=gnustep-corebase
pkgver=37254
pkgrel=3
pkgdesc="The GNUstep CoreBase Library is a library of general-purpose, non-graphical C objects, using Clang."
arch=('any')
url="http://www.gnustep.org/"
license=('GPL3' 'LGPL2.1')
groups=('gnustep-clang-svn')
depends=('gnustep-base-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
conflicts=('gnustep-corebase-git')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/corebase/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

build() {
  cd "$_svnname"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"

  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" ./configure --prefix=/usr --libdir=/usr/lib

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
# }

package() {
  cd "$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
