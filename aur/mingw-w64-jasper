pkgname=mingw-w64-jasper
pkgver=1.900.1
pkgrel=4
pkgdesc="A software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard (mingw-w64)"
arch=(any)
url="http://www.ece.uvic.ca/~mdadams/jasper"
license=("custom:JasPer2.0")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-libjpeg-turbo)
options=(staticlibs !strip !buildflags)
source=("http://www.ece.uvic.ca/~mdadams/jasper/software/jasper-${pkgver}.zip"
"jpc_dec.c.patch"
"patch-libjasper-stepsizes-overflow.diff"
"jasper-1.900.1-CVE-2008-3520.patch"
"jasper-1.900.1-CVE-2008-3522.patch")
sha1sums=('9c5735f773922e580bf98c7c7dfda9bbed4c5191'                                             
          'c1a0176a15210c0af14d85e55ce566921957d780'                                             
          'f298566fef08c8a589d072582112cd51c72c3983'                                             
          '2483dba925670bf29f531d85d73c4e5ada513b01'
          '0e7b6142cd9240ffb15a1ed7297c43c76fa09ee4')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/jasper-$pkgver"
  patch -p1 -i "${srcdir}/jpc_dec.c.patch"
  patch -p1 -i "${srcdir}/patch-libjasper-stepsizes-overflow.diff"
  patch -p1 -i "${srcdir}/jasper-1.900.1-CVE-2008-3520.patch"
  patch -p1 -i "${srcdir}/jasper-1.900.1-CVE-2008-3522.patch"
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-shared \
      --enable-static
    make
    mkdir -p ".dllbuild"
    find "${srcdir}/${pkgname}-${pkgver}-build-${_arch}/src/libjasper" \
      -name '*.o' | xargs -rtl1 -I {} cp {} ".dllbuild"
    ${_arch}-gcc -shared .dllbuild/*.o /usr/${_arch}/lib/libjpeg.dll.a -o libjasper-1.dll \
      -Xlinker --out-implib -Xlinker libjasper.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 "libjasper-1.dll" "$pkgdir/usr/${_arch}/bin/"
    install -m644 "libjasper.dll.a" "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/man"
  done
}