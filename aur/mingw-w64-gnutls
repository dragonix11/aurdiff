pkgname=mingw-w64-gnutls
pkgver=3.2.5
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (mingw-w64)"
arch=(any)
url="http://www.gnu.org/software/gnutls"
license=("GPL3, LGPL2.1")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-libtasn1
mingw-w64-readline
mingw-w64-zlib
mingw-w64-nettle
mingw-w64-openssl
mingw-w64-gettext)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.gnutls.org/gcrypt/gnutls/v${pkgver%.*}/gnutls-${pkgver}.tar.xz")
md5sums=('c7c367ee06f7f05ddb1e36a444a142ed')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	sed -i "s|Wincrypt.h|wincrypt.h|g" "$srcdir/${pkgname#mingw-w64-}-$pkgver/lib/system.c"
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-guile \
      --with-zlib \
      --enable-threads=win32 \
      --enable-heartbeat-support \
      --disable-doc \
      --with-tpm
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}