pkgname=cockatrice-git
pkgver=0
pkgrel=1
pkgdesc="A multiplatform application for playing card games such as Magic: The Gathering over a network."
arch=('i686' 'x86_64')
url="http://www.cockatrice.de/index.php"
license=('GPL2')
depends=('qt4' 'protobuf')
makedepends=('git' 'cmake')
_gitroot="git://github.com/Daenyth/Cockatrice.git"
source=("${pkgname}"::"${_gitroot}")
md5sums=('SKIP')
 
# Set this to 1 to include servatrice in the package
_COMPILE_SERVATRICE=0
 
pkgver() {
  cd "${pkgname}"
  # Cockatrice repo doesn't have tags. :(
  # Use revision count.shortname instead, as suggested by:
  # https://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
build() {
  cd "${srcdir}/${pkgname}"
 
  rm -rf build
  mkdir build
  cd build
 
  if (( _COMPILE_SERVATRICE )); then
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DWITH_SERVER=1 ..
  else
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ ..
  fi
  make
}
 
package() {
  cd "${srcdir}/${pkgname}/build"
 
  make DESTDIR="$pkgdir" install
}
