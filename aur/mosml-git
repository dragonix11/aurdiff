# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=mosml-git
pkgver=2.00.10.225.ga889156
pkgrel=1
pkgdesc='Moscow ML is a light-weight implementation of Standard ML (SML)'
url="http://www.itu.dk/~sestoft/mosml.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('git')
provides=('mosml')
conflicts=('mosml')

source=("$pkgname"::'git+https://github.com/kfl/mosml')
md5sums=('SKIP')

prepare() {
    cd "$pkgname"
    local ver="$(git describe --tags)" ver="${ver//-/.}"
    printf "%s" "${ver:1}"
}

build() {
    cd "$pkgname"/src
    make BINDIR=/usr/bin LIBDIR=/usr/lib/mosml world
}

package() {
    cd "$pkgname"/src
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # This isn't done by the Makefile
    cd ../man
    for manual in *.1; do
        install -Dm644 "$manual" "$pkgdir/usr/share/man/man1/$manual"
    done
}
