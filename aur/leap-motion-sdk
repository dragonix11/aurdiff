# Maintainer: M0Rf30 <morfeo89 [at] hotmail [dot] it>  
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=leap-motion-sdk
pkgver=0.8.5300
pkgrel=1
pkgdesc="The Leap Motion Developer SDK"
arch=('i686' 'x86_64')
url="https://developer.leapmotion.com/downloads/leap-motion/sdk"
depends=('mesa' 'libxxf86vm')
conflicts=('leap')
license=('custom')
source=("https://dl.dropboxusercontent.com/u/7226803/DeveloperSdk_LM_0.8.0.5300_Linux.tar"
	LICENSE
	leapd.service)
install=leap-sdk.install

package() {
  cd ${srcdir}/Leap_Developer_*
  if [ "$CARCH" == 'x86_64' ]; then
    bsdtar xf Leap-0.8.0-x64.deb
  else 
    bsdtar xf Leap-0.8.0-x86.deb
  fi
  
  tar xvf data.tar.gz
    
  # Copy binaries, headers, examples & libraries
  cp -r usr ${pkgdir}
  cp -r lib ${pkgdir}/usr/
  
  mkdir -p ${pkgdir}/share/doc/leap
  cp -r Examples ${pkgdir}/share/doc/leap/
  cp -r LeapSDK/{docs,samples,util} ${pkgdir}/share/doc/leap/
  
  cp -r LeapSDK/include ${pkgdir}/usr/
  
  cp LeapSDK/lib/{LeapJava.jar,Leap.py} ${pkgdir}/usr/lib/Leap
  
  if [ "$CARCH" == 'x86_64' ]; then
    cp LeapSDK/lib/x64/* ${pkgdir}/usr/lib/Leap
  else 
    cp LeapSDK/lib/x86/* ${pkgdir}/usr/lib/Leap
  fi
  
# Copy license
  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"  
# Install systemd service
  install -Dm644 "${srcdir}/leapd.service" "${pkgdir}/usr/lib/systemd/system/leapd.service"
}

md5sums=('3f6854ae94c89bdfa8215f2a21ca057a'
         '78a4f0934b105397d1f7b17d06e4717c'
         '07287e65a3c2a4e2a956b7ba9038d816')
