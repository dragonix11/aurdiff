# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: KillYourTV <killyourtv@i2pmail.org>

########[ OPTIONS ]########################################
# Build syndie with i2p.jar from kytv's debian mirror.
#  Useful if you use i2p through a tunneled remote connection
#  and dont want to build it just for syndie.
_external_i2p=1
_i2p_ver=0.9.7.1-1s1
###########################################################

pkgname=syndie
pkgver=1.103b
pkgrel=2
pkgdesc="Java based open source system for operating secure distributed forums"
license=('Public Domain')
url="https://syndie.i2p2.de"
arch=('any')
depends=('java-runtime' 'swt')
makedepends=('java-environment' 'apache-ant')
optdepends=('words: for spelling correction support')
DLAGENTS=('https::/usr/bin/curl -kfLC - --retry 3 --retry-delay 3 -o %o %u')
source=("https://syndie.i2p2.de/files/${pkgver}/${pkgname}-${pkgver}.src.tar.bz2"
        "https://syndie.i2p2.de/files/${pkgver}/${pkgname}-${pkgver}.src.tar.bz2.sig"
        'syndie.desktop' 'syndie.xpm' 'syndie.sh')

sha256sums=('5bd8ab4e0fff3c1d82b22c9877150dae544ac4b5a7219ca2e06d63a932fecf86'
            'SKIP'
            'a4711e9876f8e185a5de84976f2b5a72a8890329c86688ffa583ae8741ecc6cc'
            '552d02ae3724d969e668996b67d0d7b1d70f39bf03ad43287073511696c995f7'
            'a3e9d28f4e2da66cf91b4f1e57bf902dcd091ac1b97dadd9ec819f383cd83df8')

if [[ "$_external_i2p" = 1 ]]; then
    source+=("https://launchpad.net/~i2p.packages/+archive/i2p/+files/i2p-router_${_i2p_ver}_all.deb")
    sha256sums+=('5d4432d8c681edeafc23776686bccee9ec56e81739ae41263b5727fd7d4be118')
    _i2p_path="../usr/share/i2p/lib/i2p.jar"
else
    makedepends+=('i2p')
fi

prepare() {
    cd "$pkgname-$pkgver"
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh
    export LG2=en

    [[ "$_external_i2p" = 1 ]] &&
        ( cd .. ; bsdtar -xf "${srcdir}/data.tar.xz" )

    sed -i build.properties \
        -e "s|#lib.dir=|lib.dir=/usr/share/java|" \
        -e "s|#hsqldb.jar=|hsqldb.jar=lib/hsqldb.jar|" \
        -e "s|#i2p.jar=|i2p.jar=${_i2p_path:-/opt/i2p/lib/i2p.jar}|"
    sed -i build.xml \
        -e 's|target="1.5"|target="1.7"|'
}

build() {
    cd "$pkgname-$pkgver"
    ant singlejar
}

package(){
    cd "$pkgname-$pkgver"
    install -Dm644 LICENSE                "$pkgdir/usr/share/licenses/syndie/LICENSE"
    install -Dm644 syndie.jar             "$pkgdir/usr/share/syndie/syndie.jar"
    install -Dm644 doc/syndie.1           "$pkgdir/usr/share/man/man1/syndie.1"
    install -Dm644 $srcdir/syndie.xpm     "$pkgdir/usr/share/pixmaps/syndie.xpm"
    install -Dm644 $srcdir/syndie.desktop "$pkgdir/usr/share/applications/syndie.desktop"
    install -Dm755 $srcdir/syndie.sh      "$pkgdir/usr/bin/syndie"
}
