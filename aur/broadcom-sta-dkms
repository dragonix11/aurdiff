# Maintainer: Armin K. <krejzi at email dot com>

pkgname=broadcom-sta-dkms
pkgver=5.100.82.112
pkgrel=7
pkgdesc="Broadcom hybrid wireless device driver for Linux"
arch=(i686 x86_64)
url="http://www.broadcom.com/support/802.11/linux_sta.php"
license=('custom')
depends=('linux-headers' 'dkms')
provides=('broadcom-wl' 'broadcom-wl-dkms')
conflicts=('broadcom-wl' 'broadcom-wl-dkms')
source=("http://ftp.de.debian.org/debian/pool/non-free/b/broadcom-sta/broadcom-sta_${pkgver}.orig.tar.gz"
        distro.patch
        fixes.patch
        broadcom-sta-dkms.conf
        dkms.conf)
install=broadcom-sta-dkms.install
md5sums=('3842465cf117f97445a39cf13d0e3c2f'
         '5ea63d8c69e891c8fff56a25a898c3ab'
         '246b2a67a5592293104615f5e2bacbb7'
         '3600df7db49c759c655bc6a7789b28d4'
         'dcba0f7704795bc4d82048cfa7f904d2')

if [[ ${CARCH} = "i686" ]]; then
   _arch=i386
else
   _arch=amd64
fi

prepare(){
  cd broadcom-sta-${pkgver}/${_arch}

  patch -Np1 -i "${srcdir}/distro.patch"
  patch -Np1 -i "${srcdir}/fixes.patch"
}

package() {
  cd broadcom-sta-${pkgver}/${_arch}

  install -dm755 "${pkgdir}/usr/src/${pkgname}-${pkgver}-${pkgrel}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  cp -dpr lib "${pkgdir}/usr/src/${pkgname}-${pkgver}-${pkgrel}"
  cp -dpr src "${pkgdir}/usr/src/${pkgname}-${pkgver}-${pkgrel}"
  cp -dpr Makefile "${pkgdir}/usr/src/${pkgname}-${pkgver}-${pkgrel}"

  install -Dm644 "${srcdir}/${pkgname}.conf" "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"
  sed -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${srcdir}/dkms.conf" > "${pkgdir}/usr/src/${pkgname}-${pkgver}-${pkgrel}/dkms.conf"

  ln -sf /usr/src/${pkgname}-${pkgver}-${pkgrel}/lib/LICENSE.txt "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
