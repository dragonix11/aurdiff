pkgname=binutils-git
_pkgname=binutils
pkgver=27022.c010311
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.17' 'zlib')
makedepends=('git')
checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install

source=(git://sourceware.org/git/binutils.git)
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    cd "${srcdir}/${_pkgname}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    rm -rf "${srcdir}/${_pkgname}-build"
    mkdir "${srcdir}/${_pkgname}-build"
}

build() {
    cd "${srcdir}/${_pkgname}-build"

    "${srcdir}/${_pkgname}/configure" --prefix=/usr \
        --with-lib-path=/usr/lib:/usr/local/lib \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-ld=default --enable-gold \
        --enable-plugins --enable-threads \
        --with-pic --enable-shared \
        --disable-werror --disable-multilib

    # check the host environment and makes sure all the necessary tools are available
    make configure-host

    make tooldir=/usr
}

check() {
    cd "${srcdir}/${_pkgname}-build"

    # unset LDFLAGS as testsuite makes assumptions about which ones are active
    # do not abort on errors - manually check log files
    make LDFLAGS="" -k check || true
}

package() {
    cd "${srcdir}/${_pkgname}-build"
    make prefix="${pkgdir}/usr" tooldir="${pkgdir}/usr" install

    # Add some useful headers
    install -m644 "${srcdir}/${_pkgname}/include/libiberty.h" "${pkgdir}/usr/include"
    install -m644 "${srcdir}/${_pkgname}/include/demangle.h" "${pkgdir}/usr/include"

    # Install PIC libiberty.a
    install -m644 libiberty/pic/libiberty.a "${pkgdir}/usr/lib"

    # Remove unwanted files
    rm "${pkgdir}/usr/share/man/man1"/{dlltool,nlmconv,windres,windmc}*
    rm "${pkgdir}/usr/share/info"/{configure,standards}.info

    # No shared linking to these files outside binutils
    rm "${pkgdir}/usr/lib"/lib{bfd,opcodes}.so
}
