# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=python-nikola
_pyname=nikola
pkgver=6.0.0
pkgrel=1
pkgdesc='A static website and blog generator.'
arch=('any')
url='http://getnikola.com/'
license=('custom')
depends=('python' 'python-doit' 'python-pygments' 'python-pillow'
         'python-docutils' 'python-mako' 'python-unidecode' 'python-lxml'
         'python-yapsy' 'python-pyrss2gen' 'python-pytz')
options=(!emptydirs)
source=("${pkgver}.tar.gz::https://codeload.github.com/ralsina/nikola/tar.gz/${pkgver}")
md5sums=('97f29dbcbdac3d71561ae9874c8713d6')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
