# Maintainer: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus-git
_gitname=mupen64plus-libretro
pkgver=375
pkgrel=1
pkgdesc="libretro implementation of mupen64plus with dynarec (even for rpi)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mupen64plus-libretro"
license=('custom' 'GPL' 'LGPL')
makedepends=('git')
source=("${_gitname}::git://github.com/libretro/${_gitname}.git"
	Makefile.rpi
	libretro-mupen64plus.info)

md5sums=('SKIP'
	 'e8ea85dbdd6290aaaa683813b0ec3a08'
	 'e4a1739796bcbcd7d9bf3fb8de21bf26')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  if [ $CARCH == "armv6h" ];then
  # ugly raspberry pi workaround!I will fix this later
  touch glide2gl/src/Glide64/3dmathsse.o
  platform=rpi make -f ${srcdir}/Makefile.rpi WITH_DYNAREC=arm
  else
  make WITH_DYNAREC=$CARCH
  fi
}

package() {
  install -Dm644 "${srcdir}/libretro-mupen64plus.info" "${pkgdir}/usr/lib/libretro/libretro-mupen64plus.info"
  install -Dm644 "${_gitname}/gles2glide64/data/Glide64mk2.ini" "${pkgdir}/usr/share/libretro/libretro-mupen64plus/Glide64mk2.ini"  
  install -Dm644 "${_gitname}/mupen64plus_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mupen64plus.so"
}
