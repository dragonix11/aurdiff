# Contributer: Sebastian Sareyko <public@nooms.de>
#
# It is possible to choose the idct implementation
# Available are:
# 
# none               use ProjectX Java implementation
# idct-mjpeg         taken from MJPEGTOOLS 1.8.0
# idct-mjpeg-mmx     taken from MJPEGTOOLS 1.8.0
#
_idct=idct-mjpeg-mmx

pkgname=projectx
pkgver=0.91.0.08
pkgrel=1
license=('GPL')
arch=('i686' 'x86_64')
pkgdesc="DVB demux tool for repairing and analyzing different stream types"
makedepends=('java-environment apache-ant')
depends=('java-runtime')
url="http://sourceforge.net/projects/project-x"
source=(http://project-x.cvs.sourceforge.net/viewvc/project-x/?view=tar 
	http://gentoo.sbriesen.de/distfiles/projectx-idctfast.tar.xz 
        IDCTFast.patch
	projectx)

md5sums=('caee95e2533701e679b5a52236441b0c'
         '0f09819b08e6105ed6c1e4705794e9c1'
         'a91d03e2935bd303e6771fc767f496cd'
         '0bb5dfb7683babb91f7fc996f5de7f2d')

build() {
    cd ${srcdir}
    cp -a Project-X/ project-x/
    if [ ${_idct} != "none" ]; then
       patch -p0 < IDCTFast.patch
    fi
    cd ${srcdir}/project-x/Project-X
    sh ./build.sh
    if [ ${_idct} != "none" ]; then
       cd lib/PORTABLE/
       make clean
       make PROJECTX_HOME=${srcdir}/project-x/Project-X IDCT=${_idct} CPLAT="$CFLAGS -fPIC"
       cd ${srcdir}/project-x/Project-X
    fi
}

package() {
    cd ${srcdir}/project-x/Project-X
    install -D -m0755 $srcdir/projectx $pkgdir/usr/bin/projectx
    install -D -m0644 ProjectX.jar $pkgdir/opt/projectx/ProjectX.jar
    if [ ${_idct} != "none" ]; then
       install -D -m0644 lib/PORTABLE/libidctfast.so \
            $pkgdir/opt/projectx/lib/libidctfast.so
    fi
    install -D -m0644 lib/commons-net-1.3.0.jar \
             $pkgdir/opt/projectx/lib/commons-net-1.3.0.jar
    install -D -m0644 lib/jakarta-oro-2.0.8.jar \
             $pkgdir/opt/projectx/lib/jakarta-oro-2.0.8.jar
    sed -i -e 's#Exec=java -jar /usr/share/projectx/ProjectX.jar#Exec=/usr/bin/projectx#' projectx.desktop
    install -D -m0644 projectx.desktop $pkgdir/usr/share/applications/projectx.desktop
}
