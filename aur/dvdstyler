# Contributor: Alexander Fehr <pizzapunk gmail com>
# Previous Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Previous Maintainer: Mario J Steele <mario@ruby-im.net>
# Maintainer: Miguel Revilla <yo@miguelrevilla.com>

pkgname=dvdstyler
pkgver=2.5.2
pkgrel=2
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'zip' 'xmlto' 'wxgtk2.9' 'wxsvg>=1.2' 'mjpegtools' 'libgnomeui' 'libexif')
makedepends=('docbook-xsl' 'flex')
optdepends=('dvdisaster: ECC support'
	    'xine-ui: DVD preview player'
	    'vlc: DVD preview player'
	    'smplayer: DVD preview player')
options=('!makeflags')
source=(http://downloads.sourceforge.net/dvdstyler/${pkgver}/DVDStyler-${pkgver}.tar.bz2)
sha1sums=('490b3b4a542bbe8f2f80e841f014fdb912156915')

build() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  chmod 755 configure
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-2.9
  sed -i 's/libwxvilla.a/libwxvilla.a \/usr\/lib\/libjpeg.so.8/' src/Makefile
  sed -i 's/wxStandardPaths paths;//g' src/*.cpp
  sed -i 's/paths.GetUserDataDir()/wxStandardPaths::Get().GetUserDataDir()/g' src/*.cpp
  make
}

package() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m 644 -D ${pkgdir}/usr/share/doc/dvdstyler/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
