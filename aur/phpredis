# Submitter: Patrick Palka <patrick@parcs.ath.cx>
# Maintainer: Mariano Iglesias <mgiglesias@gmail.com>
# Development: https://github.com/nicolasff/phpredis

pkgname=phpredis
pkgver=2.2.3
pkgrel=1
pkgdesc="A PHP extension for Redis"
arch=('i686' 'x86_64')
url="https://github.com/nicolasff/phpredis"
license=('PHP')
depends=('php')
_ininame="redis.ini"
_inifile="etc/php/conf.d/$_ininame"
backup=("$_inifile")
_filename="$pkgname-$pkgver.tgz"
source=("$_filename::https://github.com/nicolasff/$pkgname/tarball/$pkgver")
md5sums=('d4ef9573bd5128bce2e18548573e35ea')
_builddir=*-$pkgname-*

build() {
    cd $_builddir

    phpize
    ./configure --prefix=/usr --enable-redis
    make
    #make test
}

package() {
    cd $_builddir

    make INSTALL_ROOT="$pkgdir" install
    echo ";extension=redis.so" > "$_ininame"
    install -vDm644 "$_ininame" "$pkgdir/$_inifile"
}
