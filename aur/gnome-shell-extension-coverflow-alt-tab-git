# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=gnome-shell-extension-coverflow-alt-tab-git
pkgver=87
pkgrel=1
pkgdesc="Replacement of Alt-Tab, iterates through windows in a cover-flow manner"
arch=('any')
url="https://github.com/dmo60/CoverflowAltTab"
license=('GPL')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/dmo60/CoverflowAltTab.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  git rev-list --count HEAD
}

package() {
  cd ${pkgname%-*}

  mkdir -p "${pkgdir}"/usr/share/{glib-2.0,gnome-shell/extensions}
  cp -dr --no-preserve=ownership CoverflowAltTab@dmo60.de "${pkgdir}"/usr/share/gnome-shell/extensions/
  cd "${pkgdir}"/usr/share/gnome-shell/extensions/CoverflowAltTab@dmo60.de
  rm schemas/gschemas.compiled
  mv schemas "${pkgdir}"/usr/share/glib-2.0/

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
