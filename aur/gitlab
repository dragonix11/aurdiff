# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

#  lib/support/nginx/gitlab

pkgname=gitlab
pkgver=6.2.0
pkgrel=1
pkgdesc="Project management and code hosting application"
arch=('any')
url="https://www.gitlab.org"
license=('MIT')
depends=('ruby' 'ruby-bundler' 'python2' 'gitlab-shell' 'postgresql-libs') # 'ruby-rake>=10.1.0')
# not yet tested and confirmed: libxslt:
makedepends=('sudo' 'libxslt')
# base-devel zlib libyaml openssl gdbm readline ncurses libffi curl openssh redis libxml2 libxslt icu 
optdepends=(
    'mariadb: database backend'
    'postgresql: database backend'
    'python2-docutils: reStructuredText markup language support'
    'postifx: mail server in order to receive mail notifications'
)
backup=(etc/gitlab/gitlab.yml etc/gitlab/resque.yml etc/gitlab/database.yml)
source=("$pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlabhq/archive/v${pkgver}.tar.gz"
        "gitlab.service"
        "gitlab-sidekiq.service")
install='gitlab.install'
sha512sums=("783a015e57faa02b75b8897ade52f38cf10a9fa91135e46c384c22b942953798b4fce00194346391bb49013ec49b8b4de50579c434a5b19a03794a177e99a0e8"
            "f515a6a6b94b5c15405978addd2923dbfa4aaedd9eff2aa2760f53c3a88ec8148fb39b6bcb5897d5b0e586e44945475b1c3cd09350138d0ae7560aae504120dd"
            "64b81772fda07c842a0eff58ac533cca0b6f2122f7746dccb93d24f1e0ca7a1871605d0dad91ad46e5a1b6c226844c3584160f8b2504711115f84c290a5412c6")
options=('!strip')

build() {
        cd "$srcdir/gitlabhq-$pkgver"

        #gem install charlock_holmes --version '0.6.9.4'
        #gem install rake

        msg "Fetching bundled gems..."
        # will be installed in the gitlab directory vendor/bundle
        bundle install --no-cache --no-prune --deployment --without development test aws
}

package() {
    cd $srcdir/gitlabhq-$pkgver
    install -d "$pkgdir/usr/share/webapps"
	cp -r "${srcdir}/gitlabhq-${pkgver}" "${pkgdir}/usr/share/webapps/${pkgname}"

    # Creating directories
    install -d \
        "$pkgdir/etc/gitlab" \
        "$pkgdir/usr/share/webapps" \
        "$pkgdir/usr/share/doc/$pkgname" \
        "$pkgdir/usr/run/gitlab/pids" \
        "$pkgdir/usr/run/gitlab/www" \
        "$pkgdir/var/run/gitlab" \
        "$pkgdir/usr/share/webapps/$pkgname/www"
    
    # Install config files
    sed -e 's|# user: git|user: gitlab|' \
        -e "s|/home/git/repositories|/usr/share/webapps/gitlab/repositories|" \
        -e "s|/home/git/gitlab-satellites|/usr/share/webapps/gitlab/satellites|" \
        -e "s|/home/git|/var/run/gitlab|" \
        config/gitlab.yml.example > "$pkgdir/etc/gitlab/gitlab.yml"
    [ -f config/gitlab.yml ] || ln -s /etc/gitlab/gitlab.yml config/gitlab.yml
    cp config/database.yml.mysql $pkgdir/etc/gitlab/database.yml
    [ -f config/database.yml ] || ln -s /etc/gitlab/database.yml config/database.yml
    touch $pkgdir/var/run/gitlab/.secret
    touch $pkgdir/usr/share/webapps/gitlab/.secret

    # Install license, help and systemd service files
    mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "$pkgdir/usr/share/doc/$pkgname"
    install -D "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install systemd service files
    install -Dm0755 "$srcdir/gitlab.service" "$pkgdir/usr/lib/systemd/system/gitlab.service"
    install -Dm0755 "$srcdir/gitlab-sidekiq.service" "$pkgdir/usr/lib/systemd/system/gitlab-sidekiq.service"
}

# vim:set ts=4 sw=4 et:
