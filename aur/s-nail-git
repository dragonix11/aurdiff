# Maintainer: Gavin Troy <gavtroy@gmail.com>
# Based on heirloom-mailx PKGBUILD

pkgname=s-nail-git
pkgver=latest
pkgrel=3
pkgdesc="A commandline utility for sending and receiving email. Fork of Heirloom mailx"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/s-nail/"
license=('custom')
depends=('krb5' 'libidn')
makedepends=('git')
optdepends=('smtp-forwarder: for sending mail')
provides=('mailx')
conflicts=('mailx')
backup=('etc/mail.rc')
_gitname='s-nail'
source=("${_gitname}::git://git.code.sf.net/p/s-nail/code")
sha1sums=('SKIP')

pkgver() {
	cd $_gitname
	echo "`git describe --tags --abbrev=0 | cut -c 2-`_`git rev-list --count HEAD`"
}

build() {
	cd $_gitname

	# heirloom-mailx package does this, though it's hard to know why.
	printf "\nset bsdcompat" >> nail.rc

	echo PREFIX=/usr \
	     MANDIR=/usr/share/man \
	     SYSCONFDIR=/etc \
	     SYSCONFRC=/etc/mail.rc \
	     SENDMAIL=/usr/bin/sendmail \
	     MAILSPOOL=/var/spool/mail \
	     WANT_GSSAPI=1 \
	     DESTDIR="${pkgdir}" > makeflags

	make `cat makeflags`
}

package() {
	cd $_gitname
	make `cat makeflags` install

	ln -sf s-nail "${pkgdir}"/usr/bin/mail
	ln -sf s-nail "${pkgdir}"/usr/bin/mailx
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz

	install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
