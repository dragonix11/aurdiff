# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Based on the PKGBUILD for R

pkgname=r-mkl
pkgver=3.0.2
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics, set up to use Intel's MKL"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
provides=('r=3.0.2')
conflicts=('r')
depends=('intel-mkl' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'libxt' 'libxmu' 'pango' 'xz' 'desktop-file-utils')
makedepends=('jdk7-openjdk' 'gcc-fortran' 'tk')
optdepends=('tk: tcl/tk interface' 'texlive-bin: latex sty files')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories' 'etc/R/javaconf')
options=('!makeflags' '!emptydirs')
install=r-mkl.install

source=("http://cran.r-project.org/src/base/R-${pkgver%%.*}/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png'
	'R.conf'
	'r-mkl.patch')
md5sums=('f9a8374736e7650e4848f33e2e3bbee3'
         'f23f172a35249321c4ccecb712397e74'
         '00659f39e90627fe87f1645df5d4e3a7'
         '1dfa62c812aed9642f6e4ac34999b9fe'
         '6dda5d9455a877f4e18a20d59cdbc8df')
sha512sums=('206e29679d33d8804ae230e21d87ab125d8a46fc1958feea09de338ed33806865961e2ac0ecaed873bb27a15f9ccc39c0aa750240bfe03d060c1e2c042a5bedd'
            '365d96f987f99729181272304fa3176f43ab1bccb9c29e6f361ed292d98d0349da79a20fd7536f34846f5df8a27fc4f5d41b522b5b982b292096f6b898abbb62'
            '55ed6e819dcbb231d842d825134b84d1a24db177558d5dad9369d57fd21d0239d6433c4311531171a101ca3c7c0685493e9cc6c1fe9e4e0df59f2331cff150ba'
            'aae388c5b6c02d9fb857914032b0cd7d68a9f21e30c39ba11f5a29aaf1d742545482054b57ce18872eabb6605bbb359b2fc1e9be5ce6881443fdbdf6b67fab3b'
            '397a8b17374939731e4299d76759210339348eb280f9f59d575b95f07b2ae3cc258b8f9849b8bc54bb3cf51e9a0f27e55b04124918d93884df10afef292c3207')

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _intel_lib=mkl_gf_lp64
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _intel_lib=mkl_gf
fi

prepare() {
   cd R-${pkgver}
# set texmf dir correctly in makefile
   sed -i 's|$(rsharedir)/texmf|${datarootdir}/texmf|' share/Makefile.in
# fix function detection
   patch configure < ../r-mkl.patch
}

build() {
   MKL_LIB_PATH=/opt/intel/mkl/lib/${_intel_arch}
   MKL_INCLUDE_PATH=/opt/intel/mkl/include
   MKL="-fopenmp -I${MKL_INCLUDE_PATH} -L${MKL_LIB_PATH} -l${_intel_lib} -lmkl_gnu_thread -lmkl_core -ldl -lpthread -lm"
   export LD_LIBRARY_PATH=${MKL_LIB_PATH}:${LD_LIBRARY}

   cd R-${pkgver}
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc/R \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/doc/R/ \
                --with-x \
		--enable-R-shlib \
		--with-blas="${MKL}" \
                --with-lapack \
                F77=gfortran \
		LIBnn=lib
   make

# make libRmath.so
   cd src/nmath/standalone
   make shared
}

check() {
   cd R-${pkgver}
   make check-recommended
}

package() {
   cd R-${pkgver}
   make DESTDIR="${pkgdir}" install

# install libRmath.so
   cd src/nmath/standalone
   make DESTDIR="${pkgdir}" install

   # Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" "${pkgdir}/usr/bin/R"
   rm "${pkgdir}/usr/lib/R/bin/R"
   cd "${pkgdir}/usr/lib/R/bin"
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/r.desktop" \
	"${pkgdir}/usr/share/applications/r.desktop"
  install -Dm644 "${srcdir}/r.png" \
	"${pkgdir}/usr/share/pixmaps/r.png"

  # move the config directory to /etc and create symlinks
  install -d "${pkgdir}/etc/R"
  cd "${pkgdir}/usr/lib/R/etc"
  for i in *; do
    mv -f ${i} "${pkgdir}/etc/R"
    ln -s /etc/R/${i} ${i}
  done

  # Install ld.so.conf.d file to ensure other applications access the shared lib
  install -Dm644 "${srcdir}/R.conf" "${pkgdir}/etc/ld.so.conf.d/R.conf"
}
