_target=arm-linux-gnueabi
_sysroot=/usr/${_target}

pkgname=${_target}-libffi
pkgver=3.0.13
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions (${_target})"
arch=('any')
makedepends=("${_target}-gcc")
options=('!libtool' '!strip' '!buildflags' '!makeflags')
license=('MIT')
url="http://sourceware.org/libffi"
source=("ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz")
md5sums=('45f3b6dbc9ee7c7dfbbbc5feba571529')

build() {
  cd "${srcdir}/libffi-${pkgver}"

  export CC=${_target}-gcc
  export AR=${_target}-ar

  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig/"
  unset LDFLAGS
  mkdir -p build && cd build
  ../configure \
      --prefix=${_sysroot} \
      --host=${_target}
  make
}

package() {
  cd "${srcdir}/libffi-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir/${_sysroot}/share"
}

# vim:set ts=2 sw=2 et:
