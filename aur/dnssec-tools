
# Maintainer: Luca Fulchir <luca@fulchir.it>

pkgname=dnssec-tools
pkgver=2.0
pkgrel=3
pkgdesc="libval & dnssec management tools"
arch=('i686' 'x86_64')
url="http://www.dnssec-tools.org"
license=('BSD')
conflicts=('libval' 'libval_shim' 'libsres')
backup=('etc/dnssec-tools/dnssec-tools.conf' 'etc/dnssec-tools/resolv.conf' 'etc/dnssec-tools/root.hints')
depends=('dnsutils' 'perl' 'perl-timedate' 'perl-net-dns' 'perl-net-dns-sec' 'perl-mailtools' 'perl-tk' 'perl-graphviz' 'bind' 'openssl' 'glibc' 'pth')
source=(http://www.dnssec-tools.org/download/${pkgname}-${pkgver}.tar.gz)
md5sums=('b738664499c150cf81a1c4307ff07e0f')
sha1sums=('4cae015ba8fe6aba66131cd9f0ae0c8dd4524f01')
sha256sums=('7fd5d483c7a4b25277f17d9010b8de79a21948884368e6d870bec0194d007445')
install="dnssec-tools.install"


build() {
	cd "$srcdir/${pkgname}-${pkgver}"
	./configure	--exec_prefix=/usr \
			--prefix=/usr \
			--mandir=/usr/share/man \
			--sysconfdir=/etc \
			--with-ipv6 \
			--with-nsec3 \
			--with-dlv
	make DESTDIR="${pkgdir}"
}

package() {
	cd "$srcdir/${pkgname}-${pkgver}"
	make install DESTDIR="${pkgdir}"
	cp validator/etc/dnsval.conf validator/etc/root.hints ${pkgdir}/etc/dnssec-tools/
	cd "${pkgdir}/etc/dnssec-tools"
	mkdir KEY-SAFE
	sed -i 's/\/usr\/local\/sbin/\/usr\/bin/' dnssec-tools.conf
	sed -i 's/\/usr\/local\/etc/\/etc/' dnssec-tools.conf
}


