# Maintainer : Keshav Padram Amburay <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_gitroot="git://kernel.ubuntu.com/jk/sbsigntool"
_gitname="sbsigntool"
_gitbranch="master"

_pkgname="sbsigntool"
pkgname="${_pkgname}-git"

pkgdesc="Signing Utility for UEFI Secure Boot - GIT Version"

pkgver=0.6.133.951ee95
pkgrel=1
arch=('x86_64')
url="https://wiki.ubuntu.com/UEFI/SecureBoot"
license=('GPL3')
makedepends=('git' 'gnu-efi-libs' 'help2man')
depends=('util-linux' 'openssl')
options=('!strip' '!emptydirs' 'zipman' 'libtool' 'docs')

conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")

source=("${_gitname}::git+${_gitroot}#branch=${_gitbranch}"
        "ccan::git+git://git.ozlabs.org/~ccan/ccan#branch=master")

sha1sums=('SKIP'
          'SKIP')

pkgver() {
	cd "${srcdir}/${_gitname}/"
	
	_ACTUAL_VER="$(grep 'sbsigntool' "${srcdir}/${_gitname}/configure.ac" | awk '{print $2}' | sed 's|\[||g' | sed 's|\]\,||g')"
	echo "${_ACTUAL_VER}.$(git rev-list --count HEAD).$(git describe --always)" | sed -e 's|-|.|g'
	
	unset _ACTUAL_VER
}

build() {
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	cd "${srcdir}/${_gitname}_build/"
	
	git clean -x -d -f
	echo
	
	unset CFLAGS
	unset CPPFLAGS
	unset CXXFLAGS
	unset LDFLAGS
	unset MAKEFLAGS
	
	sed 's|-DEFI_FUNCTION_WRAPPER|-DEFI_FUNCTION_WRAPPER -DGNU_EFU_USE_MS_ABI|g' -i "${srcdir}/${_gitname}_build/configure.ac"
	
	rm -rf "${srcdir}/${_gitname}_build/lib/ccan.git"
	cp -r "${srcdir}/ccan" "${srcdir}/${_gitname}_build/lib/ccan.git"
	
	_submodule_commit_id="$(git submodule status | awk '{print $1}' | sed 's%-%%g')"
	
	cd "${srcdir}/${_gitname}_build/lib/ccan.git"
	git checkout --quiet "${_submodule_commit_id}"
	
	cd "${srcdir}/${_gitname}_build/"
	./autogen.sh
	echo
	
	./configure --prefix="/usr" --bindir="/usr/bin" --sbindir="/usr/bin" --libexecdir="/usr/lib" --mandir="/usr/share/man" --sysconfdir="/etc"
	echo
	
	make V=1 -j1
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/"
	
	make V=1 -j1 DESTDIR="${pkgdir}/" install
	echo
	
}
