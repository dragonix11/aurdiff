# Maintainer: RunningDroid <runningdroid AT zoho.com>
# Contributor: Justin Davis <jrcd83@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lua-yajl-git
_gitname=lua-yajl
pkgver=22.cc408ef
pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}
pkgrel=1
pkgdesc="Integrates the yajl JSON library with Lua."
arch=(i686 x86_64)
url='http://github.com/brimworks/lua-yajl'
license=(MIT)
depends=('yajl>=2' 'lua>=5.1')
makedepends=(git cmake)
options=(!libtool)
source=('git+https://github.com/brimworks/lua-yajl.git'
        'LICENSE')
md5sums=('SKIP'
         '27c5699b8602d4d18e139339b96ae5dc')

build() {
  cd $_gitname

  cmake .
  make
}

package(){
  cd $_gitname

  make DESTDIR="$pkgdir" install
  install -Dm644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mkdir -p "${pkgdir}/usr/lib/lua/5.1"
  mv -t "${pkgdir}/usr/lib/lua/5.1/" "${pkgdir}/usr/local/share/lua/cmod/"* 
  rm -R "${pkgdir}/usr/local"
} 

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
