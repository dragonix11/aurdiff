# $Id: 12dc47c421c7aed1e1536a470bd7088a6678c02e $
# Maintainer: Ido Rosen <ido@kernel.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
# 

pkgname="bmon-git"
pkgver=v3.2.0.gd47fffc
pkgrel=1
pkgdesc="Portable bandwidth monitor and rate estimator"
arch=('i686' 'x86_64')
url="http://people.suug.ch/~tgr/bmon/"
license=('MIT')
depends=('ncurses' 'confuse')
makedepends=()
source=('git+https://github.com/tgraf/bmon.git')
sha512sums=('SKIP')
provides=('bmon')
replaces=('bmon')
conflicts=('bmon')

pkgver() {
  cd "${srcdir}/${pkgname//-git/}"

  if [ -n "${BMON_VERSION}" ]; then
    git checkout "${BMON_VERSION}"
  fi

  local ver="$(git describe --tags --long --always --dirty)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  cd "${srcdir}/${pkgname//-git/}"

  if [ -n "${BMON_VERSION}" ]; then
    git checkout "${BMON_VERSION}"
  fi
}

build() {
  cd "${srcdir}/${pkgname//-git/}"

  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-asound
  make
}

package() {
  cd "${srcdir}/${pkgname//-git/}"

  make DESTDIR="${pkgdir}" install

  install -d                "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 ChangeLog   "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 LICENSE     "${pkgdir}/usr/share/doc/${pkgname}"

  install -d                "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/${pkgname}/LICENSE" \
                            "${pkgdir}/usr/share/licenses/${pkgname}"
}
