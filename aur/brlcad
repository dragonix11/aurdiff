#  Maintainer: Loui Chang <louipc dot ist at gmail company>

pkgname=brlcad
pkgver=7.24.0
pkgrel=2
pkgdesc="An extensive 3D solid modeling system."
url="http://brlcad.org"
license=('LGPL' 'BSD' 'custom:BDL')
arch=('i686' 'x86_64')
install=$pkgname.install
makedepends=('bc' 'cmake' 'libxslt')
depends=('boost' 'flex' 'libgl' 'libpng' 'mesa' 'tk' 'zlib')

# Maybe we could build brlcad with these externally some time.
#	'itcl:        libs included, but package can build with external libs'
#	'itk:         libs included, but package can build with external libs'
#	'opennurbs:   libs included, but package can build with external libs'
#	'tkhtml3-cvs: for man page browsing in mged. CVS repo dead'
#	'urt:         Utah Raster Toolkit'
optdepends=(
    'fop:   for building pdf documentation'
    'tkpng: for archer'
)

# tkhtml3-cvs is needed for manpage browsing in mged.
# Older versions won't work properly

options=('!libtool')

source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)

_pkgprefix=/opt/$pkgname

build() {
    # Default flags minus --as-needed
    LDFLAGS="-Wl,-O1,--sort-common,-z,relro"

    cd $srcdir/$pkgname-$pkgver

    mkdir $srcdir/$pkgname-build
    cd $srcdir/$pkgname-build

    cmake $srcdir/$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=${_pkgprefix} \
    -DBRLCAD-ENABLE_OPTIMIZED=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBRLCAD_ENABLE_STRICT=OFF \
    -DBRLCAD_UTAHRLE=ON \
    -DBRLCAD_TCL=ON \
    -DBRLCAD_TK=ON \
    -DBRLCAD_ITCL=ON \
    -DBRLCAD_ITK=ON \
    -DBRLCAD_IWIDGETS=ON \
    -DBRLCAD_TKHTML=ON \
    -DBRLCAD_TKPNG=ON \
    -DBRLCAD_TKTABLE=ON \
    -DBRLCAD_OPENNURBS=ON

    make
}

package() {
    cd $srcdir/$pkgname-build

    make DESTDIR=$pkgdir install

    mkdir -p $pkgdir/etc/profile.d
    echo "export PATH=\$PATH:$_pkgprefix/bin" > $pkgdir/etc/profile.d/$pkgname.sh
    chmod 755 $pkgdir/etc/profile.d/$pkgname.sh

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bdl.txt \
        $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bsd.txt \
        $pkgdir/usr/share/licenses/$pkgname
#    mkdir -p $pkgdir/etc/ld.so.conf.d
#    echo ${_pkgprefix}/lib > $pkgdir/etc/ld.so.conf.d/$pkgname.conf
}

md5sums=('ecd94dfaf0b980776c589bb9337a6786')

