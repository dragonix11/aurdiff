# Maintainer: Aaron Lindsay <aaron@aclindsay.com>

# Name of the Software your PKGBUILD will install - should be unique. See PKGBUILD#pkgname
pkgname=ccnet

# The version number for the software
pkgver=1.3.7 #Should match `grep "PACKAGE_VERSION" ccnet/Makefile`
_seafilever=2.0.7

#The release number for the arch package, as fixes are added to the PKGBUILD, the release number will increase
pkgrel=3

# The description of the package, should be about 80 characters long (one line)
pkgdesc="A framework for writing networked applications in C."

# The type of processor this software can build and work on. See PKGBUILD#arch
arch=('i686' 'x86_64' 'armv7h')

# The official website for the software your PKGBUILD will install
url="https://github.com/haiwen/ccnet/"

# The License that the software is released under. See PKGBUILD#license
license=('custom')

# Packages that your software needs to run. If the dependancy requires a minimum version number use the >= operator
depends=('libevent>=2.0' 'libzdb>=2.10.2' 'libsearpc>=1.1.0')

# Packages that must be installed to build the software, but at not necessary to run it
makedepends=('vala>=0.8' 'pacman>=4.1')

# Optional packages that extend the software's functionality
optdepends=()

# List of Package names that this PKGBUILD provides. Put modified packages that will be installed here.
provides=()

# Change the default behavior of makepkg see PKGBUILD#options
options=

source=("https://github.com/haiwen/ccnet/archive/v${_seafilever}.tar.gz"
	"https://raw.github.com/haiwen/ccnet/master/COPYRIGHT"
	"libccnet.pc.patch")
sha256sums=('e5b4555c4b1a01d04a11c541e6ccf9b7999a9c1ee7838e2ef662deb3c798f465'
	'7abb45e3e313d37337b088411fa249171f58186ebf951c19ea2bf1390927a0f3'
	'66c3b02c3981db6a80819e0ae103bedadf8dfdf81405a7f75a9cba714acf973f')

prepare () {
	cd "$srcdir/ccnet-$_seafilever"
	patch -p1 -i $srcdir/libccnet.pc.patch
}

build () {
	cd "$srcdir/ccnet-$_seafilever"
	./autogen.sh
	CFLAGS="-lpthread" ./configure --enable-server --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package () {
	#install library and header files
	cd "$srcdir/ccnet-$_seafilever"
	make DESTDIR="$pkgdir/" install

	#copy license over
	mkdir -p $pkgdir/usr/share/licenses/$pkgname
	cp $srcdir/COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/
}
