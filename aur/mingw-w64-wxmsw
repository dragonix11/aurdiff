pkgname=mingw-w64-wxmsw
pkgver=3.0.0.rc2
pkgrel=1
pkgdesc="MSW implementation of wxWidgets API for GUI (mingw-w64)"
arch=(any)
url="http://wxwidgets.org"
license=("custom:wxWindows")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt
mingw-w64-libjpeg-turbo
mingw-w64-libpng
mingw-w64-expat
mingw-w64-libtiff)
options=(staticlibs !strip !buildflags)
conflicts=(mingw-w64-wxmsw2.9-static mingw-w64-wxmsw-static)
provides=(mingw-w64-wxmsw2.9-static mingw-w64-wxmsw-static)
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver%.rc2}-rc2.tar.bz2")
md5sums=('4e4226235742a97d525cbf7255e0cab6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"


build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/wxWidgets-3.0.0-rc2/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --with-msw \
      --disable-mslu \
      --enable-shared \
      --enable-iniconf \
      --enable-std_string_conv_in_wxstring
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    mv "$pkgdir/usr/${_arch}/lib/"*.dll "$pkgdir/usr/${_arch}/bin"
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/wx"{-config,rc-3.0}
    ln -s "/usr/${_arch}/lib/wx/config/${_arch}-msw-unicode-3.0" "$pkgdir/usr/${_arch}/bin/wx-config"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
