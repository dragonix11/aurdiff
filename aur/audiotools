# Maintainer: David Scholl <djscholl at gmail dot com>
pkgname=audiotools
pkgver=2.20
pkgrel=1
pkgdesc="A collection of Python-based command-line audio handling programs"
arch=('i686' 'x86_64')
url="http://audiotools.sourceforge.net"
license=('GPL')
depends=('python2' 'python2-urwid' 'libcdio-paranoia' 'flac' 'wavpack' 'lame' \
'twolame' 'vorbis-tools' 'speex' 'faad2' 'faac' 'musepack-tools' 'pygtk')
backup=('etc/audiotools.cfg')
install=audiotools.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('a295105ad23689bdecd3952aa8736302')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed 's/#include <cdio\/cdda.h>/#include <cdio\/paranoia\/cdda.h>/' -i src/cdiomodule.h
  sed 's/#include <cdio\/paranoia.h>/#include <cdio\/paranoia\/paranoia.h>/' -i src/cdiomodule.h
  python2 setup.py build
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install --root=$pkgdir
  cd docs
  install -d $pkgdir/usr/share/man/man1
  install -d $pkgdir/usr/share/man/man5
  make install -e MAN_PATH=$pkgdir/usr/share/man -e PYTHON=/usr/bin/python2
}
