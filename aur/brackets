# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: mac <poczciwiec at gmail dot com>

pkgname=brackets
_build=32
pkgver=sprint$_build
pkgrel=1
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("brackets-lib" "gconf" "nodejs" "nspr" "systemd")
makedepends=("prelink")
optdepends=(
	"google-chrome: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
conflicts=("brackets-git")
install=$pkgname.install

if [[ "$CARCH" == "i686" ]]; then
	source=("https://www.dropbox.com/s/xe6rr7dykxo5z3f/brackets-sprint-32-LINUX32.deb")
	sha512sums=("34d6e1f2b39a193c605b745c0f0682b0e29cf32c278c4020a47f044c6b7d4c21a62f2d72eacb96357d026abcb4c56bc885f72b9e15f834e1509473d17555559a")
elif [[ "$CARCH" == "x86_64" ]]; then
	source=("https://www.dropbox.com/s/43wjl5315jpoxoc/brackets-sprint-32-LINUX64.deb")
	sha512sums=("2a9cb7b020af508caf9d496e4efa8a06d2911e717eccfd99997cdc6b26d84027149be0c883b23d7d014ced9bacbd2781be3c5323031694ef07628706eeb5ef52")
fi

prepare() {
	cd $srcdir

	echo "  -> Extracting files..."
	tar -xf data.tar.gz

	echo "  -> Fixing executable stack..."
	execstack -c opt/$pkgname/Brackets
}

package() {

	echo "  -> Installing program..."

	mkdir $pkgdir/opt
	cp -r opt/$pkgname $pkgdir/opt/$pkgname

	chmod -R a+rw $pkgdir/opt/$pkgname/samples

	install -d $pkgdir/usr/bin
	ln -s /opt/brackets/brackets $pkgdir/usr/bin/$pkgname

	echo "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -d $pkgdir/$_icon_dir/scalable/apps
	install -Dm644 $_icon_dir/scalable/apps/$pkgname.svg $pkgdir/$_icon_dir/scalable/apps/$pkgname.svg
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size=${_icon##*/appshell}
		install -d $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps
		install -Dm644 $_icon $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps/$pkgname.png
	done

	echo "  -> Installing .desktop file..."
	install -Dm755 opt/$pkgname/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop


	echo "  -> Installing license..."
	install -Dm755 usr/share/doc/$pkgname/copyright $pkgdir/usr/share/licenses/$pkgname/copyright
}