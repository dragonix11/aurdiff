# Maintainer: Ahmad24 <myitrayan at gmail dot com>
# Contributor: alucryd <alucryd at gmail dot com>
# Contributor: jarda-wien <xstej70 at gmail dot com>
# Contributor: Jason Melton <jason dot melton at gmail dot com>
# Contributor: Atilla ÖNTAŞ <tarakbumba at gmail dot com>

pkgname=nvidia-dkms-grsec
pkgver=325.15
pkgrel=1
pkgdesc="NVIDIA kernel module sources (DKMS) For linux-grsec,Also work in Linux repo And Linux-ccs Kernels"
arch=('i686' 'x86_64')
[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver} && md5sums=('528fc538584de6446384b9cec3099d6c')
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32 && md5sums=('63b2caa0cb128efa1a7808d7bddb2074')
url="http://www.nvidia.com/"
license=('custom')
depends=('linux-grsec' 'linux-grsec-headers' 'dkms' "nvidia-utils=${pkgver}")
provides=("nvidia=${pkgver}")
conflicts=('nvidia')
install="${pkgname}.install"
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'nvidia-linux3.11.patch')
md5sums+=('b629fb5aee6a0fc0ab8c18d3e76e08e3')

prepare() {  
#  [ "x$(uname -r 2>/dev/null | grep -io 'grsec')" == "x" ] && {
#    msg "Current Kernel : $(uname -r)"
#    msg "You must boot into linux-grsec kernel.Stopping build"
#    return 1
#  }
  
  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi

  sh ${_pkg}.run --extract-only

  cd ${_pkg}
  patch -Np1 -i ../nvidia-linux3.11.patch
}

package() {  
  cd ${_pkg}

  mkdir -p "${pkgdir}"/usr/src
  cp -dr --no-preserve=ownership kernel "${pkgdir}"/usr/src/nvidia-${pkgver}

# Blacklist nouveau driver
  mkdir -p "${pkgdir}"/usr/lib/modprobe.d
  echo "blacklist nouveau" > "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/conftest.sh

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s nvidia "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
