# Maintainer: Alexander Merry <dev@randomguy3.me.uk>
pkgname=polyml
pkgver=5.5.1
pkgrel=2
epoch=
pkgdesc="A full implementation of Standard ML (SML)"
arch=('i686' 'x86_64')
url="http://www.polyml.org/"
license=('LGPL')
groups=()
options=('!libtool')
depends=('gmp' 'libffi')
install='polyml.install'
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname/$pkgver/$pkgname.$pkgver.tar.gz)
md5sums=('f9e489bd69b7f6e319c12d6ceb23f453')

build() {
  cd "$srcdir/$pkgname.$pkgver"
  ./configure --prefix=/usr --without-x --with-system-libffi --with-gmp --enable-shared
  make
}

check() {
  cd "$srcdir/$pkgname.$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname.$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
