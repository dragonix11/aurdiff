# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
_relname=havana
pkgver=2013.2.rc1
pkgrel=1
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-novaclient python2-paramiko
    python2-suds)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-${pkgver##*.}/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('a89685b6a13367d1596351d0e87ac66af577e647f6953f893e835265b1e65dae')
sha512sums=('39bfb41d7e837446a904185a234e926bbbadabdcfd3f3239f567e6d6ed247e23de2f2f7485673a9f88871fc1fe3e49a0c700246d5365f945048c122202ca1279')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
