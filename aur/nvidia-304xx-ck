# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-304xx-ck
pkgver=304.88
_extramodules=extramodules-3.10-ck
pkgrel=10
_pkgdesc="NVIDIA drivers for linux-ck, 304xx legacy branch."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-ck>=3.10' 'linux-ck<3.11' "nvidia-304xx-utils=${pkgver}")
makedepends=('linux-ck-headers>=3.10' 'linux-ck-headers<3.11')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-275xx-ck' 'nvidia-ck-stable' 'nvidia-beta-ck' 'nvidia-304xx-ck-atom' 'nvidia-304xx-ck-barcelona' 'nvidia-304xx-ck-corex' 'nvidia-304xx-ck-core2' 'nvidia-304xx-ck-haswell' 'nvidia-304xx-ck-ivybridge' 'nvidia-304xx-ck-kx' 'nvidia-304xx-k10' 'nvidia-304xx-ck-nehalem' 'nvidia-304xx-ck-p4' 'nvidia-304xx-ck-piledriver' 'nvidia-304xx-ck-pentm' 'nvidia-304xx-ck-sandybridge' 'nvidia-ck' 'nvidia-ck-atom' 'nvidia-ck-barcelona' 'nvidia-ck-bulldozer' 'nvidia-ck-corex' 'nvidia-ck-core2' 'nvidia-ck-haswell' 'nvidia-ck-ivybridge' 'nvidia-ck-kx' 'nvidia-ck-k10' 'nvidia-ck-nehalem' 'nvidia-ck-p4' 'nvidia-ck-piledriver' 'nvidia-ck-pentm' 'nvidia-ck-sandybridge')
#groups=('ck-generic')
#replaces=()
license=('custom')
options=(!strip)
install=nvidia-304xx-ck.install
source=('nvidia-linux-3.10.patch')
md5sums=('f12bb3f5829b979a0d79e68b78c2ba6a')

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('f60d4aef766029c98914ed665235029a')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('35e0c7d30de795cccb6b95bda991fb77')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	### Derived from the linux-3.10 patch:
	### This patch removes AGP additional from /proc, I don't think this is correct but it makes it compile.
	patch -Np2 -i ${srcdir}/nvidia-linux-3.10.patch
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
		"${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-304xx-ck.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}

