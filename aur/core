# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=core
pkgver=4.6
pkgrel=1
pkgdesc="Common Open Research Emulator"
arch=('i686' 'x86_64')
url="http://cs.itd.nrl.navy.mil/work/core/"
license=('BSD')
depends=('libev' 'ebtables' 'iproute2' 'python2' 'bridge-utils' 'tkimg'
         'xterm')
makedepends=('dia' 'help2man' 'imagemagick')
conflicts=('core-svn')
backup=('etc/core/core.conf' 'etc/core/perflogserver.conf')
source=("https://coreemu.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        'core-daemon.service'
        'python2.patch'
        'core-gui-wish-version.patch'
        'rj45-ifconfig.patch')
md5sums=('3ef28e0cc761ab6d1224a9d5b65dd62f'
         'a49c24c341b53a969ee93806b701915c'
         '1b22a0ad28499813c1dd35c36d507d8a'
         'd72eb6ca3bc9ed388532e310a8de5735'
         '523c1ea1166e9406cb2926ebcd7affc4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../python2.patch
  patch -p1 < ../core-gui-wish-version.patch
  patch -p1 < ../rj45-ifconfig.patch

  ./configure CPPFLAGS=-Wno-strict-aliasing PYTHON=/usr/bin/python2 --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/etc/init.d/core-daemon"
  rmdir "$pkgdir/etc/init.d"

  install -D "$srcdir/core-daemon.service" "$pkgdir/usr/lib/systemd/system/core-daemon.service"
}

# vim:set ts=2 sw=2 et:
md5sums=('3ef28e0cc761ab6d1224a9d5b65dd62f'
         '469a3c8f36f33b82bb7322aeb0174ca6'
         '215d9122b2370385a09d1d19cd4e9d2c'
         'ded4f4e6cc9a1b03bf2aefc9dac911eb'
         '523c1ea1166e9406cb2926ebcd7affc4')
