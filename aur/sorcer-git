# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=sorcer-git
pkgver=67.62d9577
pkgrel=1
pkgdesc="A wavetable LV2 plugin synth, targeted at the electronic / dubstep genre"
arch=('i686' 'x86_64')
url="http://openavproductions.com/sorcer/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'gtkmm')
makedepends=('python')
provides=('sorcer')
conflicts=('sorcer')
source=("$pkgname::git+https://github.com/harryhaaren/openAV-Sorcer.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$pkgname"

  # set prefix and do not install
  sed -e 's/waf >/waf --prefix=\/usr >/' \
      -e '/^if/,$d' -i compileInstall.sh
}

build() {
  cd "$srcdir/$pkgname"
  ./compileInstall.sh
}

package() {
  cd "$srcdir/$pkgname/sorcer.lv2"
  install -d "$pkgdir/usr/lib/lv2/sorcer.lv2"
  cp * "$pkgdir/usr/lib/lv2/sorcer.lv2"
}
