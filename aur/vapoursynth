# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth
pkgver=r20
_pkgver=R20
pkgrel=2
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1')
depends=('ffmpeg' 'python' 'qt4')
makedepends=('cython' 'waf' 'yasm' 'python-sphinx')
source=("https://github.com/vapoursynth/vapoursynth/archive/${_pkgver}.tar.gz")
md5sums=('dc42135e081332e506578170dd2f60c5')


prepare() {
  cd "${pkgname}-${_pkgver}"
  mkdir doc/_static
  rm -fr _test
}

build() {

  cd "${pkgname}-${_pkgver}"
  waf configure --prefix="/usr" --docs="true" --translate --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

# check() {
#   cd "${_gitname}"
#   waf install --destdir=_test
#   export PYTHONPATH="${srcdir}/${_gitname}/_test"
#   waf test
# }

package() {
  cd "${pkgname}-${_pkgver}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 test/filter_test.py "${pkgdir}/usr/share/vapoursynth/test/filter_test.py"
}
