# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=logitechmediaserver
pkgver=7.7.3
pkgrel=1
pkgdesc='Streaming audio server supported by Logitech'
url='http://www.mysqueezebox.com/download'
license=('GPL' 'custom')
arch=('i686' 'x86_64' 'armv7h')
makedepends=('nasm' 'yasm' 'rsync' 'subversion')
[[ $CARCH = x86_64 ]] && optdepends=('lib32-glibc: transcoding' 'lib32-gcc-libs: transcoding')
depends=('perl<5.19')
source=("http://downloads.slimdevices.com/LogitechMediaServer_v${pkgver}/logitechmediaserver-${pkgver}.tgz"
        'CPAN.upstream::svn+http://svn.slimdevices.com/repos/slim/7.7/trunk/vendor/CPAN/'
        'http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/Class-XSAccessor-1.13.tar.gz'
        'perl-recent.patch'
        'service')
sha1sums=('fbe93cef1b45551308142e23ba0dcded3c759ab7'
          'SKIP'
          'da8faefa5c04a4aa022fb4f1cdcf8a111d9a0e44'
          '2e9989b5ea49e9e3b0fa2e16590ad3a6957f7b9c'
          '0a3ab10478b1f3eb6913beadae34e4d966c7f007')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*
	rm -r CPAN/arch Bin/{arm-linux,powerpc-linux,sparc-linux}
	cp ../Class-XSAccessor-1.13.tar.gz ../CPAN.upstream
	patch -p1 -i ../perl-recent.patch
	sed \
		-e 's/XSAccessor-1.05/XSAccessor-1.13/g' \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-e 's/perl5.12/perl/g' \
		-e 's/5.12/5.18/g' \
		-i ../CPAN.upstream/buildme.sh
}

build() {
	cd "${srcdir}/CPAN.upstream"
	sh buildme.sh
	rm -f build/5.18/lib/perl5/*-linux-thread-multi/XML/Parser/Expat.pm
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../CPAN.upstream/build/5.18/lib/perl5/*-linux-thread-multi/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s /opt/"${pkgname}"/License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
