# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=libva-xvba-driver
pkgver=0.8.0
pkgrel=5
pkgdesc='XvBA backend for VA-API'
arch=(i686 x86_64)
url=http://freedesktop.org/wiki/Software/vaapi
license=(GPL2)
depends=(catalyst-utils)
makedepends=(libva mesa python2 xvba-sdk)
options=(!libtool)
source=(
01-glx-assert.diff::http://anonscm.debian.org/viewvc/pkg-fglrx/xvba-video/trunk/debian/patches/01-glx-assert.diff?view=co
http://cgit.freedesktop.org/vaapi/xvba-driver/snapshot/xvba-driver-$pkgver.zip
"http://git.frugalware.org/gitweb/gitweb.cgi?p=frugalware-current.git;a=blob_plain;f=source/x11-extra/$pkgname/fixes.patch"
http://pkgbuild.com/git/aur-mirror.git/plain/xvba-video-open/xvba-video-h264-level51.patch
xvba-video-remove-installation.patch
)
md5sums=(515507d22d2a1dc1ad05721182a20e37
         2784142d7275348409cfe5d149129ae1
         6f4096f8b2b8abb837e05827109bacc7
         bce1de0a8b274049568453a53e8fce6a
         9330d0af491d139748d4c861423edc5f)
sha1sums=(a7d8d9488bcfb7d25f1b0265fb3198318597bbd5
          b9352b8b67cc7dcea3fbb838eba90bc28297fe55
          d17f16e859acdf3ce57a92c2208ff591c328bd16
          3653de094a3561fc97dc1950d0cca5bac08c4a30
          38136d5107c7d57593910fa2dbddd36180eceaa1)

build() {
  cd xvba-driver-$pkgver

  # Patches from OpenSUSE. Thanks, Vi0L0!
  patch -Np1 -i ../xvba-video-remove-installation.patch
  patch -Np1 -i ../xvba-video-h264-level51.patch

  patch -Np1 -i ../01-glx-assert.diff

  patch -Np1 -i ../fixes.patch

  export CPPFLAGS="$CPPFLAGS -I/usr/include/amd"

  ./autogen.sh
  ./configure --disable-debug --disable-tracer --disable-valgrind --prefix=/usr

  make PYTHON=python2
}

package() {
  cd xvba-driver-$pkgver

  make DESTDIR=$pkgdir install
}
