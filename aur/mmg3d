# $Id$

pkgname=mmg3d
pkgver=4.0
pkgrel=1
pkgdesc='Anisotropic tetrahedral remesher and moving mesh generator'
url="http://www.math.u-bordeaux1.fr/~cdobrzyn/logiciels/mmg3d.php"
license=('GPL')
arch=('i686' 'x86_64')
source=("${pkgname}${pkgver}.tgz::http://www.math.u-bordeaux1.fr/~cdobrzyn/logiciels/download.php?file=${pkgname}${pkgver}.tgz"
        'cmake_link.patch')
sha256sums=('f42a96bb2b0b2b861b75d91c3e94145bf1dfc97b491615de291e64b6f082748c'
            '65bbdd074270ca41778385a20178c414b29a687dfc341f30a7fe4b1b103f04a7')

prepare () {
  cd 'mmg3d4/build'

  # tell Cmake to link against pthreads and zlib
  patch -p1 < "$srcdir/cmake_link.patch"
}

build () {
  cd 'mmg3d4/build'

  # static
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIL_SHARED_LIBRARY=OFF \
    -DCOMPIL_STATIC_LIBRARY=ON

  make PREFIX=/usr

  # shared
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIL_SHARED_LIBRARY=ON \
    -DCOMPIL_STATIC_LIBRARY=OFF

  make PREFIX=/usr
}

check() {
  cd 'mmg3d4/build'

  ./testlib
}

package() {
  cd 'mmg3d4/build'

  # headers
  install -d "$pkgdir/usr/include/mmg3d4.0"
  install -m644 sources/*.h "$pkgdir/usr/include/mmg3d4.0"

  # executable
  install -Dm755 mmg3d4.0 "$pkgdir/usr/bin/mmg3d4.0"

  # lib
  install -Dm644 libmmg3dlib4.0.a "$pkgdir/usr/lib/libmmg3d.4.0.a"
  ln -s libmmg3d.4.0.a "$pkgdir/usr/lib/libmmg3d.a"
  install -Dm755 libmmg3dlib4.0.so "$pkgdir/usr/lib/libmmg3d.so.4.0"
  ln -s libmmg3d.so.4.0 "$pkgdir/usr/lib/libmmg3d.so"
}

# vim: ts=2 sw=2 et
