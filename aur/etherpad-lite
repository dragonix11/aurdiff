# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

# Todo: change user and group for etherpad-lite folder
# home dir must be writeable to fetch plugin catalogue

pkgname=etherpad-lite
pkgver=1.2.12
pkgrel=2
pkgdesc="A lightweight fork of etherpad based on javascript"
arch=(any)
url="http://etherpad.org"
_watch=('http://etherpad.org','Documentation <small>v([\d.]*)</small>')
license=('GPL')
depends=('curl' 'python' 'openssl' 'nodejs')
conflicts=('etherpad-lite-git')
backup=(usr/share/webapps/etherpad-lite/settings.json)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ether/${pkgname}/archive/${pkgver}.tar.gz"
	"etherpad-lite-bin"
	"etherpad-lite.service")
sha512sums=('cc71fe8048c2ff3aab129db5ea0add4549dbe1ead8bf8b418213e7e0a57d6e40020568b1dac164dd0d83a6991049f19ae33b6aa290b3cf8ac426f4965634f805'
	    '7294bee927fa8c020d4514ff843fa8c9774a1b9042d93bedcba7896cc10b986cd566eda74c790406e18729f08271ec5c7c5923f73e0c3774a3690d4ceb02039a'
	    '2199372422ba133ad152c101830ee928813fd3f8b2d3c14981d4175925f067123bcbb0378a83237454d6b7e1c1b03194b68884c958924a27a363db8785b3dceb')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./bin/installDeps.sh
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}/usr/share/webapps"
  cp -r . "${pkgdir}/usr/share/webapps/${pkgname}"
  install -Dm755 "${srcdir}/etherpad-lite-bin" "${pkgdir}/usr/bin/etherpad-lite"
  install -Dm644 "${srcdir}/etherpad-lite.service" "${pkgdir}/usr/lib/systemd/system/etherpad-lite.service"
}
