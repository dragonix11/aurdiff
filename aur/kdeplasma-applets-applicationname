# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-applets-applicationname
pkgver=1.8
pkgrel=1
pkgdesc="Shows the application name of the focused window"
arch=('any')
url="https://github.com/scarpin0/applicationname-plasmoid"
license=('GPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
replaces=('kdeplasma-applets-windowtitle')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/scarpin0/applicationname-plasmoid/tarball/${pkgver}")
md5sums=('b15758ddf614317039cf2405744ef8e9')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../scarpin0-applicationname-plasmoid-d241503 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
