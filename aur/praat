# Maintainer: jpate <j.k.pate@sms.ed.ac.uk>
pkgname=praat
pkgver=5.3.56
pkgrel=2
pkgdesc="A tool for 'Doing Phonetics by computer'"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
makedepends=( 'pkg-config' )
optdepends=( 'ttf-sil-fonts' )
source=("http://www.fon.hum.uva.nl/praat/praat5356_sources.tar.gz")
md5sums=('31cd2bced0df2dbc36006029038b1e8e')

prepare() {
  cd "$srcdir/sources_5356/"

  cp makefiles/makefile.defs.linux.alsa makefile.defs

  sed -i 's/-lasound/-lasound -lpthread/' makefile.defs
}

build() {
  cd "$srcdir/sources_5356/"

  make
}

package() {
  cd "$srcdir/sources_5356/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

