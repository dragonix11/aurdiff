# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2013.10
pkgrel=1
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2013.10')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/rakudo-star-$pkgver.tar.gz)
sha512sums=('43412987dcedbb6909d13fb137d1d98bf4310cda3d0b508578f873196ea9a8407d14ba9f1fc18e9ea3585f09faa7782fc416824e598c8524fef4e5d1d1886236')

build() {
  cd "$pkgname-star-$pkgver"
  perl Configure.pl --prefix=/usr \
                    --with-nqp=/usr/bin/nqp
  make
}

package() {
  cd "$pkgname-star-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}
