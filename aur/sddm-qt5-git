# Maintainer: Abdurrahman AVCI <abdurrahmanavci@gmail.com>

pkgname=sddm-qt5-git
pkgver=1d72070
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
backup=('etc/sddm.conf')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('upower' 'qt5-declarative')
makedepends=('cmake' 'git' 'qt5-tools')
conflicts=('sddm')
provides=('sddm')
source=('git://github.com/sddm/sddm.git')
md5sums=('SKIP')

_gitname=sddm

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  mkdir -p $_gitname/build
  cd $_gitname/build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=true
  make
}

package() {
  cd $_gitname/build
  make DESTDIR="$pkgdir" install
}
