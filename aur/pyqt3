# $Id: PKGBUILD 89515 2013-04-29 16:41:19Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>

pkgname=pyqt3
pkgver=3.18.1
pkgrel=13
pkgdesc="A set of Python bindings for the Qt3 toolkit"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/pyqt/intro"
depends=('python2-sip' 'qscintilla-qt3')
makedepends=('glu')
license=('GPL')
source=("http://downloads.sourceforge.net/project/pyqt/PyQt3/PyQt-x11-gpl-${pkgver}.tar.gz")
md5sums=('f1d120495d1aaf393819e988c0a7bb7e')

build() {
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  export QTDIR=/usr
  export QMAKESPEC=/usr/share/qt3/mkspecs/linux-g++
  echo yes | python2 configure.py -b /usr/bin \
    -d /usr/lib/python2.7/site-packages \
    -v /usr/share/sip \
    INCDIR_QT=/usr/include/qt3
  make CXX="g++ -DANY=void"
}

package() {
  cd ${srcdir}/PyQt-x11-gpl-${pkgver}
  make DESTDIR=${pkgdir} install
}
