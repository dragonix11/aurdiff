# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=python2-kombu
pkgver=2.5.15
pkgrel=1
pkgdesc="AMQP Messaging Framework for Python 2.x"
arch=('any')
url="http://pypi.python.org/pypi/kombu/"
license=('custom:BSD')
depends=('python2' 'python2-anyjson')
optdepends=("python2-librabbitmq: High performance AMQP transport written in C")
makedepends=('python2-distribute')
source=("http://pypi.python.org/packages/source/k/kombu/kombu-$pkgver.tar.gz")
options=(!emptydirs)

build() {
  cd "$srcdir/kombu-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/kombu-$pkgver"
  python2 setup.py install --root="$pkgdir/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('588046101550d716e114dbe4ff354c3619a2b8030c9f1d52f924374bd4a9f6865e2c6744b7626d8f27b755441dac18c1303c9f3c53d0fa678369a25c34de2497')
