# Maintainer: X0rg

pkgname=gnustep-libobjc2-clang-svn
_svnname=gnustep-libobjc2
pkgver=37254
pkgrel=3
pkgdesc="The GNUstep Objective-C runtime is designed as a drop-in replacement for the GCC runtime, using Clang"
arch=('any')
url="http://www.gnustep.org/"
license=('custom')
groups=('gnustep-clang-svn')
depends=('gcc-libs')
makedepends=('svn' 'clang' 'cmake' 'gnustep-make-clang-svn')
conflicts=('gnustep-libobjc2-git')
options=('!buildflags')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/libobjc2/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Create 'build' directory..."
  mkdir -pv ./build

  msg2 "Delete 'GNUmakefile' file..."
  rm -v ./GNUmakefile
}

build() {
  cd "$_svnname/build"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"

  msg2 "Run 'cmake'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGNUSTEP_CONFIG=/usr/bin/gnustep-config

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname/build"
#   make test
# }

package() {
  cd "$_svnname/build"
  msg2 "Install)..."
  make DESTDIR="$pkgdir" install

  cd ..
  msg2 "Install the license..."
  install -v -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
