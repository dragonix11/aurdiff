# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>
# Contributor: dserban <dserban01 at yahoo dot com>

pkgname=switchboard-bzr
pkgver=407
pkgrel=1
pkgdesc="Modular desktop settings hub"
arch=('i686' 'x86_64')
url="https://launchpad.net/switchboard"
license=('GPL3')
depends=('granite' 'libunity')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('switchboard-plug-about-bzr: About plug'
            'switchboard-plug-default-applications-bzr: Default applications plug'
            'switchboard-plug-elementary-tweaks-bzr: Elementary tweaks plug'
            'switchboard-plug-keyboard-bzr: Keyboard plug'
            'switchboard-plug-pantheon-shell-bzr: Pantheon Shell plug'
            'switchboard-plug-power-bzr: Power plug')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
