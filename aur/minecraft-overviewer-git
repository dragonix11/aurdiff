# Maintainer: Duncan Bain <duncanjbain@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=minecraft-overviewer-git
pkgver=v0.10.0.92.gac4f810
pkgver() {
	cd "$srcdir/$_gitname"
	git describe --tags --match 'v*.*.*' | sed 's|-|.|g'
}
pkgrel=1
pkgdesc="Render large resolution images of a Minecraft map with a Google Maps powered interface"
arch=('x86_64' 'i686')
url="https://github.com/overviewer/Minecraft-Overviewer"
license=('GPL')
depends=('python2' 'python-imaging' 'python2-numpy')
makedepends=('git')
source=("git://github.com/overviewer/Minecraft-Overviewer.git")
md5sums=(SKIP)
_gitname="Minecraft-Overviewer"

build() {
	cd "$srcdir/$_gitname"
	python2 "setup.py" build
}

package() {
	cd "$srcdir/$_gitname"
	python2 "setup.py" install --prefix="$pkgdir/usr"
} 
