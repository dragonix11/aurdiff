# Maintainer : Skydrome <skydrome@i2pmail.org>
# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=tahoe-lafs-i2p
pkgver=1.9.2
_patchver="${pkgver}-r0"
pkgrel=2
pkgdesc="Secure, decentralized, and fault-tolerant filesystem over the I2P network"
url='http://tahoe-lafs.org'
license=('GPL')
arch=('any')
conflicts=('tahoe-lafs')
provides=('tahoe-lafs')
BUILDENV+=(!check)

depends=('net-tools' # provides /sbin/ifconfig
         'nevow'
         'twisted'
         'python2-argparse'
         'python2-foolscap-i2p>=0.6.3'
         'python2-mock'
         'python2-pyopenssl'
         'python2-pyasn1'
         'pycrypto'
         'pycryptopp>=0.6.0'
         'pyutil'
         'python-simplejson'
         'zbase32'
         'zfec'
         'python2-zope-interface'
         'python2-distribute')

optdepends=('python2-numpy: enables reliability calculator'
            'grid-updates: helps keep you up to date with latest grid news')

source=("https://tahoe-lafs.org/source/tahoe-lafs/snapshots/allmydata-tahoe-${pkgver}.tar.bz2"
        '0001-multiple-introducer-support.patch'
        '0002-proxy-support.patch'
        '0003-allow-non-routable.patch'
        'exclude_buildtest_package.patch'
        # if this url is inaccessible, just comment it out
        "http://killyourtv.i2p.us/tahoe-lafs/patches/tahoe-lafs-i2p-${_patchver}.patch"
        'tahoe-repair-all.sh')

sha256sums=('04773b076004cfd41f60e16c1dd3f3ee247f2a3f1fc90942a7cf66bf557d2104'
            '14c841c5973708a7482137e9396c07605424074115745054ad1be268e22f7d55'
            '62dd14387825b26e0949355ccf27bdf1289833aab89f23741bff77e1bd80a2af'
            '992dd4d870f749121d874a7a2cca953672a03f2bba52e2e4b884242042778f0e'
            '469aa0ace523b64449e7d2d52958119a84032c50b07828b3dffbb81dca5327bd'
            '8e2d8caf923d413a3b343700fac6a16ba69e758e889eea1d61a82c2655cd58a3'
            '1525fd5c2ac2c93844f23160e70bb47040a9a8ee7dff8b6fff8ca48b374ac216')

prepare() {
    cd "${srcdir}/allmydata-tahoe-${pkgver}"
    if [[ ! -r "${srcdir}/tahoe-lafs-i2p-${_patchver}.patch" ]]; then
        for p in $(ls ${srcdir}/*.patch); do
            patch -Np1 -i $p
        done
    else
        patch -Np1 -i "${srcdir}/tahoe-lafs-i2p-${_patchver}.patch"
        patch -Np1 -i "${srcdir}/exclude_buildtest_package.patch"
    fi
}

build(){
    cd "${srcdir}/allmydata-tahoe-${pkgver}"
    python2 setup.py build
}

check() {
    cd "${srcdir}/allmydata-tahoe-${pkgver}"
    msg "This will take a while"
    python2 setup.py test || true
}

package(){
    cd "${srcdir}/allmydata-tahoe-${pkgver}"
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 COPYING.GPL "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    install -Dm755 "$srcdir/tahoe-repair-all.sh" "${pkgdir}/usr/bin/tahoe-repair-all"
}
