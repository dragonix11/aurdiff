# Contributor: Splex
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=inkscape-gtk3-bzr
pkgver=12698
pkgrel=1
pkgdesc="An Open Source vector graphics editor, using Scalable Vector Graphics (SVG) file format, built with experimental gtk3 enabled"
url="https://launchpad.net/inkscape"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gc' 'poppler-glib' 'libxslt' 'gsl' 'popt' 'gtkspell3' 'imagemagick' 'desktop-file-utils' 'hicolor-icon-theme' 'gdl>=3.8.0.25' 'gtkmm3' 'python2' 'libexif' 'gnome-vfs' 'libwpg')
optdepends=('python2-numpy: some extensions'
            'python-lxml: some extensions and filters'
            'uniconvertor: reading/writing to some proprietary formats')
makedepends=('boost' 'pkg-config' 'intltool' 'bzr' 'gettext' 'autoconf' 'pango' 'fontconfig')
provides=('inkscape')
conflicts=('inkscape')
options=('!libtool')
source=('inkscape::bzr+http://bazaar.launchpad.net/~inkscape.dev/inkscape/trunk/')
md5sums=('SKIP')
install=inkscape-bzr.install
_bzrmod="inkscape"

pkgver() {
  cd $srcdir/$_bzrmod
  bzr revno
}

prepare() {
  cd "$srcdir/$_bzrmod"
  sed -i 's+2,36+2,35+' src/extension/dbus/wrapper/inkscape-dbus-wrapper.c
  #fix for inkscape to use python2 with the python 3 package installed.
  sed -i '1s|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i '1s|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|python -c |python2 -c|g' share/extensions/uniconv*.py
  sed -i 's|"python"|"python2"|g' src/main.cpp
  sed -i '1s|python|python2|' share/extensions/ink2canvas/svg.py
  sed -i '1s|python|python2|' share/extensions/ink2canvas/canvas.py
}

build() {
  cd "$srcdir/$_bzrmod"
  ./autogen.sh
  sed -i 's|python -c |python2 -c|g' configure 
  ./configure LIBS='-lpangoft2-1.0 -lfontconfig' \
    --prefix=/usr \
    --with-gnome-vfs \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking \
    --enable-gtk3-experimental \
    --enable-dbusapi \
    --enable-wpg \
    --disable-rpath
  make 
}

package() {
  cd "$srcdir/$_bzrmod"
  make DESTDIR=$pkgdir install
}
