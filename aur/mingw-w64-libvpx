# Maintainer: Karl-Felix Glatzer <karl.glatzer@gmx.de>
pkgname=mingw-w64-libvpx
pkgver=1.2.0
pkgrel=2
pkgdesc="The VP8 Codec SDK (mingw-w64)"
arch=('any')
url="http://www.webmproject.org/"
license=('BSD')
depends=('mingw-w64-crt')
options=(!strip !buildflags)
makedepends=('mingw-w64-gcc' 'yasm')
source=(ftp://ftp.archlinux.org/other/libvpx/libvpx-$pkgver.tar.xz{,.sig}
        'configure.patch')
sha1sums=('75cd953cf2db694412569272803fbdc73f2b599f'
          'SKIP'
          '346b79115d6b2eda44eed827c5e1937465633ccd')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/libvpx-$pkgver

  patch -Np1 < ../configure.patch
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}
    if [ ${_arch} = "i686-w64-mingw32" ]; then
      _targetarch="x86-win32-gcc"
    else
      _targetarch="x86_64-win64-gcc"
    fi

    unset LDFLAGS CPPFLAGS
    CROSS="${_arch}-" ${srcdir}/libvpx-$pkgver/configure --prefix="/usr/${_arch}" \
        --target=${_targetarch} \
        --enable-shared \
        --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-postproc \
        --enable-pic \
        --disable-examples \
        --disable-docs \
        --disable-install-bins \
        --disable-install-docs \
        --disable-install-srcs
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}
    make DIST_DIR="$pkgdir/usr/${_arch}" install
    install -D -m 0644 ${srcdir}/libvpx-$pkgver/LICENSE "$pkgdir/usr/${_arch}/share/licenses/libvpx/LICENSE"

    #Move the hacked in shared libs to bin
    mkdir ${pkgdir}/usr/${_arch}/bin
    mv ${pkgdir}/usr/${_arch}/lib/libvpx.dll ${pkgdir}/usr/${_arch}/bin/
    mv ${pkgdir}/usr/${_arch}/lib/libvpx.dll.1* ${pkgdir}/usr/${_arch}/bin/

    ${_arch}-strip -g --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
  done
}
