# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower-nosystemd
pkgver=0.9.23
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('libusb' 'polkit-consolekit' 'pm-utils' 'dbus-glib' 'libimobiledevice' 'udev')
backup=('etc/UPower/UPower.conf')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection')
provides=("upower=${pkgver}")
conflicts=('upower')
replaces=('upower')
options=('!libtool')
source=($url/releases/upower-${pkgver}.tar.xz
        linux-clamp-percentage-for-overfull-batteries.patch)
md5sums=('39cfd97bfaf7d30908f20cf937a57634'
         'cd8eeb40c2338d28df46e22529bece62')

prepare() {
  cd "upower-$pkgver"

  patch -Np1 -i ../linux-clamp-percentage-for-overfull-batteries.patch
}

build() {
  cd "upower-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/upower \
    --disable-static \
    --enable-deprecated
  make
}

package() {
  cd "upower-$pkgver"
  make DESTDIR="$pkgdir" install
}
