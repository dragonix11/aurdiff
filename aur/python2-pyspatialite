# Maintainer: SaultDon <sault.don gmail>
_pkgname=pyspatialite
pkgbase=python-$_pkgname
pkgname=python2-$_pkgname
pkgver=3.0.1
pkgrel=2
pkgdesc="An interface to the SQLite 3.x embedded relational database engine with spatialite extensions."
arch=(any)
url="https://pypi.python.org/pypi/pyspatialite"
license=('ZLIB' 'LIBPNG')
depends=('python2' 'libspatialite')
conflicts=('$pkgname<=$pkgver-1')
replaces=('$pkgname<=$pkgver-1')
options=(!emptydirs)
changelog=$pkgname.changelog
source=(https://pypi.python.org/packages/source/p/pyspatialite/$_pkgname-$pkgver.tar.gz
        setup.cfg
        'fix_3.0.1_build_after_libspatialite_introduced_version_4.0.patch::https://pyspatialite.googlecode.com/issues/attachment?aid=90001000&name=fix_3.0.1_build_after_libspatialite_introduced_version_4.0.patch&token=i7hkf23s87mVzuofpl30hoxISeo%3A1384028525166')
md5sums=(28a6ab34b5003772e9468f2712100586
         23e4f6ab7946bc54d660cf701743d418
         f1805295449cc1b9a317d31280720c0a)

prepare() {
        cd "$srcdir/$_pkgname-$pkgver"
        patch -p1 -i "$srcdir/fix_3.0.1_build_after_libspatialite_introduced_version_4.0.patch"
}

build() {
        cd "$srcdir/$_pkgname-$pkgver"
        cp "$srcdir/setup.cfg" .
        python2 setup.py build
}

package_python2-pyspatialite() {
        cd "$srcdir/$_pkgname-$pkgver"
        python2 setup.py install --root="${pkgdir}" --optimize=1
        install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
        rm -r "${pkgdir}/usr/$_pkgname-doc"
}

# vim:set ts=2 sw=2 et:
