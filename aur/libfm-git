# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname=libfm-git
pkgver=1.1.0.581.g2389b78
pkgrel=1
pkgdesc="A glib/gio-based library to develop file managers. Does not include the GTK components."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('menu-cache')
optdepends=('libexif')
makedepends=('git' 'libexif' 'vala' 'gtk-doc')
provides=('libfm')
conflicts=('libfm')
source=("git://github.com/lxde/libfm.git")
sha256sums=('SKIP')
_gitname="libfm"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --without-gtk --disable-gtk-doc --disable-udisks --enable-actions --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
