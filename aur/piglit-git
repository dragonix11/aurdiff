# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=piglit-git
_gitname=piglit
pkgver=e37c629
pkgrel=1
pkgdesc="Open-source test suite for OpenGL implementations"
arch=('i686' 'x86_64')
url="http://piglit.freedesktop.org/"
license=('MIT' 'GPL')
depends=('waffle' 'python2-mako' 'python2-numpy' 'glu')
makedepends=('git' 'cmake' 'glproto')
conflicts=('piglit')
provides=('piglit')
source=('git://anongit.freedesktop.org/piglit'
        '10-no-rpath-in-build.patch'
        '20-change-bin-dir.patch'
        '30-change-template-dir.patch'
        '40-piglit-install-directories.patch')
md5sums=('SKIP'
         '4f4855fca4d274d3497115f3b0952e90'
         '3e2ed707f19649a81d207e2c4c07ea49'
         '8d28b0bf92dfc2d13113627f5bc89850'
         'a47378bb3a58d0bcd7733c8eb24ce3bf')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}
prepare() {
  cd $_gitname
  patch -Np1 -i ../10-no-rpath-in-build.patch
  patch -Np1 -i ../20-change-bin-dir.patch
  patch -Np1 -i ../30-change-template-dir.patch
  patch -Np1 -i ../40-piglit-install-directories.patch

  find . -type f -name '*.py' | xargs sed -i 's@^#.*python$@#!/usr/bin/python2@'
  find . -type f -name '*.tests' | xargs sed -i 's@^#.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_gitname

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_SKIP_RPATH=true \
           -DPIGLIT_BUILD_GLES1_TESTS=true \
           -DPIGLIT_BUILD_GLES2_TESTS=true
  make
}

package() {
  cd $_gitname/build
  make PREFIX=/usr DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/bin/* "$pkgdir/usr/lib/piglit"
  cp -r ../templates "$pkgdir/usr/share/piglit"
  cp -r ../generated_tests "$pkgdir/usr/share/piglit"
  cp -r generated_tests/spec "$pkgdir/usr/share/piglit/generated_tests"
  cp -r ../tests "$pkgdir/usr/share/piglit"
  cp -r tests/util "$pkgdir/usr/share/piglit/tests"

  ln -s ../lib/piglit/piglit-run.py "$pkgdir/usr/bin/piglit-run.py"
  ln -s ../lib/piglit/piglit-summary-html.py "$pkgdir/usr/bin/piglit-summary-html.py"
  ln -s ../lib/piglit/piglit-print-commands.py "$pkgdir/usr/bin/piglit-print-commands.py"
  ln -s ../lib/piglit/piglit-summary-junit.py "$pkgdir/usr/bin/piglit-summary-junit.py"
  ln -s ../lib/piglit/piglit-merge-results.py "$pkgdir/usr/bin/piglit-merge-results.py"

  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
