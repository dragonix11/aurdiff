

pkgname=corebird-git
pkgver=1136+gd5e5cf3
pkgrel=1
_realver=0.3
pkgdesc="Native Gtk+ Twitter Client"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/baedert/corebird"
depends=('gtk3>=3.10'
     'glib2>=2.38'
     'rest>=0.7'
     'libgee'
     'sqlite3'
     'libsoup>=2.4'
     'libnotify'
     'json-glib'
     'intltool')
makedepends=('vala>=0.21.1' 'git' 'cmake')
source=(git+https://github.com/baedert/corebird.git)
sha1sums=(SKIP)
install=corebird.install

pkgver() {
  cd corebird
  echo $(git rev-list --count HEAD)+g$(git rev-parse --short HEAD)
}

build() {
  cd corebird
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd corebird
  make DESTDIR=$pkgdir install
}
