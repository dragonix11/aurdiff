# Maintainer: Rikard Falkeborn <rikard.falkeborn@gmail.com>
# Contributor: Hubert Maraszek <marach5 at gmail dot com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=xye
pkgver=0.12.1
pkgrel=1
pkgdesc="Puzzle game in which the objective is to help a character that looks like a green circle to get all the gems in the room."
url="http://xye.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl_image' 'sdl_ttf')
makedepends=('imagemagick')
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz
	editorsave.cpp.int_i.patch
	gen.cpp.include.unistd.patch
	xye.desktop)
md5sums=('d6482d7490c11baacab4ca2df436af4f'
         'f85a145f75161bb7fbfec62f2c5a8787'
         '619638e5d129788b02a74e3b4792c5f5'
         '0bc624b90351f316f2c318957695b7d1')


build() {
  # Patches to fix compilation errors discussed in package comments (upstream noted).
  patch -u "$srcdir/$pkgname-$pkgver/src/gen.cpp" < "$srcdir/gen.cpp.include.unistd.patch"
  patch -u "$srcdir/$pkgname-$pkgver/src/editorsave.cpp" < "$srcdir/editorsave.cpp.int_i.patch"

  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  convert xye.ico[2] xye.png
  install -D xye.png "$pkgdir/usr/share/xye/xye.png"
  install -D ../xye.desktop "$pkgdir/usr/share/applications/xye.desktop"

}
