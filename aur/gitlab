# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

#  lib/support/nginx/gitlab

pkgname=gitlab
pkgver=6.1.0
pkgrel=1
pkgdesc="Project management and code hosting application"
arch=('any')
url="https://www.gitlab.org"
license=('MIT')
depends=('ruby' 'ruby-bundler' 'python2' 'gitlab-shell')
# sudo base-devel zlib libyaml openssl gdbm readline ncurses libffi curl openssh redis libxml2 libxslt icu 
optdepends=(
    'mysql: database backend'
    'postgresql: database backend'
    'python2-docutils: reStructuredText markup language support'
    'postifx: mail server in order to receive mail notifications'
)
backup=(etc/gitlab/gitlab.yml etc/gitlab/resque.yml etc/gitlab/database.yml)
source=("$pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlabhq/archive/v${pkgver}.tar.gz"
        "gitlab.service")
install='gitlab.install'
sha512sums=("0ae85d461db068e8200a285b11ed6698b3b23464ee5fcbdbde1289b615968ce13fb3a740efbef784e6779672c44adadc572c74285b77257ad19798ab0f6f4ed1"
            "944d18458726c7a93516332614cca23e456a72d5a362d652aab71d7edb512dd0700f72f5107c4587a5bfb30c44f65ad719995f7dcafee94cf1d68aa135947306")
options=('!strip')

build() {
        cd "$srcdir/gitlabhq-$pkgver"

        gem install charlock_holmes --version '0.6.9.4'

        msg "Fetching bundled gems..."
        bundle install --deployment --without development test aws
        # bundle install --deployment --binstubs --no-cache --without test
        # bundle install --no-deployment --binstubs --path=vendor/bundle --no-cache --without test
}

package() {
    local datadir=/usr/share/webapps/gitlab
    local homedir=/home/gitlab
    local wwwdir=$homedir/www

    cd $srcdir/gitlabhq-$pkgver

    # Creating directories
    install -d \
        "$pkgdir/etc/gitlab" \
        "$pkgdir/usr/share/webapps" \
        "$pkgdir/usr/share/doc/$pkgname" \
        "$pkgdir/usr/share/licenses/$pkgname"
    
    # Install config files
    sed -e 's|# user: git|user: gitlab|' \
        -e "s|/home/git/repositories|$datadir/repositories|" \
        -e "s|/home/git/gitlab-satellites|$datadir/satellites|" \
        -e "s|/home/git|$homedir|" \
        config/gitlab.yml.example > "$pkgdir/etc/gitlab/gitlab.yml"
    install -Dm644 config/database.yml.mysql "$pkgdir/etc/gitlab/database.yml"
    for conf in gitlab; do
        ln -s /etc/gitlab/$conf.yml config/$conf.yml
    done

    # Install license, help and systemd service files
    mv LICENSE "$pkgdir/usr/share/licenses/$pkgname"
    mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "$pkgdir/usr/share/doc/$pkgname"
    install -Dm0755 "$srcdir/gitlab.service" "$pkgdir/usr/lib/systemd/system/gitlab.service"

	cp -r "$srcdir/gitlabhq-$pkgver" "$pkgdir/usr/share/webapps/$pkgname"
}

# vim:set ts=4 sw=4 et:
