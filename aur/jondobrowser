# Maintainer: Max Roder <maxroder@web.de>

pkgname='jondobrowser'
pkgver='0.9'
pkgrel='1'
_language='en-US'
pkgdesc='Firefox tweaked to work with jondo (anonymous browsing) (english version)'
url='https://anonymous-proxy-servers.net/en/jondobrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes'
			'jondo: connection to jondo proxy cascades, gui version'
			'jondoconsole: connection to jondo proxy cascades, cli version')
install="${pkgname}.install"
source=("https://anonymous-proxy-servers.net/en/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.xz"
		"https://anonymous-proxy-servers.net/en/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.xz.asc"
		"jondobrowser.desktop")
if [[ "$CARCH" == 'x86_64' ]]; then
sha256sums=('a38503ab38f87f1d82f4b986491090038409979d29e33ab06e53048df926ccf0'
            '28eb51e9ba8e18a4072921d988b360fbef63d7dd0b66d04ef9b9bc320bab2209')
else
	sha256sums=('455a86b564a2eb812771a1fa5fd1f6aa51ffde898456ca88c6c91318fd8ca064'
				'b3bcde086c2401c059bf12a465c61518ef6eeaaf56fdf48eaafb6f4c1303e106')
fi
sha256sums+=('b7bbf376fd760e85102649166ed8710cceab88cf73b0425b67cb33be34d57749')

package() {
	cd "${srcdir}/JonDoBrowser/"

	install -Dm 644 ../${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 App/Firefox/icons/mozicon128.png	${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 start-jondobrowser.sh	${pkgdir}/usr/bin/${pkgname}
	install -Dm 644 CHANGELOG	${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG

	install -d	${pkgdir}/opt/JonDoBrowser
	cp -r App	${pkgdir}/opt/JonDoBrowser/
	cp -r Data	${pkgdir}/opt/JonDoBrowser/
	
}

# vim:set ts=2 sw=2 et:
