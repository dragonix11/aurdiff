# Maintainer: jdarch <jda.cloud plus archlinux at gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: shining <shiningxc.at.gmail.com>
# Contributor: cyberdune <cyberdune@gmail.com>
# Patches taken from the Debian packaging project

pkgname=octave-mkl
pkgver=3.6.4
pkgrel=2
pkgdesc="A high-level language, primarily intended for numerical computations. BLAS, LAPACK and FFT from Intel's MKL."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
provides=('octave=3.6.4')
conflicts=('octave')
depends=('intel-mkl' 'curl' 'graphicsmagick' 'qrupdate' 'glpk' 'hdf5' 'qhull' 'fltk' 'suitesparse' 'arpack')
makedepends=('gcc-fortran' 'texlive-core')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting')
source=(ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2{,.sig}
        no_pdf_in_print.patch
        add_info_dir_categories.patch
        correct_typos.patch
        contourc-stack-overflow.patch
        texinfo5.patch
        gcc-4.8.patch
        glpk-4.49.patch)
md5sums=('e0d3e5e3d38a66d3f8593ba065c6e2fd'
         'SKIP'
         '42b6fb087fca6d2de737c872b9e971e6'
         '4e16fcd79da4570b0cb7be65276dc4fd'
         '58e86f46752fd3be6e35370641e19fbf'
         'e1678f0b23c1f45ea3fc4f46a154e21c'
         '39624a9bd87ec6221d0ef52e8a72dcac'
         'fba47675676126db953861184723f628'
         'f0c80a604ef69137b8a19c53fc509573')
sha512sums=('a49ef50161272618c9543057be5903e1e50b7f4ecb929777f6715d4b3a07a92c36a5b13958c52d8b942b6c7e7e3f7844409d0062eb02bdb3972d7194312b7a81'
            'SKIP'
            '79b106c9c638ed44b927fdca1a8e24bab6f6e76b32d6aef83d69f49935c9fd0cb90e12180c55619c652d5f3db6a2f1cb0d6b022d054dd500e86de8e556d073df'
            '8cd95de16f4cf39dc173b6b049938c603b204a978d4cbb08acf2d8357d6b28778bfd49e19e77149853d670b14ba971f4bf39ca2980e71535705f4d6736eb4c73'
            'b147a41ad69623f6370a0aebd8cadd5303e76ede7af4a61e5d626ecc8a8198494034a1860be9207fdfbf1ad4af0c6e0896edb59dd3884d57f05183e630ffbd92'
            '9cfdbfbb924ad006abccaa4b62324d4343723b174e247dcdeec8f3e9a41ae029a9dfde69a58a3bbc6d887158c5b2552379ba65f0aaff2a84f2bb1212898a8d35'
            'f0ae06e42accbd977249cd1a9e52029c2ebd86f8d7456595fc130ce61a572f20166ad8edb1e04595199cc8e68a453a119977f9fd8bb0e86a2baec708ac61e3fb'
            'a7da029a47c97e52b26e28417b406aa5122667a8d1596929e839a8530b2c4e305007b6b114e742a27fb9c1b898ecf8845f962c346663de8b1df9ce5f566322b9'
            '12502c61e16064f77064b6ae7ba35ec63d5a6969d610e332760ec730af6ef77db319bccd79208e2eef08c590d831d5dfde625e6a1ac87e662e6ff7bc2fd17b69')
options=('!emptydirs')
install=octave-mkl.install

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _intel_lib=mkl_gf_lp64
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _intel_lib=mkl_gf
fi
_orgpkgname=octave

prepare() {
  # Apply selected patches, conveniently provided by Debian thanks to their diligent packagers
  # Refer to the patch files for details
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/no_pdf_in_print.patch"
  patch -Np1 -i "${srcdir}/add_info_dir_categories.patch"
  patch -Np1 -i "${srcdir}/correct_typos.patch"
  patch -Np1 -i "${srcdir}/contourc-stack-overflow.patch"
  patch -Np1 -i "${srcdir}/texinfo5.patch"
  patch -Np1 -i "${srcdir}/gcc-4.8.patch"
  patch -Np1 -i "${srcdir}/glpk-4.49.patch"

  autoreconf -vfi
}

build() {
  # Set environment for use of Intel MKL
  MKL_LIB_PATH=/opt/intel/mkl/lib/${_intel_arch}
  MKL_INCLUDE_PATH=/opt/intel/mkl/include
  MKL="-fopenmp -I${MKL_INCLUDE_PATH} -L${MKL_LIB_PATH} -l${_intel_lib} -lmkl_gnu_thread -lmkl_core -ldl -lpthread -lm"
  export LD_LIBRARY_PATH=${MKL_LIB_PATH}:${LD_LIBRARY}

  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static \
  --with-blas="${MKL}" \
  --with-lapack="${MKL}" \
  --with-fftw3f="${MKL}" \
  --with-umfpack="-lumfpack -lsuitesparseconfig" # https://mailman.cae.wisc.edu/pipermail/help-octave/2012-September/053991.html

  LANG=C make
}

check() {
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_orgpkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${_orgpkgname}.conf"
}
