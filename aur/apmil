# Maintainer: Alexej Magura <agm2819*gmail*>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Alexander Preisinger <alexander dot preisinger at gmail dot com>

pkgname=apmil
pkgver=0.4.0
pkgrel=10
pkgdesc="a manga image downloader"
arch=(any)
url="http://sharesource.org/project/apmil/"
license=('GPL3')
depends=('python2' 'wxpython')
source=("http://dl.sharesource.org/apmil/${pkgname}-${pkgver}.tar.bz2"
	"0001_change-xdg.patch"
	"apmil.sh")
md5sums=('17791dd76f4825ce5dc1072bdf651c51'
         '72935ddb5d596db67d332bba5a7bdae1'
	 '7a1dea81e27a1ce8ba88f92defee9d23')


prepare () {
    
    cd "$srcdir/$pkgname"

    sed -i "s/python/python2/" apmil.py
    patch -p2 -i ../0001_change-xdg.patch

    rename LICENCE.txt LICENSE.txt LICENCE.txt
}

package () {
    
    cd "$srcdir/$pkgname"

    install -d "$pkgdir"/usr/{bin,share/{licenses/$pkgname,pixmaps,applications,$pkgname/{locale,plugins}}}

    install -m 644 -t "$pkgdir"/usr/share/$pkgname \
	${pkgname}.py

    install -m 644 -t "$pkgdir"/usr/share/licenses/$pkgname \
	LICENSE.txt

    install -m 644 -t "$pkgdir"/usr/share/pixmaps \
	ap_logo.png

    install -m 644 -t "$pkgdir"/usr/share/applications \
	${pkgname}.desktop

    for files in $(find locale -mindepth 1 -name '*.mo')
    do
	install -m 644 -t "$pkgdir"/usr/share/$pkgname/locale \
	    $files
    done

    for files in $(find plugins -mindepth 1 -name '*.py')
    do
	install -m 644 -t "$pkgdir"/usr/share/$pkgname/plugins \
	    $files
    done

    install -m 755 "$srcdir"/${pkgname}.sh "$pkgdir"/usr/bin/$pkgname

    for files in $(find . -mindepth 1 -maxdepth 1 -name '*.py')
    do
	install -m 644 -t "$pkgdir"/usr/share/$pkgname \
	    $files
    done

}
