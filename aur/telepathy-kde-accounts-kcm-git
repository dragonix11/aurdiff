# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: laloch <laloch@atlas.cz>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm-git
_gitname="ktp-accounts-kcm"
pkgver=v0.6.80.1.g2c8e4b1
pkgrel=1
epoch=2
pkgdesc="KCM Module for configuring Telepathy Instant Messaging Accounts. Now includes protocol-specific UI's"
url="https://projects.kde.org/projects/kdereview/ktp-accounts-kcm"
arch=('i686' 'x86_64')
license=('GPL')
depends=('telepathy-kde-common-internals-git' 'telepathy-mission-control')
makedepends=('cmake' 'automoc4' 'git')
optdepends=('telepathy-gabble: XMPP/Jabber/GTalk support'
            'telepathy-haze: all protocols supported by libpurple')
conflicts=('telepathy-kde-accounts-kcm'
           'telepathy-accounts-kcm-git'
           'telepathy-kde-accounts-kcm-plugins'
           'telepathy-accounts-kcm-plugins-git')
provides=('telepathy-accounts-kcm-git')
source=("git://anongit.kde.org/ktp-accounts-kcm")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
