# Maintainer: SrÄ‘an Tot

pkgname=weston-rbp
pkgver=1.3.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor for Raspberry Pi'
arch=('armv6h')
url='http://wayland.freedesktop.org'
license=('MIT')
options=(!libtool)
depends=('libxkbcommon' 'poppler-glib' 'mtdev' 'libxcursor' 'pango' 'raspberrypi-firmware-tools')
conflicts=('weston')
provides=('weston')
source=("http://wayland.freedesktop.org/releases/weston-$pkgver.tar.xz"
	"bcm_host.pc"
	"egl.pc"
	"glesv2.pc"
	"drm.patch")
sha1sums=('670f95844d20dc87da579b6ac566388edc0bfa0c'
	  'f40a7a2e5ea69e4ec0327e3ba1d1fc31d70b01b0'
	  'dc64eb1bc41a85f32ddea5c006ae3181bd8222bb'
	  '01c011605b0590c4aa140cff1bf273cb03b1cfeb'
	  'f522ab3789f4fb473528936d97654921c2d9fa2b')

build() {
	cd weston-$pkgver
	pwd
	patch -p1 -i "${srcdir}"/drm.patch
	PKG_CONFIG_PATH="$srcdir" ./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-demo-clients \
		--disable-setuid-install --with-cairo=image \
		--disable-x11-compositor --disable-drm-compositor \
		--disable-fbdev-compositor --disable-wayland-compositor \
		--disable-weston-launch --disable-simple-egl-clients \
		--disable-egl --disable-libunwind --disable-colord \
		--disable-resize-optimization --disable-xwayland-test \
		WESTON_NATIVE_BACKEND="rpi-backend.so"
	make
}

package() {
	cd weston-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/weston/COPYING"
}
