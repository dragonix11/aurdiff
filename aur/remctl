# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=remctl
pkgver=3.6
pkgrel=1
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
makedepends=(
  "perl"
  "php"
  "python2"
  #"ruby" Build broken, I'm not using the ruby bindings, too lazy to fix.
)
backup=("etc/remctl/remctl.conf")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz.asc"
        "remctl.service"
        "remctl@.service"
        "remctl.socket")
sha1sums=('43ac623d0951a3a78c389113408a608bf4b12ab0'
          'SKIP'
          '951cbbb97eebf3688b45b3c76d0adc78e05c4d8a'
          '3fb20c4174afa8865d875a0a73aa66ba3dd5c936'
          'b01a79c8f7dd286fbdeb0238333e44e785b5b3d0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export REMCTL_PYTHON_VERSIONS=python2
  ./configure \
    --prefix=/usr             \
    --sbindir=/usr/bin        \
    --sysconfdir=/etc/remctl  \
    --enable-perl             \
    --enable-php              \
    --enable-python           \
    --disable-ruby            \
    ;
  # parallel builds sometimes fail with missing -lremctl
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE              "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 examples/remctl.conf "$pkgdir/etc/remctl/remctl.conf"
  mkdir -p "$pkgdir/etc/remctl/conf.d"
  mkdir -p "$pkgdir/etc/php/conf.d"
  echo 'extension=remctl.so' > "$pkgdir/etc/php/conf.d/remctl.ini"

  cd "$srcdir"
  install -Dm0644 remctl.service  "$pkgdir/usr/lib/systemd/system/remctl.service"
  install -Dm0644 remctl@.service "$pkgdir/usr/lib/systemd/system/remctl@.service"
  install -Dm0644 remctl.socket   "$pkgdir/usr/lib/systemd/system/remctl.socket"
}

# vim: ts=2:sw=2:et:ft=sh
