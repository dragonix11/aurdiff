# Maintainer:   Huulivoide      <gmail.com: jesse.jaara>
# Contributor:  Jonathan Hudson <daria.co.uk: jh+arch>

pkgname=omxplayer-git
pkgver=251.38f05ee
pkgrel=1
pkgdesc="omxplayer is a command line media player for the RaspberryPi"
arch=('arm' 'armv6h')
url="https://github.com/popcornmix/omxplayer"
license=(GPL2)
depends=(ffmpeg-compat raspberrypi-firmware-tools fbset dbus)
makedepends=(git boost)
optdepends=(ttf-freefont)
provides=(omxplayer)
conflicts=(omxplayer omxplayer-bin)
source=(git://github.com/popcornmix/omxplayer.git
        omxplayer.arch Makefile.arch Makefile.include.arch)

pkgver() {
  cd omxplayer
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/omxplayer"

  cp ../Makefile.arch Makefile
  cp ../Makefile.include.arch Makefile.include
  cp ../omxplayer.arch omxplayer
}

build() {
  cd "${srcdir}/omxplayer"

  make
}

package() {
  cd "${srcdir}/omxplayer"

  make DESTDIR="${pkgdir}" install
}

md5sums=('SKIP'
         '305f8fedbc4638434475c4a83ee78578'
         '5c0e2eaa325baad852c762d3030eb52e'
         'a8635442d02665944c04c18606453a04')
