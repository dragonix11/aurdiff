# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.42
pkgrel=1

_zpaq_ver=642

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('e12196e89d090ef2e4a7bf08eea128ac761d540013a44994034eced5e40157c23b1ee9c3bc109dae2a1978b64f6b099a884ab37b3fad3228eba769a65546c2d4')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]
	then
		CC=gcc
	fi
	if [ -z "$CXX" ]
	then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC -shared -Dunix $CXXFLAGS -DNDEBUG libzpaq.cpp -o libzpaq.so

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -Dunix -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
	install -Dm 644 libzpaq.h  "$pkgdir/usr/include/libzpaq.h"
	install -Dm 644 libzpaq.so "$pkgdir/usr/lib/libzpaq.so"
	install -Dm 755 zpaq       "$pkgdir/usr/bin/zpaq"
}
