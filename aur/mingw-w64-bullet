# Maintainer: naelstrof <naelstrof@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Paul Bienkowski <opatutlol@aol.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
pkgname=mingw-w64-bullet
pkgver=2.82
pkgrel=2
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and  animation (mingw-w64)"
arch=('any')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=('mingw-w64-crt')
options=(!strip !buildflags)
optdepends=('bullet-docs: documentation')
makedepends=('mingw-w64-cmake' 'mingw-w64-gcc')
source=(http://bullet.googlecode.com/files/bullet-${pkgver}-r2703.tgz)
md5sums=('b46c8178b2fb55e7a2515ca59fdbab6c')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}/bullet-${pkgver}-r2703
  #Requires fpermissive to allow losing precision in casts. (Should be safe)
  #echo 'set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fpermissive")' >> CMakeLists.txt
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}
    ${_arch}-cmake \
      -DBUILD_SHARED_LIBS=1 \
      -DBUILD_DEMOS=0 \
      -DBUILD_EXTRAS=1 \
      -DINSTALL_LIBS=1 \
      -DUSE_GLUT=0 \
      -DINSTALL_EXTRA_LIBS=1 \
      -DCMAKE_BUILD_TYPE=Release \
      ${srcdir}/bullet-${pkgver}-r2703
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}
    make DESTDIR=${pkgdir} install
    install -Dm644 ${srcdir}/bullet-${pkgver}-r2703/BulletLicense.txt ${pkgdir}/usr/${_arch}/share/licenses/${pkgname}/LICENSE
    mkdir -p $pkgdir/usr/${_arch}/bin
    mv ${pkgdir}/usr/${_arch}/lib/*.dll $pkgdir/usr/${_arch}/bin/
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
