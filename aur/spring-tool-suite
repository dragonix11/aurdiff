# Current maintainer: Statix
# Updates by maintainer: Chris Fordham <chris [at] fordham [hyphon] nagy [dot] id [dot] au>
# Original maintainer: Marcin

pkgname=spring-tool-suite
pkgver=3.4.0
_eclipse_pkgver=e4.3.1
_eclipse_pkgver_short=e4.3
pkgrel=1
pkgdesc="The Spring Tool Suite (STS) from SpringSource"
arch=('i686' 'x86_64')
url="http://www.springsource.com/developer/sts"
depends=('java-environment' 'gtk2' 'unzip' 'libwebkit')
conflicts=('springsource-tool-suite')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.1")

source=(
  "http://download.springsource.com/release/STS/${pkgver}/dist/${_eclipse_pkgver_short}/${pkgname}-${pkgver}.RELEASE-${_eclipse_pkgver}-linux-gtk-x86_64.tar.gz"
  "${pkgname}.xpm"
  "${pkgname}.desktop"
)

md5sums=('9bc9c3ece3a72a9eb2903c0bc5c72ca6'
         '947d3e62d478ef537f2f2b79dac6a405'
         '713d62c23b173881bf74050718ef3d3f')      # generate with makepkg -g

[ "$CARCH" = "i686" ] && source[0]="http://download.springsource.com/release/STS/${pkgver}/dist/${_eclipse_pkgver_short}/${pkgname}-${pkgver}.RELEASE-${_eclipse_pkgver}-linux-gtk.tar.gz"
[ "$CARCH" = "i686" ] && md5sums[0]='450a09f27c46897b29da2a9ed0bbec69'

package() {
  cd "${srcdir}/springsource"

  # install eclipse
  install -m755 -d "${pkgdir}/opt"
  mv "${srcdir}/springsource" "${pkgdir}/opt/springsource"

  # install misc
  install -d ${pkgdir}/usr/bin ${pkgdir}/usr/share/applications
  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
  ln -s "/opt/springsource/sts-${pkgver}.RELEASE/STS" ${pkgdir}/usr/bin/STS

  # install icon
  install -Dm644 ${srcdir}/${pkgname}.xpm ${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.xpm
}
