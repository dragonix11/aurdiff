# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>
# Contributor: dserban <dserban01 at yahoo dot com>

pkgname=switchboard
pkgver=1.0.1
pkgrel=2
pkgdesc="Modular desktop settings hub"
arch=('i686' 'x86_64')
url='https://launchpad.net/switchboard'
license=('GPL3')
depends=('granite' 'libunity')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('switchboard-plug-about: About plug'
            'switchboard-plug-default-applications-bzr: Default applications plug'
            'switchboard-plug-elementary-tweaks-bzr: Elementary tweaks plug'
            'switchboard-plug-keyboard-bzr: Keyboard plug'
            'switchboard-plug-pantheon-shell-bzr: Pantheon Shell plug'
            'switchboard-plug-power-bzr: Power plug')
source=("https://launchpad.net/${pkgname}/1.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
