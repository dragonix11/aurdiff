# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=scenechange
pkgname=vapoursynth-plugin-${_plug}
pkgver=0.2.0
pkgrel=2
pkgdesc="Plugin for Vapoursynth: ${_plug}"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=166769"
license=('LGPL2.1')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}-git")
makedepends=('git')
source=("https://dl.dropbox.com/u/6596386/vapoursynth-plugins/${_plug}-${pkgver}-${pkgrel}.7z")
md5sums=('4688a75a2d58bb43e16d8d734327c4f2')
noextract=("${_plug}-${pkgver}-${pkgrel}.7z")

build() {
  cd "${srcdir}"
  rm -fr "${_plug}-build"
  mkdir "${_plug}-build"
  cd "${_plug}-build/"
  bsdtar -xf "${srcdir}/${_plug}-${pkgver}-${pkgrel}.7z"
  cd src

  CFLAGS="-fPIC -Wall -Wshadow -std=gnu99 -I."
  XLDFLAGS="-Wl,-s"
  XCFLAGS="-Os -g0 -ffast-math -fexcess-precision=fast -fomit-frame-pointer"
  LDFLAGS="-shared -fPIC -L."
  gcc -o libscenechange.so $CFLAGS $XCFLAGS $LDFLAGS $XLDFLAGS scenechange.c
  gcc -o libtemporalsoften2.so $CFLAGS $XCFLAGS $LDFLAGS $XLDFLAGS temporalsoften.c
}

package(){
  cd "${srcdir}/${_plug}-build"
  install -Dm755 src/libscenechange.so "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm755 src/libtemporalsoften2.so "${pkgdir}/usr/lib/vapoursynth/libtemporalsoften2.so"
  install -Dm644 readme.txt "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
  install -Dm644 readme_temporalsoften.txt "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README_temporalsoften2"
  install -Dm644 temporalsoften2.py "${pkgdir}/usr/lib/python3.3/site-packages/temporalsoften2.py"
}

