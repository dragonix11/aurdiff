# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd
pkgver=1.11.0
_pkgver=$pkgver
pkgrel=1
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'smbclient'     # for libndr-nbt
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'python2'
  'samba'         # for libndr-nbt headers
)
source=("https://fedorahosted.org/released/$pkgname/$pkgname-$_pkgver.tar.gz"
        "https://fedorahosted.org/released/$pkgname/$pkgname-$_pkgver.tar.gz.asc"
        'sssd.service')
sha1sums=('c52f7a37649a5eda1cd58c0ebe8d42e42bf9528c'
          'SKIP'
          '2d10db3d6f54a58bbf8d1f27328e68555256e0ce')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr                                 \
    --sbindir=/usr/bin                            \
    --sysconfdir=/etc                             \
    --localstatedir=/var                          \
    --libexecdir=/usr/lib/sssd                    \
    --datadir=/usr/share                          \
    --enable-pammoddir=/usr/lib/security          \
    --with-autofs                                 \
    --with-initscript=systemd                     \
    --with-os=fedora                              \
    --with-pid-path=/run                          \
    --with-python-bindings                        \
    --without-selinux                             \
    --without-semanage                            \
    --with-ssh                                    \
    --with-sudo                                   \
    --with-systemdunitdir=/usr/lib/systemd/system \
    ;
  sed -i '/\<HAVE_KRB5_SET_TRACE_CALLBACK\>/d' config.h
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/lib"
  rm -rf "$pkgdir/run"
  rm -f "$pkgdir/usr/lib/ldb/modules/ldb/memberof.la"
  find "$pkgdir/usr" -depth -type d \
    -exec rmdir --ignore-fail-on-non-empty {} \;

  cd "$srcdir"
  install -Dm0644 sssd.service  "$pkgdir/usr/lib/systemd/system/sssd.service"
} 

# vim: ts=2:sw=2:et
