# Maintainer: X0rg

pkgname=gnustep-libobjc2-multilib-clang-svn
_svnname=gnustep-libobjc2
pkgver=37254
pkgrel=2
pkgdesc="The GNUstep Objective-C runtime is designed as a drop-in replacement for the GCC runtime for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('custom')
groups=('gnustep-multilib-clang-svn')
depends=('gcc-libs' 'lib32-gcc-libs')
makedepends=('svn' 'clang' 'cmake' 'gnustep-make-multilib-clang-svn')
provide=('gnustep-libobjc2-clang-svn')
conflicts=('gnustep-libobjc2-git' 'gnustep-libobjc2-clang-svn' 'lib32-gnustep-libobjc2-clang-svn')
options=('!buildflags')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/libobjc2/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Create 'build' directory..."
  mkdir -pv ./build/{32,64}

  msg2 "Delete 'GNUmakefile' file..."
  rm -v ./GNUmakefile
}

build() {
  cd "$_svnname/build/32"
  source "/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"

  msg2 "Run 'cmake' (32-bit)..."
  OBJCFLAGS="-fblocks" ASMFLAGS="-m32" CC="clang -m32" CXX="clang++ -m32" LDFLAGS="-L/usr/share/GNUstep32/lib32 -L/usr/lib32" cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr -DGNUSTEP_CONFIG=/usr/bin/linux_386/gnustep-config

  msg2 "Run 'make' (32-bit)..."
  make

  # 64-bit build
  cd ../64
  source "/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config"

  msg2 "Run 'cmake' (64-bit)..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr -DGNUSTEP_CONFIG=/usr/bin/gnustep-config

  msg2 "Run 'make' (64-bit)..."
  make
}

# check() {
#   cd "$_svnname/build/32"
#   make test
#
#   cd "$_svnname/build/64"
#   make test
# }

package() {
  cd "$_svnname/build/32"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install

  # 64-bit install
  cd ../64
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install

  cd ../..
  msg2 "Install the license..."
  install -v -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
