# Maintainer: Richard Jackson <rj@iinet.net.au>
# Contributor: Frank Vanderham <twelve.eighty (at) gmail.>
# Contributor: Hsin-Yi Chen (hychen) <hycehn (at) canonical.com>
# Contributor: Alberto Milone <alberto.milone (at) canonical.com>
# Contributor: Jasmine Hassan <jasmine.aura (at) gmail.com>
# Contributor: b3niup
# Upstream code branches: https://code.launchpad.net/ubuntu/+source/bcmwl
# https://bugs.launchpad.net/ubuntu/quantal/+source/bcmwl/+bug/923809 for first creation
# Also see: https://bbs.archlinux.org/viewtopic.php?id=145884

pkgname=broadcom-wl-no-dkms
_pkgname=broadcom-wl
pkgver=6.30.223.30
pkgrel=1
pkgdesc="Broadcom 802.11 Linux STA wireless driver BCM43142."
url="https://bbs.archlinux.org/viewtopic.php?id=145884"
arch=('i686' 'x86_64')
license=('custom')
depends=()
makedepends=('linux-headers')
conflicts=()
source=(
	"https://launchpad.net/ubuntu/+archive/primary/+files/bcmwl_${pkgver}+bdcom.orig.tar.gz"
	"https://launchpad.net/ubuntu/+archive/primary/+files/bcmwl_${pkgver}+bdcom-0ubuntu2.diff.gz"
	'modprobe.d'
	'001-Arch-Makefile.patch'
	'002-Module-License.patch'
)
sha1sums=(
	'4a7bd82ad263a4cd407b29088a0c9d7cbd4a674b'
	'15d45d9ce1fdefab8ddb5a66a11c9043b5bf9c25'
	'89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
	'90bc9022532b4769d862dfdca16d1179130111c1'
	'306bccd26b12c808977cacf630860e7d483d213f'
)

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install
_extramodules=extramodules-3.10-ARCH
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
	cd "${srcdir}"

	# apply diff patch to create the debian/ folder
	patch -p1 -i bcmwl_${pkgver}+bdcom-0ubuntu2.diff

	# apply source patches
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0001-MODULE_LICENSE.patch 
	patch -d ./bcmwl-${pkgver}+bdcom -p1 -i ../debian/patches/0002-Makefile.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0003-Make-up-for-missing-init_MUTEX.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0006-add-support-for-linux-3.8.0.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0007-nl80211-move-scan-API-to-wdev.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0008-add-support-for-linux-3.9.0.patch
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../debian/patches/0009-add-support-for-linux-3.10.0.patch

	# patch to replace use of dkpg with uname to detect 64 vs. 32 bit
	patch -p1 -i 001-Arch-Makefile.patch

	# patch for MODULE_LICENSE
	patch -d ./bcmwl-${pkgver}+bdcom/src -p1 -i ../../002-Module-License.patch

	# move folders to create Arch specific folder layout
	mv bcmwl-${pkgver}+bdcom/src/* ./

	# move Makefile to main src/ folder
	mv ./bcmwl-${pkgver}+bdcom/Makefile ./

	# delete the debian folders and files not needed for packaging
	rm -R debian/
	rm -R bcmwl-${pkgver}+bdcom/
	rm bcmwl_${pkgver}+bdcom.orig.tar.gz
	rm bcmwl_${pkgver}+bdcom-0ubuntu2.diff.gz
	rm bcmwl_${pkgver}+bdcom-0ubuntu2.diff

	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`

}

package() {
	cd "${srcdir}"
	
	install -D -m 644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
