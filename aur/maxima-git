# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=maxima-git
pkgver=5.31.base.77.g9c0d3c3
pkgrel=1
pkgdesc="a sophisticated computer algebra system - git-version"
arch=('any')
url="http://maxima.sourceforge.net"
license=('GPL')
depends=('sbcl' 'sh')
makedepends=('git' 'cl-ppcre' 'perl' 'python' 'texinfo')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
provides=('maxima')
conflicts=('maxima')
options=('!makeflags' '!zipman')
install=maxima.install
source=('maxima::git://git.code.sf.net/p/maxima/code' 'maxima.desktop')
md5sums=('SKIP'
         '24aa81126fbb8b726854e5a80d4c2415')
_gitname="maxima"

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed -e 's|[_-]|.|g' | cut -c8- 
}

prepare() {
  cd "$srcdir/$_gitname"
  mv configure.in configure.ac
  cd "$srcdir/$_gitname/doc/info"
  sed -i 's+@table @var+@table @asis+' Bugs.texi
  sed -i 's+@table @var+@table @asis+' Integration.texi
  sed -i 's+@table @var+@table @asis+' lapack.texi
  sed -i 's+@table @var+@table @asis+' Numerical.texi
  sed -i '38,39s+[{}]+ +g' "$srcdir/$_gitname/interfaces/xmaxima/doc/xmaxima.texi"
}

build() {
  cd "$srcdir/$_gitname"
  ./bootstrap
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info  --libexecdir=/usr/lib \
    --enable-sbcl --with-default-lisp=sbcl
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install  
  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/maxima.desktop \
  	${pkgdir}/usr/share/applications/${pkgname}.desktop
  
  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/branch_${pkgver//./_}_dirty/xmaxima/maxima-new.png \
	${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
