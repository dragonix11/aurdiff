# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=blink1-git
pkgver=.r0.ga66f3eb
pkgrel=1
pkgdesc="software for blink(1) USB RGB LED - git checkout"
arch=('i686' 'x86_64')
url="http://rzl.so/wiki/RZLblink"
license=('GPL')
makedepends=('git')
provides=('blink1')
conflicts=('blink1')
source=('git+https://github.com/todbot/blink1.git'
	'blink1-build.patch'
	'blink1-cky.patch')

pkgver() {
	cd blink1/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd blink1/

	# fix build, names, ...
	patch -Np1 < ${srcdir}/blink1-build.patch

	# add cyan, magenta & yellow
	patch -Np1 < ${srcdir}/blink1-cky.patch
}

build() {
	cd blink1/commandline/

	make
}

package() {
	cd blink1/commandline/

	install -D -m0755 libblink1.so ${pkgdir}/usr/lib/libblink1.so
	install -D -m0755 blink1-tool ${pkgdir}/usr/bin/blink1-tool
}

sha256sums=('SKIP'
            'fabf6cf7ab6d48dd05aaa1a9896c4cc5f01d776289aca14bd19d055439b37397'
            '38247b434291dc1ff06c0944072d19bda83c00b8016f134a8eb9c195b7f8808e')
