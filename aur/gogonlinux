pkgname=gogonlinux
pkgver=0.2.6
pkgrel=1
pkgdesc="Client for GOG games on Linux"
arch=('any')
url="http://gogonlinux.com/"
license=('BSD')
source=("http://www.gogonlinux.com/releases/${pkgname}-${pkgver}.tar.gz")
depends=('innoextract'
         'libglade'
         'python2'
         'python2-certifi'
         'python2-oauth2'
         'winetricks')
optdepends=('wine: For wine emulation'
           'scummvm: For lucasarts SCUMM virtualisation'
           'dosbox: For DOS emulation')
sha1sums=('01a10f77b9c8a90945572e40cd84dff7fa1aa7b6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/python2|" {setup.py,gog-installer,gog-tux}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  # setup.py installs to /usr/local by default, so we need to move the files
  mv "${pkgdir}/usr/local/share" "${pkgdir}/usr"
  mv "${pkgdir}/usr/local/man" "${pkgdir}/usr/share"
  rmdir "${pkgdir}/usr/local"

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
