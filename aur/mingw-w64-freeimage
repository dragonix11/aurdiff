# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
# Contributor: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=mingw-w64-freeimage
pkgver=3.15.4
pkgrel=4
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw-w64)."
arch=(any)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('mingw-w64-gcc')
options=(!strip !buildflags staticlibs)
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip" 'mingw64.patch')
md5sums=('9f9a3b2c3c1b4fd24fe479e8aaee86b1'
         '1482fbf6046cf25feaba5df7c61dd509')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}

  cd FreeImage
  patch -Np0 < ${srcdir}/mingw64.patch
  sed -i "s|IMPORTLIB = \$(TARGET).lib|IMPORTLIB = \$(TARGET).dll.a|g" Makefile.mingw
  cd ..

  for _arch in ${_architectures}; do
    rm -rf FreeImage-${_arch}
    cp -r FreeImage FreeImage-${_arch}
    pushd FreeImage-${_arch}
  
  make \
    CC=${_arch}-gcc \
    CXX=${_arch}-g++ \
    LD=${_arch}-g++ \
    RC=${_arch}-windres \
    AR=${_arch}-ar \
    DLLTOOL=${_arch}-dlltool \
    FREEIMAGE_LIBRARY_TYPE="STATIC" \
    -f Makefile.mingw
  
  make \
    CC=${_arch}-gcc \
    CXX=${_arch}-g++ \
    LD=${_arch}-g++ \
    RC=${_arch}-windres \
    AR=${_arch}-ar \
    DLLTOOL=${_arch}-dlltool \
    FREEIMAGE_LIBRARY_TYPE="SHARED" \
    -f Makefile.mingw

    popd
  done  

}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/FreeImage-${_arch}
    install -d "${pkgdir}"/usr/${_arch}/lib
    install -d "${pkgdir}"/usr/${_arch}/bin
    install -d "${pkgdir}"/usr/${_arch}/include
    install -m755 Dist/FreeImage.dll "${pkgdir}"/usr/${_arch}/bin/
    install -m644 Dist/FreeImage.dll.a "${pkgdir}"/usr/${_arch}/lib/
    install -m644 Dist/libFreeImage.a "${pkgdir}"/usr/${_arch}/lib/
    install -m644 Dist/FreeImage.h   "${pkgdir}"/usr/${_arch}/include/
       
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "${pkgdir}"/usr/${_arch}/lib/*.a
  done
}
