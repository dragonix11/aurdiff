# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: St√©phane Marguet (Stemp) <smarguet@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=eclipse-gef
pkgver=3.9.1
_reldate=201308190730
pkgrel=1
pkgdesc="GEF framework for the Eclipse platform"
arch=(any)
url=http://www.eclipse.org/gef/
license=(EPL)
depends=(eclipse)
changelog=ChangeLog
_mirror="http://www.eclipse.org/downloads/download.php?r=1&file="
source=($_mirror/tools/gef/downloads/drops/$pkgver/R$_reldate/GEF-ALL-$pkgver.zip)
sha256sums=('247f24aab10ac7c8484b9f8ce2d9c7a34b1f04531e33793124189ae2a53175d8')
sha512sums=('9ecb90d9935e2554654a5c9c140da8eaa054dbe9326abba0ee49f30a625d3507e30f00f02ca769344c9281ab74e8cf50500cde8ed1c937e8792ca94b6679b566')

package() {
    _dest="$pkgdir"/usr/share/eclipse/dropins/${pkgname#*-}/eclipse
    
    cd eclipse/
    # Features
    find features -type f | while read _feature ; do
        if [[ $_feature =~ (.*\.jar$) ]] ; then
            install -d "${_dest}"/${_feature%*.jar}
            cd "$_dest"/${_feature/.jar}
            jar xf "${srcdir}/${_feature}"
        else
            install -Dm644 $_feature "$_dest/$_feature"
        fi
    done

    # Plugins
    find plugins -type f -exec install -Dm644 '{}' "$_dest"/'{}' \;
}
