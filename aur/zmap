# Maintainer: Jens Adam <jra@byte.cx>

pkgname=zmap
pkgver=1.0.0
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64')
depends=('gmp' 'libpcap')
makedepends=('gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz")
sha256sums=('0b9d0b9fddf5ab59525aa85262359a750ea48f58fc3662075758f8510f4a2387')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}/src
  sed -i 's/ -Werror$//' Makefile
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/src
  install -D zmap "${pkgdir}"/usr/bin/zmap
  install -Dm644 zmap.1 "${pkgdir}"/usr/share/man/man1/zmap.1
  install -d "${pkgdir}"/etc/zmap
  install -m644 ../conf/* "${pkgdir}"/etc/zmap
}
