# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_gitname=libfm
pkgname="$_gitname"-gtk3-git
pkgver=20131016
pkgrel=1
pkgdesc="A glib/gio-based library to develop file managers. Does not include the GTK components."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('menu-cache')
optdepends=('libexif')
makedepends=('git' 'libexif' 'vala' 'gtk-doc' 'intltool')
provides=('libfm')
conflicts=('libfm')
source=("git://github.com/lxde/libfm.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --with-gtk=3 --enable-actions --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
