# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>

pkgname=bd-git
_gitname=bd
pkgver=20130901
pkgrel=1
pkgdesc="Quickly go back to a specific directory in linux instead of typing \"cd ../../..\" repeatedly"
arch=('any')
url="https://github.com/vigneshwaranr/bd"
license=('unknown')
makedepends=('git')
source=('git://github.com/vigneshwaranr/bd.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
  cd "$srcdir/$_gitname"

  install -D -m 755 bd "$pkgdir/usr/bin/bd"
  install -D -m 644 README.md "$pkgdir/usr/share/doc/bd/README.md"
}

# vim:set ts=2 sw=2 et:
