# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=awish-git
_gitname=awish
pkgver=344.21638e4
pkgrel=3
pkgdesc='Rewrite of the old MS-DOS game Avish (GIT-version)'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/awish.git'
license=('GPL3')
depends=('sdl_mixer' 'sh' 'awish-data')
makedepends=('git')
provides=('awish')
conflicts=('awish')

source=("${_gitname}.sh"
        "${_gitname}.desktop"
        'git+git://repo.or.cz/awish.git')

md5sums=('ce4ab7ae32e1c968157943a9a772e513'
         '8673f646ccdca6ef8caf2ae9e3a2dcda'
         'SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${srcdir}/${_gitname}
  chmod +x 0build.sh
  ./0build.sh
}

package() {
  cd "${srcdir}/${_gitname}"

  # main files
  install -dm755 "${pkgdir}/usr/share/${_gitname}/"

  install -m755 "${_gitname}" "${pkgdir}/usr/share/${_gitname}"
  cp -r data "${pkgdir}/usr/share/${_gitname}/"

  # readme
  install -D -m644 "${srcdir}/${_gitname}/README" "${pkgdir}/usr/share/doc/${_gitname}/readme-src"

  # bash script
  install -D -m755 "${srcdir}/${_gitname}.sh" "${pkgdir}/usr/bin/${_gitname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${_gitname}.desktop" "${pkgdir}/usr/share/applications/${_gitname}.desktop"
}

# vim:set ts=2 sw=2 et:
