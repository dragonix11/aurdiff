# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-applets-applicationname
pkgver=1.9
pkgrel=1
pkgdesc="Shows the application name of the focused window"
arch=('any')
url="http://kde-apps.org/content/show.php?content=154324"
license=('GPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
replaces=('kdeplasma-applets-windowtitle')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/andreascarpino/applicationname-plasmoid/tarball/${pkgver}")
md5sums=('8ab6de3a1a074253a85ab1826ff5edd3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../andreascarpino-applicationname-plasmoid-66eef5a \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
