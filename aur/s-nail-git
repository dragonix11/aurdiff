# Maintainer: Gavin Troy <gavtroy@gmail.com>
# Based on heirloom-mailx PKGBUILD

pkgname=s-nail-git
pkgver=latest
pkgrel=4
pkgdesc="A commandline utility for sending and receiving email. Fork of Heirloom mailx"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/s-nail/"
license=('custom:BSD')
depends=('libidn' 'openssl')
makedepends=('git')
optdepends=('smtp-forwarder: for sending mail')
provides=('mailx' 'heirloom-mailx')
conflicts=('mailx' 'heirloom-mailx')
backup=('etc/mail.rc')
_gitname='s-nail'
source=("${_gitname}::git://git.code.sf.net/p/s-nail/code")
sha1sums=('SKIP')

pkgver() {
	cd $_gitname
	# use "c" as commit count separator to prevent pacman thinking pkg is a
	# downgrade when we go from XX.X.X to XX.X (pity this looks ugly...)
	echo "`git describe --tags --abbrev=0 | cut -c 2-`c`git rev-list --count HEAD`"
}

build() {
	cd $_gitname
	make PREFIX=/usr \
	     SYSCONFDIR=/etc \
	     MANDIR=/usr/share/man \
	     SYSCONFRC=/etc/mail.rc \
	     MAILSPOOL=/var/spool/mail \
	     CFLAGS="${CFLAGS} ${CPPFLAGS}" \
	     LDFLAGS="${LDFLAGS}" \
	     DESTDIR="${pkgdir}" \
		 all
}

package() {
	cd $_gitname
	make packager-install

	ln -sf s-nail "${pkgdir}"/usr/bin/mail
	ln -sf s-nail "${pkgdir}"/usr/bin/mailx
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
