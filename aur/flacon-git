# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=flacon-git
pkgver=0.9.2.20131030.5e47576
pkgrel=1
pkgdesc="Extracts individual tracks from one big audio file containing the entire album of music and saves them as separate audio files. (Git Version)"
arch=('x86_64' 'i686')
url="https://github.com/SokoloffA/flacon"
license=('LGPL2.1')
depends=('qt4' 'uchardet')
makedepends=('cmake' 'git')
optdepends=('flac: For FLAC support'
            'vorbis-tools: For OGG support'
            'mac: For APE support'
            'wavpack: For WavPack support'
            'ttaenc: For TrueAudio support'
            'lame: For MP3 support'
            'shntool: for others format support'
            'mp3gain: For MP3 Replay Gain support'
            'vorbisgain: For OGG Replay Gain support')
conflicts=('flacon')
provides=('flacon')
install=flacon.install
source=('git://github.com/flacon/flacon.git')
md5sums=('SKIP')
_gitname=flacon

pkgver() {
  cd "${_gitname}"
  _major_v="$(cat CMakeLists.txt | grep -num1 MAJOR_VERSION | tr -d ')' | cut -d ' ' -f2)"
  _minor_v="$(cat CMakeLists.txt | grep -num1 MINOR_VERSION | tr -d ')' | cut -d ' ' -f2)"
  _patch_v="$(cat CMakeLists.txt | grep -num1 PATCH_VERSION | tr -d ')' | cut -d ' ' -f2)"
  echo "$(echo -n ${_major_v}.${_minor_v}.${_patch_v}).$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  # tmpdir not found patch
  sed '/setDefaultValue(Encoder_TmpDir,/c\    setDefaultValue(Encoder_TmpDir, "/tmp");' -i settings.cpp

  # capitalise Music folder patch
  sed '/setDefaultValue(OutFiles_Directory,/c\    setDefaultValue(OutFiles_Directory, "~/Music");' -i settings.cpp
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
