# This is gross, but can totally save you if you need to fix a machine
# on which you can't easily boot install media.

pkgname=pacman-static
pkgver=4.1.2
pkgrel=2
attr_ver=2.4.47
assuan_ver=2.1.1
gpgerr_ver=1.12
gpgme_ver=1.4.3
pkgdesc="Statically-compiled pacman (for fixing systems with corrupt libc)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('gnupg>=2')
makedepends=('glibc>=2.17-2' 'acl>=2.2.51-3' 'libarchive>=3.1.2'
             'curl>=7.19.4' 'pth')
source=(
  ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig}
  http://download.savannah.gnu.org/releases/attr/attr-$attr_ver.src.tar.gz{,.sig}
  ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${gpgerr_ver}.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/libassuan/libassuan-$assuan_ver.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/gpgme/gpgme-${gpgme_ver}.tar.bz2{,.sig}
)
sha256sums=('7c059b444735ee254cfbd8dc2fac8c5d61453601f8adaf001c7038cdc673334c'
            'SKIP'
            '25772f653ac5b2e3ceeb89df50e4688891e21f723c460636548971652af0a859'
            'SKIP'
            'cafc9ed6a87c53a35175d5a1220a96ca386696eef2fa059cc0306211f246e55f'
            '23e2d67779b88e90d29fe1df6b157109f1c2a647d0f1b2a0f4295bb3c0b2039d'
            '2d1cc12411753752d9c5b9037e6fd3fd363517af720154768cc7b46b60120496'
            'SKIP')

build() {
   cd $srcdir/attr-$attr_ver
   ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
   make
   make install-dev install-lib DESTDIR="$srcdir/overlay"

   cd $srcdir/libassuan-$assuan_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/libgpg-error-$gpgerr_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/gpgme-$gpgme_ver
   ./configure --prefix=/usr --disable-shared
   make install DESTDIR="$srcdir/overlay"
   
   cd $srcdir/pacman-$pkgver
   LDFLAGS="-static -L$srcdir/overlay/usr/lib" \
     LIBS="-lz -lgpg-error -lassuan -lssl -lcrypto -lssh2 -ldl \
     	       -lstdc++ -lpthread" \
     CPPFLAGS="-I$srcdir/overlay/usr/include" \
     ./configure --disable-shared --disable-doc \
       --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
       --with-scriptlet-shell=/usr/bin/bash \
       --with-ldconfig=/usr/bin/ldconfig

   make AM_LDFLAGS=-all-static
}

package() {
  install -sD $srcdir/pacman-$pkgver/src/pacman/pacman \
  	  $pkgdir/usr/bin/pacman-static
}
