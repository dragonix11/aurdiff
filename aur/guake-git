# Maintainer: Miguel Useche <migueluseche@skatox.com>
# Contributor: Anton Leontiev <unsector /at/ km.ru>
# Contributor: Sandy Carter <bwrsandman /at/ gmail.com>

pkgname=guake-git
pkgver=0.4.4.13.gdc71f60
pkgrel=1
pkgdesc="Top-down terminal for Gnome"
arch=('i686' 'x86_64' 'armv7h')
url="http://guake.org/"
license=('GPL')
depends=('python2' 'pygtk' 'gnome-python' 'vte' 'python2-notify' 'python2-dbus' 'python2-xdg' 'pkgconfig' 'gtk2' 'glib2')
makedepends=('git' 'intltool' 'gnome-common')
conflicts=('guake')
provides=('guake')
source=("$pkgname"::'git://gitorious.org/guake/guake.git' 'guake-git.patch')
sha512sums=('SKIP' 'b7094dd78a4bf92747c4b468d91c7d63d6d86aa7a411e9e4cb1c26b77f14169c7fabfa31b88e5078f1a751c2d163fd6b9fce9b590bb89aaa3d1e5b7d48588157')
install=guake.install

_gitroot="git://gitorious.org/guake/guake.git"

pkgver() {
  cd $pkgname
  local ver="$(git describe --tags)"
  echo "${ver//-/.}"
}

prepare() {
  cd $pkgname
  patch -Np1 -i $srcdir/$pkgname.patch
}

build(){
  cd $pkgname
  PYTHON="`which python2`" ./autogen.sh --prefix=/usr --sysconfdir=/usr/share --disable-static 
  make
}

package() {
  cd $pkgname
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
}
