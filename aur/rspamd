# Maintainer: ushi <ushi+arch@honkgong.info>
pkgname=rspamd
pkgver=0.5.6
pkgrel=1
epoch=
pkgdesc="Fast, free and open-source spam filtering system."
arch=('x86_64' 'i686' 'mips64el')
url="http://rspamd.com"
license=('BSD')
depends=('libevent' 'glib2' 'gmime' 'lua51')
makedepends=('cmake')
backup=('etc/rspamd.xml')
install=rspamd.install

source=("https://bitbucket.org/vstakhov/rspamd/downloads/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.conf"
        "${pkgname}.service")

sha256sums=('95e6337ec568f017ed1037c7de39ded82f642c3f2c82d0c10bc69e2be48f6ae4'
            '0e0de24f8674c5530dc8ae0b12ed114b8a6535020351ee1c3ef96725c906653c'
            'cbf61643923ec1d1db173d43379cdb9259e58dceb9e91dff255d10098a9b7062')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

  cmake                                           \
    -D CMAKE_INSTALL_PREFIX:PATH='/usr'           \
    -D ETC_PREFIX:PATH='/etc'                     \
    -D MAN_PREFIX:PATH='/usr/share/man'           \
    -D LOCALSTATES_PREFIX:PATH='/var/lib/rspamd'  \
    -D LIBDIR:PATH='/usr/lib'                     \
    -D INCLUDEDIR:PATH='/usr/include'             \
    -D LUA_INCLUDE_DIR:PATH='/usr/include/lua5.1' \
    -D LUA_LIBRARY:PATH='/usr/lib/liblua.so.5.1'  \
    -D RSPAMD_USER:STRING='rspamd'                \
    -D RSPAMD_GROUP:STRING='rspamd'               \
    .

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" install

  # Remove init script.
  rm -r "${pkgdir}/etc/init.d"

  # Use sample config.
  mv "${pkgdir}/etc/rspamd.xml.sample" "${pkgdir}/etc/rspamd.xml"

  # Use /run/rspamd/rspamd.pid as pidfile.
  sed -i 's/\(<pidfile>\).*\(<\/pidfile>\)/\1\/run\/rspamd\/rspamd.pid\2/' "${pkgdir}/etc/rspamd.xml"

  # Install the LICENSE.
  install -Dm0644 'LICENSE' "${pkgdir}/usr/shares/${pkgname}/LICENSE"

  # Install systemd files.
  cd ..
  install -Dm0644 "${pkgname}.conf"    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm0644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
