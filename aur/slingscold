# Maintainer: Diego <cdprincipe@at@gmail@dot@com>

pkgname=slingscold
pkgver=1.2.1
pkgrel=1
pkgdesc="A lighter launcher"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/slingscold/"
license=('GPL3')
depends=('libunique' 'libgee06' 'gnome-menus2' 'libwnck')
makedepends=('cmake' 'vala')
source=("http://downloads.sourceforge.net/project/$pkgname/${pkgname}_${pkgver}.tar.gz"
        '0001-Add-missing-lm-dep.patch')
md5sums=('62bb429cf8c2189e429897c2e820871d'
         '67182b4b7d23792e46c698d518116bdc')

prepare() {
  cd "$srcdir"
  rm -rf build && mkdir build

  cd "$srcdir/${pkgname}_${pkgver}"
  patch -p1 < "$srcdir"/0001-Add-missing-lm-dep.patch
}

build() {
  cd "$srcdir"/build
  cmake ../"${pkgname}_${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}
