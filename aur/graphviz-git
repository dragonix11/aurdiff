# Maintainer: Techlive Zheng <techlivezhen at gmail dot com>
# Contributor: Mauro Fruet <maurofruet@gmail.com>

_pkgname=graphviz
pkgname=graphviz-git
pkgdesc="Graph visualization software"
pkgver=8707.d83888d
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.graphviz.org/'
license=('CPL')
options=('!libtool')
install=graphviz-git.install
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'ruby: ruby bindings'
            'tcl: tcl bindings'
            'qt4: gvedit')
makedepends=('git' 'swig' 'mono' 'guile' 'lua51' 'ocaml' 'perl' 'php' 'python2' 'r' 'ruby' 'tk' 'qt4')
provides=('graphviz')
conflicts=('graphviz')
source=("git+https://github.com/ellson/graphviz.git")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"

    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_pkgname}"

  export PYTHON=python2
  export LUA=lua5.1

  ./autogen.sh

  sed \
      -e '/LIBPOSTFIX="64"/d' \
      -i configure

  ./configure --prefix=/usr

  make
}

package() {
  cd "${_pkgname}"

  make DESTDIR="${pkgdir}" install
}
