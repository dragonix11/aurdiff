# Maintainer: Eric Le Lay <cotcotpot at free dot fr>
_author=njh
_perlmod=MusicBrainz-DiscID
pkgname=perl-musicbrainz-discid
pkgver=0.03
pkgrel=2
pkgdesc="MusicBrainz::DiscID - Perl interface for the MusicBrainz libdiscid library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~$_author/$_perlmod-$pkgver/"
license=('GPL')
groups=()
depends=('perl>=5.10.0' 'libdiscid')
makedepends=('pkg-config')
provides=()
conflicts=()
replaces=()
backup=()
options=(!emptydirs)
install=
source=(http://cpan.perl.org/modules/by-authors/id/N/NJ/NJH/$_perlmod-$pkgver.tar.gz \
        pod-encoding.patch \
        new-musicbrainz-urls.patch
       )
md5sums=('4b775f38604f15fd20298d5b443d6900' \
         '03e9659f0e1a2923fb03b5aeb8d8ee53' \
         '4c38e6e6f20def002f6c4624b5604445')

prepare() {
  cd "$srcdir/$_perlmod-$pkgver"
  # fix a runny apostrophe,
  # see https://rt.cpan.org/Public/Bug/Display.html?id=85212
  chmod u+w lib/MusicBrainz/DiscID.pm
  patch -p1 -i "$srcdir/pod-encoding.patch" 
  # use new url in test,
  # see https://rt.cpan.org/Public/Bug/Display.html?id=89285
  chmod u+w t/10discid.t
  patch -p1 -i "$srcdir/new-musicbrainz-urls.patch" 
}
build() {
  cd "$srcdir/$_perlmod-$pkgver"
  
  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  { /usr/bin/perl Build.PL &&
    ./Build &&
    ./Build test; } || return 1
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  
  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  { ./Build install; } || return 1
}
# vim:set ts=2 sw=2 et:
