# An example PKGBUILD script for Civetweb upstream, git version
# Rename to PKGBUILD to build via makepkg
_pkgname=civetweb
pkgname=$_pkgname-git
pkgver=v1.4.15.g0022615
pkgrel=1
pkgdesc="Small and quick-to-use web server; https/php/cgi support; MIT license - git development version"
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/civetweb/"
license=('MIT')
groups=()
depends=()
makedepends=('git')
optdepends=('php-cgi: for php support')
provides=("$_pkgname")
conflicts=("$_pkgname")
backup=("etc/$_pkgname/$_pkgname.conf")
source=("$_pkgname::git+https://github.com/sunsetbrew/civetweb.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  make build WITH_IPV6=1
}

package() {
  install -Dm755 "$srcdir/$_pkgname/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 "$srcdir/$_pkgname/distribution/arch/$_pkgname.conf" "$pkgdir/etc/$_pkgname/$_pkgname.conf"
  install -Dm644 "$srcdir/$_pkgname/distribution/arch/$_pkgname.service" "$pkgdir/usr/lib/systemd/system/$_pkgname.service"
  install -d "$pkgdir/usr/share/$_pkgname"
  install -m644 "$srcdir/$_pkgname/docs/UserManual.md" "$srcdir/$_pkgname/README.md" "$pkgdir/usr/share/$_pkgname"
}

# vim:set ts=2 sw=2 et:
