# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=fmtconv
pkgname=vapoursynth-plugin-${_plug}
pkgver=r6
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug}"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=166504"
license=('custom')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}-git")
source=("http://ldesoras.free.fr/src/vs/${_plug}-${pkgver}.zip")
md5sums=('532fd666b5636baa04f8a5796e33dee6')

build() {
  cd "${srcdir}"
  rm -fr "${_plug}-build"
  cp -R src "${_plug}-build"
  cd "${_plug}-build"

  echo "all:
	  g++ -shared -msse2 -fPIC -o libfmtconv.so AvstpWrapper.cpp main.cpp fmtc/*.cpp fstb/*.cpp vsutl/*.cpp -I." > Makefile
  make
}

package(){
  cd "${srcdir}/${_plug}-build"
  install -Dm755 "lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -d "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/"
  install -m644 ../doc/*.{png,html,css} "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/"
  install -Dm644 ../doc/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

