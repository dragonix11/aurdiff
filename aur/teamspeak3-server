# Maintainer: Malte Rabenseifner <mail@malte-rabenseifner.de>
# Adopted by: EnteEnteEnte <ducksource@duckpond.ch>
# vim:set ts=2 sw=2 et:

pkgname="teamspeak3-server"
pkgver="3.0.10"
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
arch=('i686' 'x86_64')

if [ "${CARCH}" == "x86_64" ]; then
  _TSARCH='amd64'
  _TSMD5='e2cac6c1d524b906bf72b14004b816df'
else
  _TSARCH='x86'
  _TSMD5='1c5c45783bdaf5f9f5184dd9e5f203b9'
fi

url="http://www.teamspeak.com/"
license=('custom')
depends=('bash')
backup=('etc/conf.d/teamspeak3-server')
install="${pkgname}.install"
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/${pkgname}_linux-${_TSARCH}-${pkgver}.tar.gz"
        'teamspeak3-server.conf.d'
        'teamspeak3-server.service')
md5sums=(${_TSMD5}
         '0c96de1dcdda230f5280aa30b5bb8084'
         'da988b166ed1e8dd2f230de25c6801ee')

package() {
    cd "${srcdir}"
    
    install -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -d "${pkgdir}/usr/share/${pkgname}/sql"
    install -dm 0750 "${pkgdir}/var/lib/${pkgname}"
    install -dm 0750 "${pkgdir}/var/log/${pkgname}"
    
    install -Dm 644 "${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"
    install -Dm 644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_mysql.so" "${pkgdir}/usr/lib/libts3db_mysql.so"
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_sqlite3.so" "${pkgdir}/usr/lib/libts3db_sqlite3.so"
    install -Dm 755 "${pkgname}_linux-${_TSARCH}/ts3server_linux_${_TSARCH}" "${pkgdir}/usr/bin/${pkgname}"

    cp -a "${pkgname}_linux-${_TSARCH}/sql/"* "${pkgdir}/usr/share/${pkgname}/sql"
    cp -a "${pkgname}_linux-${_TSARCH}/doc/"* "${pkgdir}/usr/share/doc/${pkgname}"

    install -Dm 644 "${pkgname}_linux-${_TSARCH}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

