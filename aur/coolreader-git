# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>
# Contributor: Franz Rogar <franzrogar@gmail.com>

pkgname=coolreader-git
pkgver=3804.6bf5359
pkgrel=2
pkgdesc="E-Book viewer for comfort reading (epub (non-DRM), fb2, txt, rtf, html, chm, tcr formats supported) (GIT-version)"
arch=('i686' 'x86_64')
url="http://www.coolreader.org/e-index.htm"
license=('GPL2')
depends=('qt4')
# 'zlib' 'libpng' 'libjpeg' 'freetype2' depends on 'qt'
makedepends=('cmake' 'git')
conflicts=('cr3-qt4' 'coolreader3' 'coolreader3-git' 'coolreader')
replaces=('cr3-qt4' 'coolreader3' 'coolreader3-git' 'coolreader')
install="${pkgname}.install"
source=('git+git://git.code.sf.net/p/crengine/crengine')
md5sums=('SKIP')

_gitname='crengine'

pkgver() {
  cd ${srcdir}/${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_gitname}"

  mkdir -p qtbuild
  cd qtbuild
  cmake -D GUI=QT \
        -D QT_QMAKE_EXECUTABLE=qmake-qt4 \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/${_gitname}/qtbuild"
  make install DESTDIR="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
