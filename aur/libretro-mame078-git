# Maintainer: almostalive <almostalive2003 at gmail dot com>

pkgname=libretro-mame078-git
_gitname=mame2003-libretro
pkgver=113.d980525
pkgrel=1
pkgdesc="libretro implementation of MAME078. (Arcade)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mame2003-libretro"
license=('custom:MAME License')
makedepends=('git')
source=("${_gitname}::git://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/docs/mame.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/mame078_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mame078.so"
}