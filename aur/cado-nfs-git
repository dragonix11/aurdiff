# Maintainer: Jérémie Detrey <Jeremie.Detrey@loria.fr>
pkgname=cado-nfs-git
pkgver=20130920.d63cfab
pkgrel=1
pkgdesc="Implementation of the Number Field Sieve (NFS) algorithm for factoring integers"
arch=('i686' 'x86_64')
url="http://cado-nfs.gforge.inria.fr/"
license=('LGPL2')
depends=('gmp' 'perl')
makedepends=('git' 'cmake')
source=("$pkgname"::'git://scm.gforge.inria.fr/cado-nfs/cado-nfs.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git log -1 --format="%cd.%h" --date=short | sed 's/-//g'
}

build() {
  cd "$pkgname"
  make PREFIX="/usr/libexec/cado-nfs"
  sed -i -e 's/^cado_source_dir=.*$/cado_source_dir="@CADO_NFS_SOURCE_DIR@"/;
             s/^cado_build_dir=.*$/cado_build_dir="@CADO_NFS_BINARY_DIR@"/' \
    "build/`hostname`/factor.sh"
}

package() {
  cd "$pkgname"
  make PREFIX="/usr/libexec/cado-nfs" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
