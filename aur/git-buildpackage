# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=git-buildpackage
pkgver=0.6.3
_tagname="debian/$pkgver"
pkgrel=1
pkgdesc="Tools from Debian to integrate the package build system with Git"
arch=(any)
url="https://honk.sigxcpu.org/piki/projects/git-buildpackage/"
license=('GPL')
depends=('git'
         'man-db'
         'python2'
         'rpm-org')
makedepends=('python2-distribute')
source=(git://honk.sigxcpu.org/git/git-buildpackage.git)
sha256sums=('SKIP')

pkgver() {
  cd git-buildpackage/
  git checkout $_tagname
  git describe | sed 's;debian/\(.*\);\1;'
}

build() {
  cd git-buildpackage/
  git checkout $_tagname
  export WITHOUT_NOSETESTS=1
  python2 setup.py build
}

package() {
  cd git-buildpackage/
  export WITHOUT_NOSETESTS=1
  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1
}

# vim:set ts=2 sw=2 et:
