# Maintainer: jdarch <jda.cloud plus archlinux at gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=arpack-mkl
_pkgrealname=arpack
pkgver=3.1.3
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Fortran77 subroutines designed to solve large scale eigenvalue problems, BLAS&LAPACK from Intel MKL'
url='http://forge.scilab.org/index.php/p/arpack-ng/'
license=('BSD')
depends=('intel-mkl' 'openmpi')
provides=('arpack-ng')
provides=('arpack')
conflicts=('arpack')
options=('!libtool')
source=("http://forge.scilab.org/upload/${_pkgrealname}-ng/files/${_pkgrealname}-ng-$pkgver.tar.gz")
md5sums=('90ba42eae70b517ebdb72921cb4186df')
sha512sums=('30bee66edc31d1144d029a4c6c4b03f08782bdcbe1b9627e0c3df8d142121111e9c9dd875158345918931815341a2338cd889a010939016509a7cfdd1e829426')

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _intel_lib=mkl_gf_lp64
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _intel_lib=mkl_gf
fi

build() {
  MKL_LIB_PATH=/opt/intel/mkl/lib/${_intel_arch}
  MKL_INCLUDE_PATH=/opt/intel/mkl/include
  export LD_LIBRARY_PATH=${MKL_LIB_PATH}:${LD_LIBRARY_PATH}
  MKL="-I${MKL_INCLUDE_PATH} -L${MKL_LIB_PATH} -l${_intel_lib} -lmkl_sequential -lmkl_core -lpthread -lm"

  cd "$srcdir/${_pkgrealname}-ng-$pkgver"

  ./configure --prefix=/usr --enable-mpi \
  --with-blas="${MKL}" \
  --with-lapack="${MKL}"

  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd "$srcdir/${_pkgrealname}-ng-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
