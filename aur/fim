# Maintainer: Alexej Magura <agm2819*gmail*>
# Contributor: Dragonlord <dragonlord[at]seznam[.]cz>
# Contributor: daniel <quite a hack org>

pkgname=fim
_pkgname='rc1'
pkgver=0.4
pkgrel=7
pkgdesc="Highly customizable and scriptable framebuffer image viewer based on fbi."
arch=('i686' 'x86_64')
conflicts=('fim-svn')
url="https://www.autistici.org/dezperado/fim/"
license=('GPL')

# I have tentatively removed the 'bison' and 'flex' deps since namcap indicates
# that they are not needed.

# I have also removed readline since it is required by bash. I also removed
# libtiff as a dep, since it is required by djvulibre, which is required by
# fim.

# namcap also listed libspectre as required but not included. not sure what
# provides libpng that is already included as a dep, but namcap said that it
# was already satisfied.

# I'm about to add sdl to the list of deps, per namcap.
depends=('libungif' 'libjpeg-turbo' 'djvulibre' 'libspectre' 'sdl')

# It doesn't like to be parallell built
options=(!makeflags)

optdepends=('screen')
install=install
source=("http://download.savannah.gnu.org/releases/fbi-improved/$pkgname-$pkgver-${_pkgname}.tar.bz2")
md5sums=('4aea815884ba98a3d2efe7263351b848')

prepare() {
  cd "$srcdir/$pkgname-$pkgver-$_pkgname"
  ./configure \
   --prefix=/usr \
   --disable-debug \
   --enable-screen \
   --enable-unicode \
   --enable-pdf \
   --enable-hardcoded-font \
   --enable-recursive-dirs
}

build() {
  cd "$srcdir/$pkgname-$pkgver-$_pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-$_pkgname"
  make DESTDIR=$pkgdir install
}
