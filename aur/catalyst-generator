# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Shen Miren <dickeny@gmail.com> (catalyst_build_module idea and important code)
# Great Contributor: B. M. Kwapinski <lord.xml@web.de> (makepkg idea)
# Big Contributor: Mikkel Oscar Lyderik <mikkeloscar@gmail.com> (autocompletion for bash and zsh)
# Contributor: aidanlinz
# Contributor: Rip-Rip
# Contributor: nob
# Contributor: Krzysztof Kolasa (patches to fix ttys bug)
# Contributor: lano1106 (patch to fix support of intel's iommu and to remove dependency of kernel's CONFIG_AGP option)
# Contributor: Emil Karlson from gentoo (3.10 kernel patch)
# Contributor: Krzysztof Kolasa (GPL\0 workaround for 3.12 kernel)

pkgname=catalyst-generator
pkgver=13.4
pkgrel=16
_amdver=12.104
pkgdesc="AMD/ATI drivers. Generator of catalyst-{kernver} packages with fglrx module inside. Now with autocompletion"
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
options=('staticlibs' 'libtool' '!strip' '!upx')
depends=('catalyst-utils' 'gcc-libs' 'gcc>4.0.0' 'make' 'patch')
optdepends=('linux-headers: to build the fglrx module for the linux kernel'
	  'linux-lts-headers: to build the fglrx module for the linux-lts kernel')
conflicts=('catalyst-test' 'catalyst-hook' 'catalyst' 'catalyst-daemon' 'catalyst-dkms')
provides=("catalyst=${pkgver}")
install=${pkgname}.install

source=(
    http://www2.ati.com/drivers/linux/amd-catalyst-${pkgver}-linux-x86.x86_64.zip
    catalyst_build_module
    catalyst-PKGBUILD
    catalyst.install
    ati_make.sh
    makefile_compat.patch
    completion-bash
    completion-zsh
    gentoo_linux-3.10-proc.diff
    kolasa_fglrx_fixing_ttys.patch
    lano1106_fglrx_intel_iommu.patch
    lano1106_kcl_agp_13_4.patch
    kolasa-fglrx_3.12_kernel.patch)

md5sums=('558bd1b31173b06b25da647a7112e734'
         '543e818f543f2897beca49101226e460'
	 'a555dc9a3eb062d7ca4efbf7e0713a3a'
	 'd16871c840b749dcf6d6f5ab85940f6a'
	 'fd98b7e486d7fd4cad8de7b95b5b031e'
	 '3e1b82bd69774ea808da69c983d6a43b'
	 '314fd4092e7718b7e3a99d1a98a0c5cb'
	 '65caa0bc59caa377d212db5f293bdd49'
	 '5872d95907a93ada44982e355e91e59d'
	 '5f28dfc4c9fdb0fbfed69c8cb38659dd'
	 '5184b94a2a40216a67996999481dd9ee'
	 'c5156eddf81c8a1719b160d05a2e8d67'
	 'b55902def024f4b7d4e0512fc90048fe')


build() {
     /bin/sh ./amd-catalyst-13.4-linux-x86.x86_64.run --extract archive_files
}

package() {
    # License
      install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
      install -m644 ${srcdir}/archive_files/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}

      cd ${srcdir}/archive_files
      patch -Np1 -i ../makefile_compat.patch
      patch -Np0 -i ../gentoo_linux-3.10-proc.diff
      patch -Np1 -i ../kolasa_fglrx_fixing_ttys.patch
      patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
      patch -Np1 -i ../lano1106_kcl_agp_13_4.patch
      patch -Np1 -i ../kolasa-fglrx_3.12_kernel.patch

    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      cd ${srcdir}/archive_files
      install -m755 -d ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                ${pkgdir}/usr/share/ati/build_mod

      install -m755 -d ${pkgdir}/usr/bin
      install -m755 ${srcdir}/catalyst_build_module ${pkgdir}/usr/bin
      sed -i -e "s/catver=.*/catver=${pkgver}-${pkgrel}/" ${pkgdir}/usr/bin/catalyst_build_module

      install -m644 ${srcdir}/catalyst-PKGBUILD ${pkgdir}/usr/share/ati/build_mod/PKGBUILD
      sed -i -e "s/pkgver=.*/pkgver=${pkgver}/" ${pkgdir}/usr/share/ati/build_mod/PKGBUILD
      sed -i -e "s/pkgrel=.*/pkgrel=${pkgrel}/" ${pkgdir}/usr/share/ati/build_mod/PKGBUILD
      install -m644 ${srcdir}/catalyst.install ${pkgdir}/usr/share/ati/build_mod

    # modified ati's make.sh script
      install -m755 ${srcdir}/ati_make.sh ${pkgdir}/usr/share/ati/build_mod

    # autocompletion for bash and zsh
      install -m755 -d ${pkgdir}/usr/share/bash-completion/completions
      install -m644 ${srcdir}/completion-bash ${pkgdir}/usr/share/bash-completion/completions/catalyst_build_module
      install -m755 -d ${pkgdir}/usr/share/zsh/site-functions
      install -m644 ${srcdir}/completion-zsh ${pkgdir}//usr/share/zsh/site-functions/_catalyst_build_module
}