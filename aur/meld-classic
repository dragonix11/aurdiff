# Maintainer: 謝致邦<Yeking@Red54.com>

pkgname=meld-classic
_pkgname=meld
pkgver=1.7.0+
_pkgver=87c0df24981125e13b2bfa1f1d16c661a51ab25b
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'gnome-doc-utils')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
provides=('meld')
conflicts=('meld')
replaces=('meld')
source=("https://git.gnome.org/browse/meld/snapshot/meld-87c0df24981125e13b2bfa1f1d16c661a51ab25b.tar.xz")
sha1sums=('700080134d4525ecd50ab95ce2bc04d73bd74c2e')

install=install
options=('!emptydirs')

prepare() {
	cd "${_pkgname}-${_pkgver}"
	sed -i 's/update-/#update-/' Makefile
	sed -i 's/env python/env python2/' bin/meld
}

build() {
	cd "${_pkgname}-${_pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
	cd "${_pkgname}-${_pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="${pkgdir}" install
}
