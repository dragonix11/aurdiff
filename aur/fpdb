# Maintainer: Ant1 <ant[dot]vand[at]gmail[dot]com>
# Contributor: flub <flub123[at]gmx[dot]de> 
# Contributor: Iosca <dvdios[at]aol[dot]com>
pkgname=fpdb
pkgver=0.40.4
_major=0.40
pkgrel=3
pkgdesc="An open source tracker/HUD for use with on-line poker."
url="http://sourceforge.net/projects/fpdb/"
arch=('x86_64' 'i686')
license=('AGPL' 'GPL')
depends=('pygtk' 'python2-matplotlib' 'python2-wnck' 'python-xlib' 'python2-beautifulsoup3')
optdepends=('mariadb: mysql database'
            'postgresql: postgresql database')
source=("http://sourceforge.net/projects/fpdb/files/fpdb/${_major}/${pkgname}-${pkgver}.tar.gz")
md5sums=('b48738731a651d8f0eb25970dddff36a')
sha512sums=('fae60e82d2c270231f51592baf6d73cdfb4bfd1fdf73775c6aa8f7d53e068cef53cc4d4823ecbf2b12d558414041176094b5cab3e1a05c24ae5c87e2e740cefd')
package() {

  # building... setup requires ${pkgdir} which is no longer available in build()
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  sed -i 's|Terminal=true|Terminal=false|g' files/fpdb.desktop
  
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  cp -r "gfx" "${pkgdir}/usr/lib/python2.7/site-packages/"
  cp "pyfpdb/HUD_main.pyw" "${pkgdir}/usr/lib/python2.7/site-packages/fpdb/"
  cp "pyfpdb/fpdb.pyw" "${pkgdir}/usr/lib/python2.7/site-packages/fpdb/"
  cd "${pkgdir}/usr/lib/python2.7/site-packages/fpdb/"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|g' *.py
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|g' *.pyw
  chmod +x "fpdb.pyw"

  # packaging
  mkdir "${pkgdir}/usr/bin"
  ln -s "../lib/python2.7/site-packages/fpdb/fpdb.pyw" "${pkgdir}/usr/bin/fpdb"
}
