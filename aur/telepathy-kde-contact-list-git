# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: laloch <laloch@atlas.cz>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-contact-list-git
_gitname="ktp-contact-list"
pkgver=v0.6.80.0.g4b3f9b4
pkgrel=1
epoch=2
pkgdesc="KDE Telepathy contact list application"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdereview/ktp-contact-list"
license=('GPL')
depends=('telepathy-kde-common-internals-git')
makedepends=('git' 'cmake' 'automoc4')
conflicts=('telepathy-kde-contact-list'
           'telepathy-contact-list-git')
install=${pkgname}.install
source=("git://anongit.kde.org/ktp-contact-list.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_gitname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
