# Maintainer: Christian Weber <christianweber802 at gmx dot net>

pkgname=ndiswrapper
pkgver=1.58
pkgrel=19
epoch=
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors. Fixed for kernel version 3.10."
arch=('any')
url="http://sourceforge.net/projects/ndiswrapper/"
license=('GPL')
depends=('linux>=3.10.2')
makedepends=('linux-headers>=3.10.2')
optdepends=('ndisgtk: GTK+ based frontend for ndiswrapper.')
provides=('ndiswrapper')
install=".install"
source=("http://download.sourceforge.net/ndiswrapper/$pkgname-$pkgver.tar.gz"
        "kernel-3.10.patch")
md5sums=('ba6f57e5eb2f9e94cb07c7f151b2afcd'
         'cecf6a14f1f7f228327eceb57b70ec55')

prepare() {
	# find currently installed (NOT RUNNING) kernel
	KVERSION=$(pacman -Q linux | sed -e 's/linux \([0-9.-]*\)/\1-ARCH/')
	echo "Building for kernel version '${KVERSION}'..."
	
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/kernel-3.10.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make -C driver KVERS_UNAME=${KVERSION}
	make -C utils
}

package() {
	# find currently installed (NOT RUNNING) kernel
	KVERSION=$(pacman -Q linux | sed -e 's/linux \([0-9.-]*\)/\1-ARCH/')
	echo ${KVERSION}
	cd "$srcdir/$pkgname-$pkgver"
	make -C driver DESTDIR=${pkgdir} KVERS_UNAME=${KVERSION} install
	make -C utils DESTDIR=${pkgdir} install
}
