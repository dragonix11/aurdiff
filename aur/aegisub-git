# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=aegisub-git
pkgver=3.1.0.7741
pkgrel=1
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('desktop-file-utils' 'ffmpegsource' 'fftw' 'hicolor-icon-theme' 'hunspell' 'libass' 'lua51' 'wxgtk2.9')
makedepends=('boost' 'git' 'intltool' 'mesa')
optdepends=('libpulse: Pulseaudio backend'
            'openal: OpenAL backend'
            'oss: OSS backend'
            'portaudio: Portaudio backend')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/Aegisub/Aegisub.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  echo "3.1.0.$((3505 + $(git describe | sed 's|.*-\(.*\)-.*|\1|')))"
}

build () {
  cd ${pkgname%-*}/${pkgname%-*}

  ./autogen.sh
  ./configure --prefix='/usr' --with-wxdir='/usr/include/wx-2.9' --with-wx-config='/usr/bin/wx-config-2.9'
  make
}

package() {
  cd ${pkgname%-*}/${pkgname%-*}

  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/aegisub.svg

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub-git
  install -m 644 {,"${pkgdir}"/usr/share/licenses/aegisub-git/}LICENCE
}

# vim: ts=2 sw=2 et:
