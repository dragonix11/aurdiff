# Maintainer: NanoArch <nanoarch77@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell-native-theme
_pkgname=gnome-shell
pkgver=3.10.1
pkgrel=1
pkgdesc="The next generation GNOME Shell with the support of themes without extensions"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeShell"
license=(GPL2)
depends=(accountsservice caribou evolution-data-server gcr gjs gnome-bluetooth gnome-menus
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libgdm libsecret mutter network-manager-applet
         telepathy-logger telepathy-mission-control unzip)
makedepends=(intltool gtk-doc gnome-control-center)
optdepends=('gnome-control-center: System settings'
            'gdm3setup-utils-native-theme: Select the GDM Shell Theme'
            'gdm3setup: Select the GDM Shell Theme')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
groups=(gnome)
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz
        nm-libexecdir.patch
        gnome-shell-native-theme.patch)
conflicts=($_pkgname)
provides=($_pkgname=$pkgver)
sha256sums=('52f43dcd6df9dd00e5fbcd16a0d7551801cbc77f8777f047e548e7803bb90e02'
            'e5bb10ad2e5c3e0fde3d05babd1bfdda701e553e02d493f7e54cb7832ce7e607'
            '18faba56366c457f5c345807813f81556ff9a24f2593eafba674679e820ece9b')

prepare() {
  cd $_pkgname-$pkgver

  # Native theme patch
  patch -Np1 -i ../gnome-shell-native-theme.patch

  # FS#30747 FS#32730 Problems due to libexecdir different from NM
  patch -Np1 -i ../nm-libexecdir.patch

  # Build against mutter-wayland 3.9.92
  sed -ri 's/^(MUTTER_MIN_VERSION)=.*/\1=3.9.92/' configure
}

build() {
  cd $_pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
