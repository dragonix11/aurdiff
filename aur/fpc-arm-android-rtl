pkgname=fpc-arm-android-rtl
pkgver=2.7.1
pkgrel=1
pkgdesc="Free Pascal runtime library for Android (ARM)."
arch=(any)
url="http://www.freepascal.org"
license=("GPL" "LGPL" "custom")
depends=("fpc>=2.6.2" "android-ndk>=r9")
makedepends=("ppcrossarm>=$pkgver" subversion)
options=(!strip !makeflags !buildflags)
source=("${pkgname%-*}::svn+http://svn.freepascal.org/svn/fpc/trunk")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname%-*}"
  if [ $CARCH == "i686" ]; then
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86/bin
	else
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86_64/bin
	fi
  make clean crossall OS_TARGET=android CPU_TARGET=arm CROSSOPT="-CfVFPV3"
}

package() {
  cd "${srcdir}/${pkgname%-*}"
  make crossinstall OS_TARGET=android CPU_TARGET=arm CROSSOPT="-CfVFPV3" PREFIX=$pkgdir/usr
  rm -r "${pkgdir}/usr/bin"
  rm "$pkgdir/usr/lib/fpc/$pkgver/ppcrossarm"
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 arm-linux-androideabi-strip -g
}
