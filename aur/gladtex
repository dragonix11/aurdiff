# Maintainer: Volker Schatz <arch@my-name-without-hyphen.com>
# Contributor: Xyne <ac xunilhcra enyx, backwards>
pkgname=gladtex
pkgver=1.3
pkgrel=4
pkgdesc="A utility for writing LaTeX equations within HTML"
depends=('perl' 'ghostscript' 'texlive-bin' 'libpng' 'giflib')
#optdepends=('giflib')
arch=('i686' 'x86_64')
license=('GPL')
url="http://ans.hsh.no/home/mgg/gladtex/"
source=(
  "http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz"
  gladtex-1.1-png_jmpbuf.diff
  gladtex-1.3-giflib5.diff
)
md5sums=('849dbf55c43283802d2e55fe2f142ff3'
         '8a969dbfbe51121e4d8454303cf46a6c'
         '6e675d807e7a66a69fd9c684b8150e89')
sha256sums=('276cf10f57a0fd28354a9494e76befe9c3b088e9ff873336ad82811e49e0b192'
            'bb66475e8fdf3b79b38b6965bdf5ada85945eb5ac4740daa2dbe55569889b19d'
            '0094760de527bdecac3425f7999a29554e72b3d901f1f094d920389c2bfaf134')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 < "$srcdir/gladtex-1.1-png_jmpbuf.diff"
  patch -p0 < "$srcdir/gladtex-1.3-giflib5.diff"
  sed -i -e 's/"gladtex 1\.2"/"gladtex 1.3"/' Makefile
  sed -i -e 's/gladtex version 1\.2/gladtex version 1.3/' gladtex
  sed -i -e 's/defined %history/%history/' gladtex
  # fix for ghostscript 9.09 PNG device type:
  sed -i -e 's/-sDEVICE=pngmono/-sDEVICE=pnggray/' eqn2img.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i -e 's!/usr/local!'"$pkgdir"'/usr!' Makefile
  make install
  ln -s gladtex.1 "$pkgdir"/usr/share/man/man1/eqn2img.1
}
# vim: set ts=2 sw=2 et:
