# Maintainer: Dominik Heidler <dheidler@gmail.com>
pkgname=bladerf
pkgver=0.3.1
pkgrel=1
pkgdesc="Driver for bladeRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/Nuand/bladeRF"
license=('GPLv2')
depends=('libusbx>=1.0.12')
makedepends=('git' 'cmake')

#backup=("etc/bladerf.cfg")
_firmware_file='bladeRF_fw_v1.5.3.img'
source=(
	'git://github.com/Nuand/bladeRF#commit=1d5aa6e2a4f2665b89c6e03ba35fa3d562828053'
	"http://www.nuand.com/fx3/${_firmware_file}"
	'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx40.rbf'
	'http://nuand.com/fpga/ddc81d0fb1b653227b5824d8d5fcb444556da175/hostedx115.rbf'
	'88-bladerf.rules')

md5sums=('SKIP'
         'e9294571391c2d08a1aaf4f0c0c6a306'
         '2be690da46eea7053e6c3488fd7f55a1'
         '61025399248f6a93b0ca38edd631bf87'
         '062aefbf49fc9f9ec3e20b2994eeb8c2')

build() {
	cd "$srcdir/bladeRF/host"
	mkdir -p build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DINSTALL_UDEV_RULES=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr ../
	make
}

package() {
	cd "$srcdir/bladeRF/host/build"
	make DESTDIR=${pkgdir} install
	cd "$srcdir"
	install -vD -m644 88-bladerf.rules "${pkgdir}/usr/lib/udev/rules.d/88-bladerf.rules"
	install -vD -m644 $_firmware_file "${pkgdir}/usr/share/bladerf/firmware/${_firmware_file}"
	install -vD -m644 hostedx40.rbf "${pkgdir}/usr/share/bladerf/fpga/hostedx40.rbf"
	install -vD -m644 hostedx115.rbf "${pkgdir}/usr/share/bladerf/fpga/hostedx115.rbf"
}
