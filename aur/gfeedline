# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=gfeedline
pkgver=2.3.2_b3
pkgrel=1
pkgdesc="A social networking client that supports Twitter"
arch=('any')
url="http://code.google.com/p/gfeedline/"
license=('GPL3' 'custom:MIT')
depends=('webkitgtk' 'python2-beautifulsoup3' 'python2-oauth'
         'python2-gobject' 'python2-dateutil' 'python2-pyopenssl'
         'python2-xdg' 'hicolor-icon-theme' 'twisted')
makedepends=('python2-distutils-extra' 'intltool')
optdepends=('python2-gtkspellcheck: for spell checking')
install="$pkgname.install"
source=("http://gfeedline.googlecode.com/files/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('bee03ecce05b6d4fe0160a3e159bb6af')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  # python2 shebang
  find . -name "*.py" -exec \
    sed -i '1s|python|&2|' {} \;

  # twisted 13.1 fix
  sed -i '57,58d' lib/utils/urlget.py  
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  python2 setup.py install --root="$pkgdir/"

  # fix perms
  chmod -R go+r "$pkgdir/usr/share/$pkgname/html"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
