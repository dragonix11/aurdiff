# Contributor: David Virgilio <drvirgilio@gmail.com>
# Contributor: David Zaragoza <david@zaragoza.com.ve>
# Maintainer: Clément Démoulins <clement@archivel.fr>

pkgname=frescobaldi
pkgver=2.0.11
pkgrel=1
pkgdesc="A LilyPond sheet music editor for KDE4. It aims to be powerful, yet lightweight and easy to use."
arch=('any')
url="http://www.frescobaldi.org/"
license=("GPL")
depends=('lilypond' 'python2-poppler-qt' 'python2-poppler')
optdepends=('rumor' 'portmidi: MIDI playback' 'python-pygame: MIDI playback')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
install=$pkgname.install

source=(https://github.com/wbsoft/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('9c14033d8556d7bafbcb3d5c21cd7941')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  find "$pkgdir" -name '*.py' -exec sed -i -e 's/^#!\/usr\/bin\/env python$/#!\/usr\/bin\/env python2/' -e 's/^#!\/usr\/bin\/python$/#!\/usr\/bin\/python2/' {} \;
}
