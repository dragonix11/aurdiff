# Maintainer: Alastair Pharo  asppsa at gmail dot com

pkgname=pure-reduce-hg
pkgver=5029.1f2ddb179d1c
pkgrel=1
pkgdesc="Computer Algebra with Pure: A Reduce Interface"
arch=("i686" "x86_64")
license=('BSD' 'LGPL')
url="http://purelang.bitbucket.org/"
depends=('pure>=0.56')
makedepends=('gcc' 'make' 'mercurial' 'subversion')

# Revisions 1902 and 2034 of REDUCE have at least been known to compile ...
_reduce_rev=2034
source=("hg+https://bitbucket.org/purelang/pure-lang"
        "reduce-algebra-csl-new-embedded::svn+https://svn.code.sf.net/p/reduce-algebra/code/trunk/csl/new-embedded#revision=$_reduce_rev"
        "reduce-algebra-csl-generated-c::svn+https://svn.code.sf.net/p/reduce-algebra/code/trunk/csl/generated-c#revision=$_reduce_rev"
        "reduce-algebra-packages::svn+https://svn.code.sf.net/p/reduce-algebra/code/trunk/packages#revision=$_reduce_rev"
        "reduce-algebra-csl-cslbase::svn+https://svn.code.sf.net/p/reduce-algebra/code/trunk/csl/cslbase#revision=$_reduce_rev"
        "https://svn.code.sf.net/p/reduce-algebra/code/trunk/README?p=$_reduce_rev")
md5sums=(SKIP SKIP SKIP SKIP SKIP SKIP)

pkgver() {
  cd $srcdir/pure-lang
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  rm -rf $srcdir/pure-lang/pure-reduce/reduce-algebra
  mkdir -p $srcdir/pure-lang/pure-reduce/reduce-algebra/csl
  mv $srcdir/reduce-algebra-csl-new-embedded $srcdir/pure-lang/pure-reduce/reduce-algebra/csl/new-embedded
  mv $srcdir/reduce-algebra-csl-generated-c $srcdir/pure-lang/pure-reduce/reduce-algebra/csl/generated-c
  mv $srcdir/reduce-algebra-csl-cslbase $srcdir/pure-lang/pure-reduce/reduce-algebra/csl/cslbase
  mv $srcdir/reduce-algebra-packages $srcdir/pure-lang/pure-reduce/reduce-algebra/packages

  cd $srcdir/pure-lang/pure-reduce
  make -j1 || return 1
}

package() {
  cd $srcdir/pure-lang/pure-reduce
  make DESTDIR=$pkgdir install || return 1
  mkdir -p "$pkgdir/usr/share/licenses/pure-reduce-hg"

  cp "$srcdir/pure-lang/pure-reduce/COPYING" "$pkgdir/usr/share/licenses/pure-reduce-hg"
  cp "$srcdir/README" "$pkgdir/usr/share/licenses/pure-reduce-hg"
}

# vim:set ts=2 sw=2 et:
