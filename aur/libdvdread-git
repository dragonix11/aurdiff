# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdread-git
pkgver=20130428.6005034
pkgrel=1
pkgdesc="Library to access DVD disks (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu/"
license=('GPL2')
depends=('libdvdcss-git' 'bash')
makedepends=('git')
provides=('libdvdread')
conflicts=('libdvdread')
source=("git://git.videolan.org/libdvdread.git")
options=('!libtool')
md5sums=('SKIP')
_gitname="libdvdread"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
  #echo "$(git describe --always | tr - .)"
}

prepare() {
  cd "${_gitname}"
  sed '189d' -i configure.ac
  sed 's|INCLUDES|AM_CPPFLAGS|g' -i configure.ac
}

build() {
  cd "${_gitname}"

  ./autogen.sh --prefix=/usr --enable-static=no --enable-libdvdcss
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
