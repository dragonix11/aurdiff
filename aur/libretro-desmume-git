# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-desmume-git
pkgver=90.40f9af5
pkgrel=2
pkgdesc="libretro implementation of DeSmuME. (Nintendo DS)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/desmume-libretro"
license=('GPL')
makedepends=('git')

_gitname=desmume-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/desmume_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make -f Makefile.libretro DESMUME_JIT=1

  # When building on ARM uncomment the following and comment the above
  # make -f Makefile.libretro DESMUME_JIT_ARM=1
}

package() {
  install -Dm644 "${_gitname}/desmume_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-desmume.so"
  install -Dm644 "desmume_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-desmume.info"
}
