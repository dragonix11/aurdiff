# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>

pkgname=ruby-reek
_gemname="${pkgname#ruby-}"
pkgver=1.3.2
pkgrel=1
pkgdesc="A static analysis tool for finding code smells."
url="https://github.com/troessner/reek"
license=("MIT")
arch=("any")
depends=("ruby" "ruby-ruby_parser" "ruby-sexp_processor" "ruby-ruby2ruby")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('c5924969cc58de1643aad7145e6289f2')
sha1sums=('213a13f97e601b66fca53c4642e2990c07eb19cc')

package() {
  # setup
  cd "${srcdir}"
  export HOME=/tmp
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  # install gem
  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # install license
  install -Dm 644 \
    "$pkgdir"/usr/lib/ruby/gems/*/gems/"${_gemname}-${pkgver}"/License.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
}

# vim:set ts=2 sw=2 et:
