# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=artyfx-git
pkgver=49.5db7fd0
pkgrel=1
pkgdesc="A LV2 plugin bundle of artistic realtime effects"
arch=('i686' 'x86_64')
url="http://openavproductions.com/artyfx/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'cairomm' 'libsndfile' 'ntk-git')
provides=('artyfx')
conflicts=('artyfx')
source=("$pkgname::git+https://github.com/harryhaaren/openAV-ArtyFX.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  # install systemwide
  sed -i 's|~/\.|$(DESTDIR)/usr/lib/|' */makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  install -d "$pkgdir/usr/lib/lv2"
  make DESTDIR="$pkgdir/" install
}
