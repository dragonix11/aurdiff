# Maintainer: Michele Damiano Torelli <me_AT_mdtorelli_DOT_it>

pkgname=playframework2-git
pkgver=2.2.0.RC1.389.g2e92f09
pkgrel=1
pkgdesc="An open source web application framework in Java which follows the MVC pattern - Git version"
arch=('any')
url="http://www.playframework.org/"
license=('Apache')
depends=('java-environment')
makedepends=('git')
provides=('playframework2')
conflicts=('playframework' 'playframework2' 'playframework2-devel')
replaces=('playframework2')
source=('git://github.com/playframework/Play20')
sha512sums=('SKIP')

pkgver() {
  cd Play20
  git describe --always | sed 's/-/./g'
}

build() {
  cd Play20/framework

  ./build publish-local
}

package() {
  install -dm755 "${pkgdir}"/usr/share/playframework2/ "${pkgdir}"/usr/bin
  cp -r Play20/* "${pkgdir}"/usr/share/playframework2
  chgrp -R users "${pkgdir}"/usr/share/playframework2/{framework,repository}
  chmod -R g+rwx "${pkgdir}"/usr/share/playframework2/{framework,repository}
  rm "${pkgdir}"/usr/share/playframework2/{CONTRIBUTING.md,README.md,play.bat,framework/build.bat}
  ln -s /usr/share/playframework2/play "${pkgdir}"/usr/bin/play
}
