# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("spl")
#
# This script doesn't use bash variables for the version number because AUR
# doesn't properly parse bash variables. We use a custom build script to
# automatically change the appropriate variables before building in a clean
# chroot environment (using systemd-ndspawn).
#
# The build script can be found at
# https://github.com/demizer/archzfs/blob/master/build.sh
#
pkgver=0.6.2_3.11.6
pkgrel=3
pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils" "linux=3.11.6-1")
makedepends=("linux-headers=3.11.6-1")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-0.6.2.tar.gz)
groups=("archzfs")
md5sums=('f00535bf89a7fde0e08f44a14a1f1e03')
license=("GPL")
install=spl.install

build() {
  cd "$srcdir/spl-0.6.2"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel --enable-atomic-spinlocks
  else
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel
  fi
  make
}

package() {
  cd "$srcdir/spl-0.6.2"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir"/{lib,usr}
  rm -r "$pkgdir"/lib
}
