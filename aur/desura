# Maintainer: Maximilien Noal <noal dot maximilien at gmail dot com> [AUR: xcomcmdr]
# Contributor: Kirill "reflexing" Churin <reflexing@reflexing.ru>
# Contributor: Wido <widowild [at] myopera [dot] com>

pkgname=desura
pkgver=1
pkgrel=19
pkgdesc='A community-driven digital distribution service for gamers'
url='http://desura.com/'
license=('custom: "commercial"')
arch=('i686' 'x86_64')
install=${pkgname}.install
options=()
depends=(lsb-release orbit2 gtk2 libjpeg6 libpng12 libx11 libxpm libxcb libxau libxdmcp desktop-file-utils xdg-utils)
optdepends=('java-runtime: adds java support'
            'mono: Free implementation of the .NET platform')

if [[ $CARCH == i686 ]]; then
  optdepends+=('nvidia-utils: If you have nvidia graphics'
              'catalyst-utils: If you have ATI graphics'
              'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
              'adobe-air: for Adobe Air-based games')

  md5sums=('38a3aa1b69189418e823059072da7739'
           '6f8d2745304da5822762ae84f23e38a2'
           '323fe242e871e8e07e511b0a0357159c'
           'd74bf6e5275c0529470593872749a09d')
else
  optdepends+=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-curl: if you have install TRAUMA '
              'lib32-nss: if you have install TRAUMA'
              'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
              'bin32-adobe-air: for Adobe Air-based games')

  md5sums=('3cee8a2935bc1cd61523aaaf23de8e2c'
           '6f8d2745304da5822762ae84f23e38a2'
           '323fe242e871e8e07e511b0a0357159c'
           'd74bf6e5275c0529470593872749a09d') 
fi

source=(http://www.desura.com/desura-${CARCH}.tar.gz ${pkgname}.sh ${pkgname}.desktop ${pkgname}.png)

package(){
  cd ${srcdir}

  # Change permissions
  install -dm775 -g users ${pkgdir}/opt/${pkgname}

  # Install Desura
  install -Dm755 ${pkgname}/${pkgname} \
    ${pkgdir}/opt/${pkgname}/${pkgname}

  # Install launcher
  install -Dm 755 ${pkgname}.sh \
    ${pkgdir}/usr/bin/${pkgname}

  # Install bootstrapper icon
  install -m644 -D \
    ${pkgname}.png \
    ${pkgdir}/usr/share/icons/hicolor/256x256/apps/${pkgname}.png

  # Install bootstrapper desktop file
  desktop-file-install -m 644 --dir ${pkgdir}/usr/share/applications/ ${pkgname}.desktop
}
