# Maintainer: Evgeniy "arcanis" Alexeev <esalexeev@gmail.com>
# Contributor: Knut Franke - Knut.Franke*gmx.de
# Contributor: Olaf Leidinger <leidola@newcon.de>
# Contributor: Robal <nigrantis.tigris*.gmail.com>

pkgname=scidavis
pkgver=0.2.4
pkgrel=8
pkgdesc="Application for Scientific Data Analysis and Visualization, fork of QtiPlot"
arch=('i686' 'x86_64')
depends=('gsl' 'mesa' 'muparser' 'python2-pyqt4' 'shared-mime-info' 
         'qt-assistant-compat' 'qwtplot3d' 'qwt5')
license=('GPL')
url="http://scidavis.sourceforge.net/"
source=(http://prdownloads.sourceforge.net/sourceforge/scidavis/${pkgname}-${pkgver}.tar.bz2
        scidavis.pro.patch
        scidavis.sip.patch
        scidavis-python2.patch
        qt-assistant-compat.patch
        qwt5.patch)
install=${pkgname}.install
md5sums=('c5650475c5e0b22f9ee5dc49630a0e89'
         '2ec36f34039ca8cd6db2401b1215590b'
         'c140a5ce784f9e106fb637fcc3df517c'
         'ff0a7cb32e8ed439f008bfd114cbf611'
         '450b7139f913210dd8a0ffbde03e86be'
         '159aad53f13328123d42bfb7a4adf1b8')

prepare()
{
  cd ${srcdir}/${pkgname}-${pkgver}/${pkgname}
  # disable building self library
  patch < ${srcdir}/scidavis.pro.patch
  # fix bug http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=655776
  patch -p2 < ${srcdir}/scidavis.sip.patch
  # change python to python2
  patch < ${srcdir}/scidavis-python2.patch
  # fix path to QtAssistant header
  patch -p2 < ${srcdir}/qt-assistant-compat.patch
  # change qwt to qwt5
  patch < ${srcdir}/qwt5.patch
}

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}/
  lupdate-qt4 scidavis.pro
  lrelease-qt4 scidavis.pro
  qmake-qt4
  make || return 1
}

package ()
{
  cd ${srcdir}/${pkgname}-${pkgver}/
  make INSTALL_ROOT=${pkgdir} install || return 1

  _pydir=`python2 -c "from distutils import sysconfig; print sysconfig.get_python_lib()"`
  python2 `dirname ${_pydir}`/py_compile.py ${pkgdir}/etc/scidavisrc.py
  python2 `dirname ${_pydir}`/py_compile.py ${pkgdir}/usr/share/scidavis/scidavisUtil.py
}

