# Maintainer: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>
# Contributor: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-304xx-lqx
pkgver=304.88
_extramodules=extramodules-3.10-lqx
pkgrel=2
_pkgdesc="NVIDIA drivers for linux-lqx, 304xx legacy branch."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lqx>=3.10' 'linux-lqx<3.11' "nvidia-304xx-utils=${pkgver}")
makedepends=()
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-275xx-lqx' 'nvidia-lqx-stable' 'nvidia-beta-lqx' 'nvidia-304xx-lqx-atom' 'nvidia-304xx-lqx-barcelona' 'nvidia-304xx-lqx-corex' 'nvidia-304xx-lqx-core2' 'nvidia-304xx-lqx-haswell' 'nvidia-304xx-lqx-ivybridge' 'nvidia-304xx-lqx-kx' 'nvidia-304xx-k10' 'nvidia-304xx-lqx-nehalem' 'nvidia-304xx-lqx-p4' 'nvidia-304xx-lqx-piledriver' 'nvidia-304xx-lqx-pentm' 'nvidia-304xx-lqx-sandybridge' 'nvidia-lqx' 'nvidia-lqx-atom' 'nvidia-lqx-barcelona' 'nvidia-lqx-bulldozer' 'nvidia-lqx-corex' 'nvidia-lqx-core2' 'nvidia-lqx-haswell' 'nvidia-lqx-ivybridge' 'nvidia-lqx-kx' 'nvidia-lqx-k10' 'nvidia-lqx-nehalem' 'nvidia-lqx-p4' 'nvidia-lqx-piledriver' 'nvidia-lqx-pentm' 'nvidia-lqx-sandybridge')
#groups=('lqx-generic')
#replaces=()
license=('custom')
options=(!strip)
install=nvidia-304xx-lqx.install
source=('nvidia-linux-3.10.patch')
md5sums=('f12bb3f5829b979a0d79e68b78c2ba6a')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums+=('f60d4aef766029c98914ed665235029a')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums+=('35e0c7d30de795cccb6b95bda991fb77')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel
    ### Derived from the linux-3.10 patch:
    ### This patch removes AGP additional from /proc, I don't think this is correct but it makes it compile.
    patch -Np2 -i ${srcdir}/nvidia-linux-3.10.patch
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lqx.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-304xx-lqx.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
