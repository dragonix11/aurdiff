# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=1.3.3
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('any')
depends=('sh')
backup=('etc/lynis/default.prf')
source=(http://www.rootkit.nl/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7389dff4435dbb31612ceb1a4260cfd7383ed58182a216b128d8e1cfd34118f7')

package() {
  cd ${pkgname}-${pkgver}

  # install profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # install binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # install plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"
  chmod -R u=rwX,go=rX "${pkgdir}/usr/share/${pkgname}"

  # install doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG FAQ

  # install manpage
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"
}

# vim:set ts=2 sw=2 ft=sh et:
