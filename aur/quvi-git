# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=quvi-git
pkgver=0.9.3.1
pkgrel=1
pkgdesc="Command line tool for parsing flash media stream URLs. (GIT version)"
arch=('i686' 'x86_64')
url="http://quvi.sourceforge.net/"
license=('custom:LGPLv2.1+')
depends=('libquvi-git')
makedepends=('git')
options=('!libtool')
provides=('quvi')
conflicts=('quvi')
source=('git://repo.or.cz/quvi-tool.git')
sha256sums=('SKIP')

pkgver() {
  cd 'quvi-tool'
  git describe | sed 's/^v//;s/-/+/g'
}

build() {
  cd 'quvi-tool'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd 'quvi-tool'

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

# vim: ft=sh syn=sh sw=2 ts=2 et
