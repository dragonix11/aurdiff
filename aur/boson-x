# Contributor: saik0
# Maintainer: Leios <jrs0037@auburn.edu>
pkgname=boson-x
pkgver=1.0.4
_pkgver=${pkgver//\./_} # replace all '.' with '_'
pkgrel=3
pkgdesc="DRM-free rotational running game"
arch=('x86_64')
url="http://www.boson-x.com/"
license=('Custom')
source=("http://downloads.muandheyo.com/BosonX_v1_0_4_Linux.zip"
		'bosonx.sh'
		"${pkgname}.desktop")
depends=('mesa')
sha1sums=('08abffcbce4bcc34269a77d05f022ba0057e73c2'
          'a2ad55adce9bef502c0fcb83f1da7646d8822099'
          '97dadb126a3bf8a6868e3e398952997e31537767')

package() {
  cd "${srcdir}/BosonX_v${_pkgver}_Linux/"

  # Executable
  install -Dm755 bosonx \
  	"${pkgdir}/usr/share/${pkgname}/bosonx"

  # Desktop File
  install -Dm755 "${srcdir}/${pkgname}.desktop" \
	"${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Icon
  install -Dm644 data/bosonx_symbol.png_ ${pkgdir}/usr/share/pixmaps/${pkgname}.png

  # Game data
  install -d "${pkgdir}/usr/share/${pkgname}/"
  cp -rd --no-preserve=ownership data/ "${pkgdir}/usr/share/${pkgname}/data"

  # bosonx executable wants to be in the same directory as 'data/'
  # Symbolic links aren't currently working.
  install -Dm755 ${srcdir}/bosonx.sh "${pkgdir}/usr/bin/${pkgname}"
  

  # License
  install -Dm644 LICENSES.txt \
  	"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
