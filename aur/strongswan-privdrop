# Maintainer: Noel Kuntze <noel [at] familie-kuntze dot de>

pkgname=strongswan-privdrop
pkgver=5.1.0
pkgrel=1
pkgdesc="open source IPsec implementation with privilege dropping enabled and set to run as non-root user"
url='http://www.strongswan.org'
license=("GPL")
arch=('i686' 'x86_64')
depends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'gperf' 'sudo')
conflicts=('openswan' 'strongswan' 'strongswan-git')
options=(!libtool)
backup=(etc/ipsec.conf etc/strongswan.conf)
source=('http://download.strongswan.org/strongswan-5.1.0.tar.bz2' 'sudo_updown' 'sudo_updown_espmark' 'ipsec.conf')
md5sums=('c1cd0a3ba9960f590cae28c8470800e8'
	 'SKIP'
	 'SKIP'
	 'SKIP')

build() {
   cd $srcdir/strongswan-$pkgver
  ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-ipsecdir=/usr/lib/strongswan \
        --enable-sqlite \
        --enable-openssl --enable-curl \
        --enable-sql --enable-attr-sql \
        --enable-farp --enable-dhcp \
        --enable-eap-sim --enable-eap-sim-file --enable-eap-simaka-pseudonym \
        --enable-eap-simaka-reauth --enable-eap-identity --enable-eap-md5 \
        --enable-eap-gtc --enable-eap-aka --enable-eap-aka-3gpp2 \
        --enable-eap-mschapv2 --enable-eap-radius --enable-xauth-eap \
        --enable-ha --enable-gcm --enable-ccm --enable-ctr --enable-unity \
	--enable-integrity-test --enable-load-tester --enable-test-vectors \
        --enable-af-alg --disable-ldap \
	--with-user=strongswan --with-group=strongswan --with-capabilities=libcap \
	--enable-kernel-libipsec --enable-cmd
  make
}

package() {
  cd "$srcdir/strongswan-$pkgver"
  make DESTDIR=${pkgdir} install
  # The sudo_updown and sudo_updown_espmark scripts are simply the default updown scripts 
  # with "iptables" replaced by "sudo iptables" in order to make them work with privilege dropping
  # and running as non-root user.
  cp $srcdir/sudo_updown $pkgdir/usr/lib/strongswan/
  cp $srcdir/sudo_updown_espmark $pkgdir/usr/lib/strongswan/
  # modified default ipsec.conf with 
  # "conn %default 
  #  leftupdown=/usr/lib/strongswan/sudo_updown"
  # added.
  cp $srcdir/ipsec.conf	$pkgdir/etc/ipsec.conf
}

