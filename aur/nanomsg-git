# Maintainer: KokaKiwi <admin@kokaelkiwi.eu>

_pkgname=nanomsg
pkgname=nanomsg-git
pkgver=0.1.alpha.45.g52ad72b
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=('MIT')
source=('git://github.com/250bpm/nanomsg.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('git' 'asciidoc' 'xmlto')
provides=('nanomsg')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./autogen.sh
  ./configure --prefix=/usr --enable-doc=yes
  make
}

check() {
  cd "${srcdir}/${_pkgname}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${_pkgname}/COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
