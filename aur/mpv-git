# $Id$
# Maintainer: Rudolf Polzer <divVerent@xonotic.org>
# Original-Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-git
pkgver=0.36262.da6093a
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=('portaudio' 'ffmpeg' 'lcms2' 'libxinerama' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva'  'sdl2' 'libmng' 'smbclient' 'libdvdread' 'desktop-file-utils' 'libcdio-paranoia' 'hicolor-icon-theme' 'libxss' 'lirc-utils' 'libcaca')
makedepends=('mesa' 'python-docutils'  'git')
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv.install
source=('git+http://github.com/mpv-player/mpv#branch=master')
md5sums=('SKIP')

_gitname="mpv"

pkgver() {
  cd "$srcdir/$_gitname"
  microver="$(git log -1 --pretty=format:%h )"
  minorver="$(git rev-list --count HEAD)"
  echo "0.$minorver.$microver"
}

build() {
  unset CFLAGS
  unset LDFLAGS

  cd "$srcdir/$_gitname"

  ./configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-lircc \
    --disable-joystick \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-rpath \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio \
    \
    --disable-pdf \
    --enable-sdl2 \
    --extra-cflags="-I/usr/include/samba-4.0"
  make
}

package() {
  cd "$srcdir/$_gitname"
  make -j1 DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example,encoding-example-profiles}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
