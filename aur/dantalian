# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
pkgname=dantalian
pkgver=0.4.1
pkgrel=1
pkgdesc="Transparent tag-based file organization system."
arch=('any')
url="https://github.com/darkfeline/dantalian"
license=('custom')
depends=('python')
optdepends=('fuse: additional features')
makedepends=('python-sphinx')
changelog=Changelog
source=("https://github.com/darkfeline/dantalian/archive/${pkgver}.tar.gz")
sha256sums=('2a04dec0570d40a65b7ae686a586b28ecccb5d8097b33b3fa292eda8fc03462b')

build() {
    cd "$srcdir/$pkgname-$pkgver/doc"
    make man singlehtml
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/"

    # license
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dantalian/LICENSE"

    # man page
    install -Dm644 doc/_build/man/dantalian.1 "$pkgdir/usr/share/man/man1/dantalian.1"

    # html documentation
    install -dm755 "$pkgdir/usr/share/doc/dantalian"
    cp -R doc/_build/singlehtml/* "$pkgdir/usr/share/doc/dantalian"
}
