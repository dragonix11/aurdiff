pkgname=fpc-i386-android-rtl
pkgver=2.7.1
pkgrel=1
pkgdesc="Free Pascal runtime library for Android (i386)."
arch=(any)
url="http://www.freepascal.org"
license=("GPL" "LGPL" "custom")
depends=("fpc>=2.6.2" "android-ndk>=r9")
makedepends=("ppcross386>=$pkgver" subversion)
options=(!strip !makeflags !buildflags)
source=("${pkgname%-*}::svn+http://svn.freepascal.org/svn/fpc/trunk")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname%-*}"
  if [ $CARCH == "i686" ]; then
		export PATH=$PATH:$ANDROID_NDK/toolchains/x86-4.8/prebuilt/linux-x86/bin
	else
		export PATH=$PATH:$ANDROID_NDK/toolchains/x86-4.8/prebuilt/linux-x86_64/bin
	fi
  make clean crossall OS_TARGET=android CPU_TARGET=i386
}

package() {
  cd "${srcdir}/${pkgname%-*}"
  make crossinstall OS_TARGET=android CPU_TARGET=i386 PREFIX=$pkgdir/usr
  rm -r "${pkgdir}/usr/"{bin,share}
  rm "$pkgdir/usr/lib/fpc/$pkgver/ppcross386"
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 i686-linux-android-strip -g
}
