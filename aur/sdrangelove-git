# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=sdrangelove-git
pkgver=20131024
_commit=3b6d910f0c534a7
pkgrel=1
pkgdesc="Driver for Realtek RTL2832U, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="http://sdr.osmocom.org/trac/wiki/rtl-sdr"
license=('GPL')
depends=('qt5-multimedia' 'rtl-sdr')
# build with gr-osmosdr for more hardware
makedepends=('git' 'cmake')
conflicts=('sdrangelove')
source=("git://git.osmocom.org/sdrangelove.git#commit=$_commit")
md5sums=('SKIP')

_gitname="sdrangelove"

build() {
  cd "$srcdir/$_gitname"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -Wno-dev ../
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  # missing target and sane linking
  #make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/opt/$pkgname"
  cp sdrangelove   "$pkgdir/opt/$pkgname/"
  cp libsdrbase.so "$pkgdir/opt/$pkgname/"
  cp -r plugins "$pkgdir/opt/$pkgname/"
  mkdir -p "$pkgdir/usr/bin"
  echo -e "#!/bin/bash\ncd /opt/sdrangelove-git/\nLD_LIBRARY_PATH=./ ./sdrangelove\ncd -" > "$pkgdir/usr/bin/sdrangelove"
  chmod +x "$pkgdir/usr/bin/sdrangelove"
}

# vim:set ts=2 sw=2 et:
