# Maintainer: superlex

# Based on Parabola GNU/Linux iceweasel-libre PKGBUILD :

# Contributor (ConnochaetOS): Henry Jensen <hjensen@connochaetos.org>
# Contributor (Parabola): Luke Shumaker <lukeshu@sbcglobal.net>
# Contributor: Figue <ffigue at gmail>
# Contributor (Parabola): fauno <fauno@kiwwwi.com.ar>
# Contributor (Parabola): vando <facundo@esdebian.org>
# Contributor (Parabola): André Silva <emulatorman@lavabit.com>
# Contributor (Parabola): Márcio Silva <coadde@lavabit.com>
# Contributor (Arch): Jakub Schmidtke <sjakub@gmail.com>
# Thank you very much to the older contributors:
# Contributor: evr <evanroman at gmail>
# Contributor: Muhammad 'MJ' Jassim <UnbreakableMJ@gmail.com> 

# Firefox PKGBUILD (Arch Linux):

# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

_pgo=false

# We're getting this from Debian Sid (Unstable)
_debname=iceweasel
_debver=24.0
_debrel=2
_debrepo=http://ftp.debian.org/debian/pool/main/i/

pkgname=iceweasel
pkgver=$_debver.deb$_debrel
pkgrel=1
pkgdesc="Debian Browser based on Mozilla Firefox"
arch=('i686' 'x86_64')
license=('GPL' 'MPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'libevent' 'nss' 'hunspell' 'sqlite')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'libpulse' 'gstreamer0.10-base'
             'inetutils' 'quilt')
options=(!emptydirs !makeflags)
if $_pgo; then
  makedepends+=(xorg-server-xvfb)
  options+=(!ccache)
fi

optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-ugly-plugins: h.264 decoding'
            'libpulse: PulseAudio audio driver'
            'iceweasel-extension-globalmenu: Globalmenu Extension for Iceweasel'
            'iceweasel-extension-archsearch: Iceweasel Arch search engines'
            'iceweasel-extension-archforumsearch-it: Iceweasel search engines for Arch Linux Italian forum')
url="http://packages.debian.org/sid/iceweasel"
install=iceweasel.install
provides=("$_debname"="$_debver")
source=("${_debrepo}/${_debname}/${_debname}_${_debver}.orig.tar.bz2"
        "${_debrepo}/${_debname}/${_debname}_${_debver}-${_debrel}.debian.tar.gz"
        'mozconfig'
        'mozconfig.pgo'
        'iceweasel.desktop'
        'iceweasel-install-dir.patch'
        'vendor.js'
        'Bug-756390-Make-the-Reset-Firefox-feature-more-gener.patch')
md5sums=('8f24807d67599516850a4bdca2b2fbd6'
         '1f4f1347e05b15f5af137787d614ecdf'
         '9484a16f8c29b93bab16303aa1868ccc'
         'a621ecd7d711844211a6569fdd17fa74'
         '7b9e5996dd9fe0b186a43a297db1c6b5'
         'abf5ecb74caa857abb42bcfbb3442d9c'
         '5c17980a124d9fa8b83af548f49e3629'
         'f1594fa3567e5b70202c3432d7c00202')
         
prepare() {
  export DEBIAN_BUILD="mozilla-release"
  
  export QUILT_PATCHES=debian/patches
  export QUILT_REFRESH_ARGS='-p ab --no-timestamps --no-index'
  export QUILT_DIFF_ARGS='--no-timestamps'
  
  mv debian "$srcdir/$DEBIAN_BUILD"
  cd "$srcdir/$DEBIAN_BUILD"
  
  # We wont save user profile in .mozilla/iceweasel
  sed -i 's/MOZ_APP_PROFILE=mozilla\/firefox/MOZ_APP_PROFILE=mozilla\/iceweasel/g' "debian/branding/configure.sh"
  
  # Doesn't apply and seems unimportant
  rm -v debian/patches/l10n/Place-google-and-gmail-before-yandex.patch || true

  # This patch doesn't works in some parts due that has patches for others locales languages, source code hasn't it
  rm -v debian/patches/debian-hacks/Bug-756390-Make-the-Reset-Firefox-feature-more-gener.patch || true

  quilt push -av

  # Adding fixed Bug-756390-Make-the-Reset-Firefox-feature-more-gener.patch
  patch -Np1 -i "$srcdir/Bug-756390-Make-the-Reset-Firefox-feature-more-gener.patch" 
  
  # Install to /usr/lib/$pkgname
  patch -Np1 -i "$srcdir/iceweasel-install-dir.patch"

  # Load our build config
  cp "$srcdir/mozconfig" .mozconfig

  if $_pgo; then
    cat "$srcdir/mozconfig.pgo" >> .mozconfig
  fi
 
  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  mkdir "$srcdir/python2-path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/python2-path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd "$srcdir/$DEBIAN_BUILD"

  export PATH="$srcdir/python2-path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/$_pkgname"
  export PYTHON="/usr/bin/python2"
  
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS
  
  if [[ $CARCH == i686 ]]; then
    # Work around memory address space exhaustion during linking on i686
    LDFLAGS+=' -Wl,--no-keep-memory'
  fi

  if $_pgo; then
    if [[ $CARCH != i686 ]]; then
      # Set up PGO
      export DISPLAY=:99
      Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

      if ! make -f client.mk build MOZ_PGO=1; then
        kill $!
        return 1
      fi

      kill $! || true
    else
      make -f client.mk build
    fi
  else
    make -f client.mk build
  fi
}

package() {
  cd "$srcdir/$DEBIAN_BUILD"
  make -f client.mk DESTDIR="$pkgdir" install

  install -Dm644 ../vendor.js "$pkgdir/usr/lib/$pkgname/browser/defaults/preferences/vendor.js"

  # I don't even know why we're hitting the objdir, and ConnOS didn't
  _brandingdir=debian/branding
  brandingdir=moz-objdir/$_brandingdir
  icondir="$pkgdir/usr/share/icons/hicolor"
  for i in 16x16 32x32 48x48 64x64; do
    install -Dm644 "$brandingdir/default${i/x*/}.png" "$icondir/$i/apps/$pkgname.png"
  done
  install -Dm644 "$brandingdir/mozicon128.png"      "$icondir/128x128/apps/$pkgname.png"
  install -Dm644 "$_brandingdir/iceweasel_icon.svg" "$icondir/scalable/apps/$pkgname.svg"

  install -d                                        "$pkgdir/usr/share/applications"
  install -m644  "$srcdir/iceweasel.desktop"        "$pkgdir/usr/share/applications"
  
  
  # Use system-provided dictionaries
  rm -rf "$pkgdir/usr/lib/$pkgname/"{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell            "$pkgdir/usr/lib/$pkgname/dictionaries"
  ln -sf /usr/share/hyphen              "$pkgdir/usr/lib/$pkgname/hyphenation"
    
  
  # We don't want the development stuff
  rm -rf "$pkgdir"/usr/{include,lib/$pkgname-devel,share/idl}

  # Workaround for now: https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf $pkgname "$pkgdir/usr/lib/$pkgname/$pkgname-bin"

  
  # Remove $srcdir refers
  sed -i '1d' "$pkgdir/usr/lib/$pkgname/defaults/pref/channel-prefs.js"
  
  
  
  # Searchplugins section
  
  # According to debian choices, we prefer to use /etc/icewasel/searchplugins
  install -d "$pkgdir/etc/${pkgname}/searchplugins/common"
  install -d "$pkgdir/etc/${pkgname}/searchplugins/locale"
  
  # Add common web searchplugins
  install -Dm644 "$srcdir/$DEBIAN_BUILD/debian/duckduckgo.xml" "$pkgdir/etc/${pkgname}/searchplugins/common/duckduckgo.xml"
  install -Dm644 "$srcdir/$DEBIAN_BUILD/debian/debsearch.xml" "$pkgdir/etc/${pkgname}/searchplugins/common/debsearch.xml"
  
  # Add web searchplugins for default locale (en-US)
  cp -R "$pkgdir/usr/lib/$pkgname/browser/searchplugins" "$pkgdir/etc/${pkgname}/searchplugins/locale/en-US"
  
  rm -rv "$pkgdir/usr/lib/$pkgname/browser/searchplugins"    
}
