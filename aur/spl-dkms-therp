# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("spl-dkms-therp")
pkgver=0.6.1
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils-therp" "dkms")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-${pkgver}.tar.gz
        spl_git_head.patch)
groups=("archzfs")
md5sums=('0f58806d9fd9cab900181b59b21bae50'
         '634741f1cfaa163feb933999d6caefb1')
license=("GPL")
install=spl-dkms-therp.install

prepare() {
  cd "$srcdir/spl-${pkgver}"
  patch -Np1 -i ../spl_git_head.patch
}

build() {
  cd "$srcdir/spl-${pkgver}"
  ./autogen.sh
  scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n spl
}

package_spl-dkms-therp() {
   install -d ${pkgdir}/usr/src/spl-${pkgver}
   cp -a ${srcdir}/spl-${pkgver}/ ${pkgdir}/usr/src/
}
