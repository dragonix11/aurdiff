# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=llpp-hjkl-git
pkgver=1440.d77abe6
pkgrel=2
pkgdesc='Lightweight but fast and featureful PDF viewer based on MuPDF with hjkl navigation patches.'
arch=(i686 x86_64)
url='http://repo.or.cz/w/llpp.git'
changelog=changelog
license=(GPL)
provides=(llpp)
conflicts=(llpp)
options=(!strip) # Required or failure reading pdf documents
depends=(mesa libx11)
makedepends=(git wget unzip ocaml)

source=("$pkgname"::'git+http://repo.or.cz/r/llpp.git'
         'llpp.desktop'
         'hjkl.patch')

md5sums=('SKIP'
         '9c4891f779eac44569671ee3a9cc0335'
         'ee1a851d7cac52e9ca2194d46a79a0e6')

pkgver() {
    cd "$pkgname"
    printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$pkgname"
    patch -Np1 -i "$srcdir"/hjkl.patch
}

build() {
    cd "$pkgname"
    # Use upstreams buildscript, not executable - need to invoke shell
    sh buildall.sh
}

package() {
    cd "$pkgname"
    install -Dm644 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
    install -Dm755 llpp "$pkgdir"/usr/bin/llpp
}
