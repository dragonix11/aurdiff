# Maintainer: J0k3r <moebius282 e4at gmail D0_T com>

# TODO: Replace bundled Mono

pkgname=fez
_datever="09102013"
_binver="1378846930"
pkgver="${_binver}"
pkgrel=1
pkgdesc="A game where you play as Gomez, a 2D creature living in what he believes is a 2D world"
url="http://fezgame.com/"
license=('unknown')
arch=('i686' 'x86_64')
groups=("games")
depends=('gcc-libs' 'bash')
makedepends=('unzip' 'imagemagick')
# _purgelibs=('libglut.so.3' 'libogg.so.0' 'libopenal.so.1' 'libstdc++.so.6' 'libvorbis.so.0' 'libvorbisfile.so.3' 'libGLEW.so.1.6')
_archivename="fez-${_datever}-linux-${_binver}-bin"
source=("hib://${_archivename}"
        "${pkgname}.desktop")
noextract=("${_archivename}")
sha256sums=('37725ea27d054fcac0660db25315583ab89ca5bce92f9a1fda605edd18db40e8'
            '7d28fe54e85ab3b9d33337a745a412bdab8fa233b40f8137475dc142f434bbcf')
PKGEXT=".pkg.tar"



# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf."; exit 1')


package()
{
  mkdir -p "${pkgdir}/opt/${pkgname}/"

# unzip is stupid..
  unzip -qq "${srcdir}/${_archivename}" -d "${pkgdir}/opt/tmp/" 'data/*' || true

  mv "${pkgdir}/opt/tmp/data/"* "${pkgdir}/opt/${pkgname}/"
  rm -r "${pkgdir}/opt/tmp/"


  if [[ "$CARCH" == "x86_64" ]]; then

    rm -r "${pkgdir}/opt/${pkgname}/lib/"
#     for i in "${_purgelibs[@]}"; do
#       rm "${pkgdir}/opt/${pkgname}/lib64/${i}"
#     done

  else

    rm -r "${pkgdir}/opt/${pkgname}/lib64/"
#     for i in "${_purgelibs[@]}"; do
#       rm "${pkgdir}/opt/${pkgname}/lib/${i}"
#     done

  fi

# patching the included startup script so it supports being symlinked
# removing the steam bits out of the lib search path; i don't know why it contains a steam dir in the non-steam version..
  sed -i \
  -e 's|`dirname "$0"`|$(dirname \"$(readlink -f \"$0\")\")|' \
  "${pkgdir}/opt/${pkgname}/${pkgname^^}"

  mkdir -p "${pkgdir}/usr/bin/"
  ln -s "/opt/${pkgname}/${pkgname^^}" "${pkgdir}/usr/bin/${pkgname}"

# the spec only specifies png, svg and xpm..

  convert "${pkgdir}/opt/${pkgname}/${pkgname^^}.bmp" "${pkgdir}/opt/${pkgname}/${pkgname}.png"


  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${pkgdir}/opt/${pkgname}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}