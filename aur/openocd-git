# Maintainer: veox <veox ta wemakethings dot net>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>

pkgname=openocd-git
_gitname=openocd
pkgver=6101.1255b18
pkgrel=1
pkgdesc="Debugging, in-system programming and boundary-scan testing for embedded target devices (git version)"
arch=('i686' 'x86_64')
url="http://openocd.sourceforge.net/"
license=('GPL')
depends=('libftdi')
makedepends=('git' 'automake>=1.11' 'autoconf' 'libtool' 'tcl')
options=(!strip)
install=openocd-git.install
provides=('openocd')
conflicts=('openocd')
source=("${_gitname}::git://git.code.sf.net/p/openocd/code")
md5sums=('SKIP')

_features=(amtjtagaccel arm-jtag-ew buspirate ep93xx ep93xxat91rm9200 gw16012 ft2232_libftdi ftdi jlink oocd_trace opendous osbdm parport presto_libftdi remote-bitbang rlink stlink sysfsgpio ti-icdi ulink usb_blaster_libftdi usbprog vsllink)

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/${_gitname}"

  ./bootstrap
  ./configure --prefix=/usr ${_features[@]/#/--enable-} \
    --enable-maintainer-mode \
    --disable-werror

  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR=${pkgdir} install 
}
