# Maintainer: Yukicanis <yukicanis@gmail.com>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Edited by: Zachary Lund <admin@computerquip.com>

pkgname=xwayland-git
pkgver=1.14.99.1.226.g7525799
pkgrel=1
pkgdesc="Xorg Server with Wayland Support"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org/"
license=('custom')
depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc'
         'libxdmcp' 'libxfont' 'libpciaccess' 'libdrm' 'pixman>=0.28.0'
         'libgcrypt' 'libxau' 'xf86-input-evdev' 'weston' 'wayland')
conflicts=('xorg-server' 'xorg-server-devel' 'xorg-server-common' 'xwayland')
makedepends=('pixman' 'libx11' 'mesa' 'mesa-libgl' 'xf86driproto' 'xcmiscproto'
             'xtrans' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto'
             'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto'
             'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont'
             'renderproto' 'libpciaccess' 'libxv' 'xf86dgaproto' 'libxmu'
             'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst'
             'libxres' 'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util'
             'glproto' 'dri2proto' 'libgcrypt' 'git' 'mtdev' 'bison')
provides=('xorg-server' 'xorg-server-common' 'xwayland' 'xorg-server-devel')
options=('!libtool')
source=('git+git://anongit.freedesktop.org/xorg/xserver#branch=xwayland'
        xserver-1.6.0-less-acpi-brokenness.patch
        10-quirks.conf)
sha256sums=('SKIP'
            'e033f9bcc21980f7f0428e6ed6c362a3d55ad293b05fd6e6c6c1933b86f9e63a'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166')

prepare() {
  cd $srcdir/xserver
  git reset --hard HEAD
  git clean -dfx

  # From Fedora. Do not build acpid code, it is buggy and we do not need it
  patch -Np1 -i ../xserver-1.6.0-less-acpi-brokenness.patch
}

build() {
  cd $srcdir/xserver
  autoreconf -fi
  ./configure --prefix=/usr \
      --enable-wayland \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package() {
  cd $srcdir/xserver
  make DESTDIR="${pkgdir}" install

  # license
  install -m755 -d "${pkgdir}/usr/share/licenses/xwayland-git"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xwayland-git"
  
  # configuration
  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  # remove empty directories
  rmdir "${pkgdir}/usr/share/X11"
}

pkgver() {
  cd $srcdir/xserver
  git describe --always | sed 's|xorg-server-||g' | sed 's|-|.|g'
}
