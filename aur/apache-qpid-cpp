# Maintainer: Ciriaco Garcia de Celis <cgarcia at etherpilot.com>
pkgname=apache-qpid-cpp
pkgver=0.24
pkgrel=1
pkgdesc="apache qpid C++ broker and client"
arch=('i686' 'x86_64')
url="http://qpid.apache.org/index.html"
license=('APACHE')
makedepends=('util-linux' 'boost' 'python2')
depends=('boost>=1.50')
optdepends=()
source=("http://www.eu.apache.org/dist/qpid/$pkgver/qpid-cpp-$pkgver.tar.gz")
md5sums=('f6245e2e68ccd1bac8fe706db3fba707')

build() {
  cd "$srcdir/qpidc-$pkgver"

  echo "searching python scripts..."
  for py in $(find . -type f | while read f; do head -1 $f | grep -q "^#\!/usr/bin/env.*python$" && echo $f ; done)
  do
    sed -i '1 s/python$/python2/' "${py}"
    echo "patched ${py}"
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-deprecated-autotools

  make all
}

package() {
  cd "$srcdir/qpidc-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
