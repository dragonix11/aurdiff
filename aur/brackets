# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: mac <poczciwiec at gmail dot com>

pkgname=brackets
_build=33
pkgver=sprint$_build
pkgrel=1
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("brackets-lib" "gconf" "nodejs" "nspr" "systemd")
makedepends=("prelink")
optdepends=(
	"google-chrome: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
conflicts=("brackets-git")
install=$pkgname.install

if [[ "$CARCH" == "i686" ]]; then
	source=("https://www.dropbox.com/s/zmnxtgx7cxivrsi/brackets-sprint-$_build-LINUX32.deb")
	sha512sums=("c6a30123a41533e07e7aa918f8bc7618b6d48dc1678396e6a04bc0ec606e3386758f3628c7ae6b78f652f9db66019b4ce401eb2cef0a29bb54c4b4770fe82b42")
elif [[ "$CARCH" == "x86_64" ]]; then
	source=("https://www.dropbox.com/s/08wydlbb24ss4me/brackets-sprint-$_build-LINUX64.deb")
	sha512sums=("cb04d9724d5e4b190fbcb6457b296dac3a8c6a61bc860d3e2442e95aa87e6d7fe13b0cc89c8553048dc9f3f09096dffb4b7640c228ffe4f09401ab8dc9b6f776")
fi

prepare() {
	cd $srcdir

	echo "  -> Extracting files..."
	tar -xf data.tar.gz

	echo "  -> Fixing executable stack..."
	execstack -c opt/$pkgname/Brackets
}

package() {

	echo "  -> Installing program..."

	mkdir $pkgdir/opt
	cp -r opt/$pkgname $pkgdir/opt/$pkgname

	chmod -R a+rw $pkgdir/opt/$pkgname/samples

	install -d $pkgdir/usr/bin
	ln -s /opt/brackets/brackets $pkgdir/usr/bin/$pkgname

	echo "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -d $pkgdir/$_icon_dir/scalable/apps
	install -Dm644 $_icon_dir/scalable/apps/$pkgname.svg $pkgdir/$_icon_dir/scalable/apps/$pkgname.svg
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size=${_icon##*/appshell}
		install -d $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps
		install -Dm644 $_icon $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps/$pkgname.png
	done

	echo "  -> Installing .desktop file..."
	install -Dm755 opt/$pkgname/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop


	echo "  -> Installing license..."
	install -Dm755 usr/share/doc/$pkgname/copyright $pkgdir/usr/share/licenses/$pkgname/copyright
}