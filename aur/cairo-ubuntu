# Maintainer: Ner0
# Contributor: Tevin Zhang <mail2tevin {at} gmail {dot} com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=cairo-ubuntu
pkgver=1.12.16
_ubver=$pkgver-0ubuntu1
pkgrel=1
pkgdesc="Cairo vector graphics library, with Ubuntu's LCD rendering patches"
arch=('i686' 'x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa>=9.1' 'sh' 'lzo2')
makedepends=('mesa>=9.1' 'librsvg' 'gtk2' 'poppler-glib')
provides=("cairo=$pkgver" "cairo-tee=$pkgver")
conflicts=('cairo' 'cairo-cleartype' 'cairo-lcd' 'cairo-tee' 'cairo-xeffects')
options=('!libtool')
source=(http://cairographics.org/releases/cairo-$pkgver.tar.xz
        http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_$_ubver.debian.tar.gz
        cairo-respect-fontconfig.patch)
md5sums=('a1304edcdc99282f478b995ee5f8f854'
         '61cff16b50d0c17d72fdbc5321217b54'
         '79f7c141c49f3d65ab308cc706d50914')

build() {
  cd cairo-$pkgver

  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  patch -Np1 -i "$srcdir/cairo-respect-fontconfig.patch"

  autoreconf -vfi
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--enable-gl \
	--enable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--enable-gobject
  make
}

package() {
  cd cairo-$pkgver
  make DESTDIR="$pkgdir" install
}
