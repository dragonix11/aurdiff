# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxpython-light
pkgver=2.8.12.1
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk-light' 'python2')
makedepends=('mesa')
provides=('wxpython')
conflicts=('wxpython')
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2"
        'wxpython-cairo.patch'
        'wxpython-fpb_default_style.patch')
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5'
          '420700b0a216b853352ffafd054f406a82a30bb3'
          'b832d628b8ff38ea598f404d133899f40d687a22')

prepare() {
  rm -fr "${srcdir}/wxPython-build"
  cp -R "${srcdir}/wxPython-src-${pkgver}" "${srcdir}/wxPython-build"
  cd "${srcdir}/wxPython-build"
}

build() {
  cd "${srcdir}/wxPython-build"
  find . -type f -exec sed -i 's/env python/env python2/' {} \;
  mv wxPython/wx/tools/Editra/editra wxPython/wx/tools/Editra/Editra
  ./configure --prefix=/usr --libdir=/usr/lib --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} --enable-{graphics_ctx,unicode} --disable-{mediactrl,precomp-headers}
  cd "${srcdir}/wxPython-build/wxPython"
  patch -p2 -i "${srcdir}/wxpython-cairo.patch"
  patch -p1 -i "${srcdir}/wxpython-fpb_default_style.patch"
  python2 setup.py WXPORT=gtk2 UNICODE=1 build
}

package() {
  cd "${srcdir}/wxPython-build/wxPython"
  python2 setup.py WXPORT=gtk2 UNICODE=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
