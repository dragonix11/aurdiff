# Maintainer: kevku <kevku@gmx.com>
pkgname=esteidpkcs11loader
_browser=firefox
pkgver=3.7.2.1043
pkgrel=4
pkgdesc="Loads PKCS11 module for web authentication with smart cards (Official version by AS Sertifitseerimiskeskus)"
arch=('any')
url="http://www.id.ee/"
license=('LGPL')
depends=('opensc>=0.13.0' 'esteidcerts' 'ccid')
makedepends=('cmake' 'unzip' 'zip')
conflicts=('esteid-browser-plugin-svn' 'sk-esteidpkcs11loader-svn')
source=("https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz"
	"https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz.asc"
	"001-archlinux-opensc13.patch")
sha256sums=('e6307a66b2b822c51dc0d8e83689d1fdabd5b7c4d1d8a593980de17515561cf3'
            'SKIP'
            '24b6c328c40278753dcd8a26c7a699718781a7bdf59142b956ecf16af3a2e295')
install=esteidpkcs11loader.install
            
prepare() {
 patch -p1 -d "$srcdir/$pkgname-$pkgver" < 001-archlinux-opensc13.patch
}


build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR="lib" -DCMAKE_INSTALL_SYSCONFDIR="/etc"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/lib/$_browser/browser/extensions"
  cp -r "{aa84ce40-4253-a00a-8cd6-0800200f9a66}" "$pkgdir/usr/lib/$_browser/browser/extensions/"
}
