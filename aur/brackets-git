# Maintainer: Danilo Kuehn <dk at nogo-software dot de>
# Git: https://github.com/nogo/archlinux-pkgbuild

_shell=brackets-shell
_brackets=brackets
_cef=cef_binary_3.1547.1354_linux
_node_version=v0.8.20

if test "$CARCH" == i686; then
_cef_download=0B65Ib9j_qgvBcnB4ay11ME1KeU0
_cef_hash=c6e421b5488044c059192ad76071c84a
_node=node-${_node_version}-linux-x86
_node_hash=81346df67d76fed7c08f747082f01135
else
_cef_download=0B65Ib9j_qgvBX1NZZDRQa0oybVk
_cef_hash=d9e2d28ab87933249af9aa7b12ff11ed
_node=node-${_node_version}-linux-x64
_node_hash=03a051d0d5c1f108f3303bdfc89988ce
fi

# Package

pkgname=brackets-git
pkgver=8818.aecaa1c
pkgrel=2
pkgdesc="Adobe Brackets - An open source code editor for the web, written in JavaScript, HTML and CSS."
arch=('i686' 'x86_64')
url="http://brackets.io"
license=('MIT')
depends=('chromium' 'nodejs' 'gtkglext' 'brackets-lib' 'gconf')
makedepends=('git' 'p7zip' 'gtk2' 'gyp-svn' 'nodejs-grunt-cli')
provides=('brackets' 'adobe-brackets')
install=${pkgname}.install
source=("${_shell}::git+https://github.com/adobe/brackets-shell.git"
        "${_brackets}::git+https://github.com/adobe/brackets.git"
        "${_cef}.7z::https://drive.google.com/uc?export=download&id=${_cef_download}"
        "http://nodejs.org/dist/${_node_version}/${_node}.tar.gz")
md5sums=('SKIP'
         'SKIP'
         "${_cef_hash}"
         "${_node_hash}")

pkgver() {
  cd ${srcdir}/${_brackets}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ${srcdir}/${_brackets}
  git submodule update --init --recursive
}

build() {
  cd ${srcdir}/${_shell}

  mkdir deps
  
  # Link CEF into brackets shell
  ln -s ${srcdir}/${_cef} ${srcdir}/${_shell}/deps/cef
  
  # Link Node into brackets shell
  ln -s ${srcdir}/${_node} ${srcdir}/${_shell}/deps/node
  
  # Remove unnecessary resources
  rm -fR Debug include libcef_dll Release Resources tools
  ln -fs deps/cef/include/ include
  ln -fs deps/cef/libcef_dll/ libcef_dll
  ln -fs deps/cef/Release/ Release
  ln -fs deps/cef/Resources/ Resources
  
  # Make
  cp appshell.gyp.txt appshell.gyp
  gyp --depth .
  make
}

package() {
  cd ${srcdir}/${_shell}
  
  install -dm755 "${pkgdir}/opt/brackets"
  cp -R out/Release/lib "${pkgdir}/opt/brackets/lib"
  cp -R out/Release/locales "${pkgdir}/opt/brackets/locales"
  install -Dm755 out/Release/Brackets "${pkgdir}/opt/brackets/brackets"
  install -Dm644 out/Release/cef.pak "${pkgdir}/opt/brackets/cef.pak"
  install -Dm644 out/Release/devtools_resources.pak "${pkgdir}/opt/brackets/devtools_resources.pak"

  install -dm755 "${pkgdir}/usr/bin"
  ln -s /opt/brackets/brackets "$pkgdir/usr/bin/brackets"

  install -dm755 "${pkgdir}/usr/share"
  install -Dm644 installer/linux/debian/brackets.desktop "${pkgdir}/usr/share/applications/brackets.desktop"
  install -Dm644 installer/linux/debian/package-root/usr/share/icons/hicolor/scalable/apps/brackets.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/brackets.svg"
  for size in 32 48 128 256; do
    install -Dm644 "out/Release/appshell${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/brackets.png"
  done
  
  cd ${srcdir}/${_brackets}
  # Copy samples
  cp -R "samples" "${pkgdir}/opt/brackets/samples"
  # Copy www
  cp -R "src" "${pkgdir}/opt/brackets/www"

  if [ ! -f "/usr/bin/google-chrome" ]; then
    ln -s /usr/bin/chromium "$pkgdir/usr/bin/google-chrome"
  fi
}
