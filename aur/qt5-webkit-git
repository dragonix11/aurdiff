# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=qt5-webkit-git
pkgver=v5.1.1.44.gee8436b
pkgrel=1
pkgdesc="A cross-platform application and UI framework (Webkit)"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('qt5-declarative' 'mesa' 'gstreamer0.10-base')
makedepends=('git' 'ruby' 'gperf' 'python2')
source=("git://gitorious.org/qt/qtwebkit.git#branch=dev")
sha256sums=('SKIP')
_gitname="qtwebkit"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

prepare() {
	cd "$srcdir/$_gitname"

	sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
		-e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
		$(find . -name '*.py')
	sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
		-e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
		-e "s|python |python2 |" \
		$(find . -name '*.pr?') $(find . -name '*.make')
}

build() {
	cd "$srcdir/$_gitname"
	qmake
	make -j10
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
}
