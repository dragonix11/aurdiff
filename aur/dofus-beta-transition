# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>
pkgname=dofus-beta-transition
provides=('dofus-beta')
conflicts=('dofus-beta')
pkgver=2.14.0
pkgrel=13
pkgdesc="A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR (beta version)."
url="http://www.dofus.com/"
license=("custom:Dofus License")
arch=('i686' 'x86_64')
source=(http://dl.ak.ankama.com/games/linux/dofus-beta-linux-package.tar.gz
        air-generic-launcher.sh
        transition.conf.patch)
md5sums=('8cc33456db1681eae1918b2eb5fb77a3'
         'f179eaa5e6e6674b1853cf826fc33c3a'
         '7bedff08ade48480d5c56e14fd81c815')
install=dofus.install

depends=('ankama-transition' 'adobe-air-sdk')
if [ "$CARCH" == "x86_64" ];then
  depends+=('lib32-gtk2' 'lib32-libxt' 'lib32-libjpeg6' 'lib32-libpng12'
            'lib32-openssl098' 'lib32-libldap' 'lib32-nss' 'lib32-libxslt'
            'lib32-dbus-glib' 'lib32-alsa-lib')
else
  depends+=('gtk2' 'libxt' 'libjpeg6' 'libpng12' 'openssl098' 'nss' 'libxslt' 'alsa-lib')
fi

# Use rename to replace 'dofus' to 'dofus-beta' in installed file and directory names
makedepends=('util-linux')

prepare() {
  cd "$srcdir"
  msg2 "Patching transition configuration to use adl-based launchers"
  patch -p1 < transition.conf.patch

  chmod +x opt/ankama/dofus/ankama-dofus

  msg2 "Renaming files to avoid conflict with dofus package"
  sed -i 's/dofus/dofus-beta/' usr/share/applications/ankama-dofus.desktop
  # rename will fail if the target directory exists from a previous build
  rm -rf opt/ankama/dofus-beta/
  find usr '!' -name '*dofus-beta*' -exec rename -v dofus dofus-beta opt/ankama/dofus '{}' +
  ln -sf /opt/ankama/dofus-beta/ankama-dofus usr/bin/ankama-dofus-beta
}

package() {
  cd "$srcdir"
  msg2 "Installing main applications..."
  cp -r usr opt "$pkgdir"
  chgrp -R games "$pkgdir/opt/ankama/dofus-beta"
  chmod -R g+w "$pkgdir/opt/ankama/dofus-beta"

  msg2 "Installing adl based launchers..."
  install air-generic-launcher.sh $pkgdir/opt/ankama/dofus-beta/bin/
  install air-generic-launcher.sh $pkgdir/opt/ankama/dofus-beta/share/reg/bin/

  msg2 "Creating fake Adobe Air so that the updater will not try to install it..."
  ln -s /usr/bin/true "$pkgdir/usr/bin/Adobe AIR Application Installer"
}
# vim:set ts=2 sw=2 et:
