pkgname=mingw-w64-libftdi1
pkgver=1.0
pkgrel=1
license=('GPL2' 'LGPL2.1')
arch=(any)
pkgdesc='Library to talk to FTDI chips (mingw-w64)'
url='http://www.intra2net.com/en/developer/libftdi/'
depends=('mingw-w64-crt')
makedepends=('mingw-w64-libusbx' 'mingw-w64-confuse' 'cmake' 'mingw-w64-gcc' 'python' 'swig' 'git')
options=('!libtool' '!strip')
# source=(http://www.intra2net.com/en/developer/libftdi/download/libftdi-$pkgver.tar.bz2)
# sha1sums=('5be76cfd7cd36c5291054638f7caf4137303386f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  rm -rf libftdi-$pkgver
  git clone git://developer.intra2net.com/libftdi libftdi-$pkgver
  cd libftdi-$pkgver
  sed -i "s| \${LIBINTL_LIBRARIES}||g" ftdi_eeprom/CMakeLists.txt
  sed -i "s| \${LIBINTL_INCLUDE_DIR}||g" ftdi_eeprom/CMakeLists.txt
  for _arch in ${_architectures}; do
    unset LDFLAGS CFLAGS CPPFLAGS
    export PKG_CONFIG_LIBDIR=/usr/${_arch}/lib/pkgconfig/
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    cp ../Toolchain-mingw32.cmake Toolchain-mingw-w64.cmake
    sed -i "s|i386-mingw32msvc-gcc|${_arch}-gcc|g" Toolchain-mingw-w64.cmake
    sed -i "s|i386-mingw32msvc-g++|${_arch}-g++|g" Toolchain-mingw-w64.cmake
    sed -i "s|/opt/cross/i386-mingw32msvc|/usr/${_arch}|g" Toolchain-mingw-w64.cmake
    echo "set (CMAKE_RC_COMPILER ${_arch}-windres)" >> Toolchain-mingw-w64.cmake
    cmake -DCMAKE_INSTALL_PREFIX=/usr/${_arch} \
          -DCMAKE_TOOLCHAIN_FILE=Toolchain-mingw-w64.cmake \
          -DEXAMPLES=OFF ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/libftdi-${pkgver}/build-${_arch}
    make install DESTDIR=${pkgdir}
    mv $pkgdir/usr/${_arch}/bin/*.a $pkgdir/usr/${_arch}/lib
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a    
  done
}

