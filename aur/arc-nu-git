# Maintainer: Josh McGee <josh.s.mcgee@gmail.com>
# Contributor: numeral <numerical@gmail.com>

pkgname=arc-nu-git
pkgver=137.3ac7eef
pkgrel=1
pkgdesc='Rewrite of Arc 3.1 with more features, more speed, and bug fixes. Still compatible with Arc 3.1.'
arch=('i686' 'x86_64')
url='https://github.com/arclanguage/arc-nu'
license=('Artistic2.0')
groups=('lisp')
depends=('racket')
makedepends=('git')
provides=('arc-nu')
source=("$pkgname::git+https://github.com/arclanguage/arc-nu.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf '%s.%s\n' "$(git rev-list --count HEAD)" \
                   "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
  git submodule update --init --recursive
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir"/usr/{share/arc-nu,bin}
  cp -a * "$pkgdir"/usr/share/arc-nu
  rm -f "$pkgdir/usr/share/${pkgname%-git}/LICENSE"
  cd "$pkgdir"
  ln -s "../share/${pkgname%-git}/arc" "usr/bin/${pkgname%-git}"
}

# vim:set ts=2 sw=2 et:
