# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-vbam-git
pkgver=1080.5716240
pkgrel=1
pkgdesc="libretro implementation of VBA-M. (Game Boy Advance)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/vbam-libretro"
license=('GPL')
makedepends=('git')

_gitname=vbam-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/vbam_libretro.info")
md5sums=('SKIP'
         '42629304088b6be8f372374e7cbc14d3')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/src/libretro"
  make
}

package() {
  install -Dm644 "${_gitname}/src/libretro/vbam_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-vbam.so"
  install -Dm644 "vbam_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-vbam.info"
}
