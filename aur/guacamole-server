# Maintainer: Lara Maia <lara@craft.net.br>

pkgname=guacamole-server
pkgver=0.8.3
pkgrel=1
pkgdesc="Guacamole proxy daemon"
arch=('i686' 'x86_64')
url="http://guacamole.sourceforge.net/"
license=('GPL3')

replaces=('guacd'
          'libguac'
          'libguac-client-ssh'
          'libguac-client-vnc'
          'libguac-client-rdp')

depends=('pango' 'openssl' 'libvorbis')

makedepends=('libpulse' 'libvorbis'
             'openssl' 'libssh'
             'libvncserver' 'pango'
             'freerdp')

optdepends=('libssh: for ssh protocol support'
            'libvncserver: for vnc protocol support'
            'freerdp: for rdp protocol support'
            'libpulse: for pulseaudio support')

source=("http://downloads.sourceforge.net/project/guacamole/current/source/$pkgname-$pkgver.tar.gz"
        'guacd.service')

md5sums=('69238b3096c0b6baa5008d9914e8fdab'
         'fc71eab0f9a7f11fad7686267e3bd143')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr
  
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/etc/systemd/system/
  install -Dm644 "$srcdir"/guacd.service "$pkgdir"/etc/systemd/system/
}
