pkgname=mingw-w64-libwebp
pkgver=0.3.1
pkgrel=2
pkgdesc="WebP library (mingw-w64)"
arch=(any)
url="http://code.google.com/intl/en/speed/webp"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-libjpeg-turbo
mingw-w64-libpng
mingw-w64-libtiff
mingw-w64-freeglut)
options=(staticlibs !strip !buildflags)
source=("http://webp.googlecode.com/files/libwebp-$pkgver.tar.gz")
md5sums=('dc862bb4006d819b7587767a9e83d31f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-swap-16bit-csp \
      --enable-experimental \
      --enable-libwebpmux \
      --enable-libwebpdemux \
      --enable-libwebpdecoder
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
