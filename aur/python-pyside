# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jan-Erik Meyer-Luetgens <nyan at meyer-luetgens dot de>
pkgname=python-pyside
_pkgrealname=pyside
pkgver=1.2.1
pkgrel=1
pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation."
arch=('i686' 'x86_64')
license=('LGPL')
url="http://qt-project.org/wiki/PySide"
_qtver=4.8
depends=('python' "qt4>=${_qtver}" 'phonon' "python-shiboken>=$pkgver")
makedepends=('cmake' 'automoc4' 'qtwebkit')
optdepends=('qtwebkit: for PySide.QtWebKit')
source=("http://download.qt-project.org/official_releases/pyside/${_pkgrealname}-qt${_qtver}+$pkgver.tar.bz2")
md5sums=('34b05faa7cc44d3c24d5ccadd894bd3c')

build(){
    cd $srcdir/${_pkgrealname}-qt${_qtver}+$pkgver
    mkdir -p build && cd build
    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_TESTS=OFF \
      -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DPYTHON_SUFFIX=.${_pyverflags}
    make
}

package(){
    cd $srcdir/${_pkgrealname}-qt${_qtver}+$pkgver/build
    make DESTDIR=$pkgdir install
}

