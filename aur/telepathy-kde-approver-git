# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: laloch <laloch@atlas.cz>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-approver-git
_gitname="ktp-approver"
pkgver=v0.6.80.0.g260c17a
pkgrel=1
epoch=2
pkgdesc="KDE Channel Approver for Telepathy."
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-approver"
license=('GPL')
depends=('kdelibs' 'telepathy-qt')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('telepathy-kde-approver'
           'telepathy-approver-git')
source=("git://anongit.kde.org/ktp-approver.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
