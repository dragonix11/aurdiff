# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=quvi-git
pkgver=0.9.3.1
pkgrel=1
pkgdesc="Command line tool for parsing flash media stream URLs. (GIT version)"
arch=('i686' 'x86_64')
url="http://quvi.sourceforge.net/"
license=('custom:LGPLv2.1+')
depends=('libquvi-git')
makedepends=('git')
options=('!libtool')
provides=('quvi')
conflicts=('quvi')
source=('git://repo.or.cz/quvi-tool.git'
        'am_subdir_objects.patch')
sha256sums=('SKIP'
            '28c2a9464017a93f00010ad1efcd20fe31ddc3687ad3b09f9785b8ffdacf7b1c')

pkgver() {
  cd 'quvi-tool'
  git describe | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd 'quvi-tool'
  patch -p1 < "$srcdir/am_subdir_objects.patch"
}

build() {
  cd 'quvi-tool'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd 'quvi-tool'

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

# vim: ft=sh syn=sh sw=2 ts=2 et
