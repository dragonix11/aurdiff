# Maintainer: Sam S. <smls75@gmail.com>

pkgname=eetsmunchies-hib
pkgver=beta85528+h20130919
_hibver=1379626460
pkgrel=1
pkgdesc='Eets Munchies, a family-friendly puzzle game (beta; Humble Bundle version)'
url='http://kleientertainment.com/games/eets-munchies/'
arch=('i686' 'x86_64')
license=('custom: commercial')
depends=('sdl2' 'libgl' 'xdg-utils')
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

[ $CARCH == i686 ] && { _arch=32 && _md5='b043516abd229dffde278a3cf31f2b43' ;} \
                   || { _arch=64 && _md5='8bc27d422dbd5b959baa1e1add89ca51' ;}

source=("hib://Eets_Munchies_${_hibver}_${_arch}.tar.gz"
        'eetsmunchies-hib.desktop')
md5sums=("$_md5"
         '36154f360150b2812a8e2fd44b1d8021')

package() {
  cd "$srcdir"
  _target=/opt/EetsMunchies
  
  # Install game files
  mkdir -p "$pkgdir/$_target"
  cp -TRl eets2 "$pkgdir/$_target"
  
  # Remove unneeded files
  rm "$pkgdir/$_target"/bin/lib/libSDL2*
  
  # Install icon & desktop entry
  install -Dm644 eets2/eets2.xpm "$pkgdir"/usr/share/pixmaps/eetsmunchies.xpm
  install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  
  # Install launch script
  echo -e "#!/bin/sh\ncd $_target/bin && exec ./eets2 \$*" > launcher.sh
  install -Dm755 launcher.sh "$pkgdir"/usr/bin/eetsmunchies
}
