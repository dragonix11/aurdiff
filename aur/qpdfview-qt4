# Maintainer: Boris Vasilyev <bobartmail@gmail.com>

pkgname=qpdfview-qt4
_pkgname=${pkgname%-qt4}
pkgver=0.4.6beta1
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler-qt4 library.'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('libcups' 'poppler-qt4' 'qt4')
optdepends=('libspectre: for PostScript support' 'djvulibre: for DjVu support')
makedepends=('automoc4')
provides=('qpdfview')
replace=('qpdfview')
conflicts=('qpdfview')
install='qpdfview-qt4.install'
source=("http://launchpad.net/qpdfview/trunk/$pkgver/+download/qpdfview-$pkgver.tar.gz")
md5sums=('53e002a6230481df8e167db9c63ad794')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  lrelease-qt4 qpdfview.pro
  qmake-qt4 "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make "INSTALL_ROOT=$pkgdir" install
}

