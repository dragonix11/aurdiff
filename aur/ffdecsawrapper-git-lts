# Submitter:   Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Tycho LÃ¼rsen "bas-t" (responsible for hosting and development of FFdecsawrapper)
# Contributor: Petr Vacek "vaca" (contributing code from sascng-linux3-patch and cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (implementing new procfs API for kernels >= 3.10 in FFdecsawrapper kernel module)

pkgname=ffdecsawrapper-git-lts
pkgver=1
pkgrel=1
pkgdesc="FFdecsa empowered softcam (newest generation of open-sasc-ng) - for ARCH LTS kernel"
url=https://github.com/bas-t/ffdecsawrapper.git
arch=('i686' 'x86_64')
license=('GPLv3')
depends=('v4l-utils')
makedepends=('git' 'linux-lts-headers')
conflicts=('ffdecsawrapper' 'sasc-ng')
provides=('ffdecsawrapper')
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/ffdecsawrapper' 'etc/camdir/cardslot.conf')
install='ffdecsawrapper.install'

_basekernel=3.10

	if [ `uname -r | cut -c 1-4` != "$_basekernel" ]; then
		msg "Your kernel version is ""`uname -r`"  
	 	msg "This package only works with ""$_basekernel"" based kernels"
		exit 1
	fi

source=('git://github.com/bas-t/ffdecsawrapper.git#branch=stable'
	"http://www.kernel.org/pub/linux/kernel/v3.x/linux-$_basekernel.tar.bz2" \
	'cardclient.conf' 'ffdecsawrapper.rc' 'ffdecsawrapper.conf' \
	'ffdecsawrapper.lr' 'cardslot.conf' 'ffdecsawrapper.service' \
	'ffdecsawrapper.install' 'kernel-mutex.patch')

sha256sums=('SKIP'
            '46c9e55e1fddf40813b8d697d5645037a8e2af5c1a8dff52b3fe82b5021582b8'
            '5c23db2b93d1accdc0b3f1612766de38bf7ede5658f6ef973706988dd71d1b81'
            'c20155f72ea47683bc2b6ea1471184419db1bc41b97c1bb9e2018b180c9474eb'
            '6d1132cbf0cbbd7dd62cb70e171f046e62805ea7ca9db909da63f3d65b52ae57'
            'f435344dc9f1c0ed7c2e0de74ec434cd73e2130a0d7589a4d38338e45925d8db'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '1c3d9ff15aa3a5aaf3c75adc99ec83af064875c52311795683b6c7efb32e1f56'
            '94ff9346b4ad866c4c698d01d3c4f72548ce7472935e9f71ef870bee8d26b0ae'
            '7546b2822b604bc79deca4d04c33bbf17956cd1e9f4f74e212704b6152967691')

pkgver() {

	cd $srcdir/ffdecsawrapper
 	_gitver=`git describe --always | sed 's|-|.|g'`
	_kernel=`uname -r | sed -r 's/-/_/g'`
	echo "$_kernel"_"$_gitver"
}

prepare() {

	msg "Applying kernel-mutex patch..."
	cd $srcdir/linux-$_basekernel
	patch -p1 < $srcdir/kernel-mutex.patch
}

build() {

	cd $srcdir/ffdecsawrapper      
	./configure --optimize_exp
  
	cd $srcdir/linux-$_basekernel/drivers/media/dvb-core
	make -C /usr/lib/modules/`uname -r`/build M=$(pwd) modules
}

package() {

	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/updates/ffdecsawrapper
	mkdir -p $pkgdir/etc/conf.d
	mkdir -p $pkgdir/etc/rc.d
	mkdir -p $pkgdir/etc/camdir
	mkdir -p $pkgdir/etc/logrotate.d/
	mkdir -p $pkgdir/usr/lib/systemd/system/
      
	install -m0755 $srcdir/ffdecsawrapper/ffdecsawrapper  $pkgdir/usr/bin/
	install -m0644 $srcdir/ffdecsawrapper/dvbloopback.ko  $pkgdir/usr/lib/modules/`uname -r`/updates/ffdecsawrapper
	install -m0755 $srcdir/ffdecsawrapper.rc  $pkgdir/etc/rc.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.conf  $pkgdir/etc/conf.d/ffdecsawrapper
	install -m0644 $srcdir/cardclient.conf  $pkgdir/etc/camdir/cardclient.conf
	install -m0644 $srcdir/cardslot.conf  $pkgdir/etc/camdir/cardslot.conf
	install -m0644 $srcdir/ffdecsawrapper.lr  $pkgdir/etc/logrotate.d/ffdecsawrapper-git.lr
	install -m0644 $srcdir/ffdecsawrapper.service  $pkgdir/usr/lib/systemd/system/ffdecsawrapper.service
	install -m0644 $srcdir/linux-$_basekernel/drivers/media/dvb-core/dvb-core.ko  $pkgdir/usr/lib/modules/`uname -r`/updates/ffdecsawrapper
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
