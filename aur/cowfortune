# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=cowfortune
pkgver=0.1.2
pkgrel=2
pkgdesc="A configurable fortune cookie proclaiming cow (and a few other creatures)"
url="https://github.com/anthraxx/cowfortune"
arch=('any')
license=('MIT')
depends=('cowsay' 'fortune-mod')
optdepends=('lolcat: rainbow coloring support')
backup=('etc/cowfortune/config' 'etc/cowfortune/whitelist' 'etc/cowfortune/blacklist')
install=cowfortune.install
source=(https://github.com/anthraxx/cowfortune/archive/v${pkgver}.tar.gz)
sha256sums=('b4bfd5f5af4c088e34bfc0551b2eafc172f7f19a42f199204275c499eaac73cd')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
