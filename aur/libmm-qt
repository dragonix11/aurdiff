# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt
pkgver=0.5.0
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4')
makedepends=('cmake')
source=("http://download.kde.org/unstable/modemmanager-qt/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
md5sums=('c1a24e43f45ca5c1a66f6558f8749621')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
