#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-aur
pkgver=2013.8
pkgrel=2
pkgdesc='AUR-related modules and helper utilities (aurploader, aurquery, aurtomatic).'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/python3-aur"
depends=(python3 python-xdg)
optdepends=('curl: Aurploader support')
replaces=(aurploader)
conflicts=(aurploader)
provides=(aurploader)
source=(
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2013.8.tar.xz
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2013.8.tar.xz.sig
)
sha512sums=(
  a629d9116124313a26f0e5d86f5dfb962529d822c7e446702b91043844ef08fc85e5c1c38b16a1824811d6eecb2aa49474c9cc47f7978e050ebe2265c193563b
  8803beb87110ab8864b1b0a75c811c9865d7be2bc67684970d1e0727dccf0ec9eef6597a0b623248715d82db8b8a743d0e0966d3b49235cc560d5a6cd0792bfe
)
md5sums=(
  7f0df822c463db5bb7f8a702faba20fe
  559b82e1e6c32281fe4aa45f22515cf9
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  for aurfoo in aur*
  do
    if [[ -x "$aurfoo" ]]
    then
      install -Dm755 "$aurfoo" "$pkgdir/usr/bin/$aurfoo"
    fi
  done
}


# vim: set ts=2 sw=2 et:
