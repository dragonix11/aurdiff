# Maintainer: La√©rcio de Sousa <lbsousajr AT gmail DOT com>
# Contributor: David Gippner davidgippner at googlemail dot com
pkgbase=gregorio
# Small hack to make AUR accept split packages
pkgname=$pkgbase
true && pkgname=("gregorio" "gregoriotex")
pkgver=2.3
pkgrel=5
pkgdesc="Command-line tool to typeset Gregorian chant"
url=http://home.gna.org/gregorio
arch=("i686" "x86_64")
license=("GPL")
depends=("libltdl")
makedepends=("libxml2" "docbook2x" "libxslt" "glib2" "docbook-xsl")
conflicts=("gregorio-svn")
source=("http://download.gna.org/$pkgbase/releases/$pkgver/$pkgbase-$pkgver-src.tar.gz")
md5sums=("792489a21aed61d8976f98374cc417c8")
sha256sums=("51f95d2c9d8bac2adce3c7ca4c9a9000027c820e9418e372d4f5382181953c2c")

prepare() {
  cd $srcdir/$pkgbase-$pkgver/fonts
  mv Makefile Makefile.old
  grep -v texhash Makefile.old > Makefile
}

build() {
  cd $srcdir/$pkgbase-$pkgver
  msg "Starting make..."
  msg "Autoreconf'ing sources..."
  autoreconf -f -i
  msg "Configuring..."
  ./configure --prefix=/usr --enable-glib-utf8 || return 1
  msg "Compiling..."
  make || return 1
}

package_gregorio() {
  cd $srcdir/$pkgbase-$pkgver
  msg "Installing binaries, headers and libraries..."
  make DESTDIR=$pkgdir install
}

package_gregoriotex() {
  pkgdesc="LuaTeX style and fonts for Gregorian chant scores"
  depends=("texlive-fontsextra" "texlive-bin" "texlive-formatsextra")
  install=gregoriotex.install

  msg "Installing GregorioTeX fonts and TeX files..."
  cd $srcdir/$pkgbase-$pkgver/fonts
  make TEXMFROOT=$pkgdir/usr/share/texmf-dist install
}
