pkgname=mingw-w64-libtheora
pkgver=1.1.1
pkgrel=5
pkgdesc="An open video codec developed by the Xiph.org (mingw-w64)"
arch=(any)
url="http://xiph.org"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-libogg
mingw-w64-libvorbis)
options=(staticlibs !strip !buildflags)
source=("http://downloads.xiph.org/releases/theora/libtheora-${pkgver}.tar.bz2")
md5sums=('292ab65cedd5021d6b7ddd117e07cd8e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "libtheora-$pkgver"
  sed -i "s,EXPORTS,," "win32/xmingw32/libtheoradec-all.def"
  sed -i "s,EXPORTS,," "win32/xmingw32/libtheoraenc-all.def"
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/libtheora-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    sed -i 's,png2theora$(EXEEXT),,g' Makefile
    sed -i 's,png2theora$(EXEEXT),,g' "examples/Makefile"
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}