# Maintainer:  Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Juha-Matti "suolx" Heikkala (previous maintainer)
# Contributor: bas-t (providing patches for kernels > 3.7 and hosting GIT repo)
# Contributor: Petr Vacek "vaca" (contributing patch and code from sascng-linux3-patch and providing cardslot.conf for serial port SC readers)

# There are 2 choices for _version, define as needed:
# stable - version 570: most universally stable version for all systems and distro's
# final -  version 620: most recent version of sasc-ng at development stop and stable on most systems

_version=final

# !! BEFORE USE, UNCOMMENT THIS:
#pkgver=$_version_`uname -r | sed -r 's/-/_/g'`

# !! AND COMMENT THIS:
pkgver=uname_r

pkgname=open-sasc-ng
pkgrel=1
pkgdesc="A SoftCAM creating virtual DVB interface"
url="https://github.com/bas-t/sasc.git"
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux-headers')
makedepends=('git' 'linux-headers>=3.9' 'linux-headers<3.10')
conflicts=(sasc-ng)
provides=(sasc-ng)
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/sasc-ng' 'etc/camdir/cardslot.conf')
install='sasc-ng.install'

_basekernel=3.9
_gitroot="git://github.com/bas-t/sasc.git"
_gitdir="sc"
_gitbuild="sc-build"

source=("http://www.kernel.org/pub/linux/kernel/v3.x/linux-$_basekernel.tar.bz2" \
	'cardclient.conf' 'sasc-ng.rc' 'sasc-ng.conf' \
	'open-sasc-ng.lr' 'cardslot.conf' 'sasc-ng.service' \
	'sasc-ng.install' 'rev-620.patch' 'rev-570.patch' kernel.patch)

sha256sums=('97e48f31ed2197f4e7e4938d4fab8da522cf80e60c6ce69668b0805904499305'
            '7caba03e515fe55aced4aad831e72ef3c0e59a3cdcea7bcdf79f7bfff6fcec75'
            '99ad8ff3733d5eca5885081ad6fec65d6b3febd79f992456a3415b17db9d7aa0'
            '0576abd9001ac17437d7613cadd51a000f8f78423b27f63af089a091dba1bd95'
            '620da70c775ce055a3f04041cf90e6d2acf7f7a57b0eecd07f240456d0069cf4'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '95975775dbb35e6fac0f42bc7f0da4de6affe2018ecba40217aa0082cd47262f'
            'a5b3bf80c26d10765dc5881aed2faee00942cdb7d1123003f19f66b266b26933'
            'ef306c8b9a87a7dbaebe8258785176abf64d6595dfde8117ea0e74f86da0004b'
            '7f07f13a1d8e7c10f9b0aee4d2df3ec53311e95b8b0783910ae396a0a18d9b8a'
            '7546b2822b604bc79deca4d04c33bbf17956cd1e9f4f74e212704b6152967691')

if [ $_version == "stable" ]; then
	_gitbranch="rev-570"  
else
  	_gitbranch="rev-620"
fi

prepare() {

 	if [ ! -d "$srcdir/$_gitbranch" ]; then
		git clone -b "$_gitbranch" "$_gitroot" "$_gitbranch/$_gitdir"
	fi

	rm -rf "$srcdir/$_gitbuild"
	cp -r  "$srcdir/$_gitbranch" "$_gitbuild"

	msg "Applying patches..."

	cd "$srcdir/$_gitbuild"
	patch -p1 < "$srcdir/$_gitbranch.patch"
	cd "$srcdir/linux-$_basekernel"
	patch -p1 < "$srcdir/kernel.patch"

	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/configure"
	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/makelinks.sh"
	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/dvbloopback/module/config_dvb.pl"
}

build() {

	cd "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng"      
	./configure 
      
	make module || return 1
	make || return 1

	cd "$srcdir/linux-$_basekernel/drivers/media/dvb-core"
	make -C /usr/lib/modules/`uname -r`/build M=$(pwd) modules
}

package() {

	mkdir -p "$pkgdir/usr/bin"
	mkdir -p "$pkgdir/usr/lib/modules/`uname -r`/extra"
	mkdir -p "$pkgdir/etc/conf.d"
	mkdir -p "$pkgdir/etc/rc.d"
	mkdir -p "$pkgdir/etc/camdir"
	mkdir -p "$pkgdir/etc/logrotate.d/"
	mkdir -p "$pkgdir/usr/lib/systemd/system/"
	mkdir -p "$pkgdir/usr/lib/modules/`uname -r`/kernel/drivers/media/dvb-core"
      
	install -m0755 "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/sasc-ng" "$pkgdir/usr/bin/"
	install -m0644 "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/dvbloopback.ko" "$pkgdir/usr/lib/modules/`uname -r`/extra"
	install -m0755 "$srcdir/sasc-ng.rc" "$pkgdir/etc/rc.d/sasc-ng"
	install -m0644 "$srcdir/sasc-ng.conf" "$pkgdir/etc/conf.d/sasc-ng"
	install -m0644 "$srcdir/cardclient.conf" "$pkgdir/etc/camdir/cardclient.conf"
	install -m0644 "$srcdir/cardslot.conf" "$pkgdir/etc/camdir/cardslot.conf"
	install -m0644 "$srcdir/open-sasc-ng.lr" "$pkgdir/etc/logrotate.d/open-sasc-ng.lr"
	install -m0644 "$srcdir/sasc-ng.service" "$pkgdir/usr/lib/systemd/system/sasc-ng.service"
	install -m0644 "$srcdir/linux-$_basekernel/drivers/media/dvb-core/dvb-core.ko" "$pkgdir/usr/lib/modules/`uname -r`/kernel/drivers/media/dvb-core"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
