# Maintainer: Prurigro
# Contributor: Prurigro

pkgname=sbagen
pkgver=1.4.5
_riverver=1.4.1
_rhybagver=0.1.1
pkgrel=2
pkgdesc="A binural brainwave generator"
url="http://uazu.net/sbagen"
license=('GPL2')
depends=('alsa-oss' 'libmad' 'libvorbis')
optdepends=('aoss: speed up the build process by skipping the need to compile cmake')
arch=('i686' 'x86_64')
install=sbagen.install

source=("http://uazu.net/sbagen/${pkgname}-${pkgver}.tgz" "http://uazu.net/sbagen/sbagen-river-$_riverver.tgz" "http://uazu.net/sbagen/rhybag-$_rhybagver.tgz")
sha256sums=('02b05d0f89f1baa6e6b282f4a5db279b4c59ee6fc400a5a9686aa11287f220e4' '81545ec71461421f938dc2febd9379dc36886a84df30deee20cd43eae81a5765' 'd858140119b477e722ea3b6233aa670c3a189929297992c292686f785ef2838c')

prepare() {
    rm "${srcdir}/${pkgname}-${pkgver}/readme-windows.txt"
}

build() {
    #Compile sbagen
    cd "${srcdir}/${pkgname}-${pkgver}"
    gcc -DT_LINUX -Wall -O3 -s -lm -lpthread -lmad -lvorbis sbagen.c -o sbagen

    #Compile rhybag
    cd "${srcdir}/rhybag-${_rhybagver}"
    cc -O2 -s -lm main.c -o rhybag 2>&1 1>&0 | ./strip_warnings >mk.err
}

package() {
    #Install sbagen
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    #Install sbagen as sbagen-oss, a script to launch with alsa as sbagen-alsa, and link sbagen to sbagen-alsa
    install -D -m755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}-oss"
    echo -e '#!/bin/bash'"\n\naoss ${pkgname}-oss "'"$@"' > "${pkgdir}/usr/bin/${pkgname}-alsa"
    chmod 755 "${pkgdir}/usr/bin/${pkgname}-alsa"
    ln -s ${pkgname}-alsa "${pkgdir}/usr/bin/${pkgname}"
    
    install -d "${pkgdir}/usr/share/${pkgname}/doc"
    install -D -m644 *.txt "${pkgdir}/usr/share/${pkgname}/doc"
    install -d "${pkgdir}/usr/share/${pkgname}/examples/basics"
    install -D -m644 examples/basics/*.sbg "${pkgdir}/usr/share/${pkgname}/examples/basics"
    install -d "${pkgdir}/usr/share/${pkgname}/examples/contrib/ghostlab"
    install -D -m644 examples/contrib/ghostlab/*.sbg "${pkgdir}/usr/share/${pkgname}/examples/contrib/ghostlab"
    install -d "${pkgdir}/usr/share/${pkgname}/examples/contrib/jim"
    install -D -m644 examples/contrib/jim/*.sbg "${pkgdir}/usr/share/${pkgname}/examples/contrib/jim"
    install -D -m644 examples/contrib/*.sbg "${pkgdir}/usr/share/${pkgname}/examples/contrib"
    install -d "${pkgdir}/usr/share/${pkgname}/examples/focus"
    install -D -m644 examples/focus/*.sbg "${pkgdir}/usr/share/${pkgname}/examples/focus"
    install -d "${pkgdir}/usr/share/${pkgname}/scripts"
    install -D -m755 scripts/* "${pkgdir}/usr/share/${pkgname}/scripts"

    #Install rhybag
    cd "${srcdir}/rhybag-${_rhybagver}"
    install -D -m755 rhybag "${pkgdir}/usr/bin/rhybag"

    #Install river sounds
    cd "${srcdir}/${pkgname}-${_riverver}"
    install -D -m644 river1.ogg "${pkgdir}/usr/share/sbagen/river1.ogg"
    install -D -m644 river2.ogg "${pkgdir}/usr/share/sbagen/river2.ogg"
}
