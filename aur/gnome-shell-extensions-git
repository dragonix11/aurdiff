# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Diego Principe <cdprincipei at gmail dot com>
# Contributor: Brett Lempereur <sordidpanda at gmail dot com>
# Contributor: Flamelab <panosfilip at gmail dot com>

pkgname=gnome-shell-extensions-git
pkgver=3.8.3.1.real.10
pkgrel=1
pkgdesc="Extensions for GNOME shell"
arch=('any')
url="http://live.gnome.org/GnomeShell/Extensions"
license=('GPL2')
depends=('gnome-shell' 'libgtop')
makedepends=('git' 'gnome-common' 'intltool')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=("git+http://git.gnome.org/browse/${pkgname%-*}#branch=gnome-3-8")
sha256sums=('SKIP')

_ext="alternate-tab alternative-status-menu apps-menu auto-move-windows drive-menu example launch-new-instance native-window-placement places-menu systemMonitor user-theme window-list windowsNavigator workspace-indicator xrandr-indicator"

pkgver() {
  cd ${pkgname%-*}

  git describe | sed 's|\(.*-.*\)-.*|\1|;s|-|.|g'
}

build() {
  cd ${pkgname%-*}

  ./autogen.sh --prefix='/usr' --disable-schemas-compile --enable-extensions="${_ext}"
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
