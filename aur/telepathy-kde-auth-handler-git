# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Adria Arrufat <swiftscythe@gmail.com>

pkgname=telepathy-kde-auth-handler-git
_gitname="ktp-auth-handler"
pkgver=v0.6.80.0.g1160497
pkgrel=1
epoch=2
pkgdesc="Provides UI/KWallet Integration For Passwords and SSL Errors on Account Connect"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-auth-handler"
license=("LGPL")
depends=('telepathy-kde-common-internals-git' 'qjson' 'libaccounts-qt' 'signon' 'qca-ossl')
makedepends=('git' 'cmake' 'automoc4')
source=("git://anongit.kde.org/ktp-auth-handler.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=$( kde4-config --prefix )\
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
