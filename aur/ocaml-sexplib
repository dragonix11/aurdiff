# Maintainer: Aaron Chen <nextAaron at gmail.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>

_pkgname=sexplib
pkgname=ocaml-$_pkgname
pkgver=109.41.00
pkgrel=1
pkgdesc="automated S-expression conversion"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=4.00.0' 'ocaml-type_conv>=109.20.00')
makedepends=('ocaml-findlib>=1.3.2')
options=(!strip !makeflags)
url="http://janestreet.github.com"
source=("https://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('a3bf36a28a2804b955eb69c867d02469')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  mkdir -p $OCAMLFIND_DESTDIR
  make install
  install -Dm 644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
