# Maintainer: Piotr Gorski <sir_lucjan@bridgelinux.pl>

pkgname=xfwm4-4.11
_pkgname=xfwm4
pkgver=4.11.0
pkgrel=2
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui-4.11' 'libwnck' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=$_pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.11/$_pkgname-$pkgver.tar.bz2)
sha256sums=('58296ec463d6af9c58ee1d4c8302d1166f76bd30d30e54296347f7757aa413f1')

provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")


build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
