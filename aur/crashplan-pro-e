# Maintainer: Anthony DeStefano <adx [at] fastmail [dot] fm>
# Contributor: Maarten de Boer <info [at] maartendeboer [dot] net>
# Contributor: Egon Geerardyn <egon [dot] geerardyn [at] gmail [dot] com>
# Contributor: Bill Durr <billyburly [at] gmail [dot] com>
pkgname=crashplan-pro-e
pkgver=3.6.1.4
pkgrel=1
pkgdesc="An enterprise online/offsite backup solution"
url="http://www.crashplan.com/enterprise"
arch=('i686' 'x86_64')
license=('custom')
depends=('java-runtime') 
makedepends=('grep' 'cpio' 'gzip')
optdepends=('swt: needed by some to open the GUI')
backup=()
install=crashplan-pro-e.install
source=(http://download.crashplan.com/installs/linux/install/CrashPlanPROe/CrashPlanPROe_${pkgver}_Linux.tgz
        crashplan-pro-e
        crashplan-pro-e.service)
sha256sums=('6364f1e879ead3778930c826b99f5dac82cae3cc094594a1ff28ec10607027f1'
            'dff346a7e416f896744f8120ea996d68320d4bd2c09cd656dd27888d2f5c2b90'
            '1ca45875025fcf8a4ec6e6f9588807453f65da38b6a0e06c7bb94feb66edda9c')

build() {
  cd $srcdir/CrashPlanPROe-install

  echo ""
  echo "You must review and agree to the EULA before using Crashplan PROe."
  echo "You can do so at:"
  echo "  - http://support.crashplan.com/doku.php/eula"
  echo "  - /usr/share/licenses/${pkgname}/LICENSE"
  echo ""

  echo "" > install.vars
  echo "JAVACOMMON=`which java`" >> install.vars
  echo "#APP_BASENAME=CrashPlan" >> install.vars
  echo "TARGETDIR=/opt/$pkgname" >> install.vars
  echo "BINSDIR=" >> install.vars
  echo "MANIFESTDIR=/opt/$pkgname/manifest" >> install.vars
  echo "INITDIR=" >> install.vars
  echo "RUNLVLDIR=" >> install.vars
  NOW=`date +%Y%m%d`
  echo "INSTALLDATE=$NOW" >> install.vars

  sed -imod "s|Exec=.*|Exec=/opt/$pkgname/bin/CrashPlanDesktop|" scripts/CrashPlan.desktop
  sed -imod "s|Icon=.*|Icon=/opt/$pkgname/skin/icon_app_64x64.png|" scripts/CrashPlan.desktop
  sed -imod "s|Categories=.*|Categories=System;|" scripts/CrashPlan.desktop
}

package() {
  mkdir -p $pkgdir/opt/$pkgname
  cd $pkgdir/opt/$pkgname

  cat $srcdir/CrashPlanPROe-install/CrashPlanPROe_$pkgver.cpi | gzip -d -c - | cpio -i --no-preserve-owner
  chmod 777 $pkgdir/opt/$pkgname/log
  sed -i "s|<manifestPath>manifest</manifestPath>|<manifestPath>/opt/$pkgname/manifest</manifestPath>|g" $pkgdir/opt/$pkgname/conf/default.service.xml

  mkdir -p $pkgdir/usr/bin
  ln -s "/opt/$pkgname/bin/CrashPlanDesktop" $pkgdir/usr/bin/CrashPlanDesktop

  # Fix for 32 bit: 64 bit libs cannot be stripped from symbols
  # so we just remove those libs
  if [ "$CARCH" = "i686" ]; then
    rm $pkgdir/opt/$pkgname/*64.so
  fi

  # Fix for encoding troubles (CrashPlan ticket 178827)
  # Make sure the daemon is running using the same localization as
  # the (installing) user
  echo "" >> $srcdir/CrashPlanPROe-install/scripts/run.conf
  echo "export LC_ALL=$LANG" >> $srcdir/CrashPlanPROe-install/scripts/run.conf

  install -D -m 644 $srcdir/CrashPlanPROe-install/install.vars $pkgdir/opt/$pkgname/install.vars
  install -D -m 644 $srcdir/CrashPlanPROe-install/EULA.txt $pkgdir/opt/$pkgname/EULA.txt
  install -D -m 644 $srcdir/CrashPlanPROe-install/EULA.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m 755 $srcdir/CrashPlanPROe-install/scripts/CrashPlanDesktop $pkgdir/opt/$pkgname/bin/CrashPlanDesktop
  install -D -m 644 $srcdir/CrashPlanPROe-install/scripts/run.conf $pkgdir/opt/$pkgname/bin/run.conf
  install -D -m 755 $srcdir/CrashPlanPROe-install/scripts/CrashPlanEngine $pkgdir/opt/$pkgname/bin/CrashPlanEngine
  install -D -m 755 $srcdir/CrashPlanPROe-install/scripts/CrashPlan.desktop $pkgdir/usr/share/applications/crashplan.desktop

  # rc.d daemon
  install -D -m 755 $srcdir/crashplan-pro-e $pkgdir/etc/rc.d/crashplan-pro-e
  # systemd unit
  install -D -m 644 $srcdir/crashplan-pro-e.service $pkgdir/usr/lib/systemd/system/crashplan-pro-e.service
}
