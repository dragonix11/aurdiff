# $Id$

pkgname=emacs-dash
pkgver=2.2.0
pkgrel=1
pkgdesc='A modern list API for Emacs. No cl required'
arch=('any')
url="https://github.com/magnars/dash.el"
license=('GPL')
depends=('emacs')
source=('https://github.com/magnars/dash.el/archive/2.2.0.tar.gz')
sha256sums=('3ebbdc8e988554c4d459b09991b71e775de19f031d688cb176fcdb55d493645d')

build() {
  cd "dash.el-$pkgver"

  # byte compile
  emacs -batch -f batch-byte-compile dash.el
  emacs -batch -l dash.elc -f batch-byte-compile dash-functional.el
}

package() {
  cd "dash.el-$pkgver"

  # create destination path
  install -d "$pkgdir/usr/share/emacs/site-lisp/dash"

  # copy over files into path
  install -Dm644 *.el *.elc "$pkgdir/usr/share/emacs/site-lisp/dash"
}

# vim: ts=2 sw=2 et
