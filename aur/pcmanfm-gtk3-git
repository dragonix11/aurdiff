# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: TDY <tdy@gmx.com>

_gitname=pcmanfm
pkgname="$_gitname"-gtk3-git
pkgver=20131016
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
depends=('libfm-gtk3-git')
makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: mounting of local and remote drives'
            'gnome-menus: applications menu and "Open with..." dialog')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install
source=(git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm)
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
  cd "${srcdir}/${_gitname}"


  msg "Starting make..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtk=3
  make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
