# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=zerobrane-studio
pkgver=0.38
pkgrel=1
pkgdesc="A lightweight Lua-based IDE for Lua"
arch=(any)
url="http://studio.zerobrane.com/"
license=('MIT')
depends=('wxlua' 'lua51-bitop' 'lua51-socket' 'hicolor-icon-theme')
makedepends=('cmake')
provides=('zerobrane-studio')
conflicts=('zerobrane-studio-git')
optdepends=('love: to debug love programs')
install=zerobrane-studio.install
_github_user="pkulchenko"
_github_project="ZeroBraneStudio"
_github_rev="2b078d5"
source=("https://github.com/$_github_user/$_github_project/tarball/${pkgver}"
        "zbstudio.patch")
md5sums=('cd63c8103f8e5d0a8415ee016f3d566e'
         '3bb356b8549b60352e8ab36b9a6d9a92')

build() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev"

  patch -p1 < "$srcdir/zbstudio.patch"

  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DLUA_EXECUTABLE=/usr/bin/lua5.1

  make
}

package() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev/build"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp ../LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
