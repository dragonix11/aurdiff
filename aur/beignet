# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=beignet
pkgver=0.3
pkgrel=1
pkgdesc='A GPGPU System for Intel Ivybridge GPUs'
arch=('x86_64')
url='http://cgit.freedesktop.org/beignet/'
license=('LGPL2.1')
depends=('mesa' 'libsm' 'glu')
makedepends=('clang' 'cmake')
conflicts=('opencl-headers')
source=("git://anongit.freedesktop.org/beignet#tag=Release_v${pkgver}")
sha256sums=('SKIP')

build() {
	cd "$srcdir/$pkgname"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RELEASE \
		-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir/" install
}
