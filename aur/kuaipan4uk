# Maintainer: Jason <jsfaint@gmail.com>

pkgname=kuaipan4uk
pkgver=0.2
pkgrel=3
pkgdesc="Kingsoft kuaipan, is a document sync service same with dropbox"
arch=('x86_64')
license=("Custom")
url="http://www.ubuntukylin.com/applications/showimg.php?lang=cn&id=21"
source=("http://www.ubuntukylin.com/downloads/files/${pkgname}_13_${pkgver}_amd64.deb"
"http://curl.haxx.se/download/curl-7.23.1.tar.gz"
"kuaipan4uk")
md5sums=('abd805975ea09d07baec4944de61d1a2' '8e23151f569fb54afef093ac0695077d' '54db0e697fd6400c410731b44860d7bc')

depends=('glib2' 'boost-libs' 'qt4' 'gcc-libs-multilib' 'libpng' 'zlib' 'freetype2' 'libsm' 'libice' 'libxrender' 'fontconfig' 'libxext' 'libx11' 'openssl' 'pcre' 'bzip2' 'libffi' 'util-linux' 'expat' 'libxcb' 'libxau' 'libxdmcp' 'crypto++')

curl_ver="curl-7.23.1"

build()
{
    #Build libcurl
    cd ${srcdir}
    tar xf ${curl_ver}.tar.gz
    cd ${curl_ver}

    ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --with-ssl

    make
}

package()
{
    #Extract kuaipan4uk
    tar xf ${srcdir}/data.tar.gz -C ${pkgdir}/
    rm ${pkgdir}/usr/lib/libboost* -rf
    chmod -R 755 ${pkgdir}/usr

    #Create symbolic link
    if [[ ! -e /usr/lib/libcrypto++.so.9 ]]; then
        ln -s /usr/lib/libcryptopp.so ${pkgdir}/usr/lib/libcrypto++.so.9
    fi

    #install libcurl
    cd ${srcdir}/${curl_ver}
    make DESTDIR="${pkgdir}" install

    #Remove curl binary
    rm ${pkgdir}/usr/bin/curl
    rm ${pkgdir}/usr/bin/curl-config

    #Remove man page
    rm ${pkgdir}/usr/share/man -rf

    #Remove header file, we don't need it
    rm -rf ${pkgdir}/usr/include
    rm -rf ${pkgdir}/usr/lib/libcurl.a
    rm -rf ${pkgdir}/usr/lib/libcurl.la
    rm -rf ${pkgdir}/usr/lib/pkgconfig

    install -d ${pkgdir}/opt/${pkgname}
    mv ${pkgdir}/usr/* ${pkgdir}/opt/${pkgname}/

    #Install wrap script and desktop icon
    install -Dm755 ${srcdir}/kuaipan4uk ${pkgdir}/usr/bin/kuaipan4uk

    install -d ${pkgdir}/usr/share/applications
    install -d ${pkgdir}/usr/share/pixmaps
    ln -s ../../../opt/${pkgname}/share/applications/kuaipan4uk.desktop ${pkgdir}/usr/share/applications/kuaipan4uk.desktop
    ln -s ../../../opt/${pkgname}/share/pixmaps/kuaipan4uk.png ${pkgdir}/usr/share/pixmaps/kuaipan4uk.png

    #Fix desktop icon wrong category issue, move it to office group
    sed 's/Graphics;/Office;/g' -i ${pkgdir}/opt/${pkgname}/share/applications/kuaipan4uk.desktop

    #Fix duplicate of libxlive binary
    cd ${pkgdir}/opt/${pkgname}/lib
    rm libxlive.so.1 libxlive.so.1.0
    ln -s libxlive.so.1.0.0 libxlive.so.1
    ln -s libxlive.so.1.0.0 libxlive.so.1.0

    #Fix duplicate of libiconv
    rm libiconv.so.2
    ln -s libiconv.so.2.5.1 libiconv.so.2
}
