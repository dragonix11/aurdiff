# $Id: 5f48deca9ec69b45c0390e7b07de7c3482d65384 $
# Maintainer: Ido Rosen <ido@kernel.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
# 
# From the Stan website, http://mc-stan.org/ :
#   ``Stan is a package for obtaining Bayesian inference using the No-U-Turn
# sampler, a variant of Hamiltonian Monte Carlo.'' (9/22/2013)

pkgname='stanc'
pkgdesc="A package for obtaining Bayesian inference using the No-U-Turn sampler, a variant of Hamiltonian Monte Carlo."
pkgver=1.3.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://mc-stan.org/'
license=('BSD')
depends=('gcc-libs')
makedepends=('texlive-bin' 'texlive-core' 'doxygen')
options=('!libtool' '!strip' '!makeflags')
source=(https://stan.googlecode.com/files/stan-src-1.3.0.tgz
        makefile-add-bin-to-libstan-target.patch)
sha512sums=('31a5620adfe7234d767d23b6612590c6e5958453cd0cffa4f0e2dbc0c192d2b83ddad80b655a4630b588563eb94c2e2bb0073647db70cd5501269835e47a5dc5'
            'd8ffd7ebcbe019117027097c3f135a9ec267b3fd6aac81a37035dcacfcf034cc1ccbcc98d6f07a783d09e41dbb90b1e3fa7d4601c7ab270f5e089c06d2434d43')

prepare() {
  cd "${srcdir}/stan-src-${pkgver}"
  
  patch makefile "${srcdir}/makefile-add-bin-to-libstan-target.patch"
}

build() {
  cd "${srcdir}/stan-src-${pkgver}"
  
  make all
}

check() {
  cd "${srcdir}/stan-src-${pkgver}"

  #make test-all
}

package() {
  cd "${srcdir}/stan-src-${pkgver}"
  
  # Stan's makefile doesn't have a make install command...
  # Install binaries:
  install -dm755                  "${pkgdir}/usr/bin"
  install -m755 bin/stanc         "${pkgdir}/usr/bin"
  install -Tm755 bin/print         "${pkgdir}/usr/bin/stanc-print"

  # Install static library:
  install -dm755                  "${pkgdir}/usr/lib"
  install -m644 bin/libstan.a     "${pkgdir}/usr/lib"
  install -m644 bin/libstanc.a    "${pkgdir}/usr/lib"

  # Install header files:
  install -dm755                  "${pkgdir}/usr/include/stan"
  cd src
  find . -iregex './stan.*.hpp$' -type f -exec install -DTm644 "{}" "${pkgdir}/usr/include/{}" \;
  cd .. 
 
  # Copy RStan:
  install -dm755                  "${pkgdir}/usr/share/doc/stanc/rstan"
  cp -r rstan/.                   "${pkgdir}/usr/share/doc/stanc/rstan/."

  # Install LICENSE file:
  install -dm755                  "${pkgdir}/usr/share/licenses/stanc"
  cp -r licenses/.                "${pkgdir}/usr/share/licenses/stanc/."

  # Install documentation:
  install -dm755                  "${pkgdir}/usr/share/doc/stanc/api"
  install -m644 doc/stan-reference-1.3.0.pdf \
                                  "${pkgdir}/usr/share/doc/stanc"
  cp -r doc/api/.                 "${pkgdir}/usr/share/doc/stanc/api/."
}
