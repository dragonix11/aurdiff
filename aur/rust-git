# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Taras Shpot <mrshpot@gmail.com>
pkgname=rust-git
pkgver=0.7.2946.gdb78fdc
epoch=2
pkgrel=1
pkgdesc="A safe, concurrent, practical language from Mozilla."
arch=(i686 x86_64)
url="http://www.rust-lang.org/"
license=('custom:MIT' 'Apache')
depends=('gcc-libs' 'ncurses' 'shared-mime-info')
makedepends=('git' 'gcc' 'curl'
             'libffi' 'python2')
optdepends=('emacs: to build the emacs mode'
            'haskell-pandoc: to build rust.pdf')
provides=('rust=0.8')
conflicts=('rust')
options=('!makeflags')
install=rust.install
source=("git+https://github.com/mozilla/rust.git")
sha1sums=(SKIP)

pkgver() {
  cd rust
  git describe | sed -e 's/^release-//' -e 'y/-/./'
}

build() {
  cd rust

  ./configure --prefix=/usr

  _lib_dirs=($CARCH-unknown-linux-gnu/{,stage0/,stage1/,stage2/,stage3/}lib)
  mkdir --parents "${_lib_dirs[@]}"
  for _lib_dir in "${_lib_dirs[@]}"; do
    ln --symbolic /usr/lib/libncursesw.so "$_lib_dir"/libtinfo.so.5
  done

  make all
}

package() {
  cd rust

  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/usr/lib/rustc/$CARCH-unknown-linux-gnu/lib/libmorestack.a"

  install --directory "$pkgdir/usr/share/licenses/rust-git/"
  install -m644 COPYRIGHT LICENSE-* "$pkgdir/usr/share/licenses/rust-git/"

  _docdir="$pkgdir/usr/share/doc/rust"
  install --directory "$_docdir"
  cd doc
  for _doc in *.{md,html,pdf,css} std extra; do
    test -e "$_doc" || continue
    cp -r "$_doc" "$_docdir/"
    chmod -R 644 "$_docdir/$_doc"
  done
  cd ..
  find "$_docdir" -type d -exec chmod 755 {} +

  install --directory "$pkgdir/usr/share/apps/katepart/syntax/"
  cp src/etc/kate/rust.xml "$pkgdir/usr/share/apps/katepart/syntax/"

  cp -a src/etc/gedit/share "$pkgdir/usr/"

  install --directory "$pkgdir/usr/share/vim/"
  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"

  install --directory "$pkgdir/usr/share/zsh/functions/Completion/Zsh/"
  cp -a src/etc/zsh/* "$pkgdir/usr/share/zsh/functions/Completion/Zsh/"

  cd src/etc/emacs
  emacs --eval '(byte-recompile-directory "." 0)' --quick --batch 2> /dev/null || true
  install --directory "$pkgdir/usr/share/emacs/site-lisp/"
  cp -a * "$pkgdir/usr/share/emacs/site-lisp/"
}
