# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa-qt4
pkgver=0.7.4
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt4"
arch=('i686' 'x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qjson' 'aspell')
provides=('pumpa')
install="pumpa.install"
source=("$url/pumpa/archive-tarball/v$pkgver")
md5sums=('8c5bb5a866d16a22671e551b486be360')

prepare() {
  cd "$srcdir/pumpa-pumpa"

  # desktop file paths fix
  sed -i 's|./\(pumpa\).*|\1-qt4|;s|Pumpa|& (Qt4)|' pumpa.desktop
}

build() {
  cd "$srcdir/pumpa-pumpa"
  qmake-qt4
  make
}

package() {
  cd "$srcdir/pumpa-pumpa"

  # bin
  install -Dm755 pumpa \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 pumpa.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/pumpa.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
