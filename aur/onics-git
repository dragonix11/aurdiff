# Contributor: Sergio Tridente <tioduke at gmail dot com>

_pkgname=onics
pkgname=onics-git
pkgver=504.ff8ac2d
pkgrel=1
pkgdesc="A command line tool suite to capture, dissect, manipulate and send network data."
arch=('i686' 'x86_64')
url="https://gitorious.org/onics/pages/Home"
license=('GPL')
depends=('glibc' 'libpcap')
makedepends=('git' 'make')
conflicts=('onics')
provides=('onics')
source=('git://gitorious.org/catlib/catlib.git#branch=master'
        'git://gitorious.org/onics/onics.git#branch=master')
md5sums=('SKIP' 'SKIP')


pkgver() {
  cd "$srcdir/${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  sed -i "s|INSTALL_DIR=/usr/local|INSTALL_DIR=${pkgdir}/usr/share|g" "$srcdir/${_pkgname}/doc/Makefile"
  sed -i "s|INSTALL_DIR=/usr/local|INSTALL_DIR=${pkgdir}/usr|g" "$srcdir/${_pkgname}/scripts/Makefile"
  sed -i "s|INSTALL_DIR=/usr/local|INSTALL_DIR=${pkgdir}/usr|g" "$srcdir/${_pkgname}/src/Makefile"
}

build() {
  # Make catlib
  cd "$srcdir/catlib"
  make

  # Make ONICS
  cd "$srcdir/${_pkgname}"
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/man/man1"
  make install
}
