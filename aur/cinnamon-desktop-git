# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=cinnamon-desktop
pkgname=${_pkgname}-git
pkgver=32.ea72b22
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=('gtk3' 'libxkbfile' 'xkeyboard-config' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'itstool' 'git' 'gnome-common')
url="https://github.com/linuxmint/cinnamon-desktop"
groups=()
options=(!libtool)
source=("git+https://github.com/linuxmint/${_pkgname}.git")
sha512sums=('SKIP')
install="${pkgname}.install"

pkgver() {
  cd ${srcdir}/${_pkgname}
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd ${srcdir}/${_pkgname}
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/${_pkgname} --disable-static \
    --disable-schemas-compile --disable-schemas-install
  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR="$pkgdir" install
}
