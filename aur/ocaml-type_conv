# Maintainer: Aaron Chen <nextAaron at gmail.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>

_pkgname=type_conv
pkgname=ocaml-$_pkgname
pkgver=109.41.00
pkgrel=1
pkgdesc="Generates code from type specifications"
arch=(i686 x86_64)
url="http://janestreet.github.com"
license=(LGPL)
depends=('ocaml>=4.00.0')
makedepends=('ocaml-findlib>=1.3.2')
source=(https://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz)
md5sums=('96861562c24a5e127900c4f1765ed848')
options=(!strip !makeflags)

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR"
  make install
}
