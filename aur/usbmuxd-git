# Maintainer: Alex Brinister <alex_brinister at yahoo dot com>

pkgname=usbmuxd-git
pkgver=v1.0.8.8.g10939f3
pkgrel=1
pkgdesc="A USB multiplexing daemon."
url="http://sukimashita.com/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'libxml2')
makedepends=('git' 'cmake')
provides=('usbmuxd')
conflicts=('usbmuxd')
install='usbmuxd.install'
_gitname="usbmuxd"
source=("git+http://cgit.sukimashita.com/${_gitname}.git"
				"usbmuxd.service")
sha512sums=('SKIP'
            '9771b422e9b48e11a0c2ecaafda06ea9acee651afd4f7e7589e9704d60bd9eb014f6b737d1e279735225f68d910183c698949ee153275e0873940dae00911210')

pkgver() {
	cd $srcdir/$_gitname
	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}
build() {
	msg "Removing old build directory..."
	rm -Irf ${srcdir}/$_gitname-build

	msg "Creating new build directory..."
	cp -r ${srcdir}/$_gitname ${srcdir}/$_gitname-build

	cd ${srcdir}/$_gitname-build

	msg "Starting make..."

    cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr -DLIB_SUFFIX=""

    make || return 1
}

package() {

	cd ${srcdir}/$_gitname-build

	make DESTDIR=${pkgdir} install || return 1
	
	# fix udev dir being hardcoded
	mv ${pkgdir}/lib/udev ${pkgdir}/usr/lib/
	rmdir ${pkgdir}/lib
	# install systemd service files
	install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service
	# use udev rule that works with systemd
	install -m644 ${srcdir}/$_gitname-build/udev/85-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/
}

# vim:set ts=2 sw=2:
