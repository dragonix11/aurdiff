# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Christophe LAVIE <christophe.lavie@laposte.net>


pkgname=devolo-dlan-cockpit
pkgver=4.1.3
pkgrel=1
install=${pkgname}.install
pkgdesc="Display and configure settings of your devolo device (GNOME and KDE Desktops)"
arch=('i686' 'x86_64')
url="http://www.devolo.com/consumer/dlan-cockpit-most-simple-configuration-software-for-your-dlan-network.html?l=en"
license=('nonfree')

if [ "${CARCH}" = "x86_64" ]; then
  _arch="amd64"
  depends=( 
    'adobe-air' 'kdesudo' 'kdeutils-kwallet' 'gnome-keyring' 'libgnome-keyring' 'lib32-libgnome-keyring' 
    'kdebase-runtime' 'lib32-gtk2'
  )
else
  _arch="i386"
  depends=( 
    'adobe-air' 'kdesudo' 'kdeutils-kwallet' 'gnome-keyring' 'libgnome-keyring' 
    'kdebase-runtime' 'gtk2'
  )
fi 

source=("http://www.devolo.com/downloads/software/software-dlan-cockpit-linux-v${pkgver//./-}.run"
  'devolonetsvc.service'
  'eulaAccepted'
  'dlancockpit-run.sh'
  'dlancockpit-run-service-embedded.sh'
  'dlancockpit-launcher.sh'
)


build() {
  cd $srcdir
  skip=$(grep -a -m1 -n "HERE_BE_DRAG[O]NS" "software-dlan-cockpit-linux-v${pkgver//./-}.run" | cut -d: -f1)
  tail "software-dlan-cockpit-linux-v${pkgver//./-}.run" -n +$((skip+1)) | tar -x -C .
  ar x "devolo-dlan-cockpit_${pkgver}-0_${_arch}.deb"
  find . -name "adobeair*${_arch}.deb" -print | xargs ar x
  tar xvf data.tar.gz
  mkdir -p adobeair  
  tar -C adobeair -xvf  data.tar.lzma
  sed -i 's/dlancockpit-run.sh/dlancockpit-launcher.sh/' "${srcdir}/usr/share/applications/devolo-dlan-cockpit.desktop"
}

package() {
    cp -r "${srcdir}/opt" "${srcdir}/usr" "${pkgdir}/"
    [ ! -d "/opt/Adobe AIR" ] &&  ln -s "/opt/adobe-air-sdk/runtimes/air/linux/Adobe AIR/" "${pkgdir}/opt/Adobe AIR"
  	[ ! -d "/etc/opt/Adobe/certificates/AIR" ] &&  cp -r "${srcdir}/adobeair/etc" "${pkgdir}/"
  	[ ! -d "/var/lib/devolonetsvc" ] &&  mkdir -p "${pkgdir}/var/lib/devolonetsvc"
  	install -Dm644 "${srcdir}/devolonetsvc.service" "${pkgdir}/usr/lib/systemd/system/devolonetsvc.service"
    install -Dm644 "${srcdir}/eulaAccepted" "${pkgdir}/opt/devolo/dlancockpit/share/eulaAccepted"
  	install -Dm755 "${srcdir}/dlancockpit-run.sh" "${pkgdir}/opt/devolo/dlancockpit/bin/dlancockpit-run.sh"
    install -Dm755 "${srcdir}/dlancockpit-run-service-embedded.sh" "${pkgdir}/opt/devolo/dlancockpit/bin/dlancockpit-run-service-embedded.sh"
    install -Dm755 "${srcdir}/dlancockpit-launcher.sh" "${pkgdir}/opt/devolo/dlancockpit/bin/dlancockpit-launcher.sh"    
 }
 
 
md5sums=('7faed0d4509b3488d0fd5306ba559ff6'
         '6784ae1b639217439ddcb3f43a99c7bb'
         'c81e728d9d4c2f636f067f89cc14862c'
         '665d64543a7b9835f1be9bf9f7a2a11f'
         'a7c42e820825e22c555f6ab27e25ab0f'
         'f76d509372a1430eafe9d3381ca0d051')
