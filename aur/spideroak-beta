# Maintainer: Martin F. Schumann <mfs@mfs.name>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Limao Luo <luolimao@gmail.com>
# Contributor: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak-beta
_pkgname=spideroak
_PkgName=SpiderOak
pkgver=5.0.3
pkgrel=1
_beta="yes"
pkgdesc="Secure file backup, sync and sharing client (Beta version)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
depends=(qt4 krb5 python2)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=('!strip')
install=$pkgname.install
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
source=("${_pkgname}_${pkgver}_${_arch}.tgz"::"https://spideroak.com/getbuild?platform=slackware&arch=${_arch}&beta=${_beta}"
        "terms.txt")
sha1sums=('d87dbc7cfc79bbe12a2f8887cb5e258fb4b5c5f8'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')
[ "$CARCH" = 'i686' ] && sha1sums[0]='a8aea913474d76c1b308c170844660f52e5ad0af'

package() {
    # install config files
    install -Dm644 $srcdir/etc/dbus-1/system.d/$_PkgName.dbus.conf $pkgdir/etc/dbus-1/system.d/$_PkgName.dbus.conf

    # install app in /opt
    install -d $pkgdir/opt/
    cp -r $srcdir/opt/* $pkgdir/opt/

    # install start script file
    install -Dm755 $srcdir/usr/bin/$_PkgName $pkgdir/usr/bin/$_PkgName
    cd $pkgdir/usr/bin
    ln -s $_PkgName $_pkgname
    cd $srcdir

    # install desktop and pixmap files
    install -Dm644 $srcdir/usr/share/applications/$_PkgName.desktop $pkgdir/usr/share/applications/$_PkgName.desktop
    install -Dm644 $srcdir/usr/share/pixmaps/$_PkgName.png $pkgdir/usr/share/pixmaps/$_PkgName.png

    # fix desktop file
    sed -i \
	-e "/Encoding=UTF-8/d" \
	-e "/^Name=/s: Backup::" \
	-e "/^Comment=/s:$_PkgName ::" \
	-e "/^Categories=/s:$_PkgName;::" \
	-e "/^Icon=/s:=.*$:=$_PkgName:" \
	-e "/^Exec=/s:=.*$:=$_PkgName:" \
	$pkgdir/usr/share/applications/$_PkgName.desktop

    # install xdg menu
    install -Dm755 $srcdir/etc/xdg/menus/applications-merged/$_PkgName.menu $pkgdir/etc/xdg/menus/applications-merged/$_PkgName.menu

    # install man page
    install -Dm644 $srcdir/usr/share/man/man1/$_PkgName.1.gz $pkgdir/usr/share/man/man1/$_PkgName.1.gz

    # install custom license file
    install -Dm644 terms.txt $pkgdir/usr/share/licenses/$_PkgName/terms.txt
}
