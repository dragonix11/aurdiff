# Maintainer : kuri <sysegv at gmail dot com>
# Contributor : Kevin C <kiven at kiven dot fr>
# Contributor: Alex 'AdUser' Z
pkgname=fusioninventory-agent
_pkgname="FusionInventory-Agent"
pkgver=2.3.0
pkgrel=1
pkgdesc="FusionInventory Agent is an application for keeping track of the hardware and software"
arch=('i686' 'x86_64')
url="http://fusioninventory.org"
license=('GPL')
depends=('perl' 'dmidecode'
         'perl-file-which' 'perl-ipc-run'
         'perl-net-ssleay' 'perl-libwww' 'perl-xml-simple'
         'perl-xml-treepp' 'perl-yaml' 'perl-http-daemon'
         'perl-text-template' 'perl-proc-pid-file'
         'perl-universal-require' 'perl-proc-processtable'
         'perl-proc-daemon' 'perl-json' 'perl-io-compress'
         'perl-net-ip' 'perl-http-proxy' 'perl-http-server-simple'
         'perl-io-capture' 'perl-io-socket' 'perl-io-socket-ssl'
         'perl-lwp-protocol-https' 'perl-parse-edid')
makedepends=('perl-extutils-makemaker')
optdepends=('hdparm' 'pciutils' 'perl-net-cups')
source=("http://forge.fusioninventory.org/attachments/download/1016/$_pkgname-$pkgver.tar.gz"
        'fusioninventory-agent.service'
        'fusioninventory-agent.config')
md5sums=('a4b17fe8d9cc7ceae673d81ea2917dc3'
         'cd0d59b266a41977f51d9e99ecca8cd5'
         '48d1b22c1bb2fcdf8a2cd9aa9846bdee')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  perl Makefile.PL \
  PREFIX="/usr" \
  SYSCONFDIR="/etc/fusioninventory" \
  LOCALSTATEDIR="/var/lib/fusioninventory-agent"

  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -s DESTDIR="$pkgdir/" install
  install -D -m644 ${srcdir}/fusioninventory-agent.service \
          ${pkgdir}/usr/lib/systemd/system/fusioninventory-agent.service
  mv ${pkgdir}/etc/fusioninventory/agent.cfg \
     ${pkgdir}/etc/fusioninventory/agent.cfg.default
  install -D -m644 ${srcdir}/fusioninventory-agent.config \
          ${pkgdir}/etc/conf.d/fusioninventory-agent
  mkdir -p "$pkgdir/var/lib/fusioninventory-agent"

}
