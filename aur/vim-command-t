# Maintainer: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>

pkgname="vim-command-t"
pkgver=1.5.1
pkgrel=1
pkgdesc="an extremely fast, intuitive mechanism for opening files and buffers with a minimal number of keystrokes"
arch=("i686" "x86_64")
url="https://wincent.com/products/command-t"
license=("BSD")
groups=("vim-plugins")
depends=("vim" "ruby")
makedepends=("git")
install="vimdoc.install"

_gitroot="git://git.wincent.com/command-t.git"
_gitname="${pkgname#vim-}"


prepare() {
  local _gitdir="${srcdir}/${_gitname}"

  msg "GIT: Connecting to server..."
  if [[ -d "$_gitdir" ]]; then
    cd "$_gitdir" && git pull origin master
    msg "GIT: pull done..."
  else
    git clone "$_gitroot" "$_gitdir"
    msg "GIT: clone done..."
  fi
  cd "$_gitdir"

  msg "GIT: cleaning and resetting working tree..."
  git clean -dfx
  git reset --hard HEAD

  msg "GIT: checking out tagged version ${pkgver}..."
  git checkout tags/"$pkgver"
}


build() {
  local _gitdir="${srcdir}/${_gitname}"
  cd "${_gitdir}/ruby/command-t"

  ruby "extconf.rb"
  make
}


package() {
  local _gitdir="${srcdir}/${_gitname}"
  cd "${_gitdir}"

  local _installprefix="${pkgdir}/usr/share/vim/vimfiles"
  install -m755 -d "${_installprefix}"/{doc,plugin,"ruby/${_gitname}"/{vim,finder,scanner}}
  install -m644 -t "${_installprefix}/doc"                        "doc"/*.txt
  install -m644 -t "${_installprefix}/plugin"                     "plugin"/*.vim
  install -m644 -t "${_installprefix}/ruby/${_gitname}"           "ruby/${_gitname}"/*.{rb,so}
  install -m644 -t "${_installprefix}/ruby/${_gitname}/vim"       "ruby/${_gitname}/vim"/*.rb
  install -m644 -t "${_installprefix}/ruby/${_gitname}/finder"    "ruby/${_gitname}/finder"/*.rb
  install -m644 -t "${_installprefix}/ruby/${_gitname}/scanner"   "ruby/${_gitname}/scanner"/*.rb

  # install license files
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim:set ts=2 sw=2 et:
