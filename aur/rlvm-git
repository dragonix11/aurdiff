# Maintainer: hi117 hi117IRC@gmail.com
pkgname=rlvm-git
_gitname=rlvm
pkgver=1449.32e234b
pkgrel=1
pkgdesc="RealLive clone for Linux and OSX"
arch=(i686 x86_64)
url="http://www.elliotglaysher.org/rlvm/"
license=('GPL')
depends=('boost-libs>=1.46' glew libmad sdl_mixer sdl_image sdl_ttf)
makedepends=(scons boost)
conflicts=('rlvm')
provides=('rlvm')
source=('git://github.com/eglaysher/rlvm.git' build.patch)

pkgver() {
  cd $_gitname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $srcdir/$_gitname

  patch -Np0 < $srcdir/build.patch || return 1
  scons --release -j 2
}

package() {
  install -D "$srcdir/$_gitname/build/release/rlvm" "$pkgdir/usr/bin/rlvm"
  install -D "$srcdir/$_gitname/src/Platforms/gtk/rlvm.desktop" "$pkgdir/usr/share/applications/rlvm.desktop"
  for r in 16 24 32 48 128 256; do
    install -d "$pkgdir/usr/share/icons/hicolor/${r}x$r"
    install -D "$srcdir/$_gitname/resources/$r/rlvm.png" "$pkgdir/usr/share/icons/hicolor/${r}x$r/apps/rlvm.png"
  done
}

# vim:set ts=2 sw=2 et:
sha512sums=('SKIP'
            'd806707445e770fd1761a52d85c88248e45c70b6a4c49c33c155fea47af16e516c88880520ba113937e5c074a487d95478cf73ce872ea6ad31a072985ffc03d9')
