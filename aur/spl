# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("spl")
pkgver=0.6.1_3.10.7
pkgrel=1
pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils=0.6.1_3.10.7-1" "linux=3.10.7-1")
makedepends=("linux-headers=3.10.7-1")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-0.6.1.tar.gz
        spl_git_head.patch)
groups=("archzfs")
md5sums=('0f58806d9fd9cab900181b59b21bae50'
         '634741f1cfaa163feb933999d6caefb1')
license=("GPL")
install=spl.install

prepare() {
  cd "$srcdir/spl-0.6.1"
  patch -Np1 -i ../spl_git_head.patch
}

build() {
  cd "$srcdir/spl-0.6.1"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel --enable-atomic-spinlocks
  else
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=kernel
  fi
  make
}

package() {
  cd "$srcdir/spl-0.6.1"
  make DESTDIR=$pkgdir install

  # move module tree /lib -> /usr/lib
  cp -r $pkgdir/{lib,usr}
  rm -r $pkgdir/lib
}
