# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based On: Sergej Pupykin <pupykin.s+arch@gmail.com>

_patchlevel=0
_basever=0.151

pkgname=sdlmame-wout-toolkits
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support. Without toolkits (gtk and qt)"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
conflicts=('sdlmame')
depends=('sdl>=1.2.11' 'fontconfig' 'sdl_ttf' 'alsa-lib')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')
install="sdlmame.install"

for i in $(seq 1 "${_patchlevel}"); do
  _patches="${_patches} sdlmame-${_basever/./}u${i}_diff.zip::http://mamedev.org/updates/${_basever/./}u${i}_diff.zip"
done

source=("mame${_basever/./}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever/./}s.zip"
        "sdlmame.sh"
        "extras.tar.gz"
        ${_patches})
md5sums=('b1dec00c2c77ada2d670f5392de39d62'
         'e11a7d26704a9e2cff223b9b867d6a05'
         '420b61240bf5ae11615ba7c6100ee00d')
noextract=("extras.tar.gz")

prepare() {
  cd "${srcdir}"
  rm -fr build && mkdir build && cd build
  bsdtar -xf "${srcdir}/mame.zip"
  bsdtar -xf "${srcdir}/extras.tar.gz"
  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
  for i in $(seq 1 "${_patchlevel}"); do
    msg2 "Patch#${i}"
    patch --silent -p0 -E -i "${srcdir}/${_basever/./}u${i}.diff"
  done

  sed -e 's|# NO_X11|NO_X11|g' -e 's|# NO_USE_QTDEBUG|NO_USE_QTDEBUG|g' -i src/osd/sdl/sdl.mak
}

build() {
  cd "${srcdir}/build"

  make PYTHON=python2 OPTIMIZE=2 NOWERROR=1 ARCHOPTS=-march=native FULLNAME=mame
}

package() {
  cd "${srcdir}/build"

  # Install the sdlmame script
  install -Dm755 "${srcdir}/sdlmame.sh" "${pkgdir}/usr/bin/sdlmame"

  # Install the applications and the UI font in /usr/share
  install -Dm755 mame "${pkgdir}/usr/share/sdlmame/sdlmame"

  # Install the extra bits
  install -d "${pkgdir}/usr/share/sdlmame/"{artwork,ctrlr,keymaps,shader}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlmame/artwork/"
  install -m644 ctrlr/* "${pkgdir}/usr/share/sdlmame/ctrlr/"
  install -m644 src/osd/sdl/keymaps/* "${pkgdir}/usr/share/sdlmame/keymaps/"
  install -m644 src/osd/sdl/shader/glsl*.*h "${pkgdir}/usr/share/sdlmame/shader/"

  # Install man
  install -Dm644 src/osd/sdl/man/mame.6 "${pkgdir}/usr/share/man/man6/sdlmame.6"

  # Include the license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # FS#28203
  sed -e 's|KEYCODE_2_PAD|KEYCODE_2PAD|' \
      -e 's|KEYCODE_4_PAD|KEYCODE_4PAD|' \
      -e 's|KEYCODE_6_PAD|KEYCODE_6PAD|' \
      -e 's|KEYCODE_8_PAD|KEYCODE_8PAD|' \
      -i "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg
}
