# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libsystemd-qt-git
pkgver=208.1.ge83d529
pkgrel=1
pkgdesc='Qt-only wrapper for Systemd API'
arch=('i686' 'x86_64')
url='https://github.com/scarpin0/libsystemd-qt'
license=('GPL')
depends=('qt5-base' 'systemd')
makedepends=('git')
provides=('libsystemd-qt')
conflicts=('libsystemd-qt')
source=('git://github.com/scarpin0/libsystemd-qt')
md5sums=('SKIP')

pkgver() {
  cd libsystemd-qt
  local ver="$(git describe --tags)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libsystemd-qt \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
