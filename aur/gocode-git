# Maintainer: Daniel YC Lin <dlin.tw at> gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=gocode-git
pkgver=528.4c6097b
pkgrel=1
pkgdesc="An autocompletion/refactoring daemon for the Go programming language (for use in Vim)"
arch=(i686 x86_64)
url="https://github.com/nsf/gocode"
license=('GPL')
depends=('go')
provides=('gocode')
conflicts=('gocode')
install=gocode.install
makedepends=('git')
depends=('glibc')
optdepends=('auto-complete: emacs autocompletion')
source=('git+https://github.com/nsf/gocode')
md5sums=('SKIP')

_gitname="gocode"
pkgver() {
  cd $_gitname
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_gitname
  if [ -z "$GOROOT" ] ; then
    source /etc/profile.d/go.sh
  fi
  GOPATH=$srcdir go build -o gocode
}

package() {
  cd $_gitname
  install -Dm755 gocode $pkgdir/usr/bin/gocode
  install -Dm644 vim/autoload/gocomplete.vim "$pkgdir/usr/share/vim/vimfiles/autoload/gocomplete.vim"
  install -Dm644 vim/ftplugin/go/gocomplete.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/go/gocomplete.vim"
  install -Dm644 emacs/go-autocomplete.el "$pkgdir/usr/share/emacs/site-lisp/go-autocomplete.el"
  msg2 "changelog(last 2)"
  (cd $srcdir/$_gitname ; git log -2 --decorate=no | cat)
}
# vim:et sw=2 ts=2 ai:
