# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=qomp
pkgver=0.3
pkgrel=1
pkgdesc="A platform-independent online music player application that supports Yandex.Music, Myzuka.ru and ProstoPleer.com"
arch=('i686' 'x86_64')
url="https://code.google.com/p/qomp/"
license=('GPL2')
depends=('phonon' 'qtwebkit')
makedepends=('rpmextract')
source=(https://qomp.googlecode.com/files/$pkgname-${pkgver}_beta-1.src.rpm)
md5sums=('3ed8d7d7a9a41f10192919547f1eab54')

prepare() {
  # Extract source RPM
  rpmextract.sh $pkgname-${pkgver}_beta-1.src.rpm
  tar -zxvf $pkgname-${pkgver}_beta.tar.gz

  # Fix phonon paths
  cd $srcdir/$pkgname-${pkgver}_beta
  sed -i 's|<Phonon/MediaObject>|"/usr/include/qt4/phonon/mediaobject.h"|' src/qompphononplayer.{cpp,h}
  sed -i 's|<Phonon/AudioOutput>|"/usr/include/qt4/phonon/audiooutput.h"|' src/qompphononplayer.cpp
  sed -i 's|<Phonon/SeekSlider>|"/usr/include/qt4/phonon/seekslider.h"|' src/qompphononplayer.cpp
  sed -i 's|<Phonon/VolumeSlider>|"/usr/include/qt4/phonon/volumeslider.h"|' src/qompphononplayer.cpp
  sed -i 's|<Phonon/MediaObject>|"/usr/include/qt4/phonon/mediaobject.h"|' src/qompphononmetadataresolver.h
 
  # Fix install dir -> /usr
  sed -i 's|usr/local|usr|' qomp.pro
 
  qmake-qt4

  # Fix lrelease -> lrelease-qt4
  sed -i 's|lrelease|lrelease-qt4|' Makefile
}

build() {
  cd $srcdir/$pkgname-${pkgver}_beta
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver}_beta
  make INSTALL_ROOT=$pkgdir install

  # Desktop icon
  install -Dm644 src/icons/qomp.png  $pkgdir/usr/share/pixmaps/qomp.png
}
