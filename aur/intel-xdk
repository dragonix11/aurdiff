# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=intel-xdk
pkgver=0154
pkgrel=1
_rpmver=2.0-0
pkgdesc='Comprehensive, cross-platform HTML5 development environment'
arch=('i686' 'x86_64')
url='http://xdk.intel.com'
license=('custom')
depends=('node-webkit')
makedepends=('rpmextract')

source=("${pkgname}")
md5sums=('70cfe6d22badf44cbb9bdb0c3b4d5058')

if [ "${CARCH}" == 'i686' ]; then
  _arch='32'
  _carch='i486'
  md5sums+=('5ef3aca133a124caae09fa8167cb94ea')
elif [ "${CARCH}" == 'x86_64' ]; then
  _arch='64'
  _carch='x86_64'
  md5sums+=('50fa8c049180af5014799a75773bcc31')
fi

_base="xdk_web_linux${_arch}"
source+=("http://d2bnc8freht07j.cloudfront.net/${_base}_master_${pkgver}.tgz")

package() {
  cd "${srcdir}/${_base}/rpm"
  rpmextract.sh "${pkgname}-${pkgver}-${_rpmver}.${_carch}.rpm"

  mkdir -p "${pkgdir}/opt/intel" "${pkgdir}/usr/bin" "${pkgdir}/usr/share/licenses/${pkgname}"

  cp -r opt/intel/XDK/xdk "${pkgdir}/opt/intel/"
  install -m755 "${srcdir}/intel-xdk" "${pkgdir}/usr/bin/"  
  install -m644 "${pkgdir}/opt/intel/xdk/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
