# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=realmd
pkgver=0.14.5
pkgrel=1
pkgdesc="DBus service for configuring kerberos and other online identities."
arch=(i686 x86_64)
url="http://freedesktop.org/software/realmd/"
license=(GPL3)
depends=(dbus krb5 openldap polkit)
makedepends=(intltool packagekit xmlto)
source=("http://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz"
        "http://www.freedesktop.org/software/realmd/releases/$pkgname-$pkgver.tar.gz.sig")
sha1sums=('f99d33ea68497a88a1e68293b9518cd234e0ddce'
          'SKIP')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       \
    --with-distro=defaults  ;
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
} 

# vim: ts=2:sw=2:et
