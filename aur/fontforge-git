# $Id: PKGBUILD 191917 2013-08-01 06:59:46Z bpiotrowski $
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com

pkgname=fontforge-git
pkgver=15319.7dd97f1
pkgrel=1
epoch=1
pkgdesc='Outline and bitmap font editor'
url='http://fontforge.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'libpng' 'python2' 'libspiro'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('git')
source=('git://github.com/fontforge/fontforge.git')
sha1sums=('SKIP')
provides=('fontforge')
conflicts=('fontforge')
replaces=('fontforge')
options=('!libtool' '!makeflags')
install=install

_gitname=fontforge

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
	cd "${_gitname}"
	./autogen.sh
}

build() {
	cd "${_gitname}"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-devicetables \
		--enable-double \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \

	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
	
	# remove uneeded
	rm -fr "${pkgdir}/usr/share/doc"
	rm -fr "${pkgdir}/usr/share/fontforge/osx"
}
