# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: György Balló <ballogy@freestart.hu>

_pkgname=gtk-sharp
pkgname=$_pkgname-git
pkgver=2.99.1.66.g8b99886
pkgrel=1
pkgdesc="Mono/.NET binding to the cross platform Gtk+ GUI toolkit"
arch=('i686' 'x86_64')
url="https://github.com/mono/gtk-sharp"
license=('LGPL')
depends=('mono' 'gtk3')
makedepends=('git' 'gtk2')
options=('!libtool')
conflicts=('gtk-sharp-3')
provides=('gtk-sharp-3')
source=('git://github.com/mono/gtk-sharp.git#branch=master')
md5sums=('SKIP')


pkgver() {
	cd $_pkgname
	git describe | sed 's/^v//;s/-/./g'
}

build() {
	cd "$srcdir/$_pkgname"

	./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-static
	make
}

package() {
	cd "$srcdir/$_pkgname"

	make DESTDIR="$pkgdir/" install
	rm -r "$pkgdir"/usr/lib/monodoc
}
