# Contributor: Piotr Gorski <sir_lucjan@birdgelinux.pl>

pkgname=nvidia-bridge-pl
pkgver=325.15
_extramodules=extramodules-3.11-bridge-pl
pkgrel=3
pkgdesc="NVIDIA drivers for linux-bridge-pl"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-bridge-pl>=3.11' 'linux-bridge-pl<3.12' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-bridge-pl-headers>=3.11' 'linux-bridge-pl-headers<3.12')
conflicts=('nvidia-bridge-pl-96xx' 'nvidia-bridge-pl-173xx')
provides=("nvidia=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)
[ "$CARCH" = "i686" ] && _arch='x86' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}" && md5sums=('528fc538584de6446384b9cec3099d6c')
[ "$CARCH" = "x86_64" ] && _arch='x86_64' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32" && md5sums=('63b2caa0cb128efa1a7808d7bddb2074')
#source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/usr/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia_bfs.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
