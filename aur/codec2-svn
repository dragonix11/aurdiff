# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=codec2-svn
pkgver=289
pkgrel=1
pkgdesc="Open source 2400 bit/s speech codec"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://codec2.org/"
depends=('glibc')
makedepends=('subversion')
source=('svn+https://freetel.svn.sourceforge.net/svnroot/freetel/codec2')
md5sums=('SKIP')
_svnmod=codec2

pkgver() {
  cd "$_svnmod"
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$_svnmod"
  ./configure --prefix=/usr
  make 
}

package() {
  cd "$_svnmod/src"
  install -m755 -D c2enc "$pkgdir/usr/bin/c2enc"
  install -m755 -D c2dec "$pkgdir/usr/bin/c2dec"
  install -m755 -D c2sim "$pkgdir/usr/bin/c2sim"
  #install -m755 -D c2demo "$pkgdir/usr/bin/c2demo"
  install -m644 -D "$srcdir/$_svnmod/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
