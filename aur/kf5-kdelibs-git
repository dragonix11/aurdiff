# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-kdelibs-git
pkgver=v4.10.3.4220.61d0c8b
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('strigi' 'kf5-attica-git' 'kf5-libdbusmenu-qt-bzr'
        'kf5-phonon-git' 'qttools-git' 'qtwebkit-git' 'libxcursor'
        'enchant' 'libjpeg' 'giflib' 'docbook-xsl' 'hunspell'
        'kf5-polkit-qt-git' 'udisks2' 'libutempter' 'media-player-info'
        'qtscript-git' 'krb5' 'xcb-util-renderutil' 'libxss' 'jasper'
        'openexr')
makedepends=('cmake' 'extra-cmake-modules-git' 'hspell' 'avahi' 'git' 'qtchooser')
conflicts=('kf5-kdelibs')
provides=('kf5-kdelibs')
install=${pkgname}.install
options=('debug')
source=('git://anongit.kde.org/kdelibs#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kdelibs
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
  export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
  export QT_SELECT=git

  cd build
  cmake ../kdelibs \
    -DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DKDE_DEFAULT_HOME='.kf5' \
    -DHUPNP_ENABLED=FALSE \
    -DWITH_SOLID_UDISKS2=ON \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}"/opt/kf5/share/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt \
    "${pkgdir}"/opt/kf5/share/kssl/ca-bundle.crt
}
