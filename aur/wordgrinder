# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>
# Contributor: Joshua Stiefer <facedelajunk@gmail.com>

pkgname=wordgrinder
pkgver=0.4.1
pkgrel=1
pkgdesc="A word processor for processing words"
url="http://wordgrinder.sourceforge.net/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('ncurses' 'lua-filesystem')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('8dfa1bc649caa7e9f018a5d0523f047d')


prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -e "s#llua5.2#llua#" \
      -e "s#/man/#/share/man/#" -i Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install
  install -dm755 "$pkgdir/usr/share/licenses/wordgrinder"
  install -m644 COPYING "$pkgdir/usr/share/licenses/wordgrinder"
}
