# Maintainer:
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libsignon-glib
pkgver=1.10
pkgrel=1
pkgdesc='GLib-based client library for applications handling account authentication through the Online Accounts Single Sign-On service'
arch=('i686' 'x86_64')
url='http://code.google.com/p/accounts-sso/'
license=('LGPL')
depends=('signon')
makedepends=('python2-gobject')
options=('!libtool')
source=(http://accounts-sso.googlecode.com/files/$pkgname-$pkgver.tar.gz)
sha1sums=('224f66c9662843705da4b35d781a50d6aeb8ca5f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Signon.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Signon.py"
}

# vim:set ts=2 sw=2 et:
