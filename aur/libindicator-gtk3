# Maintainer: alucryd <alucryd at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libindicator-gtk3
pkgver=12.10.1
pkgrel=3
pkgdesc="Shared functions for Ayatana indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
depends=('gtk3')
provides=('libindicator3')
conflicts=('libindicator3')
options=('!libtool')
source=("https://launchpad.net/${pkgname%-*}/${pkgver%.*}/${pkgver}/+download/${pkgname%-*}-${pkgver}.tar.gz")
sha256sums=('b2d2e44c10313d5c9cd60db455d520f80b36dc39562df079a3f29495e8f9447f')

prepare() {
  cd ${pkgname%-*}-${pkgver}

  sed 's/install-libindicatorincludeHEADERS install-pkgconfigDATA/install-pkgconfigDATA/g' -i libindicator/Makefile.in
  sed 's/gtk_icon_info_free/g_object_unref/' -i libindicator/indicator-image-helper.c
}

build() {
  cd ${pkgname%-*}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --libexecdir="/usr/lib/${pkgname%-*}" --disable-{static,tests}
  make
}

package(){
  cd ${pkgname%-*}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
