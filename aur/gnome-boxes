# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes
pkgver=3.10.0
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Boxes"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib' 'tracker'
         'gtk-vnc' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info' 'vala')
optdepends=('qemu-spice: to create new VMs')
makedepends=('intltool' 'itstool')
install=gnome-boxes.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('906d5e7aaf4c0d90ea34ce019d5d2cf8665827cea28c21e811e6a2a509a7459e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --libexec=/usr/lib/gnome-boxes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
