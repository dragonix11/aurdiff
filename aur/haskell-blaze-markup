# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>

_hkgname=blaze-markup
pkgname=haskell-blaze-markup
pkgver=0.5.1.5
pkgrel=2
pkgdesc="Core modules of a blazingly fast markup combinator library for the Haskell programming language"
url="http://hackage.haskell.org/package/${_hkgname}"
license=(custom:BSD3)
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-blaze-builder' 'haskell-bytestring-show' 'haskell-text')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=$pkgname.install
md5sums=('faf0c80851b34987e74541b23cef3218')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/$_hkgname-$pkgver"
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
