# Author: Christoph Jaeger <christophjaeger@linux.com>
# Maintainer: Marc Rozanc <marc@rozanc.fr>

pkgbase="tuxedo-wmi"
_modname="tuxedo-wmi"
pkgname="${pkgbase}-dkms"
pkgver="1.1.0"
pkgrel=3
pkgdesc='A reverse engineering driver for the Clevo P150EM/P170EM/P150SM/P157SM/P170SM/P177SM backlight keyboard, by Christoph Jaeger (DKMS version)'
arch=('any')
license=('GPL')
url='http://www.linux-onlineshop.de/forum/index.php?page=Thread&threadID=26&s=82130b57e71bd5b6ea569abc1424025c6f8d412d'
depends=('dkms' 'linux-headers')
provides=("tuxedo-wmi")
replaces=("clevo-wmi-dkms")
conflicts=("clevo_wmi-git" "clevo-wmi" "clevo-wmi" "clevo-wmi-dkms" "tuxedo-wmi")
source=("tuxedo-wmi-${pkgver}.src.tar.gz::http://www.linux-onlineshop.de/forum/index.php?page=Attachment&attachmentID=76&h=e1c3a51c297ded51bbddfac915ec65a050f576ca&s=da97045136775d797dba92988670cc65bbfe88d7"
        "dkms.conf.in")
md5sums=('82334439f8ff9bc67c07199460e513a8'
         '75fa1f51d5b4c15edcdfb8d60dd6ae54')
install='tuxedo-wmi-dkms.install'


build() {
    cd "${srcdir}/tuxedo-wmi-${pkgver}/src"
    make
}

package() {
    cd "${srcdir}/tuxedo-wmi-${pkgver}/src"

    install -dm755 "${pkgdir}/usr/src/${_modname}-${pkgver}/"
    for i in "${srcdir}/${_modname}-${pkgver}/src/"{Makefile,tuxedo-wmi.c}; do
        install -D -m644 "${i}" "${pkgdir}/usr/src/${_modname}-${pkgver}/"
    done
    sed "s/#MODULE_VERSION#/${pkgver}/" "${srcdir}/dkms.conf.in" > "${pkgdir}/usr/src/${_modname}-${pkgver}/dkms.conf"
}

