# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=java-swing-joxy-git
pkgver=394.bc84e8d
pkgrel=1
pkgdesc="Java Swing Look and Feel theme integrated with KDE Oxygen"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/joxy/"
license=('GPL3')
depends=('java-runtime' 'qt4')
makedepends=('git' 'maven')
source=('joxy::git+git://git.code.sf.net/p/joxy/code')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/joxy"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  # source profile to make sure the $JAVA_HOME is defined
  if [ -f /etc/profile/openjdk6.sh ] ; then
    . /etc/profile/openjdk6.sh
  else
    . /etc/profile.d/jdk.sh
  fi

  cd "$srcdir/joxy/joxy"
  mvn package -Dmaven.test.skip=true
}

check() {
    # source profile to make sure the $JAVA_HOME is defined
  if [ -f /etc/profile/openjdk6.sh ] ; then
    . /etc/profile/openjdk6.sh
  else
    . /etc/profile.d/jdk.sh
  fi

  cd "$srcdir/joxy/joxy"
  mvn test
}

package() {
  [ $CARCH == 'i686' ] && _arch=i386
  [ $CARCH == 'x86_64' ] && _arch=amd64

  cd "$srcdir/joxy/joxy/install"

  install -dm755 "$pkgdir/$JAVA_HOME"/jre/lib/{ext,$_arch}
  install -m644 joxy-*.jar "$pkgdir/$JAVA_HOME/jre/lib/ext/"
  install -m644 libjoxy.so "$pkgdir/$JAVA_HOME/jre/lib/$_arch/"
}

# vim:set ts=2 sw=2 et:
