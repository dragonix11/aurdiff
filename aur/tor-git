# Contributor: skydrome <skydrome@i2pmail.org>
# Maintainer: skydrome <skydrome@i2pmail.org>

pkgname=tor-git
_branch=master
pkgver=0.2.5.0.alpha.dev.c7113e7
pkgrel=1
pkgdesc="An anonymizing overlay network (development version)"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.torproject.org"
license=('BSD')
depends=('openssl' 'ca-certificates' 'libevent' 'libseccomp')
makedepends=('asciidoc')
optdepends=('torsocks-git: for torify support')
# this is an optdep because the torsocks in community requires tor which is wrong
# and the tor in extra installs tsocks which support for has been removed for years
conflicts=('tor')
provides=('tor')
install='tor.install'
backup=('etc/tor/torrc')
source=("git+https://git.torproject.org/tor.git#branch=${_branch}"
        'torrc'
        'tor.service')
sha256sums=('SKIP'
            '3a882f38708b0a2e09f5716c3e90b1c58bf6a906280274cf0207017150d477ee'
            'e9380b32c941ac56d3b42b6033ebb6af1462c6e3ab7e9c46bddcd6d45fadd292')

pkgver () {
    cd "$srcdir/tor"
    echo "$(grep AC_INIT configure.ac \
        | sed 's/.*\[\(.*\)\].*/\1/;s/-/./g').$(git rev-parse --short $_branch)"
}

prepare() {
     cd "$srcdir/tor"
    ./autogen.sh
}

build() {
    cd "$srcdir/tor"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
        #--with-tcmalloc
        #--enable-openbsd-mallo
        #--with-dmalloc
    make
}

check() {
    cd "$srcdir/tor"
    make test #|| true
}

package() {
    cd "$srcdir/tor"
    make DESTDIR="$pkgdir" install

    rm -f "$pkgdir/etc/tor/tor-tsocks.conf"
    install -dm700 "$pkgdir/var/lib/tor"
    install -Dm644 "$srcdir/torrc"       "$pkgdir/etc/tor/torrc"
    install -Dm644 "$srcdir/tor.service" "$pkgdir/usr/lib/systemd/system/tor.service"
    install -Dm644 LICENSE               "$pkgdir/usr/share/licenses/tor/LICENSE"
}
