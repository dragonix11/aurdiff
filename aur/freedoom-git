# Maintainer:  Frederic Bezies <fredbezies at gmail.com>
# Contributor: Edoardo Maria Elidoro <edoardo.elidoro@gmail.com>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: felix <at yandex.com, m.p.isaev>

pkgname=freedoom-git
pkgver=20131008
pkgrel=1
pkgdesc="Free game data files for Doom gaming engines"
arch=('any')
url="http://www.nongnu.org/freedoom/"
license=('BSD')
makedepends=('git' 'imagemagick' 'deutex-devel')
provides=('freedoom')
conflicts=('freedoom')
replaces=('freedoom-svn')
source=(git://github.com/chungy/freedoom.git)
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/freedoom"
  git show --quiet --format=format:%cd --date=short | tr -d -
}

prepare() {
  cd "$srcdir/freedoom"
  find * -type f -print0 | xargs -0 sed -i -e '1 s/\(#!.*\)python/\1python2/'
}

build() {
  cd "$srcdir/freedoom"
  make wads/{doom,doom2,freedm}.wad PYTHON=python2 
}

package() {
  cd "$srcdir/freedoom"
  mkdir -p $pkgdir/usr/share/doom
  install -m644 wads/{doom,doom2,freedm}.wad $pkgdir/usr/share/doom/
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
