# Maintainer: mac <poczciwiec at gmail dot com>
pkgname=brackets
pkgver=sprint29
pkgrel=3
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("brackets-lib" "gconf" "nodejs" "nspr" "systemd")
makedepends=("prelink")
optdepends=(
	"chromium: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
provides=("brackets" "adobe-brackets")
conflicts=("brackets-git")
replaces=()
backup=()
options=()
install=${pkgname}.install

_build=29
if [[ "$CARCH" == "i686" ]]; then
	_arch="LINUX32"
	sha512sums=(
		"f85d5a342f650376b8c77ab5e1583404a0692be3c5f31e627b12fc1307fad89285ad4e169adb3be85e1de72ddb7ac3a4dee4bba708efb0d563f6bf64a81af14f"
	)
elif [[ "$CARCH" == "x86_64" ]]; then
	_arch="LINUX64"
	sha512sums=(
		"b058bcedf8f3f4a238b1eba9fe481e881125a5aac0335ea3e5495770a0dfd9c1355d9a6101ca52704a5ac397b27e1194c2ce896507dc06f592c99f0fcd5aa934"
	)
fi

source=(
	"http://download.brackets.io/file.cfm?platform=${_arch}&build=${_build}"
)


prepare() {
	cd "$srcdir"

	echo "  -> Extracting files..."
	tar -xf data.tar.gz

	echo "  -> Fixing executable stack..."
	execstack -c "opt/${pkgname}/Brackets" 
}

package() {
	cd ${srcdir}

	echo "  -> Installing program..."
	for _file in "opt/${pkgname}/"*.pak; do
		install -Dm644 \
			"${_file}" \
			"${pkgdir}/${_file}"
	done

	for _file in "opt/${pkgname}/lib/"*; do
		local _filename="${_file##*/}"
		install -Dm755 \
			"${_file}" \
			"${pkgdir}/usr/lib/${_filename}"
	done

	for _file in "opt/${pkgname}/locales/"*; do
		install -Dm644 \
			"${_file}" \
			"${pkgdir}/${_file}"
	done

	cp -dpr --no-preserve=ownership \
		"opt/${pkgname}/www" "${pkgdir}/opt/${pkgname}/www"

	install -Dm755 \
		"opt/${pkgname}/Brackets" \
		"${pkgdir}/opt/${pkgname}/Brackets"

	install -Dm755 \
		"usr/bin/${pkgname}" \
		"${pkgdir}/usr/bin/${pkgname}"

	cp -dpr --no-preserve=ownership \
		"opt/${pkgname}/samples" "${pkgdir}/opt/${pkgname}/samples"


	echo "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -Dm644 \
		"${_icon_dir}/scalable/apps/${pkgname}.svg" \
		"${pkgdir}/${_icon_dir}/scalable/apps/${pkgname}.svg"
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size="${_icon##*/appshell}"
		install -Dm644 \
			"${_icon}" \
			"${pkgdir}/${_icon_dir}/${_icon_size%.png}x${_icon_size%.png}/apps/${pkgname}.png"
	done


	echo "  -> Installing .desktop file..."
	install -Dm755 \
		"opt/${pkgname}/${pkgname}.desktop" \
		"${pkgdir}/usr/share/applications/${pkgname}.desktop"


	echo "  -> Installing license..."
	install -Dm755 \
		"usr/share/doc/${pkgname}/copyright" \
		"${pkgdir}/usr/share/licenses/${pkgname}/copyright"

}


