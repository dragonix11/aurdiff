# Maintainer: Bertrand Bonnefoy-Claudet <bertrandbc@gmail.com>

pkgname=bcpkix
pkgver=1.48
pkgrel=1
pkgdesc="Java cryptography APIs (Bouncy Castle) package for PKIX/CMS/EAC/DVCS/PKCS/TSP/OPENSSL"
arch=('any')
url="http://www.bouncycastle.org/java.html"
license=('custom')
depends=('java-runtime' 'bcprov')
makedepends=('java-environment')
source=("http://www.bouncycastle.org/download/bcpkix-jdk15on-${pkgver/./}.tar.gz")
md5sums=('bbfd4d0b9fae047b786f8ad4f1292a99')

build() {
  cd "${srcdir}/bcpkix-jdk15on-${pkgver/./}"
  jar xf src.zip

  [ -d classes ] || mkdir classes
  find . -name '*.java' | grep -v '\/test\/' > src.list # Remove tests, they are broken
  javac -encoding ISO-8859-1 -cp /usr/share/java/bcprov.jar -d classes @src.list

  cd classes
  find . -depth -type d -empty -delete
  jar cf ../bcpkix.jar *
}

package() {
  cd "${srcdir}/bcpkix-jdk15on-${pkgver/./}"
  install -m755 -d "${pkgdir}/usr/share/java"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 bcpkix.jar "${pkgdir}/usr/share/java/"
  install -m644 LICENSE.html "${pkgdir}/usr/share/licenses/${pkgname}/"
}
