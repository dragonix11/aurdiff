# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=crafty
pkgver=23.4
pkgrel=2
pkgdesc="A computer chess program"
arch=('i686' 'x86_64')
url="http://www.cis.uab.edu/hyatt/"
license=('custom')
backup=()
source=(http://ftp-archive.freebsd.org/pub/FreeBSD/ports/distfiles/crafty/$pkgname-$pkgver.zip
        http://ftp-archive.freebsd.org/pub/FreeBSD/ports/distfiles/crafty/book.bin
        http://ftp-archive.freebsd.org/pub/FreeBSD/ports/distfiles/crafty/books.bin
        copyright
        i686.patch
        x86_64.patch)
md5sums=('7e0811dd2d801428d8da48a4e487885a'
         '085e207bd19bd974d511e77fbef36507'
         '966a852a2ccd3c32e9167061592928d3'
         '438cec9f32fb79f58822f97cf64e7afb'
         'b8e1b65042b9a0e0f8403aa3e80b0905'
         'fde93eb828a062ab76118fce2b61a9af')

build()
{
    unset LDFLAGS
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/$CARCH.patch"
    make profile
}

package()
{
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm755 crafty "$pkgdir/usr/bin/crafty"
    install -Dm644 crafty.hlp "$pkgdir/usr/share/crafty/crafty.hlp"

    cd "$srcdir"
    install -m644 book.bin "$pkgdir/usr/share/crafty/"
    install -m644 books.bin "$pkgdir/usr/share/crafty/"
    install -Dm644 copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"
}
