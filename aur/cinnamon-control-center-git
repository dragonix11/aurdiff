# Maintainer: twa022 <twa022 at gmail dot com>
# Based on PKGBUILD for gnome-control-center 3.6.3-1

_pkgname=cinnamon-control-center
pkgname=${_pkgname}-git
pkgver=227.ebeaa58
pkgrel=1
pkgdesc="The Control Center for Cinnamon"
arch=('i686' 'x86_64')
depends=('cinnamon-git' 'cinnamon-translations-git' 'libgtop' 'network-manager-applet')
optdepends=('cheese: webcam integration for user applet'
            'cinnamon-bluetooth-git: bluetooth module'
            'mesa-demos: provides glxinfo for graphics information'
            'gnome-color-manager: for color management tasks')
makedepends=('cheese' 'intltool' 'gnome-common' 'libgnomekbd' 'git')
url="https://github.com/linuxmint/cinnamon-control-center"
install=${_pkgname}.install
license=('GPL')
options=('!libtool' '!emptydirs')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git://github.com/linuxmint/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/${_pkgname}"

  autoreconf -fi
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --sbindir=/usr/bin \
      --disable-static --enable-systemd \
      --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
}

build() {
  cd "$srcdir/${_pkgname}"
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  make DESTDIR="$pkgdir" install

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
