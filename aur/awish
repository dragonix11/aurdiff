# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=awish
pkgver=1.0.beta3
pkgrel=2
pkgdesc='Rewrite of the old MS-DOS game Avish'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/awish.git'
license=('GPL3')
depends=('sdl_mixer' 'sh' 'awish-data')

source=("${pkgname}-${pkgver}.tar.gz::http://repo.or.cz/w/awish.git/snapshot/5e0bd8e7556092169a478aa1c1dc8c90e1edfa75.tar.gz"
        "${pkgname}.sh"
        "${pkgname}.desktop")

md5sums=('SKIP'
         'ce4ab7ae32e1c968157943a9a772e513'
         '8673f646ccdca6ef8caf2ae9e3a2dcda')

build() {
  cd "${srcdir}/${pkgname}"
  chmod +x 0build.sh
  ./0build.sh
}

package() {
  # main files
  install -dm755 "${pkgdir}/usr/share/${pkgname}/"

  install -m755 "${srcdir}/${pkgname}/${pkgname}" "${pkgdir}/usr/share/${pkgname}"
  cd "${srcdir}/${pkgname}"
  cp -r data "${pkgdir}/usr/share/${pkgname}/"

  # readme
  install -D -m644 "${srcdir}/${pkgname}/README" "${pkgdir}/usr/share/doc/${pkgname}/readme-src"

  # bash script
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
