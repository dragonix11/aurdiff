# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=udev-notify-bzr
pkgver=28
pkgrel=1
pkgdesc="Visual hardware notifications for Linux. (Bazaar version)"
arch=('i686' 'x86_64')
url="https://launchpad.net/udev-notify"
license=('GPL3')
depends=('notification-daemon' 'python2' 'python2-udev' 'python2-notify')
makedepends=('bzr')
source=('udev-notify::bzr+https://code.launchpad.net/udev-notify/trunk')
md5sums=('SKIP')
conflicts=('udev-notify')
provides=('udev-notify')

_bzrmod="udev-notify"

pkgver() {
  cd "${_bzrmod}"
  echo "$(bzr revno)"
}

prepare() {
  cd "${_bzrmod}"

  # Prepare Makefile
  sed -e '11d' -e '14e' -e '21d' -e '24d' -e 's|\./build/debian|$(DESTDIR)|g' -i Makefile
  # Force use python2
  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i src/udev-notify.py
}

package() {
  cd "${_bzrmod}"
  make DESTDIR="${pkgdir}"
}

