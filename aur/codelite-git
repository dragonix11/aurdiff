# Maintainer: Pyro Devil <p dot devil at gmail dot com>
pkgname=codelite-git
_gitname=codelite
pkgver=5.2.200.g217d721
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
url="http://codelite.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk2.9>=2.9.5' 'clang')
makedepends=('git' 'cmake' 'webkitgtk2' 'libssh')
optdepends=('graphviz: callgraph visualization')
conflicts=('codelite')
replaces=()
provides=('codelite')
backup=()
install=
source=("${_gitname}::git://git.code.sf.net/p/codelite/codelitegit")
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --tags | sed 's|-|.|g'
}

build() {
  cd "$_gitname"
  
  sed -i 's/wx-config/wx-config-2.9/g' CMakeLists.txt
  cmake -G "Unix Makefiles" -DPREFIX="/usr" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9 .
  make
}

package() {
  cd "${_gitname}"

  make -j1 DESTDIR=${pkgdir} install
  install -m 644 -D "${srcdir}/${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
}
