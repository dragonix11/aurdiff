# Maintainer: Splex

pkgname=inkscape-bzr
_bzrname=inkscape
pkgver=12700
pkgrel=1
pkgdesc="An Open Source vector graphics editor, with capabilities similar to Illustrator, CorelDraw, or Xara X, using the W3C standard Scalable Vector Graphics (SVG) file format."
url="https://launchpad.net/inkscape"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gc' 'gsl' 'gtkmm' 'gtkspell' 'imagemagick' 'libxslt' 'poppler-glib' 'popt'
         'python2' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('pstoedit: latex formulas'
            'texlive-core: latex formulas'
            'python2-numpy: some extensions'
            'python2-lxml: some extensions and filters'
            'pyxml: some extensions'
            'uniconvertor: reading/writing to some proprietary formats')
makedepends=('boost' 'pkg-config' 'intltool' 'bzr' 'gettext' 'autoconf')
provides=('inkscape')
conflicts=('inkscape')
options=('!libtool')
source=('inkscape::bzr+http://bazaar.launchpad.net/~inkscape.dev/inkscape/trunk/')
md5sums=('SKIP')
install=install

pkgver() {
  cd "${srcdir}/${_bzrname}"
  bzr revno
}

prepare() {
  cd "${srcdir}/${_bzrname}"
  ./autogen.sh

  #Fix for inkscape to use python2 with the python 3 package installed.
  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|python -c |python2 -c|g' configure share/extensions/uniconv*.py
  sed -i 's|"python"|"python2"|g' src/main.cpp

  #Fix for Glib build errors
  sed -i '/^#include <g.kmm/i #include <glibmm.h>' src/*{,/*{,/*{,/*}}}.{h,cpp}
}

build() {
  cd "${srcdir}/${_bzrname}"
  ./configure --prefix=/usr \
    --with-python \
    --with-perl \
    --without-gnome-vfs \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking

  make
}

package() {
  cd "${srcdir}/${_bzrname}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
