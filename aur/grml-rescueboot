# Maintainer: Michael La√ü <bevan@bi-co.net>
# Contributor: Max Roder <maxroder@web.de>

pkgname='grml-rescueboot'
pkgver='0.4.3'
pkgrel='2'
pkgdesc="grub2 script to add grml iso images to the grub2 boot menu"
url="http://deb.grml.org/pool/main/g/grml-rescueboot/"
arch=('any')
license=('GPL')
depends=('grub')
install="${pkgname}.install"
source=("http://deb.grml.org/pool/main/g/grml-rescueboot/${pkgname}_${pkgver}.tar.gz"
        "0001-Fix-leaked-file-descriptors.patch")
sha256sums=('5fcd814b80ad4f254dfe770a876d1f5faca67795c0886cbb9e6bdc16cfdf710e'
            'fef7d2677cb14420acc067e70e63bad64d10871c7e9e20892b20ed2a06f83a29')

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# fix leaked file descriptors
	patch -p1 < ${srcdir}/0001-Fix-leaked-file-descriptors.patch
	
	# adjust path of grub-mkconfig_lib
	sed -i 's/${libdir}\/grub/${prefix}\/share\/grub/g' 42_grml

	install -Dm755 42_grml ${pkgdir}/etc/grub.d/42_grml
	install -Dm644 etc/default/${pkgname} ${pkgdir}/etc/default/${pkgname}
	install -d ${pkgdir}/boot/grml
}
