# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=epiphany
pkgname=$_pkgname-git
pkgver=3.9.3
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup-gnome' 'libnotify' 'gsettings-desktop-schemas' 'webkitgtk3>=2.1.1' 
		  'nss' 'iso-codes' 'dconf' 'gobject-introspection' 'desktop-file-utils' 
		  'hicolor-icon-theme' 'gcr' 'gnome-desktop')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common' 'git' 'startup-notification')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
replaces=('epiphany')
provides=('epiphany' 'epiphany=3.9.3')
conflicts=('epiphany')
source=('git://git.gnome.org/epiphany')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"

  msg "Starting build"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-maintainer-mode --disable-deprecations
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
