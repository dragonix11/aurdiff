# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.5.1
pkgrel=2
pkgdesc="A library and program to extract, create, and modify WIM files"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/wimlib"
license=("GPL3")
depends=("openssl" "fuse" "libxml2" "ntfs-3g" "attr")
optdepends=("cdrkit: for making ISO image of Windows PE"
	"mtools: for making disk image of Windows PE"
	"syslinux: for making disk image of Windows PE"
	"cabextract: for extracting Windows PE from the WAIK"
	"ntfsprogs: for making NTFS filesystems")
checkdepends=("ntfsprogs")
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	"wimlib-1.5.1-fix_alignment_bug.patch")

build() {
	cd "$pkgname-$pkgver"
	patch -p1 < "${srcdir}/wimlib-1.5.1-fix_alignment_bug.patch"
	./configure --with-libcrypto --with-fuse --with-ntfs-3g \
			--enable-xattr --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	for file in README NEWS README.WINDOWS; do
		install -Dm644 $file "$pkgdir/usr/share/doc/$pkgname/$file"
	done
}
md5sums=('25e79203d2b827d01cece99c99473585'
         'd55e4cbf93382934057d5df80850063b')
