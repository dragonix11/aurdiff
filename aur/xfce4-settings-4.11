# Maintainer: Piotr Gorski <sir_lucjan@bridgelinux.pl>

pkgname=xfce4-settings-4.11
_pkgname=xfce4-settings
pkgver=4.11.1
pkgrel=2
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'libnotify' 'libxklavier'
         'gnome-icon-theme' 'gtk-engines')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.11/$_pkgname-$pkgver.tar.bz2
        xfce4-settings-xml-4.10.0.patch)
sha256sums=('bae5dc0fee841d191f24ada14658e030d69a434e2a1abd81a940a645f370fc8f'
            '78e68ef2cbf19760707ba24f3d57dd59fbf40a540d50d20e7cc62ba02196aaaa')


prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # enable gnome icon theme, clearlooks theme and font hinting by default
  # (taken from Fedora)
  patch -Np1 -i "$srcdir/xfce4-settings-xml-4.10.0.patch"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --disable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}