# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-nxengine-git
pkgver=303.4070c26
pkgrel=2
pkgdesc="libretro implementation of NXEngine. (Cave Story)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/nxengine-libretro"
license=('GPL3')
makedepends=('git')

install=libretro-nxengine.install

_gitname=nxengine-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/nxengine_libretro.info")
md5sums=('SKIP'
         'c29b9b44591cd46046e8c882e19762a0')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/nxengine_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-nx.so"
  install -Dm644 "nxengine_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-nx.info"
  install -Dm644 "${_gitname}/datafiles/Doukutsu.exe" "${pkgdir}/usr/share/libretro/libretro-nx/datafiles/Doukutsu.exe"
  cp -r "${_gitname}/datafiles/data/" "${pkgdir}/usr/share/libretro/libretro-nx/datafiles/"
}
