# Maintainer: Simon 'ALSimon' Gilliot <simon@gilliot.fr>
pkgname=mitmproxy
pkgver=0.9.1
pkgrel=1
pkgdesc="SSL-capable man-in-the-middle HTTP proxy"
arch=(any)
url="http://mitmproxy.org/"
license=('GPL')
depends=('python2-netlib' 'python2-pyopenssl' 'python2-pyasn1' 'python2-urwid' 'python2-imaging' 'python2-lxml' 'python2-flask')
optdepends=('python-pyamf: For extended content decoding of AMF' 'python2-protobuf: For extended content decoding')
conflicts=('mitmproxy-git')
source=("http://mitmproxy.org/download/$pkgname-$pkgver.tar.gz")
md5sums=('2ebd39e1118c6dbad8781164d71ee626')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's/^#\!\/usr\/bin\/env python$/#\!\/usr\/bin\/env python2/' libmproxy/contrib/html2text.py
}
 
package() {
        cd "$srcdir/$pkgname-$pkgver"
        python2 setup.py install --root=$pkgdir
}
