# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [testing]'s xorg-server

pkgname=xorg-server-dev
pkgname+=('xorg-server-xephyr-dev' 'xorg-server-xdmx-dev' 'xorg-server-xvfb-dev' 'xorg-server-xnest-dev' 'xorg-server-common-dev' 'xorg-server-devel-dev')
pkgver=1.14.99.901  # 1.15 RC1: http://lists.x.org/archives/xorg/2013-November/056148.html
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto' 'videoproto'
             'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess'
             'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'xorg-xkbcomp' 'xorg-util-macros'
             'xorg-font-util' 'glproto' 'dri2proto' 'libgcrypt') 
source=(${url}/releases/individual/xserver/xorg-server-${pkgver}.tar.bz2
        autoconfig-nvidia.patch
        autoconfig-sis.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
sha256sums=('6864845d9b0f62eb92d57c75c7400036c32f009b798007f018ae515850c4419c'
            '643b9f701e11af7ba7562fe00399f6197a0ddceea6d763ada6cf2c7d023f0acc'
            '08659276a700eb36fdf313a209d291046483888682b4f54f4a036de4852aa8f0'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166')

prepare() {
  cd "xorg-server-${pkgver}"

  msg2 "Use nouveau/nv/nvidia drivers for nvidia devices"
  patch -Np1 -i ../autoconfig-nvidia.patch

  msg2 "Use unofficial imedia SiS driver for supported SiS devices"
  patch -Np0 -i ../autoconfig-sis.patch
}

build() {
  cd "xorg-server-${pkgver}"

  msg2 "Starting autoreconf..."
  autoreconf -fi

  msg2 "Starting ./configure..."
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts

  msg2 "Starting make..."
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-dev() {
  pkgdesc="Xorg server common files - Bleeding edge version"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=('xorg-server-common')
  conflicts=('xorg-server-common')

  cd "xorg-server-${pkgver}"
  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-common"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xorg-server-common"
  
  msg2 "Starting make install-data..."
  make -C xkb DESTDIR="${pkgdir}" install-data

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"
}

package_xorg-server-dev() {
  pkgdesc="Xorg X server - Bleeding edge version"
  depends=(libxdmcp libxfont libpciaccess libdrm 'pixman>=0.28.0' libgcrypt libxau xorg-server-common-dev xf86-input-evdev)
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  provides=('X-ABI-VIDEODRV_VERSION=15.0' 'X-ABI-XINPUT_VERSION=20.0' 'X-ABI-EXTENSION_VERSION=8.0' 'x-server' 'xorg-server')
  groups=('xorg')
  conflicts=('nvidia-utils<=290.10' 'xorg-server')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var"

  rm -f "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -f "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server/COPYING"

  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share/aclocal"
}

package_xorg-server-xephyr-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=(libxfont libgl libgcrypt libxv 'pixman>=0.28.0' xorg-server-common-dev)
  provides=('xorg-server-xephyr')
  conflicts=('xorg-server-xephyr')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}/hw/kdrive"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xvfb-dev() {
  pkgdesc="Virtual framebuffer X server - Bleeding edge version"
  depends=(libxfont libxdmcp libxau libgcrypt 'pixman>=0.28.0' xorg-server-common-dev)
  provides=('xorg-server-xvfb')
  conflicts=('xorg-server-xvfb')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}/hw/vfb"
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-xnest-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=(libxfont libxext libgcrypt 'pixman>=0.28.0' xorg-server-common-dev)
  provides=('xorg-server-xnest')
  conflicts=('xorg-server-xnest')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}/hw/xnest"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xdmx-dev() {
  pkgdesc="Distributed Multihead X Server and utilities - Bleeding edge version"
  depends=(libxfont libxi libgcrypt libxaw libxrender libdmx libxfixes 'pixman>=0.28.0' xorg-server-common-dev)
  provides=('xorg-server-xdmx')
  conflicts=('xorg-server-xdmx')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}/hw/dmx"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-devel-dev() {
  pkgdesc="Development files for the X.Org X server - Bleeding edge version"
  # see pkg/usr/lib/pkgconfig/xorg-server.pc
  depends=(xproto randrproto renderproto xextproto inputproto kbproto 
           fontsproto pixman videoproto xf86driproto glproto 
           mesa dri2proto xineramaproto libpciaccess
           # see FS#32146
           resourceproto scrnsaverproto
           # not technically required but almost every Xorg pkg needs it to build
           xorg-util-macros)
  provides=('xorg-server-devel')
  conflicts=('xorg-server-devel')

  msg2 "Starting make install..."
  cd "xorg-server-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
  rm -rf "${pkgdir}/usr/share/doc"
  rm -rf "${pkgdir}/usr/share/X11"
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel/COPYING"
}
