# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=wingpanel
pkgver=0.2.3
pkgrel=3
pkgdesc="A super sexy space-saving top panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/wingpanel"
license=('GPL3')
depends=('granite' 'libindicator3')
optdepends=('indicator-datetime: Display date and time'
            'indicator-pantheon-session-bzr: Pantheon session indicator'
            'indicator-power: Battery idicator')
makedepends=('cmake' 'vala0.16')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/${pkgver%?}x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('064c4ac11c330cd084d96a1497cd2dc38d488afd9410b3ae484d2ed73f749ee3')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DVALA_EXECUTABLE='/usr/bin/valac-0.16' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}

# vim: ts=2 sw=2 et:
