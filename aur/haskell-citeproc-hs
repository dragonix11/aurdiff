# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Arch Haskell Team <>
# Contributor: Lex Black <autumn-wind at web dot de>

_hkgname=citeproc-hs
pkgname=haskell-citeproc-hs
pkgver=0.3.8
pkgrel=3
pkgdesc="A Citation Style Language implementation in Haskell"
url="http://gorgias.mine.nu/repos/citeproc-hs/"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=(haskell-http haskell-bytestring haskell-containers haskell-directory
         haskell-filepath haskell-json haskell-mtl haskell-old-locale haskell-hs-bibutils
         haskell-pandoc-types haskell-parsec haskell-syb haskell-time haskell-utf8-string haskell-xml)
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/${_hkgname}-${pkgver}.tar.gz")
install=$pkgname.install
sha256sums=('e873673b2960d0eebbcede9d8c18e2acea0e4fed295552fa635814f4bae48e72')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid -f bibutils
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/$_hkgname-$pkgver"
  install -D -m744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
