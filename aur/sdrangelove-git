# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=sdrangelove-git
pkgver=20131104
pkgrel=1
pkgdesc="A high speed SDR waterfall display."
arch=('i686' 'x86_64')
url="http://sdr.osmocom.org/trac/wiki/sdrangelove"
license=('GPL')
depends=('qt5-multimedia' 'rtl-sdr' 'pulseaudio')
# build with gr-osmosdr for more hardware
makedepends=('git' 'cmake')
conflicts=('sdrangelove')
source=("git://git.osmocom.org/sdrangelove.git")
md5sums=('SKIP')

_gitname="sdrangelove"

pkgver() {
  cd "$srcdir/$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}


build() {
  cd "$srcdir/$_gitname"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -Wno-dev ../
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  # missing target and sane linking
  #make DESTDIR="$pkgdir" install
  install -Dm755 sdrangelove "$pkgdir/opt/$pkgname/sdrangelove"
  install -Dm644 libsdrbase.so "$pkgdir/opt/$pkgname/libsdrbase.so"
  cp -r plugins "$pkgdir/opt/$pkgname/"
  mkdir -p "$pkgdir/usr/bin"
  echo -e "#!/bin/bash\ncd /opt/sdrangelove-git/\nLD_LIBRARY_PATH=./ ./sdrangelove\ncd -" > "$pkgdir/usr/bin/sdrangelove"
  echo -e "#!/bin/bash\npulseaudio --start\nsdrangelove\npulseaudio -k" > "$pkgdir/usr/bin/sdrangelove-alsa"
  chmod +x "$pkgdir/usr/bin/sdrangelove"
  chmod +x "$pkgdir/usr/bin/sdrangelove-alsa"
}

# vim:set ts=2 sw=2 et:
