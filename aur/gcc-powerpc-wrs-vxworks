#Maintainer: Alex Brinister (alex_brinister@yahoo.com)
pkgname=gcc-powerpc-wrs-vxworks
pkgver=4.8.1
pkgrel=4
pkgdesc="GCC modded for VxWorks 6.3"
arch=('i686' 'x86_64')
url="http://firstforge.wpi.edu/sf/projects/c--11_toochain"
license=('GPL')
provides=('gcc-powerpc-wrs-vxworks')
conflicts=('vxworks-gcc-toolchain-bin')
depends=('wrs-vxworks-headers' 'binutils-powerpc-wrs-vxworks')
makedepends=('wget')
options=('!strip' '!libtool' '!zipman' '!buildflags' '!makeflags')
source=(http://ftp.gnu.org/gnu/gcc/gcc-"$pkgver"/gcc-"$pkgver".tar.bz2
		patch-vxworks-mkdir-for-libgcov.patch)
sha512sums=('1becc874aad77a469069b6d9da4158aae9e013e24afa9364fe4feff9a5094d0673ee7694b3840e892c860f73a56b3ece6174338a8214438c42b9f86dd6c35ea7'
            'b9fb647e6b1ffca0e409a47c9fa525d28a9f55ee76358277fde777f666e16bfbdceca1d74201407dc3ac5f589f66194a6ad470ead14d43c230b22398400d411a')
_gcc_name="gcc-$pkgver"

build() {
	cd $srcdir
	mv patch-vxworks-mkdir-for-libgcov.patch $_gcc_name
	cd $_gcc_name
	patch -Np0 < patch-vxworks-mkdir-for-libgcov.patch
	rm -r patch-vxworks-mkdir-for-libgcov.patch
	
	cd $srcdir/$_gcc_name
	./contrib/download_prerequisites
	cd $srcdir
	mkdir -p build && cd build

	export CFLAGS_FOR_TARGET="-g -O2 -mlongcall"
	../$_gcc_name/configure \
		--prefix=/usr \
		--target=powerpc-wrs-vxworks \
		--with-gnu-as \
		--with-gnu-ld \
		--with-headers \
		--disable-shared \
		--disable-libssp \
		--disable-multilib \
		--with-float=hard \
		--enable-languages=c,c++ \
		--enable-libstdcxx \
		--enable-threads=vxworks \
		--without-gconv \
		--disable-libgomp \
		--disable-nls \
		--with-cpu-PPC603 \
		--disable-libmudflap \
		--disable-symvers \
		--enable-cxx-flags=-mlongcall
	make ${MAKEFLAGS} || return 1
}
package(){
  cd "$srcdir/build"
  make ${MAKEFLAGS} DESTDIR=$pkgdir install || return 1

  rm -r $pkgdir/usr/share/{info,man/man7}
  rm -r $pkgdir/usr/share/gcc-$pkgver
  rm $pkgdir/usr/lib/libiberty.a
}

# vim:set ts=2 sw=2:
