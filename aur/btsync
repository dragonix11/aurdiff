# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgname=btsync
pkgver=1.1.70
pkgrel=1
pkgdesc="BitTorrent Sync - automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64' 'armv6' 'armv7')
url="http://labs.bittorrent.com/experiments/sync.html"
install=btsync.install
backup=('etc/btsync.conf')
source=(license.html::http://www.bittorrent.com/legal/terms-of-use btsync.service btsync@.service)
sha1sums=('SKIP' '9187bafe0cc30895d55b8a8a69074ebdca325e4c' '0e9a7888776958d160f2b719e61257efc60d312a')

if [ "$CARCH" == x86_64 ]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/${pkgname}_x64-${pkgver}.tar.gz")
	sha1sums+=('8f46fcd783b93bffb23c2bdd1a42db18817381d0')
elif [ "$CARCH" == i686 ]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/${pkgname}_i386-${pkgver}.tar.gz")
	sha1sums+=('de30f0fec8d45bf3dc768ebf16cb67e974b70705')
elif [[ "$CARCH" == armv6 || "$CARCH" == armv7 ]]; then
	source+=("http://syncapp.bittorrent.com/${pkgver}/${pkgname}_arm-${pkgver}.tar.gz")
	sha1sums+=('8313f7c723ad4f598ded1e2ec2746a14076a8f8d')
fi

package() {
	cd ${srcdir}
	install -Dm755 btsync ${pkgdir}/usr/bin/btsync
	# generate and install system-wide configand systemd unit
	mkdir -p ${pkgdir}/etc
	./btsync --dump-sample-config | sed 's:/home/user/\.sync:/var/lib/btsync:g' > ${pkgdir}/etc/btsync.conf
	# install systemd units
	install -Dm644 btsync.service ${pkgdir}/usr/lib/systemd/system/btsync.service
	install -Dm644 btsync@.service ${pkgdir}/usr/lib/systemd/system/btsync@.service
	# install license
	install -Dm644 license.html "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
}
