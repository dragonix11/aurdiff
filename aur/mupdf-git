# Contributor: Army 
# Maintainer: Stefan Husmann <stefan-husmannt-online.de> 

_pkgname=mupdf
pkgname=$_pkgname-git
pkgver=1.3.148.g3379bea
pkgrel=2
pkgdesc="lightweight PDF viewer and toolkit written in portable C"
arch=('i686' 'x86_64')
url="http://mupdf.com"
license=('GPL3')
depends=('libxext' 'jbig2dec' 'desktop-file-utils' 'libjpeg' 'freetype2' 'openssl')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
install=$pkgname.install
source=("$_pkgname::git+git://git.ghostscript.com/mupdf.git" 'mupdf.desktop')
md5sums=('SKIP'
         '54abfeb678a7dfca238fa8d423f8d2fc')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git describe --tags | sed 's|-|.|g')
}

prepare() {
  cd "$srcdir/$_pkgname"
  git submodule update --init thirdparty/openjpeg
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'
  cd "$srcdir/$_pkgname"
  make build=release prefix="$pkgdir/usr"
}

package() {
  cd "$srcdir/$_pkgname"
  make build=release prefix="$pkgdir/usr" install
  install -Dm644 $srcdir/mupdf.desktop \
    $pkgdir/usr/share/applications/mupdf.desktop
  rm "$pkgdir"/usr/bin/mupdf-x11

  # apparently an upstream bug, namcap doesn't like it, so I fix it
  cd "$pkgdir/usr/lib"
  for i in *;do chmod -x "$i";done

}
