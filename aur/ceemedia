# Contributor: 3ED <kas1987@o2.pl>

arch=('i686' 'x86_64') # I do not have 64. Please, send me info if it does not work.
license=('GPL')
pkgname=ceemedia
pkgver=0.5.4
_subver=1
pkgrel=3
pkgdesc="CeeMedia is a program for cataloging your movie collection."
depends=('python>=2.3' 'pygtk>=2.6' 'libglade>=2.5')
url="http://ceemedia.sosdg.org/index.html"
source=(http://ceemedia.sosdg.org/releases/${pkgname}-${pkgver}-${_subver}.tar.bz2
        python2.patch)
sha256sums=('5b5c94c1fd03454650fdbea08c05729441844e7ce2ff85b6370fe9de266672fb'
            'd2f71a0578657545e5614ebb5c2247934c373f824382769351ef69b061ec7c47')

prepare() {
  cd ${pkgname}-${pkgver}/src
  patch -Np0 -i "$srcdir"/python2.patch
}

package() {
  cd ${pkgname}-${pkgver}/src

  install -dm755 "$pkgdir"/usr/{bin,share/{applications,pixmaps,ceemedia/{plugins,images}}}

  install -m755 ceemedia "$pkgdir"/usr/bin/
  install -m644 ceemedia.desktop "$pkgdir"/usr/share/applications/
  install -m644 ceemedia.glade *.py "$pkgdir"/usr/share/ceemedia/
  install -m644 plugins/* "$pkgdir"/usr/share/ceemedia/plugins/
  install -m644 images/* "$pkgdir"/usr/share/ceemedia/images/

  ln -s ../ceemedia/images/ceemedia.png "$pkgdir"/usr/share/pixmaps/ceemedia.png

  #### locale ####
  cd i18n
  for i in */; do
    test -f ${i}LC_MESSAGES/ceemedia.mo || continue
    install -dm755 "$pkgdir"/usr/share/locale/${i}LC_MESSAGES
    install -m644 ${i}LC_MESSAGES/ceemedia.mo "$pkgdir"/usr/share/locale/${i}LC_MESSAGES/ceemedia.mo
  done
}
