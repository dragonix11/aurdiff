# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=videoscope
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130820.0cc4c5a
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/dubhater/vapoursynth-${_plug}"
license=('custom:WTFPL')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git' 'clang')
source=("${_plug}::git://github.com/dubhater/vapoursynth-${_plug}.git"
        'COPYING')
md5sums=('SKIP'
         '389a9e29629d1f05e115f8f05c283df5')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"

  echo "all:
	clang -O3 -Wall -Wextra -Wno-unused-parameter -fPIC -shared -o libvideoscope.so videoscope.c" > Makefile
}

build() {
  cd "${_gitname}"

  make
}

package(){
  cd "${_gitname}"
  install -Dm755 "libvideoscope.so" "${pkgdir}/usr/lib/vapoursynth/libvideoscope.so"
  install -Dm644 readme.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
  install -Dm644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

