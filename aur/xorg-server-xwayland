# Maintainer: mortdeus <mortdeus@gocos2d.org>
# Contributor: Yukicanis <yukicanis@gmail.com>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Edited by: Zachary Lund <admin@computerquip.com>

pkgname=xorg-server-xwayland

pkgname+=(
'xorg-server-xwayland' 'xorg-server-common-xwayland' 
'xorg-server-devel-xwayland' 'xorg-server-xdmx-xwayland'   
'xorg-server-xephyr-xwayland' 'xorg-server-xnest-xwayland'  
'xorg-server-xvfb-xwayland')

pkgver=1.14.99.1
pkgrel=1
pkgdesc="Xorg Server with Wayland Support"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org/"
license=('custom')

provides='xwayland'

depends=( 'weston-git' 'wayland-git')

conflicts=('weston' 'wayland' 'xwayland')

makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto'
             'xtrans' 'xextproto' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto'
             'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto'
             'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont'
             'renderproto' 'libpciaccess' 'libxv' 'xf86dgaproto' 'libxmu'
             'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst'
             'libxres' 'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util'
             'glproto' 'dri2proto' 'libgcrypt' 'git' 'mtdev' 'bison')


options=('!libtool')

source=('git+git://anongit.freedesktop.org/xorg/xserver#branch=xwayland'
        autoconfig-nvidia.patch   autoconfig-sis.patch
        xvfb-run      xvfb-run.1  10-quirks.conf)

sha256sums=('SKIP'
            '643b9f701e11af7ba7562fe00399f6197a0ddceea6d763ada6cf2c7d023f0acc'
            '08659276a700eb36fdf313a209d291046483888682b4f54f4a036de4852aa8f0'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166')

prepare() {
  cd $srcdir/xserver
  git reset --hard HEAD
  git clean -dfx

  msg2 "Use nouveau/nv/nvidia drivers for nvidia devices"
  patch -Np1 -i ../autoconfig-nvidia.patch

  msg2 "Use unofficial imedia SiS driver for supported SiS devices"
  patch -Np0 -i ../autoconfig-sis.patch
}

build() {
  cd $srcdir/xserver
  autoreconf -fi
  ./configure --prefix=/usr \
      --enable-wayland \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}
package_xorg-server-common-xwayland() {
  pkgdesc="Xorg server common files (xwayland)"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=('xorg-server-common' 'xorg-server-common-xwayland')
  conflicts=('xorg-server-common' 'xorg-server-common-xwayland')

  cd $srcdir/xserver
  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-common"
  install -m644 COPYING "$pkgdir/usr/share/licenses/xorg-server-common"
  
  msg2 "Starting make install-data..."
  make -C xkb DESTDIR="$pkgdir" install-data

  install -m755 -d $pkgdir/usr/share/man/man1
  install -m644 man/Xserver.1 $pkgdir/usr/share/man/man1/

  install -m755 -d "$pkgdir/usr/lib/xorg"
  install -m644 dix/protocol.txt "$pkgdir/usr/lib/xorg/"
}
package_xorg-server-xwayland() {
  pkgdesc="Xorg X server (xwayland)"
  
  depends=(
  libxdmcp libxfont libpciaccess libdrm 'pixman>=0.28.0' 
  libgcrypt libxau xorg-server-common-xwayland xf86-input-evdev)
  
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  
  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions -
  # we provide major numbers that drivers can depend on
  groups=('xorg' 'wayland')

  provides=(
  'X-ABI-VIDEODRV_VERSION=14.1' 'X-ABI-XINPUT_VERSION=19.1'
  'X-ABI-EXTENSION_VERSION=7.0' 'x-server' 'xorg-server' 'xorg-server-xwayland')
  
  conflicts=('nvidia-utils<=290.10' 'xorg-server')

  msg2 "Starting make install..."
  cd $srcdir/xserver
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/etc/X11"
  mv "$pkgdir/usr/share/X11/xorg.conf.d" "$pkgdir/etc/X11/"
  install -m644 "$srcdir/10-quirks.conf" "$pkgdir/etc/X11/xorg.conf.d/"

  rmdir "$pkgdir/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir/usr/lib/xorg/modules/extensions/libglx.so" \
     "$pkgdir/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "$pkgdir/var"

  rm -f "$pkgdir/usr/share/man/man1/Xserver.1"
  rm -f "$pkgdir/usr/lib/xorg/protocol.txt"

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server/COPYING"

  rm -rf "$pkgdir/usr/lib/pkgconfig"
  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/share/aclocal"
}

package_xorg-server-xephyr-xwayland() {
  pkgdesc="A nested X server that runs as an X application (xwayland)"
  
  depends=(
  libxfont libgl libgcrypt libxv 
  'pixman>=0.28.0' xorg-server-common-xwayland)
  
  provides=('xorg-server-xephyr-xwayland' 'xorg-server-xephyr')
  conflicts=('xorg-server-xephyr')

  msg2 "Starting make install..."
  cd $srcdir/xserver/hw/kdrive
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xvfb-xwayland() {
  pkgdesc="Virtual framebuffer X server (xwayland)"

  depends=(
  libxfont libxdmcp libxau libgcrypt 'pixman>=0.28.0'
  xorg-server-common-xwayland xorg-xauth)
  
  provides=('xorg-server-xvfb' 'xorg-server-xvfb-xwayland')
  conflicts=('xorg-server-xvfb')

  msg2 "Starting make install..."
  cd $srcdir/xserver/hw/vfb
  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/xvfb-run" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/xvfb-run.1" "$pkgdir/usr/share/man/man1/"

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-xnest-xwayland() {
  pkgdesc="A nested X server that runs as an X application (xwayland)"

  depends=(
  libxfont libxext libgcrypt 
  'pixman>=0.28.0' xorg-server-common-xwayland)
  
  provides=('xorg-server-xnest' 'xorg-server-xnest-xwayland')
  conflicts=('xorg-server-xnest')

  msg2 "Starting make install..."
  cd $srcdir/xserver/hw/xnest
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xdmx-xwayland() {
  pkgdesc="Distributed Multihead X Server and utilities (xwayland)"

  depends=(
  libxfont libxi libgcrypt libxaw libxrender 
  libdmx libxfixes 'pixman>=0.28.0' xorg-server-common-xwayland)
 
  provides=('xorg-server-xdmx')
  conflicts=('xorg-server-xdmx')

  msg2 "Starting make install..."
  cd $srcdir/xserver/hw/dmx
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-devel-xwayland() {

  pkgdesc="Development files for the X.Org X server (xwayland)"
  # see pkg/usr/lib/pkgconfig/xorg-server.pc
  depends=(
           xproto randrproto renderproto xextproto inputproto kbproto 
           fontsproto pixman videoproto xf86driproto glproto 
           mesa dri2proto xineramaproto libpciaccess
           
           # see FS#32146
           resourceproto scrnsaverproto

           # not technically required but almost 
           # every Xorg pkg needs it to build
           xorg-util-macros)
  
  provides=('xorg-server-devel' 'xorg-server-devel-xwayland')
  conflicts=('xorg-server-devel')

  msg2 "Starting make install..."
  cd $srcdir/xserver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/share/man"
  rm -rf "$pkgdir/usr/share/doc"
  rm -rf "$pkgdir/usr/share/X11"
  rm -rf "$pkgdir/usr/lib/xorg"
  rm -rf "$pkgdir/var"

  install -m755 -d "$pkgdir/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-devel/COPYING"
}
