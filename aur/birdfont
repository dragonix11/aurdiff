# Maintainer: Matej Lach <matej.lach@gmail.com>

pkgname=birdfont
pkgver=0.31
pkgrel=2
pkgdesc='A free font editor that lets you create vector graphics and export TTF, EOT & SVG fonts'
arch=(i686 x86_64)
url='http://birdfont.org/'
license=('GPL3')
depends=(
	'vala' 'libgee' 'glib2' 'gtk2' 'libxml2' 'webkitgtk2' 'python2' 'gettext' 'desktop-file-utils'
	'hicolor-icon-theme')
makedepends=('vala' 'gettext' 'libgee')
source=(
	'http://birdfont.org/releases/birdfont-0.31.tar.gz'
	'http://www.unicode.org/Public/UNIDATA/NamesList.txt')
sha512sums=(
	'717738fb38238e1568c4392c0db0e2dfea98d7b7e69ab803bd705bb67825f88385026fad99555bd007f2f16141a9c4981baab7dea7f19f631e7b6f372678f68d'
	'74294f0a49fae2eb6a43137b3a6bd73379fb8d7e67144d5c311fa314f37750abcbc9668308d1a6e5a341a3500e2b42b00ab7caba1ce48b9b177bda1cf7cd49c3')
install=birdfont.install

prepare() {
	cd "${srcdir}/$pkgname-$pkgver"
    sed -i -e 's/gee-1\.0/gee-0.8/g' ./scripts/build.py	
}

build() {
	cd "${srcdir}/$pkgname-$pkgver"

	python2 ./configure
	python2 ./scripts/linux_build.py
}

package() {
	cd "${srcdir}/$pkgname-$pkgver"
	
	python2 ./install.py -d "${pkgdir}"
	mkdir -p "${pkgdir}/usr/share/unicode"
	# there is a way to change the directory to this, but
	# it keeps manpages unzipped. so using this kluge.
	mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
	cp "${srcdir}/NamesList.txt" "${pkgdir}/usr/share/unicode"
}
