# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
_actual_ver=13.04.0
_extra_ver=+13.10.20130823
_ubuntu_rel=0ubuntu1
# AUR is broken once again. How is ${_actual_ver}.${_extra_ver} greater than
# 32 characters?
pkgver=13.10
pkgrel=2
pkgdesc="The default wallpapers for Ubuntu (from 9.10 Karmic Koala to 13.10 Saucy Salamander)"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python2-distutils-extra')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('f5d869194292fbdb4d7cd658f2ff87b3a8fbbf549f22e6539f675c73c3725375315ac2d3565a1cbf0422e3a91a16cd39d0a13c3473c817a0e961a3a98a095516'
            '29782e9107bc8575b0687699bca4f26edd57a9416f6cd1908b431ec43d31987ff2302bfe67a255d7b72b03b98ed2e0812da602690cd82b4750a5060d83f3f3ec')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # The setup.py script reads the package version from debian/*
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
