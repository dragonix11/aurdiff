# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=gtklp
pkgver=1.3.0
pkgrel=1
pkgdesc="Tools to use and administrate CUPS"
arch=('i686' 'x86_64')
url="http://gtklp.sirtobi.com/"
license=('GPL')
depends=('gtk2')
makedepends=('libcups' 'libtool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.src.tar.gz"
        ${pkgname}{q,}.desktop)
md5sums=('594c4fee610470d2fd5a161787cfd1d4'
         'c4b73e5d3fe725bfd09dda0efbca95f0'
         '673bbc897e557f90777c6fad3a5b1071')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/gettextize/s/^/#/' autogen.sh
}

build() {
  cd $pkgname-$pkgver
  sh autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 ../*.desktop "$pkgdir/usr/share/applications"
}
