# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=excellent-bifurcation
pkgver=0.0.20071015
pkgrel=3
pkgdesc="A shmup with an evil twist."
arch=('i686' 'x86_64')
url="http://www.allegro.cc/depot/ExcellentBifurcation"
_watch="none"
license=('GPL3')
depends=('allegro4')
source=(http://ftp.de.debian.org/debian/pool/main/e/$pkgname/${pkgname}_$pkgver.orig.tar.gz
    http://ftp.de.debian.org/debian/pool/main/e/$pkgname/${pkgname}_$pkgver-2.diff.gz
    config.diff
    $pkgname)
md5sums=('de530c35c9bb678fb6abe55b84aeb235'
         'f3365bd0aade32a4c7c814bf81a47b3d'
         '5d0322c17e8bdb810e381b956d2204bd'
         '4bf1f7b39d8d5ac6ee4ebeccbe6f5e09')

build() {
    patch -i ${pkgname}_$pkgver-2.diff -d ./
    cd "$pkgname-$pkgver/src"
    patch main.c ../../config.diff 
    gcc -O2 -Wall *.c -o $pkgname.bin $(allegro-config --cflags --libs) -lm
    cd ..
    # undo silly patch
    #sed -i 's/Windowed = 1/Windowed = 0/' init.txt
}

package() {
    cd "$pkgname-$pkgver"
    # Need to take a closer look at deb's patches,
    # some of this might be redundant.
    install -d "$pkgdir/usr/bin"
    install -d "$pkgdir/usr/share/$pkgname"
    cp -R wavs "$pkgdir/usr/share/$pkgname/"
    cp -R gfx  "$pkgdir/usr/share/$pkgname/"
    install -D -m755 src/$pkgname.bin "$pkgdir/usr/share/$pkgname/"
    install -D -m644 *.txt        "$pkgdir/usr/share/$pkgname/"
    cd ..
    install -D -m755 $pkgname     "$pkgdir/usr/bin/"
}

