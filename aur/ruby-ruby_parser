# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
#
# Source: https://github.com/Ichimonji10/PKGBUILDs
# Old source: https://github.com/nicoulaj/archlinux-packages
#
# Note: namcap warns that 'ruby-sexp_processor' (in the `depends` array) is
# unnecessary. This is untrue. The README.txt file states that sexp_processor is
# a dependency.

_gemname=ruby_parser
pkgname="ruby-${_gemname}"
pkgver=3.2.2
pkgrel=1
pkgdesc="Ruby parser written in pure ruby."
arch=(any)
url="https://github.com/seattlerb/ruby_parser"
license=(MIT)
depends=(ruby-sexp_processor)
makedepends=(rubygems)
source=(LICENSE "http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")

md5sums=(
  'c7abe5196141f56171e2928ec03de6bc'
  'a4c4be7a61bfe8fc34d910b7df93f789'
)
sha256sums=(
  '46870842926785c757eb95c36c7c77235871c7fd2310e93ef72b681ac9ccfd53'
  '117d814b406876a8b6a85a28e2745de524924edefced9233d597bae6fefd7fcb'
)

package() {
  # setup
  cd "${srcdir}"
  export HOME=/tmp
  local readonly _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  # install gem
  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # install license
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
