# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=lzlib
pkgver=1.5
pkgrel=1
pkgdesc="A library providing in-memory LZMA compression and decompression functions"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/lzip/lzlib.html"
license=('GPL3')
depends=('gcc-libs')
makedepends=('make')
install=$pkgname.install
source=(http://download.savannah.gnu.org/releases/lzip/$pkgname-$pkgver.tar.gz)
sha256sums=('dde4812c40c210e1fd0c9ba54208a5120cc0030c01d12ff85d32deb4a0ba07c8')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -Dm755 minilzip ${pkgdir}/usr/bin/minilzip
}
