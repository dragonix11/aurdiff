# Maintainer: Aaron DeVore <aaron.devore@gmail.com>

pkgname=python2-parse
pkgver=1.6.1
pkgrel=1
pkgdesc='Parse strings using a specification based on the Python format() syntax.'
arch=('any')
url='http://pypi.python.org/pypi/parse'
license=('BSD')
depends=('python2')
makedepends=('python2-distribute')
optdepends=('python-parse-docs: documentation')
changelog='Changelog'
source=("http://pypi.python.org/packages/source/p/parse/parse-${pkgver}.tar.gz"
  license.txt)
md5sums=('d82bc6fc1202249f4d811da97b1848f6'
         'db7ed26afd7dab7d5cb906e06ea68988')

build() {
  cd "$srcdir/parse-$pkgver"
  python2 setup.py build
}

check() {
  # Is there a script for this?
  cd "$srcdir/parse-$pkgver"
  python2 test_parse.py
}

package() {
  cd "parse-$pkgver"
  # license from the bottom of parse.py
  install -D -m644 "${srcdir}/license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
