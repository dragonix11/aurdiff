# Maintainer:
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel
pkgver=3.8.0
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel"
url="https://live.gnome.org/GnomePanel"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager')
optdepends=('gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
install=$pkgname.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        libgweather-3.10.patch
        session.patch)
sha256sums=('d1cda458a38cb4f7d2a50c61b1a72f4bf4c39cab65ae0e6cfdc4e0c122252e7a'
            'd9e21449fc6c5f235673fe47e5b817ac7597490518a557908b14ca875263d90e'
            'cd4c21cf394ea5a702c660bb0d31cae31260bec1cee4e7ca8ee6c8aec2707ebb')

prepare() {
  cd "$pkgname-$pkgver"

  # Fix build with libgweather 3.10
  patch -Np1 -i "$srcdir/libgweather-3.10.patch"

  # Add more required components to the session file
  patch -Np1 -i "$srcdir/session.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # Avoid file conflict with gnome-flashback-session
  rm "$pkgdir"/usr/lib/gnome-panel/gnome-panel-add "$pkgdir"/usr/lib/gnome-panel/gnome-session-flashback "$pkgdir"/usr/share/gnome-session/sessions/gnome-flashback.session "$pkgdir"/usr/share/xsessions/gnome-fallback.desktop
}

# vim:set ts=2 sw=2 et:
