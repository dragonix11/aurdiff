# Maintainer: Ben R <thebenj88 *AT* gmail *DOT* com>
# Contributor: xylosper <darklin20@gmail.com>

_pkgbase=cmplayer

pkgname=cmplayer-git
pkgver=0.8.3.r18.g24bfd7d
pkgrel=1
pkgdesc="Powerful and easy-to-use multimedia player (git version)"
arch=('i686' 'x86_64')
url="http://$_pkgbase.github.io"
license=('GPL')
install=$_pkgbase.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols' 'icu'
         'libdvdread' 'libcdio-paranoia' 'libcdio' 'libquvi-0.4'
         'alsa-lib' 'libpulse' 'portaudio' 'openal' 'jack' 'libchardet'
         'mpg123' 'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('git' 'mesa' 'gcc' 'pkg-config')
provides=('cmplayer')
conflicts=('cmplayer')
source=(git+https://github.com/xylosper/${_pkgbase}.git)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g' | cut -c2-
}

build() {
  export MAKE="make -j $(nproc)"
  cd "$srcdir/$_pkgbase"
  install -d build/lib
  ./init-mpv
  ./build-mpv
  make PREFIX=/usr cmplayer
}

package() {
  cd "$srcdir/$_pkgbase"
  make DEST_DIR=$pkgdir PREFIX=/usr install
}
