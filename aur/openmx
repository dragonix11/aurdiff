# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=openmx
pkgver=3.7
_patchver=6
_patchdate=13Sep01
pkgrel=1
pkgdesc='A software package for nano-scale material simulations based on density functional theories'
arch=('i686' 'x86_64')
url="http://www.openmx-square.org/"
license=('GPL')
depends=('blas' 'fftw' 'lapack' 'openmpi')
source=("http://www.openmx-square.org/openmx${pkgver}.tar.gz" "http://www.openmx-square.org/bugfixed/${_patchdate}/patch${pkgver}.${_patchver}.tar.gz" "makefile.patch")
noextract=("patch${pkgver}.${_patchver}.tar.gz")

prepare() {
	cp "${srcdir}/patch${pkgver}.${_patchver}.tar.gz" "${srcdir}/openmx${pkgver}/source"
	cd "${srcdir}/openmx${pkgver}/source"
	tar zxvf patch${pkgver}.${_patchver}.tar.gz
	patch < "${srcdir}/makefile.patch"
}

build() {
	cd "${srcdir}/openmx${pkgver}/source"
	make
}

package() {
	cd "${srcdir}/openmx${pkgver}/source"
	install -Dm755 openmx "${pkgdir}/usr/bin/openmx"
	cd "${srcdir}/openmx${pkgver}"
	install -Dm644 "openmx${pkgver}.pdf" "${pkgdir}/usr/share/doc/${pkgname}/openmx${pkgver}.pdf"
	
	mkdir "${pkgdir}/usr/share/${pkgname}"
	cp -r "${srcdir}/openmx${pkgver}/work" "$pkgdir/usr/share/${pkgname}"
	cp -r "${srcdir}/openmx${pkgver}/DFT_DATA13" "$pkgdir/usr/share/${pkgname}"
}

md5sums=('085942c10bdb5933485304906966275b'
         '91ebd6e3df66a68e723cc5accc16c62a'
         'd3cc22bbbf54dac20b309fbf8ca9cc3b')
