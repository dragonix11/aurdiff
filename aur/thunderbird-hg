# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Enable PGO on x86_64?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-hg
pkgver=13126.52135ff5bc1a
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="https://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libnotify' 'libvpx' 'libxt' 'mime-types' 'mozilla-common' 'nss' 'sqlite' 'startup-notification')
makedepends=('autoconf2.13' 'mercurial' 'mesa' 'python2' 'unzip' 'wireless_tools' 'yasm' 'zip')
[ "$_pgo" = "1" ] && makedepends+=('imake' 'xorg-server-xvfb')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
options=('!emptydirs' '!makeflags')
source=('hg+https://hg.mozilla.org/comm-central'
        'mozconfig'
        'mozconfig-pgo'
        "$pkgname.desktop"
        'vendor.js')
md5sums=('SKIP'
         '16887fc4de525cd624b384fe5793abfa'
         'c2e9b0736543722fa7ee71aabae6c106'
         '19ea9f30321b65fa8de67f0cf7456be3'
         '5a53179d14ae9631b7afe5e4d0fc0b25')
_hgrepo="comm-central"

pkgver() {
  cd $_hgrepo
  echo $(hg identify -n).$(hg identify -i)
}

prepare() {
  cd $_hgrepo
  python2 client.py checkout

  if [ "$CARCH" = 'x86_64' ] && [ $_pgo = 1 ]; then
    cp "$srcdir"/mozconfig-pgo .mozconfig
  else
    cp "$srcdir"/mozconfig .mozconfig
  fi

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd $_hgrepo

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname-r$pkgver"
  export PYTHON="/usr/bin/python2"

  if [ "$CARCH" = 'i686' ]; then
    # Work around memory address space exhaustion during linking on i686
    LDFLAGS+=' --no-keep-memory'
    make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  elif [ "$CARCH" = 'x86_64' ] && [ $_pgo = 0 ]; then
    make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  else
    # Set up PGO
    export DISPLAY=:99
    Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

    if ! make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS" MOZ_PGO=1; then
      kill $!
      return 1
    fi

    kill $! || true
  fi
}

package() {
  cd $_hgrepo/obj-*
  make package

  cd mozilla/dist/
  tar -xf thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir"/opt/$pkgname-r$pkgver/
  ln -sf /opt/$pkgname-r$pkgver/thunderbird "$pkgdir"/usr/bin/$pkgname

  install -Dm644 "$srcdir"/vendor.js "$pkgdir"/opt/$pkgname-r$pkgver/defaults/preferences/vendor.js

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir"/$_hgrepo/other-licenses/branding/thunderbird/mailicon${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/$pkgname.png
  done

  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  rm -rf "$pkgdir"/opt/$pkgname-r$pkgver/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir"/opt/$pkgname-r$pkgver/dictionaries
  ln -s /usr/share/hyphen "$pkgdir"/opt/$pkgname-r$pkgver/hyphenation

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir"/opt/$pkgname-r$pkgver/defaults/pref/channel-prefs.js
}