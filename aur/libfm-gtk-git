# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname=libfm-gtk-git
pkgver=1.1.0.736.gcbb09b5
pkgrel=1
pkgdesc="A glib/gio-based library to develop file managers. Includes GTK2 widgets and dialogs."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('gtk2' 'menu-cache')
optdepends=('libexif')
makedepends=('git' 'gtk-doc' 'intltool' 'libexif' 'vala')
provides=('libfm' 'libfm-gtk')
conflicts=('libfm' 'libfm-gtk')
source=("git://github.com/lxde/libfm.git")
sha256sums=('SKIP')
_gitname="libfm"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --with-gtk --enable-gtk-doc --disable-udisks --enable-actions --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
