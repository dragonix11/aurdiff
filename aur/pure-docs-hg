# Maintainer: Albert Graef <aggraef at gmail.com>

pkgname=pure-docs-hg
pkgver=74.eb9294b9c169
pkgrel=1
pkgdesc="Pure Language and Library Documentation, latest snapshot"
arch=('any')
url="http://purelang.bitbucket.org/"
license=('FDL')
provides=('pure-docs=0.58')
conflicts=('pure-docs')
source=("hg+https://agraef@bitbucket.org/purelang/purelang.bitbucket.org")
md5sums=(SKIP)

pkgver() {
  cd $srcdir/purelang.bitbucket.org
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd $srcdir/purelang.bitbucket.org/docs
  make 
}

package() {
  cd $srcdir/purelang.bitbucket.org/docs
  make DESTDIR="$pkgdir" install install-tm
}

# vim:set ts=2 sw=2 et:
