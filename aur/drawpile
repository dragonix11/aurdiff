# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=drawpile
pkgver=0.6.0
pkgrel=2
pkgdesc='Collaborative drawing program specially made for use with pen tablets'
arch=('x86_64' 'i686')
url='http://drawpile.sourceforge.net/'
license=('GPL2')
depends=('qt4' 'cmake' 'gcc' 'boost')
makedepends=('gendesk')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('ab65bfe02441d8281bf8089e9c916ef456595ba80abc2f5152eb5336cddbf7d2')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --categories 'Graphics'
}

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr . && make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "src/client/ui/icons/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
