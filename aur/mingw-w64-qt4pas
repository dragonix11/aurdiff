pkgname=mingw-w64-qt4pas
pkgver=2.5
pkgrel=1
pkgdesc="Free Pascal Qt4 Binding Library (mingw-w64)"
arch=(any)
url="http://users.telenet.be/Jan.Van.hijfte/qtforfpc/fpcqt4.html"
license=("LGPL")
makedepends=(mingw-w64-gcc fpc ppcross386)
depends=(mingw-w64-crt
mingw-w64-qt4
fpc-i386-win32-rtl
fpc-x86_64-win64-rtl)
options=(!libtool !strip !buildflags)
source=("http://users.telenet.be/Jan.Van.hijfte/qtforfpc/V$pkgver/qt4pas-V${pkgver}_Qt4.5.3.tar.gz")
md5sums=('8249bc17e4167e077d22c7f5fb118bb2')
_fpcver=`fpc -iV`

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	cd "$srcdir"
  for _arch in ${_architectures}; do
		mkdir "build-${_arch}"
		cp -r "qt4pas-V${pkgver}_Qt4.5.3/"* "build-${_arch}"
		pushd "build-${_arch}"
    unset LDFLAGS
    ${_arch}-qmake Qt4Pas.pro
    if [ ${_arch} = 'i686-w64-mingw32' ]; then
      ppcross386 -Xs -XX -Twin32 qt4.pas
    else
      fpc -Xs -XX -Twin64 qt4.pas
      sed -i "s,-mthreads,-mthreads -fpermissive," Makefile
    fi
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    install -Dm644 libQt4Pas5.a "$pkgdir/usr/${_arch}/lib/libQt4Pas5.a"
    install -Dm644 Qt4Pas5.dll "$pkgdir/usr/${_arch}/bin/Qt4Pas5.dll"
		if [ ${_arch} = 'i686-w64-mingw32' ]; then
			install -Dm644 libimpqt4.a "$pkgdir/usr/lib/fpc/${_fpcver}/units/i386-win32/qt4pas/libimpqt4.a"
			install -m644 qt4.o "$pkgdir/usr/lib/fpc/${_fpcver}/units/i386-win32/qt4pas/"
			install -m644 qt4.ppu "$pkgdir/usr/lib/fpc/${_fpcver}/units/i386-win32/qt4pas/"
			find "$pkgdir/usr/lib/fpc/${_fpcver}/units/i386-win32/qt4pas/" -name '*.a' -o -name '*.o' |
				xargs -rtl1 ${_arch}-strip -g
		else
			install -Dm644 libimpqt4.a "$pkgdir/usr/lib/fpc/${_fpcver}/units/x86_64-win64/qt4pas/libimpqt4.a"
			install -m644 qt4.o "$pkgdir/usr/lib/fpc/${_fpcver}/units/x86_64-win64/qt4pas/"
			install -m644 qt4.ppu "$pkgdir/usr/lib/fpc/${_fpcver}/units/x86_64-win64/qt4pas/"
			find "$pkgdir/usr/lib/fpc/${_fpcver}/units/x86_64-win64/qt4pas/" -name '*.a' -o -name '*.o' |
				xargs -rtl1 ${_arch}-strip -g
		fi
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' |xargs -rtl1 ${_arch}-strip -g
  done
}