# Contributor: C Anthony Risinger
# Contributer: JÃ¶rg Thalheim <joerg@higgsboson.tk>

pkgname=z-push
pkgver=2.0.7
pkgrel=1690
pkgdesc="open-source implementation of the ActiveSync protocol"
arch=('any')
url="http://z-push.sf.net/"
license=('AGPL3')
depends=('php')
install=z-push.install
backup=('etc/webapps/z-push/config.php'
        'etc/webapps/z-push/.htaccess'
        'etc/webapps/z-push/apache.conf'
        'etc/webapps/z-push/nginx.conf')
options=('!strip')
source=("http://zarafa-deutschland.de/z-push-download/final/2.0/${pkgname}-${pkgver}-${pkgrel}.tar.gz"
        "apache.conf"
        "nginx.conf"
        "htaccess")

md5sums=('b0dd7f6da6b6d6bac49266cca28a4140'
         '970cfa46a056abaf1fd63ea291c98720'
         'e768c5ed6e631d18e156f94b3bc46948'
         '1091aa1ba272ef05bf628f73b05c527a')

package() {
  mkdir -p ${pkgdir}/etc/webapps/z-push
  mkdir -p ${pkgdir}/usr/share/webapps/z-push
  mkdir -p ${pkgdir}/var/{lib,log}/z-push
  cd ${pkgdir}/usr/share/webapps/

  cp -r ${srcdir}/${pkgname}-${pkgver}-${pkgrel}/* z-push

  mv z-push/config.php ${pkgdir}/etc/webapps/z-push/config.php
  ln -s /etc/webapps/z-push/config.php z-push/config.php

  cp ${srcdir}/htaccess ${pkgdir}/etc/webapps/z-push/.htaccess
  ln -s /etc/webapps/z-push/.htaccess z-push/.htaccess

  cp ${srcdir}/{apache,nginx}.conf ${pkgdir}/etc/webapps/z-push/
}
