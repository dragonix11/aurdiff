# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=isl
pkgname=lib32-${_pkgbase}
pkgver=0.12.1
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints (32 bit)"
arch=('x86_64')
url="http://freecode.com/projects/isl"
depends=('lib32-gmp' "${_pkgbase}")
makedepends=('gcc-multilib')
license=('LGPL2.1')
options=('!libtool')
source=(ftp://ftp.linux.student.kuleuven.be/pub/people/skimo/isl/${_pkgbase}-${pkgver}.tar.bz2)
md5sums=('27d381085ff1b5733c3e8a9b6ad24a4d')

build() {
  export CC='gcc -m32'
  export CXX='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make -j1 DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}/usr"/{include,lib32/libisl.so.*.*.*-gdb.py}
}
