# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libquvi-scripts-git
pkgver=0.9.20130805
pkgrel=1
pkgdesc="Embedded lua scripts for libquvi. (GIT version)"
arch=('any')
url="http://quvi.sourceforge.net/"
license=('LGPL')
depends=('curl' 'lua' 'lua52-socket')
makedepends=('git' 'asciidoc')
options=('!libtool')
provides=('libquvi-scripts')
conflicts=('libquvi-scripts')
source=('git://repo.or.cz/libquvi-scripts.git'
        'am_subdir_objects.patch')
sha256sums=('SKIP'
            '7817d9008e2beedb11496dd9ce58e8dc5418d3d8ad8c12f2799013de5f4ab361')

pkgver() {
  cd 'libquvi-scripts'
  git describe | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd 'libquvi-scripts'
  patch -p1 < "$srcdir/am_subdir_objects.patch"
}

build() {
  cd 'libquvi-scripts'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr \
    --with-nlfy \
    --with-nsfw \
    --with-geoblocked

  make
}

package() {
  cd 'libquvi-scripts'

  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh sw=2 ts=2 et
