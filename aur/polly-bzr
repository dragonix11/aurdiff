# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=polly-bzr
pkgver=419
pkgrel=1
pkgdesc="Linux Twitter client designed for multiple columns of multiple accounts"
arch=(any)
url="https://launchpad.net/polly"
license=('GPL')
depends=(python2-notify python2-httplib2 pyxdg python2-gconf python2-pycurl socksipy python2-gtkspell hspell python2-keyring python2-oauth2 python2-numpy librsvg)
makedepends=(python2-distribute)
install=$pkgname.install
source=('polly-bzr::bzr+http://bazaar.launchpad.net/~conscioususer/polly/1.0')
sha256sums=('SKIP')

pkgver() {
	cd $srcdir/polly-bzr
	bzr revno
}

build() {
	cd "$srcdir/polly-bzr"
	python2 setup.py build
}

package() {
	cd "$srcdir/polly-bzr"
	python2 setup.py install --skip-build -O1 --root="$pkgdir" --prefix=/usr
}

