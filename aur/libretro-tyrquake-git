# Maintainer: almostalive <almostalive2003 at gmail dot com>

pkgname=libretro-tyrquake-git
_gitname=tyrquake
pkgver=1297.66a5d56
pkgrel=1
pkgdesc="libretro implementation of tyrquake. (Quake)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/tyrquake"
license=('GPL')
makedepends=('git')
source=("${_gitname}::git://github.com/libretro/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/tyrquake_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-tyrquake.so"
}