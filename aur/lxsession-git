# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=lxsession
pkgname=$_pkgname-git
pkgver=727.099bd8b
pkgrel=1
pkgdesc="LXDE and LXQt session manager."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('qt4' 'gtk2')
optdepends=('lsb-release: For lxsession-logout')
makedepends=('git' 'cmake' 'vala')
provides=($_pkgname)
conflicts=($_pkgname)
source=("git://github.com/lxde/$_pkgname.git")
sha256sums=('SKIP')
_gitname=$_pkgname

pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc --disable-buildin-clipboard --disable-buildin-polkit
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
