# Maintainer: 3ED_0 <krzysztof1987 at gmail>
# Contributor: City-busz <AUR>
# Contributor: Maximilien Noal <noal dot maximilien at gmail dot com>
# Contributor: graysky <graysky AT archlnux.us>

pkgname=nvidia-96xx-ck
pkgver=96.43.23
pkgrel=14
_kver_ge="3.10"
_kver_lt="3.11"
pkgdesc="NVIDIA drivers, 96xx branch, for linux-ck."
url="http://www.nvidia.com/"
arch=('i686' 'x86_64')
depends=("linux-ck>=${_kver_ge}"
         "linux-ck<${_kver_lt}"
         "nvidia-96xx-utils")
makedepends=("linux-ck-headers>=${_kver_ge}"
             "linux-ck-headers<${_kver_lt}")
optdepends=('xorg-server1.12: latest compatible Xorg server')
provides=('nvidia-96xx')
conflicts=('nvidia'
           'nvidia-all'
           'nvidia-96xx-all'
           'nvidia-173xx-all'
           'nvidia-173xx'
           'nvidia-275xx'
           'nvidia-304xx'
           'nvidia-beta-all'
           'nvidia-beta')
license=('custom')
install='nvidia.install'
options=(!strip)

[ "$CARCH" = "i686" ] && _arch=x86 && _nv=0
[ "$CARCH" = "x86_64" ] && _arch=x86_64 && _nv=2
source=("http://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}.run"
        "173.14.36-37.patch"
        "fix-build-with-linux-3.10.patch")

md5sums=('ca0bc6ae3b37cb259f3a906b4dc4670b'
         '944ed806c8d0f9174d5e9e16ae065bf6'
         'edda124f64772b87314945eb95abfa24')
[ "$CARCH" = "x86_64" ] && md5sums[0]='dd8546e5ae7d10da072306e5f13952b8'

_extramodules=$(echo /usr/lib/modules/extramodules-[0-9.]*-ck/version|cut -d/ -f5)
_kernver=$(cat /usr/lib/modules/${_extramodules}/version)

prepare() {
  test ! -d NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv} \
    || rm -rf NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}

  sh NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}.run --extract-only

  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}/usr/src/nv
  patch -p1 -i "$srcdir"/173.14.36-37.patch
  patch -p4 -i "$srcdir"/fix-build-with-linux-3.10.patch
}

build() {
  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}/usr/src/nv
  make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_nv}
  install -dm755 "$pkgdir/usr/share/licenses/${pkgname}/"
  install  -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/"

  cd usr/src/nv
  install -dm755 "$pkgdir/usr/lib/modules/${_extramodules}/"
  install  -m644 nvidia.ko "$pkgdir/usr/lib/modules/${_extramodules}/"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

  install -dm755 "$pkgdir/etc/modprobe.d"

  sed -i -e "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "$startdir/nvidia.install"
}
