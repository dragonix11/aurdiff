# Original Contributor: Vinzenz Vietzke <vinz@archlinux.us>
# Modified By: Farhan Yousasf <farhany@gmail.com>
# Modified by c0mmando , 05.11.2013
# Maintainer: c0mmand0 <ptl_aka_ptl88@yahoo.com.ar>
pkgname=atari800
pkgver=3.0.0
pkgrel=5
pkgdesc="An emulator of the Atari 800/800XL/130XE/5200 with various extensions"
arch=('i686' 'x86_64')
url="http://atari800.sourceforge.net/"
license=('GPL')
depends=('libpng' 'sdl')
optdepends=(
  'xboxdrv: XBox/360gamepad driver - supports a multitude of controllers ,like PS3 USB controller'
  'qtsixa: utility to configure and use sixaxis controller via bluetooth'
  'jstest-gtk: simple GTK2-based joystick tester and calibrator'
)
makedepends=('unzip')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
  http://sourceforge.net/projects/atari800/files/ROM/Original%20XL%20ROM/xf25.zip)
install=atari800.install
md5sums=('92a74a275def5515f055974c78e77d7e' \
         '4dc3b6b4313e9596c4d474785a37b94d')

build()
{
  # This emulator has a few linux compatible targets. Edit the target
  # and depends variables if you'd like a different target.
  # Valid choices are: default , android , falcon , javanvm , javanvm-basic , ps2 , windx , x11 
  # see -> ./configure --target --help
  #default=autodetect graphics and sound 
  _target=default
  
  # Pass the reponse to the configure script
  
  cd ${srcdir}/${pkgname}-${pkgver}/src
  ./configure --target=${_target} --prefix=/usr
  make
}

package() 
{
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=${pkgdir} install

  # Copy the required ROMs and disks to /usr/share/atari800
  cd ${srcdir}
  install -d ${pkgdir}/usr/share/${pkgname}
  for _n in *.ROM *.XFD *.ATR; do
    install -m644 ${_n} ${pkgdir}/usr/share/${pkgname}/
  done
}