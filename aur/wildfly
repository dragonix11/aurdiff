# Wildfly Application Server package
# Maintainer: Zdenek Obst <zdenek dot obst at gmail dot com>

pkgname=wildfly

_majorver=8.0
pkgver=${_majorver}.0.Alpha4
pkgrel=1

pkgdesc='Wildfly Application Server'
arch=('any')
url='http://www.wildfly.org/'
license=('LGPL')
depends=('java-environment>=7')
makedepends=('java-environment>=7')

_pkgloc=usr/share
backup=(etc/profile.d/${pkgname}.sh
        ${_pkgloc}/${pkgname}/standalone/configuration/standalone.xml
        ${_pkgloc}/${pkgname}/standalone/configuration/mgmt-users.properties
        ${_pkgloc}/${pkgname}/standalone/configuration/logging.properties
        ${_pkgloc}/${pkgname}/domain/configuration/domain.xml
        ${_pkgloc}/${pkgname}/domain/configuration/mgmt-users.properties
        ${_pkgloc}/${pkgname}/domain/configuration/logging.properties
        ${_pkgloc}/${pkgname}/bin/standalone.conf
        ${_pkgloc}/${pkgname}/bin/domain.conf)
install=${pkgname}.install
source=(http://download.jboss.org/wildfly/${pkgver}/wildfly-${pkgver}.tar.gz
        wildfly.service
        wildfly.sh
	systemd-wrapper.sh
        wildfly.install)
md5sums=('fa9d64285065ba608d9708f568a56a7c'
         '15d1ee926b7d90625e0623a1ff7c8e13'
         '823566b7e49e42f883713f8830fbaed0'
         '654e5bd743e86f1923f5f7fc11aa319e'
         '2163e07b2a29aeaaa75428dcf2a7b23e')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm -f bin/*.{bat,exe}
  install -dm755 ${pkgdir}/${_pkgloc}
  cp -R ${srcdir}/${pkgname}-${pkgver} ${pkgdir}/${_pkgloc}/${pkgname}

  # Prepare systemd service file
  install -Dm755 ${srcdir}/wildfly.service ${pkgdir}/usr/lib/systemd/system/wildfly.service

  # Prepare file for environment variables to profile.d
  install -Dm755 ${srcdir}/wildfly.sh ${pkgdir}/etc/profile.d/wildfly.sh

  # Include wrapper script for systemd startup
  install -Dm755 ${srcdir}/systemd-wrapper.sh ${pkgdir}/${_pkgloc}/wildfly/bin/systemd-wrapper.sh
}
