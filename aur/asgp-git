# Maintainer: Hakim Zulkufli <acrox999@gmail.com>

pkgname=asgp-git
pkgver=78.e1082bc
pkgrel=1
pkgdesc="An open source shooter on rails game developed by Stuff-O-Matic."
arch=('i686' 'x86_64')
url="http://www.stuff-o-matic.com/asgp/"
license=('GPL3' 'CCPL')
depends=('sdl_mixer' 'boost' 'gettext' 'sdl' 'libclaw' 'freetype2' 'wxgtk')
makedepends=('make' 'cmake' 'git')
conflicts=('asgp')
provides=('asgp')
source=("asgp"::'git+https://github.com/j-jorge/asgp.git'
	"bear"::'git+https://github.com/j-jorge/bear.git'
	'CMakeLists.txt')
md5sums=('SKIP'
         'SKIP'
         'f2e6ad1c6a0354361bbb57e70cdd8af6')

pkgver() {
	cd "asgp"
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=release .
	make
}

package() {
	cd "$srcdir"
	make DESTDIR="$pkgdir" install
}
