# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=yosys-git
_pkgbasename=yosys
pkgver=0
pkgrel=1
epoch=1
pkgdesc="A framework for Verilog RTL synthesis with extensive Verilog-2005 support"
arch=('i686' 'x86_64')
url="http://www.clifford.at/yosys/"
license=('custom:ISC')
depends=('minisat-git' 'tcl' 'qt4' 'graphviz')
makedepends=('git')
source=(git+https://github.com/cliffordwolf/yosys.git
        yosys-Makefile-Arch-Linux.patch
        license.txt)
sha256sums=('SKIP'
            '2f2ffd373b0d6df4782bf658415c70b7a00b89a8a691783375e15b8d73d61cb9'
            'e09c086c53f9cc8ebecc207ae7d854f544e26f89baf65c002b7471313cde02b8')

pkgver() {
  cd "${srcdir}/${_pkgbasename}"

  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_pkgbasename}"

  patch -Np0 -i ${srcdir}/yosys-Makefile-Arch-Linux.patch
}

build() {
  cd "${srcdir}/${_pkgbasename}"

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}"

  install -d "${pkgdir}/usr/bin"

  make DESTDIR="${pkgdir}/usr" install

  install -D -m 644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/yosys-git/license.txt"
}

# vim: set ts=2 sw=2 ft=sh noet:
