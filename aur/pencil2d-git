# Maintainer: Otto VonStein <otto>
# Contributor: ex-maintainer Benjamin Robin <benjarobin>

pkgname=pencil2d-git
_gitname=pencil2d
pkgver=883.2d08386
pkgrel=2
pkgdesc="Animation/drawing software, that lets you create traditional hand-drawn animation using both bitmap and vector graphics"
arch=('i686' 'x86_64')
url="http://www.pencil2d.org/"
license=('GPL')
depends=('qt4' 'ming' 'phonon' 'ffmpeg')
makedepends=('git')
provides=('pencil')
conflicts=('pencil')
install=pencil2d.install
source=('pencil2d::git+https://github.com/pencil2d/pencil.git'
        'pencil2d-help.patch'
        'pencil.desktop')
md5sums=('SKIP'
         '7a2ac11bc8feb260b1dfe3c536234165'
         '01a9978f3326c5dc0abe946a00e23fca')

pkgver()
{
    cd "${_gitname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare()
{
    cd "${_gitname}"
    patch -p1 -i "${srcdir}/pencil2d-help.patch"
}

build()
{
    cd "${_gitname}"
    msg "Starting qmake..."
    qmake-qt4
    msg "Starting make..."
    make
}

package()
{
    cd "${_gitname}"
	install -D -m755 Pencil "${pkgdir}/usr/share/pencil/pencil"
	install -D -m644 "Help/User Manual.pdf" "${pkgdir}/usr/share/pencil/Help/User Manual.pdf"
	install -D -m644 icons/icon.png "${pkgdir}/usr/share/pixmaps/pencil.png"
	install -D -m644 "${srcdir}/pencil.desktop" "${pkgdir}/usr/share/applications/pencil.desktop"
	mkdir -p "${pkgdir}/usr/bin/"
	ln -sf "/usr/share/pencil/pencil" "${pkgdir}/usr/bin/pencil"
}
