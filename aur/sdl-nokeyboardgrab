pkgname=sdl-nokeyboardgrab
pkgver=1.2.15
pkgrel=2
pkgdesc='Simple DirectMedia Layer'
arch=(i686 x86_64)
url=http://libsdl.org
license=(LGPL2.1)
depends=(libx11 libxext libxrender)
optdepends=(alsa-lib: ALSA audio driver
            libpulse: PulseAudio audio driver)
makedepends=(alsa-lib glu libpulse mesa)
provides=(sdl=$pkgver)
source=(
fix_joystick_misc_axes.diff::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/fix_joystick_misc_axes.diff?h=packages/sdl
$url/release/SDL-$pkgver.tar.gz
libsdl-1.2.15-resizing.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libsdl-1.2.15-resizing.patch?h=packages/sdl
sdl-1.2.14-disable-mmx.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/sdl-1.2.14-disable-mmx.patch?h=packages/sdl
sdl-1.2.14-fix-mouse-clicking.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/sdl-1.2.14-fix-mouse-clicking.patch?h=packages/sdl
sdl_x11sym.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/sdl_x11sym.patch?h=packages/sdl
)
md5sums=(687586a108b597a2a6b73070c1d37e51
         9d96df8417572a2afb781a7c4c811a85
         3dd50347d8856160a9cbd7f80383a1f8
         e5c16b7611f62c7cb25f0534eff68852
         04d8c179f125e04bcd4c9d60e013c2d7
         148b285bccee9d21bb96db807363b962)

prepare() {
  cd SDL-$pkgver

  sed -i '/XGrabKeyboard/{s_^_//_;n;s_^_//_}' src/video/x11/SDL_x11wm.c

  patch -Np1 < ../sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 < ../sdl-1.2.14-disable-mmx.patch
  patch -Np1 < ../fix_joystick_misc_axes.diff
  patch -Np1 < ../libsdl-1.2.15-resizing.patch
  patch -Np0 < ../sdl_x11sym.patch
}

build() {
  cd SDL-$pkgver

  ./configure --disable-nasm --disable-rpath --disable-static --enable-alsa --prefix=/usr --with-x

  make
}

package() {
  cd SDL-$pkgver

  make DESTDIR=$pkgdir install
}
