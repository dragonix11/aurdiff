# Maintainer: Brendan MacDonell <macdonellba at gmail dot com>

pkgname=python-llfuse
pkgver=0.39
pkgrel=2
pkgdesc="A set of Python bindings for the low level FUSE API."
arch=('i686' 'x86_64')
url="http://code.google.com/p/python-llfuse/"
license=('LGPL3')
depends=('python' 'fuse')
options=(!emptydirs)
source=(http://python-llfuse.googlecode.com/files/llfuse-$pkgver.tar.bz2)
md5sums=('4ec1ac5727f736ba4341eae09332ab33')

build() {
  cd $srcdir/llfuse-$pkgver
  export CFLAGS+=' -Wno-error=unused-but-set-variable'

  # Work around the out-of-date version of distribute bundled with llfuse
  sed -i "s/\(use_setuptools(version=\)'0.6.12'/\1'0.6.49'/" setup.py

  python setup.py build
}

package() {
  cd $srcdir/llfuse-$pkgver
  python setup.py install --root=$pkgdir/ --optimize=1
}
