# Contributor: Yannik Stein <yannik.stein [at] gmail.com>
# Submitter: vbPadre <vbpadre [at] gmail.com>

pkgname=ajenti
pkgver=1.0.0
pkgrel=1
pkgdesc="An easy server administration frontend."
arch=('any')
url="http://ajenti.org/"
license=('LGPL')
depends=('python2' 'python2-lxml' 'python2-pyopenssl' 'python2-feedparser' 'python2-gevent')
makedepends=('python2' 'python2-distribute')
optdepends=('python2-psutil: Task Manager plugin'
            'python2-beautifulsoup3: Munin plugin')
install='ajenti.install'
source=("$pkgname-$pkgver.tar.gz::https://github.com/Eugeny/$pkgname/archive/$pkgver.tar.gz"
         "https://raw.github.com/Eugeny/ajenti/dev/packaging/files/ajenti.service"
	 "https://raw.github.com/Eugeny/ajenti/dev/packaging/files/ajenti.conf")
md5sums=('57ab8205007c3a6b6e5e4bf6d2dfab8c'
         'a031fbf9543b000f72b7d0534e332c43'
         '92328347a13c68ce63e10a4da2208eb0')
backup=('etc/ajenti/ajenti.conf' 'etc/ajenti/users/admin.conf')


prepare() {
  cd $pkgname-$pkgver
  
 # Changing python shebang to python2
  find . -type f -exec sed -i \
    -e'1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;
}

package() {
  cd $pkgname-$pkgver
  
  python2 setup.py install --prefix=/usr --root=${pkgdir}
 
  install -D "${srcdir}/ajenti.service" "$pkgdir/usr/lib/systemd/system/ajenti.service"
  install -D "${srcdir}/ajenti.conf" "$pkgdir/etc/ajenti/ajenti.conf"
  # Fix config file permissions
  chmod o-rwx "$pkgdir/etc/ajenti/ajenti.conf"
  rm -r $pkgdir/etc/init.d
}
