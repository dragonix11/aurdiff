# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-xattr
pkgver=0.7.2
pkgrel=1
pkgdesc="Module for manipulating filesystem extended attributes"
arch=(i686 x86_64)
url=http://undefined.org/python/#xattr
license=(MIT)
depends=(python2)
makedepends=(python2-setuptools)
source=($pkgname-$pkgver.zip::https://github.com/xattr/${pkgname#*-}/archive/v$pkgver.zip)
sha256sums=('2009e3d338b3098f7d997c066821bd292843a8f03f3f33943babcc0aa190918c')
sha512sums=('699a49a45040b767fe8ed97798b66bf0adaf3d93e35b2ebb4f6d83e36de1715b15f6c960e92b3d580ff1527f33c87876a6f946179ec8d34c23eb914c7c1bf75f')

prepare() {
    sed -ri 's:^#!/usr/bin/(env )?python$:&2:' ${pkgname#*-}-$pkgver/${pkgname#*-}/tool.py
}

build() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
