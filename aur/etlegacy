# Maintainer: Spyhawk
#
# ET:Legacy is provided as a 32-bit version only for compatibility reason with W:ET 2.60b servers.
# On a 64 bits system, [multilib] must be enabled and multilib-devel installed for 32 bits cross-compilation.
# The engine is dynamically compiled, while the official precompiled mod libraries are provided.

pkgname=etlegacy
pkgver=2.71rc3
pkgrel=1
pkgdesc="Fully compatible client and server for the popular online FPS game Wolfenstein: Enemy Territory"
arch=('i686' 'x86_64')
url="http://www.etlegacy.com/"
license=('GPL3')
if [[ "$CARCH" == "i686" ]]; then
    depends=('enemy-territory-data' 'alsa-plugins' 'curl' 'gcc-libs' 'glew' 'libjpeg' 'libsm' 'libvorbis' 'sdl')
    optdepends=('libtxc_dxtn: S3 Texture Compression (S3TC) library for Mesa')
else
    depends=('enemy-territory-data' 'lib32-alsa-plugins' 'lib32-curl' 'lib32-gcc-libs' 'lib32-glew' 'lib32-libjpeg' 'lib32-libsm' 'lib32-libvorbis' 'lib32-sdl')
    optdepends=('lib32-libtxc_dxtn: S3 Texture Compression (S3TC) library for Mesa')
fi
makedepends=('cmake' 'zip')
install=etlegacy.install
source=("https://github.com/etlegacy/$pkgname/archive/v$pkgver.tar.gz"
        "http://mirror.etlegacy.com/$pkgname-linux-$pkgver.zip")
md5sums=('8dfd3a602b9422f8582aa33046eb7ef8'
         '1d7144d5d1cf095f489a673c3371197c')

build() {
    cd "$pkgname-$pkgver"

    if [[ "$CARCH" == "i686" ]]; then
        cmakeopts+=(
            '-D CMAKE_LIBRARY_PATH=/usr/lib'
            '-D CROSS_COMPILE32=0'
        )
    else
        cmakeopts+=(
            '-D CMAKE_LIBRARY_PATH=/usr/lib32'
            '-D CROSS_COMPILE32=1'
        )
    fi

    cmake ${cmakeopts[@]} \
        -D CMAKE_C_FLAGS="-lm" \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D INSTALL_DEFAULT_BASEDIR=/usr/share/etlegacy \
        -D BUILD_SERVER=1 \
        -D BUILD_CLIENT=1 \
        -D BUILD_MOD=0 \
        -D BUILD_MOD_PK3=0 \
        -D BUILD_PAK3_PK3=0 \
        .
    make
}

package() {
    cd "$pkgname-$pkgver"

    # engine
    make DESTDIR="$pkgdir/" install

    # mod
    mv ../legacy $pkgdir/usr/share/$pkgname/

    # assets
    mkdir -p $pkgdir/usr/share/etlegacy/etmain
    ln -s /usr/share/enemy-territory/etmain/{mp_bin,pak0,pak1,pak2}.pk3 $pkgdir/usr/share/$pkgname/etmain/

    # doc and man pages
    mkdir -p $pkgdir/usr/share/{doc/$pkgname,man/man6}
    mv $pkgdir/usr/share/$pkgname/{README.md,COPYING.txt} $pkgdir/usr/share/doc/$pkgname/
    install -Dm 644 docs/game/anticheat.html $pkgdir/usr/share/doc/$pkgname/
    install -Dm 644 docs/linux/man/man6/{etl,etlded}.6 $pkgdir/usr/share/man/man6/
}

# vim:set ts=4 sw=2 et:
