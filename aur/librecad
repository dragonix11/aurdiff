# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=2.0.0rc2
_pkgtag="abf9eb6"
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad"
arch=('i686' 'x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt5-base' 'qt5-svg' 'qt5-tools' 'muparser')
makedepends=('boost' 'imagemagick')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
install=librecad.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LibreCAD/LibreCAD/tarball/${pkgver}")

build() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	# fix version string
	sed -i "/^SCMREVISION/c SCMREVISION=\"${pkgver}\"" librecad/src/src.pro

	export CPPFLAGS="-std=c++0x"
	qmake-qt5 librecad.pro
	make
}

package() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	install -D -m0755 unix/librecad "${pkgdir}/usr/bin/librecad"
	install -D -m0755 unix/ttf2lff "${pkgdir}/usr/bin/ttf2lff"

	install -D -m0644 desktop/librecad.desktop "${pkgdir}/usr/share/applications/librecad.desktop"
	install -D -m0644 desktop/librecad.1 "${pkgdir}/usr/share/man/man1/librecad.1"

	for SIZE in 16 24 32 48 64 96 128; do
		convert -scale ${SIZE} \
			desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg \
			${srcdir}/librecad.png
		install -D -m0644 ${srcdir}/librecad.png "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/librecad.png"
	done
	install -D -m0644 desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/librecad.svg"

	mkdir -p "${pkgdir}/usr/share/librecad/"
	cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

sha256sums=('6ab3b490b905e65f9bf5c8dc33e5bda72734c8bafd8a807b93e92bd571d2804b')
