# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
pkgname=mingw-w64-glfw
pkgver=3.0.1
pkgrel=2
pkgdesc="A free, open source, portable framework for OpenGL application development (mingw-w64)"
arch=('any')
url="http://www.glfw.org/"
license=('ZLIB')
makedepends=('mingw-w64-gcc' 'cmake')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags')
source=(glfw-${pkgver}.tar.gz::"http://sourceforge.net/projects/glfw/files/glfw/${pkgver}/glfw-${pkgver}.tar.gz/download")
md5sums=('cc0a9310b80d882760f8b833f746ff6a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
  	rm -rf "${srcdir}/build-${_arch}"
	mkdir "${srcdir}/build-${_arch}"
	cd "${srcdir}/build-${_arch}"

    	echo "SET(CMAKE_SYSTEM_NAME Windows)" > win32.cmake
    	echo "SET(CMAKE_C_COMPILER ${_arch}-gcc)" >> win32.cmake
    	echo "SET(CMAKE_CXX_COMPILER ${_arch}-g++)" >> win32.cmake
    	echo "SET(CMAKE_RC_COMPILER ${_arch}-windres)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH /usr/${_arch})" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake

	cmake "${srcdir}/glfw-${pkgver}" \
	      -DCMAKE_TOOLCHAIN_FILE=win32.cmake \
	      -DCMAKE_INSTALL_PREFIX="/usr/${_arch}" \
	      -DGLFW_BUILD_EXAMPLES=OFF \
	      -DGLFW_BUILD_TESTS=OFF \
	      -DBUILD_SHARED_LIBS=ON \
	      -DGLFW_BUILD_TYPE=RELEASE
	make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    mkdir -p "${pkgdir}/usr/${_arch}/bin/"
    mv "${pkgdir}/usr/${_arch}/lib/"*.dll "${pkgdir}/usr/${_arch}/bin/"
    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  done

  #copy license file
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp "${srcdir}/glfw-${pkgver}/COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}"
}
