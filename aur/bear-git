# Maintainer: Moritz Lipp <mlq@pwmt.org>

pkgname=bear-git
pkgver=20130708
pkgrel=1
pkgdesc="tool to generate compilation database for clang tooling"
arch=('i686' 'x86_64')
url="https://github.com/rizsotto/Bear"
license=('GPL3')
makedepends=('git' 'cmake' 'make')
optdepends=('haskell-pandoc')
conflicts=('bear')
provides=('bear')

_gitroot=https://github.com/rizsotto/Bear.git
_gitname=bear

prepare() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
}

build() {
  cd "$srcdir/$_gitname-build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make all
}

check() {
  cd "$srcdir/$_gitname-build"
  PATH=/usr/bin:$PATH make -k check
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
