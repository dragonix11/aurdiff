# Submitter:   Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Tycho LÃ¼rsen "bas-t" (responsible for hosting and development of FFdecsawrapper)
# Contributor: Petr Vacek "vaca" (providing cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (implementing new procfs API for kernels >= 3.10 in FFdecsawrapper kernel module)

pkgname=ffdecsawrapper-git-v4l
pkgver=kernel_gitvers
pkgrel=1
pkgdesc="FFdecsa empowered softcam (newest generation of open-sasc-ng) - compiled with V4L GIT lib's"
url="https://github.com/bas-t/ffdecsawrapper.git"
arch=('i686' 'x86_64')
license=('GPLv3')
depends=('linux-headers')
makedepends=('git' 'patchutils' 'perl-proc-processtable')
conflicts=('ffdecsawrapper' 'sasc-ng')
provides=('ffdecsawrapper')
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/ffdecsawrapper' 'etc/camdir/cardslot.conf')
install='ffdecsawrapper.install'

source=('git://github.com/bas-t/ffdecsawrapper.git#branch=stable' \
	'git://linuxtv.org/media_build.git' \
	'cardclient.conf' 'cardslot.conf' 'ffdecsawrapper.conf' 'ffdecsawrapper.install' \
	'ffdecsawrapper.lr' 'ffdecsawrapper.service' 'ffdecsawrapper.rc' 'v4l-mutex.patch')
	 
sha256sums=('SKIP'
            'SKIP'
            '5c23db2b93d1accdc0b3f1612766de38bf7ede5658f6ef973706988dd71d1b81'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '04ac7290fae233756e05e4fd57714a287f38a8c5aa49f590ce585174ad00072b'
            '94ff9346b4ad866c4c698d01d3c4f72548ce7472935e9f71ef870bee8d26b0ae'
            'f435344dc9f1c0ed7c2e0de74ec434cd73e2130a0d7589a4d38338e45925d8db'
            'abcfe7ea87d842c3e02095caf5e39e59fc79f7a618f07a807d038a4cf336ed80'
            'f20b9a95247c827efb661546e06fae8a4565b2d95d1e8a577d11e9ddb590cfe8'
            'eb64024d654308b80f0ec13596fddb7ca2a394e1e012702fb7614d31f2a04bfd')

pkgver() {

	cd $srcdir/ffdecsawrapper
 	_gitffdecsawrapper=`git describe --always | sed 's|-|.|g'`

	cd $srcdir/media_build 
	_gitv4l=`git describe --always | sed 's|-|.|g'`

	_kernel=`uname -r | sed -r 's/-/_/g'`

	echo "$_kernel"_"$_gitffdecsawrapper"_"$_gitv4l"
}

prepare() {

	cd $srcdir/media_build
	sed -i 's/system ("make") == 0 or die "build failed";/#system ("make") == 0 or die "build failed";/' build
	./build

	msg "Applying V4l-mutex patch..."
	patch -p1 < $srcdir/v4l-mutex.patch
	echo ""
	msg "Here's a few seconds to confirm the patch has been applied sucessfully..."
	sleep 5

	make distclean || return 1
	make || return 1
}

build() {

	cd $srcdir/ffdecsawrapper      
	./configure --compiletype=release --dvb_dir=$srcdir/media_build/linux
}

package() {

	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/updates/{v4l,ffdecsawrapper}
	mkdir -p $pkgdir/etc/conf.d
	mkdir -p $pkgdir/etc/rc.d
	mkdir -p $pkgdir/etc/camdir
	mkdir -p $pkgdir/etc/logrotate.d
	mkdir -p $pkgdir/usr/lib/firmware/updates/v4l
	mkdir -p $pkgdir/usr/lib/systemd/system

	install -m0644 $srcdir/cardclient.conf  $pkgdir/etc/camdir/cardclient.conf
	install -m0644 $srcdir/cardslot.conf  $pkgdir/etc/camdir/cardslot.conf
	install -m0755 $srcdir/ffdecsawrapper.rc  $pkgdir/etc/rc.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.conf  $pkgdir/etc/conf.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.lr  $pkgdir/etc/logrotate.d/ffdecsawrapper-git-v4l.lr
	install -m0644 $srcdir/ffdecsawrapper.service  $pkgdir/usr/lib/systemd/system/ffdecsawrapper.service      
	install -m0755 $srcdir/ffdecsawrapper/ffdecsawrapper  $pkgdir/usr/bin
	install -m0644 $srcdir/ffdecsawrapper/dvbloopback.ko  $pkgdir/usr/lib/modules/`uname -r`/updates/ffdecsawrapper
	find "$srcdir/media_build" -name '*.fw' -exec cp {} $pkgdir/usr/lib/firmware/updates/v4l \;
	find "$srcdir/media_build" -name '*.ko' -exec cp {} $pkgdir/usr/lib/modules/`uname -r`/updates/v4l/ \;

	echo ""
	msg "Compressing V4l modules, this might take awhile..."
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	chmod 644 -R $pkgdir/usr/lib/modules/`uname -r`/updates
	chmod 644 -R $pkgdir/usr/lib/firmware/updates
}
