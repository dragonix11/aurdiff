# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgname=cinnamon
pkgname=${_pkgname}-git
pkgver=2942.ae09c76
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
depends=('accountsservice' 'caribou' 'clutter-gtk' 'cjs-git' 'cinnamon-translations-git' 'gnome-bluetooth'
         'gnome-icon-theme' 'gnome-menus' 'cinnamon-settings-daemon-git' 'cinnamon-session-git'
         'gnome-themes-standard' 'gstreamer0.10' 'gksu' 'libgnome-keyring' 'librsvg'
         'networkmanager' 'muffin-git' 'pygtk' 'python2-dbus' 'python2-imaging'
         'python2-pam' 'python2-pexpect' 'python2-pyinotify' 'python2-lxml' 'webkitgtk3')
makedepends=('gnome-common' 'intltool' 'git')
optdepends=('cinnamon-control-center-git: extended configurations for Cinnamon'
            'cinnamon-screensaver-git: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nemo-git: Cinnamon file manager')
options=('!libtool' '!emptydirs')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install=${pkgname}.install
source=("${_pkgname}"::git+https://github.com/linuxmint/Cinnamon.git)
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/${_pkgname}"

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
                 --libexecdir=/usr/lib/cinnamon \
                 --localstatedir=/var \
                 --disable-static \
                 --disable-schemas-compile \
                 --disable-schemas-install \
                 --enable-compile-warnings=yes
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}/" install

  # Python 2 fixes
  find "${pkgdir}/usr/bin" -type f -exec sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' '{}' \;
  find "${pkgdir}" -type f -name "*py" -exec sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' '{}' \;
    
  # fix for the python2 PAM module  
  sed -i 's:import PAM:import pam:' "$pkgdir"/usr/lib/cinnamon-settings/modules/cs_user.py
  
  # Hide the Rhythmbox and xnoise tray icons
  sed -i 's:\(let hiddenIcons = \[.*\)\]:\1, "rhythmbox", "xnoise"\]:' "$pkgdir"/usr/share/cinnamon/applets/systray@cinnamon.org/applet.js
}
