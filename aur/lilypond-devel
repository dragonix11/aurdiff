# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=lilypond-devel
pkgver=2.17.95
pkgrel=1
pkgdesc="An automated music engraving system (development version)"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('fontconfig'
         'freetype2'
         'ghostscript'
         'guile1.8'
         'pango'
         'python2')
makedepends=('fontforge'
             'gsfonts'
             't1utils'
             'texlive-core>=2011.23170')
optdepends=('netpbm: building HTML documentation'
            'imagemagick: building HTML documentation'
            'ttf-kochi-substitute: building HTML documentation'
            'texi2html>=1.82: building HTML documentation'
            'rsync: installing HTML documentation')
provides=("lilypond=$pkgver")
conflicts=('lilypond' 'lilypond-git')
source=(http://download.linuxaudio.org/lilypond/sources/v2.17/lilypond-$pkgver.tar.gz)
sha256sums=('5f286112624ed3d8c52b6d3776651ad88c2fd6c08b3f1d0049101d37488a3db4')

prepare() {
  cd "$srcdir/lilypond-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  sed -i 's|GUILE_CFLAGS=.*|GUILE_CFLAGS="`pkg-config --cflags guile-1.8`"|' configure
  sed -i 's|GUILE_LDFLAGS=.*|GUILE_LDFLAGS="`pkg-config --libs guile-1.8`"|' configure

  rm -rf python/out/

}

build() {
  cd "$srcdir/lilypond-$pkgver"

  export GUILE=/usr/bin/guile1.8
  export GUILE_CONFIG=/usr/bin/guile-config1.8
  export PYTHON="python2"
  export PYTHON_CONFIG="python2-config"

  ./configure \
          --prefix=/usr \
          --disable-documentation

  make all
}

package() {
  cd "$srcdir/lilypond-$pkgver"
  make DESTDIR="$pkgdir/" vimdir="/usr/share/vim/vimfiles" install

  rm -rf "$pkgdir/usr/share/man"
}

# vim:set ts=2 sw=2 et:
