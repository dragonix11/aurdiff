# Contributor: chochem <chochem@gmail.com>
# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=myman
pkgver=0.7.0
pkgrel=4
pkgdesc="A Pacman clone with an ncurses and a 'graphic' interface"
arch=('i686' 'x86_64')
url="http://myman.sourceforge.net/"
license=('BSD')
depends=('ncurses' 'xterm')
makedepends=('gcc' 'sed' 'make>=3.80' 'coreutils')
source=("http://xent.com/~bsittler/${pkgname}-${pkgver}.tar.gz")
md5sums=('ca34d3f12c973558dc52516144b64530')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --with-xterm --prefix="${pkgdir}/usr"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install
  rm -r "${pkgdir}/usr/share/man/cat6"
  rm "${pkgdir}/usr/share/man/index.db"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: set ts=2 sw=2 et:
