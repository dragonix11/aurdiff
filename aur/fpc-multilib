pkgname=fpc-multilib
pkgver=2.6.2
pkgrel=2
pkgdesc="The Free Pascal Compiler for multilib"
arch=('x86_64')
url="http://www.freepascal.org/"
license=('GPL' 'LGPL' 'custom')
backup=("etc/fpc.cfg")
depends=(ncurses)
makedepends=(binutils-multilib fpc fpc-multilib-helper)
conflicts=(fpc ppcross386)
provides=(fpc=${pkgver} ppcross386)
options=(zipman staticlibs)
source=("ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz"
		"rev22236-22237.patch")
md5sums=('89c7e60db6280f3d5cc006a4a9ff43a9'
		'33e2cef08bbe950fef9ea79171cf56d9')

build() {

  cd "${srcdir}/fpcbuild-$pkgver"
  patch -p0  < ../../rev22236-22237.patch
  cd "${srcdir}/fpcbuild-$pkgver"
  fpcmake -v -w -Tx86_64-linux,i386-linux  
  make NOGDB=1 CPU_TARGET=i386 build
 
}

package() {
  cd "${srcdir}/fpcbuild-$pkgver"
 
  export HOME=$srcdir
  make -j1 CPU_TARGET=x86_64 NOGDB=1 PREFIX="${pkgdir}/usr" install
  make -j1 CPU_TARGET=i386 NOGDB=1 PREFIX="${pkgdir}/usr" crossinstall CROSSOPT="-XP"

  install -Dm0644 "fpcsrc/rtl/COPYING.FPC" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.FPC"

  ln -sv "/usr/lib/fpc/${pkgver}/ppcross386" "${pkgdir}/usr/bin/ppcross386"
  ln -sv "/usr/lib/fpc/${pkgver}/ppcx64" "${pkgdir}/usr/bin/"

  mkdir -p "${pkgdir}/etc"
  ${pkgdir}/usr/lib/fpc/${pkgver}/samplecfg "$pkgdir/usr/lib/fpc/${pkgver}" "${pkgdir}/etc"
  
  # use -fPIC by default
  echo -e "#ifdef cpux86_64\n# for x86_64 use -fPIC by default\n-Cg\n#endif" >> "$pkgdir/etc/fpc.cfg"

  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"

  find "$pkgdir/etc/" -type f -exec sed -i "s|$pkgdir||g" {} \;
}
