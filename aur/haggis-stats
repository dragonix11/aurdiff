# Maintainer: Karl Nicoll <karl@whoiskarl.com>

pkgname=haggis-stats
pkgver=2.1.0
pkgrel=1
pkgdesc="A Lightweight script that prints system stats in an easy-to-read format, and provides screenshot capabilities."
arch=(any)
depends=('coreutils' 'perl' 'perl-switch')
license=('BSD')
source=(
	${pkgname}-${pkgver}.tar.gz::https://github.com/xorangekiller/haggis-stats/archive/v${pkgver}.tar.gz
)
url=('http://www.neowin.net/forum/topic/1179351-linux-system-stats-script-next-generation/')
sha256sums=('a06c5223e150a019db41388f35d677688ed19c334ccbc4ea9519559880a51463')

package() {
	pushd "${srcdir}/${pkgname}-${pkgver}" > /dev/null || return 1
	
		# Install files.
		install -D -m755 "src/stats.pl"	"${pkgdir}/usr/bin/${pkgname}"
		install -D -m644 "AUTHORS"      "${pkgdir}/usr/share/${pkgname}/AUTHORS"
		install -D -m644 "ChangeLog"    "${pkgdir}/usr/share/${pkgname}/ChangeLog"
		install -D -m644 "COPYING"      "${pkgdir}/usr/share/licenses/haggis-stats/COPYING"
		install -D -m644 "INSTALL"      "${pkgdir}/usr/share/${pkgname}/INSTALL"
		install -D -m644 "NEWS"         "${pkgdir}/usr/share/${pkgname}/NEWS"
		install -D -m644 "README"     	"${pkgdir}/usr/share/${pkgname}/README"
		
		# Store logos for reference.
		mkdir -p "${pkgdir}/usr/share/${pkgname}/logos"
		install -m644 -t "${pkgdir}/usr/share/${pkgname}/logos" logos/*
		
		# Install man file.
		install -D -m644 "man/stats.1" "${pkgdir}/usr/share/man/man1/haggis-stats.1"
		gzip -9 "${pkgdir}/usr/share/man/man1/haggis-stats.1"

	popd > /dev/null
}
