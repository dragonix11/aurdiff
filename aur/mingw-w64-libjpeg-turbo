pkgname=mingw-w64-libjpeg-turbo
pkgver=1.3.0
pkgrel=2
pkgdesc="JPEG image codec with accelerated baseline compression and decompression (mingw-w64)"
arch=(any)
url="http://libjpeg-turbo.virtualgl.org"
license=("GPL, custom")
depends=(mingw-w64-crt)
makedepends=(nasm mingw-w64-gcc mingw-w64-cmake)
provides=("mingw-w64-libjpeg=8.0.2")
conflicts=(mingw-w64-libjpeg)
replaces=(mingw-w64-libjpeg)
options=(staticlibs !strip !buildflags)
source=("http://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-$pkgver.tar.gz")
md5sums=('e1e65cc711a1ade1322c06ad4a647741')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libjpeg-turbo-$pkgver"
  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake ..
    make
    popd
  done
}

package() {
  cd "$srcdir/libjpeg-turbo-$pkgver"
  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="$pkgdir" install
    popd
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -rf "${pkgdir}/usr/${_arch}/"{doc,share}
  done
}
