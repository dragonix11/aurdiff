# Maintainer: Jens Adam <jra@byte.cx>

pkgname=zmap
pkgver=1.0.3
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64')
depends=('gmp' 'libpcap')
#makedepends=('gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz")
sha256sums=('c92603475edeea7582cf7ca91c5e734f9f3ca7cccbccbe802ef17c4ec58afee3')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}/src
  sed -i 's/ -Werror$//' Makefile
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/src
  install -D zmap "${pkgdir}"/usr/bin/zmap
  install -Dm644 zmap.1 "${pkgdir}"/usr/share/man/man1/zmap.1
  install -d "${pkgdir}"/etc/zmap
  install -m644 ../conf/* "${pkgdir}"/etc/zmap
  cd ..
  for F in AUTHORS CHANGELOG INSTALL README; do
    install -Dm644 ${F} "${pkgdir}"/usr/share/doc/zmap/${F}
  done
  cp -a examples "${pkgdir}"/usr/share/doc/zmap
}
