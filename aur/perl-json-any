# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-json-any'
pkgver='1.32'
pkgrel='1'
pkgdesc="Wrapper Class for the various JSON classes."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-json>=2.02' 'perl-json-xs>=2.3' 'perl-yaml-syck')
makedepends=()
url='http://search.cpan.org/dist/JSON-Any'
source=("http://search.cpan.org/CPAN/authors/id/P/PE/PERIGRIN/JSON-Any-${pkgver}.tar.gz")
md5sums=('723886f48b820faad0c1306a036e73bd')
sha512sums=('1e7b1dd35c176b41d273758d9bf5cd02abe9e3cd5ce06eae6c656700adfa19b6ebcdcb589eb38a588c90c379cc659a512afb49fc79695fccc7cb621e2785cd8f')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/JSON-Any-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
