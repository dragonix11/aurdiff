# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Max Roder <maxroder@web.de>
# Contributor: Lex Rivera <x-demon@x-demon.org>

pkgname=airoscript-ng
pkgver=1.2rc3
pkgrel=6
pkgdesc='A gui mainly for aircrack-ng. Automates cracking of WEP and WPA'
arch=('any')
url='http://code.google.com/p/airoscript/'
license=('GPL2')
depends=('aircrack-ng' 'macchanger')
optdepends=('mdk3: bruteforce and deauth support'
            'sslstrip: HTTPS MITM plugin support'
            'dsniff: sniffing and spoofing support'
            'zenity: additional graphical dialog support'
            'john: fast password cracking support')
backup=('etc/airoscript-ng/airoscript-ng.conf'
        'etc/airoscript-ng/airoscript-ng_advanced.conf'
        'etc/airoscript-ng/screenrc.airoscript')
options=('!makeflags !emptydirs')
source=("http://airoscript.googlecode.com/files/${pkgname}-1.2-3.tar.gz")
sha256sums=('2283e333a0de02c1403815b34ad1526b8ada7f505d7ce31dd3495ebd8300c67d')

prepare() {
  cd ${pkgname}-1.2
  
  # remove unwanted install parts
  sed -i '/install-locale \\/ d' Makefile-Linux
  sed -i '/install-desktop \\/ d' Makefile-Linux
  sed -i '/for i in doc/ d' Makefile-Linux

  # install binary to bin instead of sbin
  sed -i 's/\/sbin\//\/bin\//g' Makefile-Linux
  sed -i 's/\/sbin\//\/bin\//g' src/airoscript-ng_advanced.conf

  # replace usr/local by usr
  sed -i 's/usr\/local/usr/g' src/airoscript-ng_advanced.conf
  
  # replace config file location
  sed -i 's/etc\//etc\/airoscript-ng\//g' src/${pkgname}

  # remove .swp file from src dir
  rm src/functions/attacks/.wpa.swp
}

package() {
  cd ${pkgname}-1.2

  make DESTDIR="${pkgdir}" \
    prefix=usr \
    picdir="${pkgdir}/usr/share/pixmaps/" \
    appdir="${pkgdir}/usr/share/applications/" \
    etcdir="${pkgdir}/etc/${pkgname}"
  
  # only distribute the .rst doc files rather then html
  rm -rf "${pkgdir}"/usr/share/doc/airoscript-ng/*
  cp doc/html/*.rst "${pkgdir}/usr/share/doc/airoscript-ng/"
}

# vim: ts=2 sw=2 et:
