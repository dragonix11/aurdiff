# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-dosbox-git
pkgver=44.9c195a7
pkgrel=2
pkgdesc="libretro implementation of DOSBox."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/dosbox-libretro"
license=('GPL')
makedepends=('git')

_gitname=dosbox-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/dosbox_libretro.info")
md5sums=('SKIP'
         'acb254c0a717670c77ec1dd182a3836b')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/dosbox_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-dosbox.so"
  install -Dm644 "dosbox_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-dosbox.info"
}
