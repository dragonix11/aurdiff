# Maintainer: twa022 <twa022 at gmail dot com>
# Based on gnome-settings-daemon PKGBUILD

_pkgname=cinnamon-settings-daemon
pkgname=${_pkgname}-git
pkgver=114.fc4d7f2
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'cinnamon-desktop-git' 'hicolor-icon-theme' 'libcanberra-pulse' 'libgnomekbd' 'libnotify'
         'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'ibus' 'librsvg')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl' 'gnome-common')
options=('!emptydirs' '!libtool')
install=${_pkgname}.install
url="http://cinnamon.linuxmint.com/"
source=(${_pkgname}::git+https://github.com/linuxmint/cinnamon-settings-daemon.git)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd ${srcdir}/${_pkgname}

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/cinnamon-settings-daemon --sbindir=/usr/bin \
      --disable-static --enable-systemd --disable-schemas-compile

  #https://bugzilla.cinnamon.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR="$pkgdir" install
}
