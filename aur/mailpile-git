# Maintainer: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=mailpile-git
pkgver=20131107
pkgrel=1
pkgdesc="A modern, fast web-mail client with user-friendly encryption and privacy features."
arch=('any')
license=('AGPL3','Apache')
depends=('python2-imaging' 'python2-lxml' 'python2-jinja')
url="http://www.mailpile.is"
provides=("mailpile")
source=('git://github.com/pagekite/Mailpile.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/Mailpile"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {

	cd "${srcdir}/Mailpile"

	# python2 fixes
	find . -type f -exec sed -i 's^env python^env python2^g' {} \;
	find . -type f -exec sed -i 's^bin/python^bin/python2^g' {} \;
	sed -i 's^python ^python2 ^g' Makefile

	# Set absolute paths for static files
	sed -i "s^('static/^('/usr/share/mailpile/static/^g" mailpile/config.py
	sed -i "s^'static/^'/usr/share/mailpile/static/^g" mailpile/defaults.py
	sed -i 's^__file__^"/usr/share/mailpile/python"^g' mp

	# Compile bytecode
	printf '#!/bin/env python2\nimport compileall\ncompileall.compile_dir("mailpile", force=1)' > compile.py
	chmod +x compile.py
	./compile.py

}

package() {

	mkdir -p "${pkgdir}/usr/bin"
	mkdir -p "${pkgdir}/usr/share/mailpile"

	cp -r "${srcdir}/Mailpile/static" "${pkgdir}/usr/share/mailpile/static"
	cp -r "${srcdir}/Mailpile/mailpile" "${pkgdir}/usr/share/mailpile/"
	cp "${srcdir}/Mailpile/mp" "${pkgdir}/usr/bin/"

	find "${pkgdir}/usr" -type f -exec chmod 644 {} \;
	find "${pkgdir}/usr" -type d -exec chmod 755 {} \;
	chmod 755 "${pkgdir}/usr/bin/mp"

}
