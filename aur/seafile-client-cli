# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-client-cli
pkgver=2.0.6
pkgrel=1
pkgdesc="Seafile is an online file storage and collaboration tool. This is seafile cli client for synchronizing your local files with seafile server."
arch=('any')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('seafile-shared>=2.0.5' 'libevent' 'python2-simplejson')
makedepends=('vala' 'sed')
optdepends=()
options=('!libtool' '!emptydirs')
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz")
md5sums=('106ba7d121044d811c3ba229a2094862')
sha256sums=('eea7fa6507484e604f460c316c89e859b7ce0a1ce6ca6ceb6bfe1d6b4eae2647')

build ()
{
	cd "$srcdir/seafile-${pkgver}"
	./autogen.sh
	./configure --disable-server --enable-client --disable-gui --disable-console --prefix=/usr PYTHON=/usr/bin/python2
	make -j1

	# Fix cli client's python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' ${srcdir}/seafile-${pkgver}/app/seaf-cli
}

package ()
{
	# Install cli client
	install -D -m755 ${srcdir}/seafile-${pkgver}/app/seaf-cli ${pkgdir}/usr/bin/seaf-cli

	# Install license
	install -D -m644 ${srcdir}/seafile-${pkgver}/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}
