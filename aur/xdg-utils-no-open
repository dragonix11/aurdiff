# Maintainer: RunningDroid <runningdroid AT zoho.com>
# Major contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xdg-utils-no-open
pkgver=1.1.0.git20130520
pkgrel=1
pkgdesc="Command line tools that assist applications with a variety of desktop integration tasks"
arch=('any')
url="http://portland.freedesktop.org/"
license=('MIT')
provides=('xdg-utils')
conflicts=('xdg-utils')
depends=('sh' 'xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl' 'lynx' 'xmlto' 'git')
optdepends=('linopen: for xdg-open support'
            'expro: for xdg-open support'
            'busking-git: for xdg-open support'
            'mimi-git: for xdg-open support')
source=(git://anongit.freedesktop.org/xdg/xdg-utils#commit=2373d9b2b7)
md5sums=('SKIP')

pkgver() {
	cd xdg-utils
	echo "1.1.0.git$(git log -1 --format="%cd" --date=short | sed 's|-||g')"
}

prepare(){
    cd xdg-utils/scripts
    sed -i -e '20 h' -e '20 d' -e '23 c \    xdg-settings\n#    xdg-open' Makefile.in
}

build() {
	cd xdg-utils
	./configure --prefix=/usr --mandir=/usr/share/man
	make scripts-clean -C scripts # needed if it's a git snapshot
	make man scripts -C scripts # needed if it's a git snapshot
#	make # for release builds
}

package() {
	cd xdg-utils
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
	install -dm755 $pkgdir/usr/share/desktop-directories
}
