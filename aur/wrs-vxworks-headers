#Maintainer: Alex Brinister (alex_brinister@yahoo.com)
pkgname=wrs-vxworks-headers
pkgver=6.3
pkgrel=10
pkgdesc="The VxWorks 6.3 Headers"
arch=(any)
provides=('wrs-vxworks-headers')
url="http://firstforge.wpi.edu/sf/projects/c--11_toochain"
license=('GPL' 'custom=Wind River')
makedepends=('unzip' 'dos2unix')
options=('!strip' 'libtool' '!zipman')
source=(ftp://ftp.ni.com/pub/devzone/tut/updated_vxworks63gccdist.zip
        LICENSE)
_vxworks="updated_vxworks63gccdist.zip"
_dist="gccdist"
noextract=($_vxworks)
sha512sums=('91e7d1705a5adc1d2b49802af3bd71535572b712e6b31fc1438fbdf6ad0553b1d8deadd93580b59e6e82d229fb3f337f1e48304a3ea126e884947bbf9cae9ff9'
            '02a0d5b7ac7c812977138d29da5db95aa6bce72d2a87fbc6ea0a01f839b764b94127091f46d89069b981c5e86caa3e0a0a4813455ee7928d17e7fbcc66a7c9f2')
install='wrs-vxworks-headers.install'
package() {
  cd "$srcdir"
  unzip $_vxworks
  mkdir -p $pkgdir/usr/powerpc-wrs-vxworks/{wind_base/target,share/ldscripts}
  cp -dpr --no-preserve=ownership $_dist/WindRiver/vxworks-6.3/host $pkgdir/usr/powerpc-wrs-vxworks/wind_base
  cp -dpr --no-preserve=ownership $_dist/WindRiver/vxworks-6.3/host/resource $pkgdir/usr/powerpc-wrs-vxworks/wind_base
  cp -dpr --no-preserve=ownership $_dist/WindRiver/vxworks-6.3/target/h/. $pkgdir/usr/powerpc-wrs-vxworks/sys-include
  cp -dpr --no-preserve=ownership $_dist/WindRiver/vxworks-6.3/target/h/wrn/coreip/. $pkgdir/usr/powerpc-wrs-vxworks/wind_base/target/h
  sed '/ENTRY(_start)/d' < $_dist/WindRiver/vxworks-6.3/target/h/tool/gnu/ldscripts/link.OUT > $pkgdir/usr/powerpc-wrs-vxworks/share/ldscripts/dkm.ld
  chmod -R 755 $pkgdir/usr/powerpc-wrs-vxworks/
  find $pkgdir -type f -exec dos2unix {} +
  install -Dm644 $srcdir/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  mkdir -p $pkgdir/etc/profile.d
  touch $pkgdir/etc/profile.d/wind_base.sh
  echo 'export WIND_BASE=/usr/powerpc-wrs-vxworks/wind_base' >> $pkgdir/etc/profile.d/wind_base.sh
  chmod 755 $pkgdir/etc/profile.d/wind_base.sh
}

# vim:set ts=2 sw=2:
