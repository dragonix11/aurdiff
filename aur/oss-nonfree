# Maintainer: Alex Wiggins <alex.d.wiggins@gmail.com>
# Contributer: Kyle Keen <keenerd@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Sebastian Schwarz <seschwar@gmail.com>

pkgbase=oss-nonfree
pkgname=oss-nonfree
true && pkgname=(oss-nonfree libflashsupport-oss)
pkgver=4.2_2008
pkgrel=9
arch=(i686 x86_64)
url="http://www.opensound.com/oss.html"
license=('custom:4Front Commercial License')
source=("http://www.4front-tech.com/release/oss-linux-v4.2-2008-$CARCH.tar.bz2"
        LICENSE
        'linux-3.8.0.patch::http://www.opensound.com/forum/download/file.php?id=113'
        'linux-3.10.0.patch::http://www.opensound.com/forum/download/file.php?id=126'
        'linux-3.10.7.patch::http://www.opensound.com/forum/download/file.php?id=129'
        oss.service
        remove-hal.patch
        rm-init-scripts.patch
        soundon.patch)
noextract=("oss-linux-v4.2-2008-$CARCH.tar.bz2")
md5sums=('d41d8cd98f00b204e9800998ecf8427e'
         '39201b4c5da86cb8ab63849a7d6a5079'
         'effecefb774f1d08ef9f0af0ad205c34'
         'c7fc0fa5c739eda9d265bb9a4f69ecd9'
         '1860b69bea048109cec8d49cc2696586'
         '39c46b10a6bc56fc146d669a61bd3028'
         'cd7f1dc6166bba8c94d96f3a28e948a5'
         'b9a380a0ac8896390d71ac13676f27e1'
         '566a3e0585ffb7eb0d8f77b9cc94d8c7')
if [[ "$CARCH" == i686 ]]; then
    md5sums[0]=d800f5d4244b83bcba62817c4109ff8c
else
    md5sums[0]=9c26c16f14c11737d4f6596442ed8ce3
fi

build() {
    cd "$srcdir"
    bsdtar xf "oss-linux-v4.2-2008-$CARCH.tar.bz2" \
            usr/lib/oss/lib/flashsupport.c
    gcc $CFLAGS -shared -fPIC -Wall -Werror \
            usr/lib/oss/lib/flashsupport.c -o libflashsupport.so
}

package_libflashsupport-oss() {
    pkgdesc="Adobe flash plugin support lib (OSSv4)"
    conflicts=(libflashsupport libflashsupport-pulse)
    depends=(oss)

    install -dm755 "$pkgdir/usr/lib"
    ln -s oss/lib/libflashsupport.so "$pkgdir/usr/lib/libflashsupport.so"
}

package_oss-nonfree() {
    pkgdesc="Open Sound System UNIX audio architecture (including nonfree drivers)"
    depends=(gcc make linux-headers libtool sed)
    optdepends=('gtk2: for graphical mixer (ossxmix)'
                'libflashsupport-oss: Adobe flash plugin support')
    provides=(oss)
    conflicts=(oss oss-linux oss-linux-free oss-testing)
    backup=(usr/lib/oss/soundon.user)
    install=oss.install

    cd "$pkgdir"
    bsdtar xf "$srcdir/oss-linux-v4.2-2008-$CARCH.tar.bz2"

    # make OSS compile with current Linux kernels
    cd usr/lib
    patch -p2 < "$srcdir/linux-3.8.0.patch"
    patch -p2 < "$srcdir/linux-3.10.0.patch"
    patch -p2 < "$srcdir/linux-3.10.7.patch"
    cd ../..

    # remove outdated stuff
    patch -p0 < "$srcdir/rm-init-scripts.patch"
    rm usr/lib/oss/etc/S89oss
    patch -p0 < "$srcdir/remove-hal.patch"
    rm usr/lib/oss/scripts/*oss_usb-create-device*
    patch -p0 < "$srcdir/soundon.patch"

    # usr-move fixes
    mv usr/sbin/* usr/bin
    rmdir usr/sbin
    grep -IlrZ '\<s\?bin\>' . \
            | xargs -0 sed -i 's,\<\(usr/\)\?s\?bin\>,usr/bin,g' --
        grep -IlrZ '\<lib/modules\>' . \
            | xargs -0 sed -i 's,\<\(usr/\)\?lib/modules\>,usr/&,g' --

    # make OSS install its modules to /usr/lib/modules/$KERNEL/extramodules/oss
    grep -IlrZ '\<usr/lib/modules/[^/]\+/kernel/oss\>' . \
            | xargs -0 sed -i 's,\<usr/lib/modules/\([^/]\+\)/kernel/oss\>,usr/lib/modules/\1/extramodules/oss,g' --

    chmod -R a+rX .  # FS#13815
    install -Dm755 "$srcdir/libflashsupport.so" \
            usr/lib/oss/lib/libflashsupport.so
    install -Dm644 "$srcdir/LICENSE" "usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "$srcdir/oss.service" usr/lib/systemd/system/oss.service
}
