# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mpv-build-git
pkgver=0.2.0.git.d3c140b
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'enca' 'fribidi' 'libbluray' 'libcaca' 'libcdio-paranoia'  'libguess' 'libmng' 'libdvdread' 'libpulse' 'libva'
         'libquvi' 'libxinerama' 'libxkbcommon' 'libxss' 'libxv' 'lirc-utils' 'mpg123' 'openal' 'portaudio' 'rsound' 'sdl' 'sdl2' 'smbclient' 'sndio-cvs')
license=('GPL2')
url="http://mpv.io/"
makedepends=('git' 'live-media' 'mesa' 'python-docutils' 'vstream-client' 'yasm' 'texlive-bin')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('git://github.com/mpv-player/mpv-build.git'
        'git://github.com/mpv-player/mpv.git'
        'git://source.ffmpeg.org/ffmpeg.git'
        'git+https://code.google.com/p/libass'
        'git://anongit.freedesktop.org/fribidi/fribidi.git')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')
_gitname="mpv-build"

pkgver() {
#   cd "${srcdir}/mpv"
#   echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"

  cd "${srcdir}/mpv"
  # add VERSION file with a latest tag. this is a little hack to generate a VERSION file like a file ship in release package
  git describe --tags "$(git rev-list --tags --max-count=1)" | tr -d v > VERSION
  ./version.sh
  echo "$(cat version.h | grep VERSION | cut -d '"' -f2 | tr - .)"
  rm version.h
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv ffmpeg libass fribidi ffmpeg_build build_libs
  mv ../{mpv,ffmpeg,libass,fribidi} .

  # Make mpv/ffmpeg build flags
  sed 's|OPTIONS="|OPTIONS="--disable-programs |g' -i scripts/ffmpeg-config
  sed 's|OPTIONS="|OPTIONS="--prefix=/usr --confdir=/etc/mpv --extra-cflags=-I/usr/include/samba-4.0 --enable-sdl --enable-sdl2 --disable-rpath --disable-libquvi4 --enable-openal|g' -i scripts/mpv-config
}

build() {
  unset CFLAGS
  unset LDFLAGS

  cd "${_gitname}"
  make
}

package() {
  cd "${_gitname}/mpv"
  make DESTDIR="${pkgdir}" install-strip
  install -Dm755 TOOLS/mpv_identify.sh "${pkgdir}/usr/bin/mpv_identify"

  install -Dm644 etc/input.conf "${pkgdir}/etc/mpv/input.conf"
  install -Dm644 etc/example.conf "${pkgdir}/etc/mpv/mpv-example.conf"
  install -Dm644 etc/encoding-example-profiles.conf "${pkgdir}/etc/mpv/encoding-example-profiles.conf"

  sed -e 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|' \
      -e 's|/path/to/the/file/you/want/to/include|/etc/mpv/encoding-profiles.conf|' \
      -e 's|/path/to/this|/etc/mpv|' \
      -i "${pkgdir}/etc/mpv/"*.conf

  install -Dm644 DOCS/tech-overview.txt "${pkgdir}/usr/share/doc/mpv/tech-overview.txt"
}