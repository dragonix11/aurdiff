# $Id$
# Maintainer: imotec <info(at)imotec(dot)nl>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

# for customization
_pkgname=cpp-netlib
_ext=
_basedir=/usr
_boost=boost${_ext}
# note: depends=() needs separate customization

pkgname=${_pkgname}${_ext}
pkgver=0.10.1
pkgrel=2
pkgdesc="A library that provides application layer protocol support using modern C++ techniques"
arch=('i686' 'x86_64')
url="http://cpp-netlib.github.com/"
license=('custom')
depends=("boost${_ext}>=1.45.0")
optdepends=('openssl: for https')
makedepends=("cmake>=2.8.10")
source=(http://commondatastorage.googleapis.com/$_pkgname-downloads%2F$pkgver%2F$_pkgname-$pkgver.tar.gz
        CMakeLists.txt.diff
        CMakeLists.txt.2.diff
        server_async_run_stop_concurrency.cpp.diff
        options.hpp.diff
         )
md5sums=(
         'afaabb96c22fe50a852432cbc5959557'
         'bc253184abe20ed49cd7bea748e85a37'
         'c1182260b21c771cdf7fe6cfe440b87a'
         '9ff164c6eba6ec91b2f44d9f9ced79d8'
         '51020aa3c6b4fde83ea162c2869f858a'
         )

build() {
  msg "Applying patches..."
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np0 -i "${srcdir}/CMakeLists.txt.diff"
  patch -Np0 -i "${srcdir}/CMakeLists.txt.2.diff"
  patch -Np0 -i "${srcdir}/server_async_run_stop_concurrency.cpp.diff"
  patch -Np0 -i "${srcdir}/options.hpp.diff"
  install -d $srcdir/build
  cd $srcdir/build
  cmake -DBOOST_ROOT=$_basedir -DBOOST_DYN_LINK=ON ../"$_pkgname-$pkgver"
  make
}

package() {
  #Preparing directories
  install -d ${pkgdir}${_basedir}/{include,lib}
  install -d ${pkgdir}/usr/share/licenses/$pkgname

  #Install includes, library files and license
  cd "$srcdir/$_pkgname-$pkgver"
  cp -r boost/ ${pkgdir}${_basedir}/include/
  install -Dm644 LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE_1_0.txt

  #Install static library files as well
  cd $srcdir/build
  cp -r libs/network/src/*.a ${pkgdir}${_basedir}/lib/
}

