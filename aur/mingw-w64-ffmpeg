# Maintainer : Karl-Felix Glatzer <karl.glatzer@gmx.de>

pkgname=mingw-w64-ffmpeg
pkgver=2.0.1
pkgrel=1
epoch=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix (mingw-w64)"
arch=('any')
url="http://ffmpeg.org/"
license=('GPL')
depends=('mingw-w64-crt' 'mingw-w64-bzip2' 'mingw-w64-fontconfig' 'mingw-w64-gsm' 
'mingw-w64-lame' 'mingw-w64-libass' 'mingw-w64-libbluray' 'mingw-w64-libmodplug'
'mingw-w64-libtheora' 'mingw-w64-libvorbis' 'mingw-w64-libvpx' 'mingw-w64-opencore-amr'
'mingw-w64-openjpeg' 'mingw-w64-opus' 'mingw-w64-pkg-config' 'mingw-w64-rtmpdump' 'mingw-w64-schroedinger'
'mingw-w64-sdl' 'mingw-w64-speex' 'mingw-w64-x264' 'mingw-w64-zlib')
options=(!strip !buildflags)
makedepends=('mingw-w64-gcc' 'yasm')
source=(http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.bz2)
sha256sums=('611eae7e3485d5fd9ff03e0ac7575ea89bc8105e40e99a40de02324957482f95')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}

    unset LDFLAGS CPPFLAGS
    ${srcdir}/ffmpeg-$pkgver/configure \
      --prefix=/usr/${_arch} \
      --enable-cross-compile \
      --cross-prefix="${_arch}-" \
      --target-os=mingw32 \
      --arch=${_arch%%-*} \
      --disable-debug \
      --enable-static \
      --enable-avresample \
      --enable-dxva2 \
      --enable-fontconfig \
      --enable-gpl \
      --enable-libass \
      --enable-libbluray \
      --enable-libfreetype \
      --enable-libgsm \
      --enable-libmodplug \
      --enable-libmp3lame \
      --enable-libopencore_amrnb \
      --enable-libopencore_amrwb \
      --enable-libopenjpeg \
      --enable-libopus \
      --enable-librtmp \
      --enable-libschroedinger \
      --enable-libspeex \
      --enable-libtheora \
      --enable-libvorbis \
      --enable-libvpx \
      --enable-libx264 \
      --enable-zlib \
      --enable-postproc \
      --enable-runtime-cpudetect \
      --enable-shared \
      --enable-swresample \
      --enable-version3 \
      --disable-doc \
      --disable-programs

    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}
    make DESTDIR="$pkgdir" install

    ${_arch}-strip -x -g ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a

    mv ${pkgdir}/usr/${_arch}/bin/*.lib ${pkgdir}/usr/${_arch}/lib/
  done
}

# vim:set ts=2 sw=2 et:
