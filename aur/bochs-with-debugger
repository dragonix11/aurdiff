# Mantainer: Gianni Vialetto <gianni at rootcube dot net>
# Contributor: Alex Weil <ajonat at gmail dot com>

pkgname=bochs-with-debugger
_pkgrealname=bochs
pkgver=2.6.2
pkgrel=2
pkgdesc="A portable x86 PC emulation software package with builtin debugger"
arch=(i686 x86_64)
license=('LGPL')
url="http://bochs.sourceforge.net/"
source=(http://downloads.sourceforge.net/project/${_pkgrealname}/${_pkgrealname}/${pkgver}/${_pkgrealname}-${pkgver}.tar.gz)
md5sums=('82ecaff9826d4f29fa46f3062e2957b8')
sha256sums=('d973b72b79a5de96ebe9337b8421f19dcd775dce16ed25a0232fa6d543a54b10')
sha512sums=('b35cf940520d2489657a1724f399553f7a52428fd4a84b1c38452723cc47bfbc242853489701c1aa577903045ce3dae4ca475c8934953b66bcc247d50835c7d3')
depends=('gcc-libs' 'libxrandr' 'gtk2')
conflicts=('bochs')

build() {
    cd "${srcdir}/${_pkgrealname}-${pkgver}"
    ./configure \
        --enable-smp \
        --enable-all-optimizations \
        --with-all-libs \
        --prefix=/usr \
        --enable-pci \
        --enable-pcidev \
        --enable-usb \
        --enable-3dnow \
        --enable-debugger \
        --enable-x86-debugger \
        --disable-plugins \
        --disable-docbook
    make
}

package() {
    cd "${srcdir}/${_pkgrealname}-${pkgver}"
    make prefix=${pkgdir}/usr install
}
