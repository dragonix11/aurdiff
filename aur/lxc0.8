# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

_pkgname=lxc
pkgname=${_pkgname}0.8
pkgver=0.8.0
epoch=1
pkgrel=2
pkgdesc="Linux Containers"
arch=('i686' 'x86_64')
url="http://lxc.sourceforge.net/"
depends=('bash' 'perl')
conflicts=('lxc')
provides=('lxc')
license=('LGPL')
source=("http://lxc.sourceforge.net/download/lxc/$_pkgname-${pkgver/_/-}.tar.gz")
md5sums=('7304fa57146ce687f749b5df876bb20d')

build() {
  cd "$srcdir/$_pkgname-${pkgver/_/-}"
  ./configure \
	--prefix=/usr \
	--localstatedir=/var \
	--libexecdir=/usr/bin \
	--sysconfdir=/etc \
	--disable-apparmor \
	--disable-doc
  make
}

package() {
  cd "$srcdir/$_pkgname-${pkgver/_/-}"

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir/var/lib/lxc"

  cd doc
  find . -type f -name '*.1' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man1/{}" \;
  find . -type f -name '*.5' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man5/{}" \;
  find . -type f -name '*.7' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man7/{}" \;
}
