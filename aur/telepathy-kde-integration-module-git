# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Adria Arrufat <swiftscythe@gmail.com>

pkgname=telepathy-kde-integration-module-git
_gitname="ktp-kded-module"
pkgver=v0.6.80.3.g175ac4d
pkgrel=1
epoch=2
pkgdesc="A module that sits in KDED and takes care of various bits of system integration like setting user to auto-away or handling connection errors."
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-kded-module"
license=("GPL")
depends=('telepathy-kde-common-internals-git')
makedepends=('git' 'cmake' 'automoc4')
conflicts=('telepathy-kde-integration-module'
           'telepathy-kde-kded-module-git') 
source=("git://anongit.kde.org/ktp-kded-module")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=$( kde4-config --prefix ) \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install 
}
