# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
_relname=havana
pkgver=2013.2
pkgrel=1
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-novaclient python2-paramiko
    python2-suds)
makedepends=(python2-setuptools)
source=($url/$_relname/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('55a51f8d8b6c7b0ba6f8ff9c48604bb82a90bdc3f21460ed325d1cee2dfea95a')
sha512sums=('655d6f5a4ab9ddfb741a920417061808bf22521c967d324f0fa1856c801795969df6f4982362bce26836975c09e7f41e25575309cde5c6788ed32e69304381ea')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
