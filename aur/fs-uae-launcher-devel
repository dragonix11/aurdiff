# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae-launcher
pkgname=fs-uae-launcher-devel
pkgver=2.3.6dev
pkgrel=1
pkgdesc="FS-UAE Launcher is a graphical user interface for setting up FS-UAE (development version)."
arch=("any")
url="http://fs-uae.net/download-devel"
license=("GPL2")
depends=("fs-uae" "xdg-utils" "wxpython" "python2-pygame" "python2-setuptools" "python2-lhafile" "hicolor-icon-theme")
optdepends=("p7zip: for .7z zip support" "python2-pyqt4: for qt4 ui" "python2-pyside: for qt4 ui")
install="${pkgname}.install"
source=("http://fs-uae.net/devel/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
provides=("fs-uae-launcher")
conflicts=("fs-uae-launcher")
md5sums=('f9130c969f65577ced8240ba32938bbd')



build() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py build
   make
}



package() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py install --skip-build --prefix=/usr \
      --install-scripts=/usr/share/${_pkgname} \
      --install-lib=/usr/share/${_pkgname} --root="${pkgdir}"
   make install DESTDIR="${pkgdir}"

   install -d "${pkgdir}"/usr/bin
   ln -s /usr/share/${_pkgname}/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
}


