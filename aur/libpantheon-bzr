# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=libpantheon-bzr
pkgver=23
pkgrel=1
pkgdesc="Library for the Pantheon Desktop Environment"
arch=('i686' 'x86_64')
url="https://launchpad.net/libpantheon"
license=('GPL3')
depends=('gtk3')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala0.16')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("bzr+lp:${pkgname%-*}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  bzr revno
}

build() {
  cd ${pkgname%-*}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE=/usr/bin/valac-0.16
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
