# Maintainer: Nicolas Reynolds <fauno@kiwwwi.com.ar>
# Based on haskell-pandoc

# Run `makepkg -sp SRCBUILD` if you want to update the source tarball

pkgname=pandoc-static
_pkgname=pandoc
pkgver=1.12
pkgrel=1
pkgdesc='Conversion between markup formats (no Haskell libs)'
url='http://johnmacfarlane.net/pandoc/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc' 'sh' 'cabal-install' 'alex' 'happy')
options=(strip !makeflags !distcc !emptydirs)
#source=(https://repo.parabolagnulinux.org/other/${_pkgname}-${pkgver}-$pkgrel-any.src.tar.xz{,.sig}
source=(https://repo.parabolagnulinux.org/other/${_pkgname}-${pkgver}-1-any.src.tar.xz{,.sig}
        SRCBUILD)
#        citeproc-hs-pre-0.3.7.patch)
replaces=('pandoc')
provides=('pandoc')
optdepends=('texlive-most: for pdf creation')
md5sums=('74acd042fcd85bee9edb093cc3fe5c2e'
         'SKIP'
         'ee9d5924632ac07b5049be834a1ed054')

# PKGBUILD functions
build() {
    mkdir -p ${srcdir}/{build,${_pkgname}-${pkgver}}
    cd ${srcdir}/${_pkgname}-${pkgver}

# Patches from the next citeproc-hs release
# These are needed because 0.3.6 with embed_data_files is broken
# darcs diff --from-tag=0.3.6
#   pushd ${srcdir}/${_pkgname}-${pkgver}/citeproc-hs-0.3.6
#   patch -Np1 -i ${srcdir}/citeproc-hs-pre-0.3.7.patch
#   popd

   while read _hkpkg; do
# Skip already built
       [ -d ${srcdir}/build/usr/lib/$_hkpkg ] && continue

       pushd ${srcdir}/${_pkgname}-${pkgver}/${_hkpkg} >/dev/null

       msg2 "Building $_hkpkg"

       case $_hkpkg in
        $_pkgname-$pkgver)
         HOME=${srcdir}/${_pkgname}-${pkgver} \
         cabal configure --flags='-library' \
                         --prefix=/usr \
                         --libdir=${srcdir}/build/usr/lib -v

         HOME=${srcdir}/${_pkgname}-${pkgver} \
         cabal build
         ;;

        citeproc-hs-*)
         HOME=${srcdir}/${_pkgname}-${pkgver} \
         cabal install --flags='embed_data_files' \
                       --prefix=${srcdir}/build/usr -v
         ;;

        *)
         HOME=${srcdir}/${_pkgname}-${pkgver} \
         cabal install --prefix=${srcdir}/build/usr
         ;;
       esac

       popd >/dev/null
   done <BUILDORDER

}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}/${_pkgname}-${pkgver}

  runghc Setup.hs copy --destdir=${pkgdir}/

# For some reason the library is installed anyway
# Remove all files and !emptydirs takes care of the rest
  msg2 "Removing lib files..."
  find ${pkgdir} -iname lib -print0 | xargs -0 rm -rvf

# EC is unfree and makes Parabola TeXLive cry
# besides, it's unneeded
  sed "/fontenc/d" -i ${pkgdir}/usr/share/${_pkgname}-${pkgver}/data/templates/default.latex

  find ${pkgdir}/usr/share -type f -exec chmod 644 {} \;
  find ${pkgdir}/usr/share -type d -exec chmod 755 {} \;

  msg2 "Installing licenses"
  install -d ${pkgdir}/usr/share
  cp -rv ${srcdir}/build/usr/share/doc ${pkgdir}/usr/share/
}
md5sums=('3d5f23060cad2ff29ba2df5651e92f8a'
         'SKIP'
         'c2e9f9b880468455b6e6a7f9ba7308e8')
