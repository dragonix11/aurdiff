# lMaintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-cpan-meta-check'
pkgver='0.007'
pkgrel='1'
pkgdesc="Verify requirements in a CPAN::Meta object"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-deep')
url='http://search.cpan.org/dist/CPAN-Meta-Check'
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/CPAN-Meta-Check-$pkgver.tar.gz")
md5sums=('56f71df79cea8d308a552b3eb996deb0')
sha512sums=('816d5517c42a05fc7a489a49193d9a1ff01ff027f67671a7497171898f820f6aa0d5b0984cbfa54d67d1a9b23140ac4d46edb199cf7901c3fa9b9b0175b1051c')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/CPAN-Meta-Check-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
