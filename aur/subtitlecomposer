# Contributor: Mladen Milinkovic <maxrd@smoothware.net>
# Maintainer: Mladen Milinkovic <maxrd@smoothware.net>

pkgname=subtitlecomposer
pkgver=0.5.5
pkgrel=1
pkgdesc="A KDE subtitle editor"
arch=('i686' 'x86_64')
url="https://github.com/maxrd2/subtitlecomposer"
license=('GPL')
depends=('kdelibs' 'gettext')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('subtitlecomposer-git')
optdepends=(
	'mplayer: for MPlayer backend'
	'gstreamer: for GStreamer backend'
	'xine-lib: for Xine backend'
	)
source=("https://github.com/maxrd2/subtitlecomposer/archive/v${pkgver}.tar.gz")
md5sums=('12c4a3083300f7b9ec7bb9d5ad4f4bda')

build() {
  cd ${srcdir}/subtitlecomposer-${pkgver}
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/subtitlecomposer-${pkgver}
  make DESTDIR=${pkgdir} install
}
