# Maintainer: Willy GOIFFON <willy at mailoo dot org>
pkgname=arielle-git
_pkgname=${pkgname%-git}
pkgver=2.4.84.a650448
pkgrel=2
pkgdesc='An IRC robot which can parse commands and interact with the channel it is connected to'
arch=('i686' 'x86_64')
url='http://gitorious.org/pndc/arielle'
license=('MIT')
depends=('libconfig')
makedepends=('git')
provides=('arielle')
backup=('etc/arielle.conf'
        'usr/share/arielle/tricks.qot'
        'usr/share/arielle/random.qot'
        'usr/share/arielle/quotes.qot')
source=('git://gitorious.org/pndc/arielle.git'
        'arielle.service'
        'arielle.env')
md5sums=( 'SKIP'
          'e733bffc14f6b186e971ea9fb1a19e18'
          '8596303d2dc1fea89fa2b4250ab2cf33')

pkgver() {
  cd "$_pkgname"
  echo "2.4.$(git rev-list --count HEAD).$(git log -1 --pretty=format:%h)"
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -D -m644 ${srcdir}/arielle.env "${pkgdir}/etc/conf.d/arielle"
  install -D -m644 ${srcdir}/arielle.service "${pkgdir}/usr/lib/systemd/system/arielle.service"
}

# vim:set ts=2 sts=2 sw=2 et:

