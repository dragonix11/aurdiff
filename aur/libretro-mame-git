# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-mame-git
pkgver=62.00da6d6
pkgrel=1
pkgdesc="libretro implementation of MAME 2013 (0.150). (Arcade)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mame2013-libretro"
license=('custom:MAME License')
makedepends=('git')

install=libretro-mame.install

_gitname=mame2013-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mame_libretro.info")
md5sums=('SKIP'
         'e09347ed3a8a2af2ad31c2cb63ebdd7a')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/0150"
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/0150/mame_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mame.so"
  install -Dm644 "mame_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mame.info"
  install -Dm644 "${_gitname}/0149/mame.ini" "${pkgdir}/usr/share/libretro/libretro-mame/mame.ini"
  install -Dm644 "${_gitname}/0150/docs/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
