# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
pkgver=5.40.1
pkgrel=1
pkgdesc='Offload browser profiles to RAM (tmpfs) for speed and wear reduction.'
arch=('any')
url='https://github.com/graysky2/profile-sync-daemon'
license=('MIT')
depends=('rsync')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store' 'goanysync'
'goanysync-git' 'tmpfs-sync' 'user-profile-sync-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=('etc/psd.conf')
install=psd.install
sha256sums=('319e7ced1eccd1d0433a2252b6aa3b5b63db4040a974ff1c6c0dfa5a45d59a99')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-systemd-all
  install -Dm644 MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
