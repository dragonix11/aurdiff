# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=glacier-cmd-git
pkgver=0.280.a2d5763
pkgrel=3
pkgdesc="Amazon Glacier command-line utility"
arch=('any')
url="https://github.com/uskudnik/amazon-glacier-cmd-interface"
license=('MIT')
depends=(
  'python2'
  'python2-distribute'
  'python2-argparse'
  'python2-dateutil'
  'python2-prettytable'
  'python2-pytz'
  'boto-git'
)
makedepends=('git')
options=(!emptydirs)
source=("${pkgname}::git+${url}.git#branch=master")
md5sums=('SKIP')

pkgver () {
  cd "${srcdir}/${pkgname}"
  echo "0.$(git rev-list --count HEAD).$(git describe --always | sed 's|-|.|g')"
}

package() {
  cd "${srcdir}/${pkgname}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
