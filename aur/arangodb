# Maintainer: Bambang Purnomosidi D. P. <bpurnomo at akakom dot ac dot id>
# Contributor: Jan Steemann <j.steemann at triagens dot de>
pkgname=arangodb
pkgver=1.4.0
pkgrel=1
pkgdesc="A universal open-source database with a flexible data model for documents, graphs, and key-values."
arch=('i686' 'x86_64')
url="http://www.arangodb.org/"
license=('Apache')
depends=('openssl' 'bash' 'systemd' 'icu')
makedepends=('python2')
install=arangodb.install
source=("http://www.arangodb.org/repositories/Source/ArangoDB-$pkgver.tar.bz2"
        "arangodb.service")
md5sums=('e56c9443bd90f44c7e30e099a7989c58'
         '4eb8bd829c2e0f5b058280466d94c2ac')

build() {
  cd "$srcdir/ArangoDB-$pkgver"
  ln -s -f /usr/bin/python2 python
  export PATH="`pwd`:$PATH"
  ./configure --bindir=/usr/bin --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share
  make CFLAGS="-Wno-c++0x-compat" CXXFLAGS="-Wno-c++0x-compat" 
}

check() {
  cd "$srcdir/ArangoDB-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/ArangoDB-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/share/doc/arangodb
  cp -R $srcdir/ArangoDB-$pkgver/Documentation/* $pkgdir/usr/share/doc/arangodb/
  mkdir -p $pkgdir/usr/lib/systemd/system
  # This line:
  # cp $srcdir/ArangoDB-$pkgver/etc/systemd/arangodb.service $pkgdir/usr/lib/systemd/system/
  # changed into:
  cp ../../arangodb.service $pkgdir/usr/lib/systemd/system/
  # see:
  # https://www.archlinux.org/news/binaries-move-to-usrbin-requiring-update-intervention/
  rm -rf $pkgdir/usr/share/doc/arangodb/man*
}

# vim:set ts=2 sw=2 et:
