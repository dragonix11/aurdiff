# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-snes9x-git
pkgver=371.25c0e7c
pkgrel=2
pkgdesc="libretro implementation of SNES9x. (Super Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/snes9x"
license=('custom' 'GPL' 'LGPL')
makedepends=('git')

_gitname=snes9x
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/snes9x_libretro.info")
md5sums=('SKIP'
         '6b84c8b8a90c12c0bd5fda6fa6776a85')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${_gitname}/libretro
  make
}

package() {
  install -Dm644 "${_gitname}/docs/snes9x-license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/libretro/snes9x_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-snes9x.so"
  install -Dm644 "snes9x_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-snes9x.info"
}
