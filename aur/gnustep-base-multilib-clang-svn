# Maintainer: X0rg

pkgname=gnustep-base-multilib-clang-svn
_svnname=gnustep-base
pkgver=37254
pkgrel=2
pkgdesc="The GNUstep base package for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('GPL2' 'GPL3' 'LGPL3')
groups=('gnustep-multilib-clang-svn')
depends=('libffi'      'libxml2'       'libxslt'       'openssl'       'gnutls'       'icu'       'avahi'       'xz' \
         'lib32-libffi' 'lib32-libxml2' 'lib32-libxslt' 'lib32-openssl' 'lib32-gnutls' 'lib32-icu' 'lib32-avahi' 'lib32-xz' 'gnustep-libobjc2-multilib-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-multilib-clang-svn' 'lib32-gmp' 'lib32-libgcrypt' 'lib32-zlib' 'lib32-glibc')
optdepends=('iconv: only if you don`t have glibc'
	'ffcall: alternative for libffi')
provide=('gnustep-base-clang-svn')
conflicts=('gnustep-base' 'gnustep-base-svn' 'gnustep-base-clang-svn' 'lib32-gnustep-base-clang-svn')
options=('!emptydirs' '!buildflags')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/base/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Make a clone of $_svnname"
  cp -Rv "$_svnname" "$_svnname-32"
}

build() {
  cd "$_svnname-32"
  source "/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep32.conf"

  msg2 "Run 'configure' (32-bit)..."
  OBJCFLAGS="-fblocks" CC="clang -m32" CXX="clang++ -m32" LDFLAGS="-L/usr/share/GNUstep32/lib32 -L/usr/lib32" ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-default-config=/etc/GNUstep/GNUstep32.conf --with-config-file=/etc/GNUstep/GNUstep32.conf	\
    --disable-unicodeconstants --with-ffi-include=/usr/lib32/libffi-`pacman -Q lib32-libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Run 'make' (32-bit)..."
  make

  # 64-bit build
  cd "$srcdir/$_svnname"
  source "/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"

  msg2 "Run 'configure' (64-bit)..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" ./configure --prefix=/usr --libdir=/usr/lib \
    --with-default-config=/etc/GNUstep/GNUstep.conf --with-config-file=/etc/GNUstep/GNUstep.conf \
    --disable-unicodeconstants --with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Run 'make' (64-bit)..."
  make
}

# check() {
#   cd $_svnname-32
#   make check
#
#   cd "$srcdir/$_svnname"
#   make check
# }

package() {
  cd "$_svnname-32"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep32.conf"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install

  # 64-bit install
  cd "$srcdir/$_svnname"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install

  msg2 "Fix permissions..."
  chown -Rc root:root "$pkgdir/"
}
