# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Arch Haskell Team <>
# Contributor: Lex Black <autumn-wind at web dot de>

pkgname=haskell-texmath
_hkgname=texmath
pkgver=0.6.4
pkgrel=1
pkgdesc="Conversion of LaTeX math formulas to MathML."
url="https://github.com/jgm/texmath"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=(haskell-containers haskell-parsec haskell-syb haskell-xml)
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
install=$pkgname.install
md5sums=('d00583d680710c816c263d198cbf6311')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/$_hkgname-$pkgver"
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
