pkgname=fpc32-lazarus
pkgver=1.0.12
pkgrel=1
pkgdesc="Lazarus Component Library for multilib"
url="http://www.lazarus.freepascal.org"
license=("GPL2" "MPL" "custom:LGPL")
arch=(x86_64)
depends=(lazarus)
makedepends=(fpc-multilib)
options=(!emptydirs !makeflags)
source=("http://downloads.sourceforge.net/project/lazarus/Lazarus%20Zip%20_%20GZip/Lazarus%20$pkgver/lazarus-$pkgver-0.tar.gz")
md5sums=('daeb68f09e9ec9939f91c992f52305d5')

build() {
  cd "$srcdir/lazarus"
  lazbuild --os=linux --ws=gtk2 --cpu=i386 lcl/lclbase.lpk
  lazbuild --os=linux --ws=gtk2 --cpu=i386 lcl/interfaces/lcl.lpk
}

package() {
  cd "$srcdir/lazarus"
  mkdir -p "$pkgdir/usr/lib/lazarus"
  find . -type f -path *i386-linux* -exec cp --parents '{}' "$pkgdir/usr/lib/lazarus/" \;
}

