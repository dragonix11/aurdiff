# Maintainer: Prurigro
# Contributor: Erik Weber <mail at erikweber dot org>

pkgname=rename
pkgver=1.3
pkgrel=6
pkgdesc="Fast command-line rename utility with string/regex find-and-replace and lowercase/uppercase change support"
url="http://rename.berlios.de"
license=('GPL2')
arch=('i686' 'x86_64')
install=$pkgname.install

source=("http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz" "${pkgname}.c.patch")
sha256sums=("02aed28f46b22213490a94b8490ee2a84b12c0962db33b4ea88c8c67a7adb8f4" "ef37f0ee8cff2ca103e7cab553177363b96cbfd05ecf2d06eb4f1e8b3f729327")

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 < ../rename.c.patch
}

build() {
    unset CPPFLAGS

    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m755 rename "${pkgdir}/usr/bin/renamexm"
    install -D -m644 rename.1 "${pkgdir}/usr/share/man/man1/renamexm.1"
}
