# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-themes
_actual_ver=13.04
_extra_ver=+13.10.20130925
pkgver=${_actual_ver}daily13.09.25
pkgrel=100
pkgdesc="Ubuntu monochrome icon theme, Abmiance and Radiance themes, and Ubuntu artwork"
arch=('any')
url="https://launchpad.net/ubuntu-themes"
license=('CCPL')
groups=('unity-extra')
depends=('humanity-icon-theme' 'gnome-icon-theme' 'hicolor-icon-theme' 'gtk-engine-unico' 'gtk-engine-murrine')
makedepends=('python2' 'imagemagick' 'icon-naming-utils')
provides=('light-themes' 'ubuntu-mono')
conflicts=('light-themes' 'ubuntu-mono')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('e5b843565b3c67d96ee614bf426cbddcae65acbe4880d1f187217b6f481b07d1dd829c0a089cb75a469f87373540bc86d09f81c7153582d63201e5be715e751c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Python 2 fix
  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Install themes
  install -dm755   "${pkgdir}/usr/share/themes/"
  cp -av Ambiance/ "${pkgdir}/usr/share/themes/"
  cp -av Radiance/ "${pkgdir}/usr/share/themes/"

  # Install icons
  install -dm755            "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-dark/  "${pkgdir}/usr/share/icons/"
  cp -av ubuntu-mono-light/ "${pkgdir}/usr/share/icons/"
  cp -av LoginIcons/        "${pkgdir}/usr/share/icons/"

  install -Dm644 distributor-logo.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/distributor-logo.png"
}

# vim:set ts=2 sw=2 et:
