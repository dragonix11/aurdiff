pkgname=lib32-libreoffice-de
pkgver=4.0.5
_pkgver=${pkgver}.2
pkgrel=1
pkgdesc="LibreOffice DE (rpm version)"
url="http://www.freedesktop.org/wiki/Software/LibreOffice"
url_aur="https://aur.archlinux.org/packages/lib32-libreoffice-de/"
arch=('x86_64')
license=('LGPL3')
depends=('lib32-gtk2' 'lib32-fontconfig' 'lib32-libxtst' 'nss' 'libxaw' 'python2' 'hicolor-icon-theme'
         'desktop-file-utils' 'kdelibs' 'lib32-libpng12' 'lib32-gtk-engines' 'lib32-libcanberra' 'lib32-libsm'
         'lib32-libidn' 'lib32-dbus-glib')
optdepends=('lib32-java' 'lib32-nss' 'lib32-xaw')
makedepends=('rpmextract')
conflicts=()
provides=()
options=('!strip')
install=libreoffice.install
_server="http://download.documentfoundation.org/libreoffice/stable/${pkgver}/rpm/x86/"
source=(${_server}/LibreOffice_${pkgver}_Linux_x86_rpm.tar.gz
        ${_server}/LibreOffice_${pkgver}_Linux_x86_rpm_langpack_de.tar.gz
        ${_server}/LibreOffice_${pkgver}_Linux_x86_rpm_helppack_de.tar.gz
        'https://github.com/JanMarvin/lib32-libreoffice4/raw/aa9e4215d38cc30f26b45cff86ca3e4bc4fde66a/libstlport_gcc.so'
        )

md5sums=('cd75d84ec9e442722c5ca1f84bfcb95f'
         '37978a869855d473e4fe0073f9c1cb8a'
         'eb64752f9e94c05c8a6968d9d07e7a1a'
         '1e2e891af4486a243bb5782b62853668')

package() {
  # normal file mask
  umask 0022

  cd $pkgdir
  msg "extract RPMs Standard (en-US)"
  for i in $srcdir/LibreOffice_${_pkgver}_Linux_x86_rpm/RPMS/*.rpm; do
    msg "extract $i"
    rpmextract.sh $i
  done
  msg "extract langpack RPMs DE"
  for i in $srcdir/LibreOffice_${_pkgver}_Linux_x86_rpm_langpack_de/RPMS/*.rpm; do
    msg "extract $i"
    rpmextract.sh $i
  done
  msg "extract helppack RPMs DE"
  for i in $srcdir/LibreOffice_${_pkgver}_Linux_x86_rpm_helppack_de/RPMS/*.rpm; do
    msg "extract $i"
    rpmextract.sh $i
  done

  # Info: I never checked this. I do not use a DE.
  msg "install desktop stuff"
  rpmextract.sh $srcdir/LibreOffice_${_pkgver}_Linux_x86_rpm/RPMS/desktop-integration/*freedesktop*.rpm

  msg "fix permissions of directories and files"
  find . -type d -exec chmod 0755 {} \;
  chmod 0644 $pkgdir/opt/libreoffice4.0/share/xdg/*.desktop

  # LibreOffice somehow forgets the location of its libs. This sed inserts a link inside
  # the soffice script.
  msg "add LD_Libraphy_PATH in program/soffice"
  sed -i 's/VALGRINDCHECK=/VALGRINDCHECK=\n#export LD_LIBRARY\nexport LD_LIBRARY_PATH=\/opt\/libreoffice4.0\/ure\/lib\//' $pkgdir/opt/libreoffice4.0/program/soffice

  msg "drop the LibreOffice libstdc++.so.6"
  # it somehow fails to work with oxygen-gtk
  # Info: I do not use oxygen-gtk so unless someone complains I expect this to be correct.
  rm -rf ${pkgdir}/opt/libreoffice4.0/ure/lib/libstdc++.so.6
  ln -s /usr/lib32/libstdc++.so.6 ${pkgdir}/opt/libreoffice4.0/ure/lib/libstdc++.so.6

  # libstlport_gcc.so was droped by LO for release 4.x still the lib is needed to use 
  # the extension Duden-Korrektor. This links a file from my github account. It was extracted
  # from a LO 3.6 release.
  msg "add old libstlport_gcc.so"
  cp ${srcdir}/libstlport_gcc.so ${pkgdir}/opt/libreoffice4.0/ure/lib/

  msg "remove old files"
  rm -rf ${srcdir}/*
}
