# Contributor:	refujee		<gmail.com: refujee>
# Contributor:	sausageandeggs	<archlinux.us: sausageandeggs>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=powder-toy
_sver=89
_mver=0
_build=274
pkgver=${_sver}.${_mver}
pkgrel=1
pkgdesc="Desktop version of the classic falling sand physics sandbox, simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua51' 'fftw' 'bzip2' 'zlib' 'gcc-libs')
makedepends=('python2' 'scons')
url="http://powdertoy.co.uk/"
license=('GPL3')
source=(http://github.com/FacialTurd/The-Powder-Toy/archive/v${_sver}.${_mver}.${_build}.tar.gz
	${pkgname}.desktop ${pkgname}.png)

build() {
  unset _xarch _ssever
  cd "${srcdir}/The-Powder-Toy-${_sver}.${_mver}.${_build}"

  #Disable the updates. I cant get the buildsystem to not compile a beta version.
  #Also I do not know the logic behind the generated snapshotids.
  sed 's|//#define I|#define I|' -i src/Config.h

  if $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="sse3"
  elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="sse2"
  else _ssever="sse"
  fi

  [[ "${CARCH}" == "x86_64" ]] && _xarch="--64bit"

  msg2 "building powder with options with following extra flags ${_xarch} --${_ssever}"
  scons --lin ${_xarch} --release  --${_ssever} --save-version=${_sver} \
	--minor-version=${_mver} --build-number=${_build} --stable=True \
	${MAKEFLAGS}

  mv build/{powder*,binary}
}

package() {
  install -Dm 755 "${srcdir}/The-Powder-Toy-${_sver}.${_mver}.${_build}/build/binary" "${pkgdir}/usr/bin/powder-toy"
  install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm 644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

md5sums=('a5a15c91986e7082d6a8104c9409cb39'
         '8901d334c53c04738cbd3518c80fa37c'
         'bb40bf9c2fa3982e2872b5d32de3b006')
