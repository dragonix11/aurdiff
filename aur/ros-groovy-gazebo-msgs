#Maintainer: Benjamin Chretien <chretien at lirmm dot fr>
pkgdesc="Message and service data structures for interacting with Gazebo from ROS."
url='http://www.ros.org/'

pkgname=ros-groovy-gazebo-msgs
pkgver=2.4.0
arch=('i686' 'x86_64')
pkgrel=1
license=('BSD')
makedepends=('cmake' 'ros-build-tools')
depends=('gazebo'
         'ros-groovy-geometry-msgs'
         'ros-groovy-message-generation'
         'ros-groovy-message-runtime'
         'ros-groovy-sensor-msgs'
         'ros-groovy-std-msgs'
         'ros-groovy-std-srvs'
         'ros-groovy-trajectory-msgs')
md5sums=('SKIP')

_name=gazebo_ros_pkgs
_dir=${_name}
source=("$_dir"::"git://github.com/ros-simulation/${_name}.git"#tag=${pkgver})

# Subdirectory that will be installed
_subdir="gazebo_msgs"

build() {
    mkdir -p "${srcdir}/build/${_subdir}"
    cd "${srcdir}/build/${_subdir}"

    /usr/share/ros-build-tools/fix-python-scripts.sh "${srcdir}/build/${_subdir}"

    cmake ${srcdir}/${_dir}/${_subdir} \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/groovy \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
    make
}

package() {
    cd "${srcdir}/build/${_subdir}"
    make DESTDIR="${pkgdir}/" install
}

