# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: AshtonBRSC <michael@ashtonbrsc.co.uk>
# Contributor: pluckypigeon <pluckypigeon@ArchForums.com>
# Contributor: canton7

_pkgname=get_iplayer
pkgname=$_pkgname-git
pkgver=640.fff8fc9
pkgrel=1
pkgdesc="Allows you to download or stream any iPlayer programme from the BBC in H.264 (Quicktime/mp4) format, any radio programmes in MP3 or RealAudio format"
arch=('any')
url="http://www.infradead.org/get_iplayer/html/get_iplayer.html"
license=('GPL3')
depends=('perl-libwww' 'perl-html-parser' 'perl-www-mechanize' 'perl-http-cookies' 'perl-net-http')
optdepends=(
  'rtmpdump: record high-quality flash-based content'
  'flvstreamer: download files that are in flash (flv) format'
  'ffmpeg: convert flash (flv) files'
  'atomicparsley: add tags to MP4 files'
  'id3v2: add basic tags to MP3 files (if perl-mp3-tag not installed)'
  'lame: re-encode Real files into MP3'
  'mplayer: download files that are in Real or WMA format'
  'perl-mp3-tag: add tags to MP3 files'
  'perl-xml-simple: Series and Brand pid parsing'
  'vlc: download files in that are n95 format')
makedepends=('git')
conflicts=($_pkgname)
provides=($_pkgname)
source=($pkgname::git://git.infradead.org/get_iplayer.git)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname
  install -m755 -D get_iplayer "$pkgdir/usr/bin/get_iplayer"
  install -m644 -D README.md "$pkgdir/usr/share/doc/get_iplayer/README.md"
  install -m644 -D get_iplayer.1 "$pkgdir/usr/share/man/man1/get_iplayer.1"

  install -m755 -d "$pkgdir/usr/share/get_iplayer/plugins"
  install -m644 plugins/*.plugin "$pkgdir/usr/share/get_iplayer/plugins/"
  install -m755 plugins/plugin.template "$pkgdir/usr/share/get_iplayer/plugins/"
}

