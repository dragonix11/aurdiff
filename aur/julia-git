# Maintainer: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# With contributions from many kind people at http://aur.archlinux.org/packages.php?ID=56877

_pkgname=julia
pkgname=julia-git
pkgver=0.2.0.rc2.101.g1e96e2b
pkgrel=1
pkgdesc="Julia is a high-level, high-performance, dynamic programming language."
arch=('i686' 'x86_64')
url="http://julialang.org"
license=('GPL')
depends=('arpack' 'fftw' 'git' 'gmp' 'llvm' 'libunwind' 'mpfr' 'pcre' 'librmath' 'readline' 'zlib' ) # 'suitesparse'
makedepends=('gcc-fortran')
optdepends=('gnuplot: If using the Gaston Package from julia')
provides=('julia')
conflicts=('julia')
options=('!emptydirs' 'staticlibs')
source=('git://github.com/JuliaLang/julia.git'
        '0001_Makefile_Destdir.patch')
md5sums=('SKIP'
         'cf17f880c4e998aef970e3e4bfa7f183')


pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch -p1 -i $srcdir/0001_Makefile_Destdir.patch
}

build() {
  cd "$srcdir/$_pkgname"
  
  make PREFIX=/usr SYSCONFDIR=/etc \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_READLINE=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_BLAS64=0
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR=$pkgdir PREFIX=/usr SYSCONFDIR=/etc \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_READLINE=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_BLAS64=0 \
    install
    
    # Remove man-page from julia/doc (one is enough)
    rm -rv $pkgdir/usr/share/julia/doc/man
}

# vim:set ts=2 sw=2 et:
