# Maintainer: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=lib32-sdl2-hg
pkgver=7638
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2.0). (Mercurial Version) (32 bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-glibc' 'sh')
provides=('lib32-sdl2')
conflicts=('lib32-sdl2')
makedepends=('mercurial')
options=('!libtool')
_local_repo="lib32-sdl2"
source=("${_local_repo}::hg+http://hg.libsdl.org/SDL")
sha256sums=('SKIP')

pkgver()
{
	cd "${srcdir}/${_local_repo}"
	hg identify -n
}

build() 
{
	export CC='gcc -m32'
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
	
	cd "${srcdir}/${_local_repo}"
	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

package()
{
	cd "${srcdir}/${_local_repo}"
	make DESTDIR="$pkgdir/" install
	
	#already included in sdl2-hg
	rm -rf "${pkgdir}/usr/share/aclocal/" "${pkgdir}/usr/include/" "${pkgdir}/usr/bin/"
	
	cd "${srcdir}/${_local_repo}/"
	install -Dm644 "COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}