# Maintainer: Firef0x <Firef0x (at) gmail (dot) com>
# Contributor: Firef0x <Firef0x (at) gmail (dot) com>

_archurl='x64'
_pkgname=sublime_text_3

pkgname=sublime-text-dev-imfix
pkgver=3.3047
pkgrel=1
pkgdesc="Sophisticated text editor for code, markup and prose , a dry fixed for development build, which supported Fcitx input method"
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/3"
license=('custom')
depends=(libpng gtk2)
provides=('sublime-text-dev' 'sublimre-text-nightly')
conflicts=('sublime-text-dev' 'sublime-text-nightly')
options=('!strip')
install=${pkgname}.install
changelog='changelog.txt'
source=("http://c758482.r82.cf2.rackcdn.com/${_pkgname}_build_${pkgver:2}_${_archurl}.tar.bz2"
		"${_pkgname}.desktop"
		"subl3"
		"sublime_imfix.c"
		"LICENSE")
md5sums=('11da4865b27fa9ebd34c61172ed3ee5f'
		 '89594b9c6b9a8c7e6a8ce414a0e58243'
		 '70b182c165360e69dcaac167e40e2096'
		 'b9cfc9418af10b5c8b509f98f310da7f'
		 'ee96c697ef707e92077d0c55ec14922a')

[[ "${CARCH}" = i686 ]] && md5sums[0]='1ecb61c5b438aeef15a4a8f423bb7dd6'

build() {
	cd "${srcdir}"

	gcc -shared -o libsublime-imfix.so sublime_imfix.c  `pkg-config --libs --cflags gtk+-2.0` -fPIC
}

package() {
	cd "${srcdir}"

	install -dm755 "${pkgdir}/opt"
	cp --preserve=mode -r "${_pkgname}" "${pkgdir}/opt/${_pkgname}"

	cp --preserve=mode -r libsublime-imfix.so \
	${pkgdir}/opt/${_pkgname}/libsublime-imfix.so

	for res in 128x128 16x16 256x256 32x32 48x48; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
		ln -s "/opt/${_pkgname}/Icon/${res}/sublime-text.png" \
		"${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime-text.png"
	done

	install -dm755 "${pkgdir}/usr/share/applications"
	install -Dm644 ${_pkgname}.desktop \
	"${pkgdir}/usr/share/applications/${_pkgname}.desktop"

	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -dm755 "${pkgdir}/usr/bin"
	install -Dm755 subl3 "${pkgdir}/usr/bin/subl3"
}
