# Maintainer: Simo Huhtiranta <simo_huhtirantaATpp_inet_fi>  
# Contributor: Tilmann Becker <tilmann.becker@web.de> 

pkgname=scribus-svn
pkgver=18425
pkgrel=1
pkgdesc="A desktop publishing program - Version from SVN"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scribus.net"
install=${pkgname}.install
depends=('qt5-webkit'     \
  'hunspell'              \
  'podofo'                \
  'python2'               \
  'graphicsmagick'        \
  'shared-mime-info'      \
  'cairo' 'poppler' \
  'desktop-file-utils'
  )
makedepends=('subversion' 'cmake' 'qt5-tools')
conflicts=('scribus')
provides=('scribus')
options=('!emptydirs')
source=('scribus::svn://scribus.net/trunk')
md5sums=('SKIP')
_svnmod='scribus'

pkgver() {
  cd $srcdir/${_svnmod}
  svnversion
}

build() {
  cd $srcdir/$_svnmod/Scribus
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DWANT_SYSTEM_CAIRO=1 -DCMAKE_SKIP_RPATH:BOOL=YES \
    -DWANT_GRAPHICSMAGICK=1 -DCMAKE_INCLUDE_PATH=/usr/include/python2.7 \
    -DUSE_QT5=1 -DQT_PREFIX="/usr"
  make
}

package () {
  cd $srcdir/$_svnmod/Scribus
  make DESTDIR=$pkgdir install
  for _i in $pkgdir/usr/share/scribus/{scripts,samples}/*.py
  do
    sed -i '1s+python+python2+' ${_i}
  done
  install -Dm644 scribus.desktop $pkgdir/usr/share/applications/scribus.desktop
}


