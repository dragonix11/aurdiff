pkgname=fpc-x86_64-win64-lnet
pkgver=0.6.5
pkgrel=1
pkgdesc="lNet or Lightweight Networking Library is a collection of classes and components to enable event-driven TCP or UDP networking. (x86_64-win64)"
url="http://wiki.freepascal.org/lNet"
license=("LGPL")
arch=(any)
makedepends=(fpc dos2unix mingw-w64-binutils)
depends=(fpc-x86_64-win64-rtl)
options=(!strip)
source=("http://downloads.sourceforge.net/lazarus-ccr/lnet-$pkgver.zip")
sha1sums=('829aef9af514aa30aa1b25c94a04fed5ec5722d6')
_unittgt=x86_64-win64
_fpcver=`fpc -iV`

prepare() {
	cd "${srcdir}/lnet/lib"
	find . -type f -exec dos2unix {} \;
}

build() {
  cd "${srcdir}/lnet/lib"
  for file in `ls *.pp`
  do
    fpc -O3 -Xs -XX -Isys -Sm -Twin64 $file
  done
  for file in `ls *.pas`
  do
    fpc -O3 -Xs -XX -Isys -Sm -Twin64 $file
  done
}

package() {
  cd "${srcdir}/lnet/lib"
  find . -name '*.o' -o -name '*.ppu' -o -name '*.rst' -o -name '*.a'|
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/fpc/$_fpcver/units/$_unittgt/lnet/"{}
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 x86_64-w64-mingw32-strip -g
}
