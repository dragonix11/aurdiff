# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=switchboard-plug-elementary-tweaks-bzr
pkgver=148
pkgrel=1
pkgdesc='Elementary Tweaks plug for Switchboard'
arch=('i686' 'x86_64')
url="https://code.launchpad.net/elementary-community/elementary-tweaks"
license=('GPL3')
depends=('libpantheon-bzr' 'gconf' 'switchboard')
makedepends=('bzr' 'cmake' 'vala')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("bzr+http://bazaar.launchpad.net/~versable/elementary-community/elementary-tweaks/")
sha256sums=('SKIP')

pkgver() {
  cd elementary-tweaks

  bzr revno
}

build() {
  cd elementary-tweaks

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd elementary-tweaks/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
