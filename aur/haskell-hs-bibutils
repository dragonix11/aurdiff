# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Ivy Foster <joyfulgirl (at) archlinux (dot) # us>
# Contributor: Alexander Dunlap <alexander.dunlap at gmail # dot com>

_hkgname=hs-bibutils
pkgname=haskell-hs-bibutils
pkgver=5.0
pkgrel=2
pkgdesc="Haskell bindings to bibutils, the bibliography conversion utilities."
url="http://hackage.haskell.org/package/$_hkgname"
license=('GPL')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-syb>=0.3' 'bibutils')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz)
install=$pkgname.install
md5sums=('611e02005ad8bbb69218ebf15f3587c6')

build() {
  cd "$_hkgname-$pkgver"
  runhaskell Setup configure -O \
    --enable-split-objs \
    --enable-shared \
    --prefix=/usr \
    --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid \
    --enable-library-profiling
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r \
    -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" \
    unregister.sh
}

package() {
  cd "$_hkgname-$pkgver"
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755  "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
}
