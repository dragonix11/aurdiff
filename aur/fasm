# Maintainer: Krzysztof Wloch <wloszekk [at] gmail [dot] com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>
pkgname=fasm
pkgver=1.71.14
pkgrel=1
pkgdesc="A fast and small assembler for x86 and x86-64"
url="http://flatassembler.net/"
license=('custom')
depends=('gcc')

if test "$CARCH" == x86_64; then
    depends=('gcc-multilib' 'libtool-multilib')
fi

arch=('i686' 'x86_64')
source=("http://flatassembler.net/fasm-$pkgver.tgz")
options=('!strip')

md5sums=('01c71f27d07bbae0c972509ca534e11e')

build() {
  cd "$srcdir/fasm"

  #tools
  local s
  for s in listing prepsrc symbols; do
    ./fasm tools/libc/$s.asm $s.o
    gcc -m32 -o $s $s.o
  done
}

package() {
  cd "$srcdir/fasm"
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/$pkgname
  install -d $pkgdir/usr/share/licenses/$pkgname
  #echo install -m 755 -t $pkgdir/usr/bin/ fasm
  install -m 755 -t $pkgdir/usr/bin/ fasm
  
  #tools
  local s
  for s in listing prepsrc symbols; do
    install -D $s $pkgdir/usr/bin/fasm-$s
  done
  
  #license
  install -m 644 -t $pkgdir/usr/share/licenses/$pkgname/ license.txt
  cp -R fasm.txt whatsnew.txt examples tools source $pkgdir/usr/share/$pkgname/
}
