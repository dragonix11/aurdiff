# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributors: Ner0 Lari Tikkanen, oke3, Flamelab, WAntilles

pkgname=smplayer-qt5-svn
pkgver=5812
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('i686' 'x86_64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('mplayer' 'qt5-base')
makedepends=('subversion')
optdepends=('smtube-svn: For watching YouTube videos')
provides=('smplayer')
conflicts=('smplayer')
install=smplayer.install
source=('smplayer::svn+https://subversion.assembla.com/svn/smplayer/smplayer/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/smplayer"
  svnversion
}

prepare() {
  cd "$srcdir/smplayer"
  sed -i 's:#\(DEFINES += NO_DEBUG_ON_CONSOLE\):\1:' src/smplayer.pro
}

build() {
  cd "$srcdir/smplayer"
  make PREFIX=/usr
}

package() {
  cd "$srcdir/smplayer"
  make PREFIX="$pkgdir/usr" install
}
