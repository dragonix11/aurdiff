# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Maintainer: Jonas Heinrich <onny@project-insnaity.org>

pkgname=gitlab-shell
pkgver=1.7.4
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=(any)
url="http://gitlab.org/"
license=('MIT')
depends=('redis' 'ruby' 'git')
options=('!strip')

install=gitlab-shell.install
backup=(etc/gitlab/shell.yml)
source=($pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v$pkgver.tar.gz)
sha512sums=('5d0d511c993ba07b26fddbd7009450ae0b73df851750ab0483f15c64c9558655ca0b85e1d6ef31ae8444fa76b84cf4e72f19c509bf1a1b51331dbf72435b56d2')

package() {
	local datadir=/usr/share/webapps/gitlab
	local homedir=/var/lib/gitlab
	
	install -d \
	  	"${pkgdir}${datadir}/gitlab-shell" \
		"${pkgdir}${homedir}"

	cd "$srcdir/$pkgname-$pkgver"
	install -dm755 "$pkgdir$homedir/gitlab-shell" "$pkgdir/etc/gitlab"
	
	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|$datadir/repositories|" \
		-e "s|/home/git|$homedir|" \
		config.yml.example > "${pkgdir}/etc/gitlab/shell.yml"
	ln -s /etc/gitlab/shell.yml "${pkgdir}${datadir}/gitlab-shell/config.yml"
	
	cp -a VERSION bin hooks lib spec support "${pkgdir}${datadir}/gitlab-shell"
	
	install -dm700 "$pkgdir$homedir/.ssh"
	touch "$pkgdir$homedir/.ssh/authorized_keys"
	
	install -dm770 "$pkgdir$datadir/"{repositories,satellites}
	chmod g+s "$pkgdir$datadir/repositories"
	ln -s "$datadir/repositories" "$pkgdir$homedir/repositories"
	ln -s "$datadir/satellites" "$pkgdir$homedir/satellites"
	
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
