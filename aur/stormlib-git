# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname=stormlib-git
pkgver=v8.21.29.g24f9faf
pkgrel=1
pkgdesc="A C/C++ API to read and write MPQ files with support for merged archives, patch MPQs and more."
arch=('i686' 'x86_64')
url="http://www.zezula.net/en/mpq/stormlib.html"
license=('CC0')
depends=('bzip2' 'zlib')
makedepends=('git' 'cmake')
provides=('stormlib')
conflicts=('stormlib')
source=("git://github.com/stormlib/StormLib.git")
sha256sums=('SKIP')
_gitname="StormLib"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_gitname"
	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
