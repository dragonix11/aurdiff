# Maintainer: Jens Staal <staal1978@gmail.com>

pkgname=ubase-git
pkgver=0.r124.g040ec40
pkgrel=1
pkgdesc="An extension of the sbase *nix core utilities - git checkout"
arch=('i686' 'x86_64')
license=('MIT')
url="http://git.suckless.org/ubase/"
depends=('sbase-git')
makedepends=('git')
source=('git://git.suckless.org/ubase')
sha256sums=('SKIP')

pkgver() {
	cd $srcdir/ubase/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd $srcdir/ubase/
	#temporary hacks
	sed -i 's|MAN = $(SRC:.c=.1)|MAN = chvt.1 |g' Makefile
}

build() {
	cd $srcdir/ubase/
	make
}

package() {
	cd $srcdir/ubase/
  
	make DESTDIR=${pkgdir} PREFIX=/opt/sbase MANPREFIX=/opt/sbase/man install 
	install -D -m0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}