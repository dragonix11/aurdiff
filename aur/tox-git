# Maintainer: Boohbah <boohbah at gmail.com> 

pkgname=tox-git
_gitname=ProjectTox-Core
pkgver=1246.947d62d
pkgrel=1
pkgdesc="A FOSS instant messaging application aimed to replace Skype (git version)"
arch=('i686' 'x86_64')
url="https://github.com/irungentoo/ProjectTox-Core"
license=('GPL')
depends=('libsodium-git' 'libconfig>=1.4')
makedepends=('git')
source=('git://github.com/irungentoo/ProjectTox-Core.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  autoreconf -fi
  ./configure --prefix="/usr"
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/tox/COPYING"
}
