# Libguestfs: Installer: Arch
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Nikos Skalkotos <skalkoto (at) Gmail.com>
# 
# special thanks go to Evaggelos Balaskas < ebalaskas _AT_ ebalaskas _DOT_ gr >
# for all the help

pkgname=libguestfs
pkgver=1.24.0
pkgver_short=${pkgver%.*}
pkgrel=1
pkgdesc="Access and modify virtual machine disk image"
arch=("i686" "x86_64")
url="http://libguestfs.org"
license=("GPL2" "LGPL2.1")
backup=("etc/libguestfs-tools.conf")
# backup=("etc/libguestfs-tools.conf" "etc/php.d/guestfs_php.ini")
install="appliance.install"
depends=("qemu"
#         "febootstrap>=3.21"
         "augeas"
         "hivex>=1.3.2"
         "libconfig"
         "libvirt"
         "cpio"
	 "wget")
makedepends=("qemu"
             "pcre"
             "fuse"
             "cdrkit"
             "libvirt"
             "libconfig"
             "libxml2"
             "gperf"
             "python2"
             "perl"
             "perl-string-shellquote"
             "perl-libintl-perl"
             "perl-sys-virt"
#             "lua"
#             "ghc"
#             "ruby"
#             "erlang"
#             "gjs"
#             "php"
#             "gobject-introspection"
             "ocaml-findlib"
	     "ocaml")
optdepends=("python2: Python bindings"
#            "ruby: Ruby Bindings"
#            "gjs: Javascript Bindings for GNOME"
#            "php: PHP bindings"
#            "erlang: Erlang Bindings"
#            "ghc: Haskell Bindings"
#            "lua: Lua Bindings"
            "perl: Perl Bindings"
            "perl-sys-virt: Sys-Virt tools"
	    "ocaml: Ocaml libs")
provides=("guestfish=${pkgver}")
options=()
source=("http://libguestfs.org/download/${pkgver_short}-stable/${pkgname}-${pkgver}.tar.gz"
        "update-libguestfs-appliance")
sha512sums=('e19a257ba049d038e322f6a53866b448262e48841abe54b8bd36dd94af894b8d4ef61fede6b35e085d81c702b00526d2ef1388b0a4170c780d5d7da2e0ae5ea2'
'1d3518c587f9e14f768ef9a10fb46adf98d0f96f7043c64fdabbc145a8d0bbca961c5fe494e6b866900633717e575563d8eef0e249bb4f83ff1a2c0150af73ce')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Currently lua, erlang, php, haskel, ruby, ghc, GObject and java bindings are
# disabled. If you want to create any of the aforementioned language bindings
# uncomment the appropriate line in makedepends and remove the --disable-*
# option in ./configure

  ./configure \
    PYTHON=python2 \
    --prefix=/usr \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --disable-probes \
    --disable-appliance \
    --disable-daemon \
    --without-java \
    --enable-introspection=no \
    --disable-lua \
    --disable-erlang \
    --disable-php \
    --disable-haskell \
    --disable-ruby \
    --disable-gobject

  make -j3
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/usr/lib/guestfs"
  install -Dm755 "${srcdir}/update-libguestfs-appliance" "$pkgdir/usr/bin/update-libguestfs-appliance"
}
