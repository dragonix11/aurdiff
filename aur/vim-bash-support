# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=vim-bash-support
pkgver=4.0
_srcid=20562
pkgrel=1
pkgdesc='BASH-IDE - Write and run BASH-scripts using menus and hotkeys. '
arch=('any')
url='http://vim.sourceforge.net/scripts/script.php?script_id=365'
license=('custom:vim')
depends=('vim-vim-support>=2.1')
optdepends=('bashdb: bash debugger')
install='vimdoc.install'
source=("${pkgname}-${pkgver}.zip::http://www.vim.org/scripts/download_script.php?src_id=$_srcid")
noextract=("${pkgname}-${pkgver}.zip")
sha256sums=('117a38cd36288e18f940b2f51ddd5fc89bb500b92340e3b1bde58f80d76f8a51')

package() {
  _DESTDIR="$pkgdir"/usr/share/vim/vimfiles

  install -dm755 "${_DESTDIR}"

  bsdtar -xf ${pkgname}-${pkgver}.zip -C "${_DESTDIR}" \
    --exclude "autoload/mmtemplates/core.vim" \
    --exclude "doc/templatesupport.txt" \
    --exclude "syntax/template.vim"

  # Fix perms
  find "${_DESTDIR}" -type d -print0 | xargs -0 chmod 0755
  find "${_DESTDIR}" -type f -print0 | xargs -0 chmod 0644
}
