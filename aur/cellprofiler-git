# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=cellprofiler-git
pkgver=10914.7a59aaf
pkgrel=1
pkgdesc="Analysis software for cellular images"
arch=('i686' 'x86_64')
url="http://www.cellprofiler.org/"
license=('GPL')
depends=('python2-libtiff-svn' 'python2-numpy' 'python2-scipy' 'wxpython' 'python2-decorator' 'mysql-python' 'python2-matplotlib' 'cython2' 'python2-imaging')
makedepends=('git' 'python2-pytz' 'python2-nose' 'python2-h5py')
optdepends=('ilastik' 'python2-nuageux' 'python2-pytz' 'python2-nose' 'python2-h5py')
provides=('cellprofiler')
conflicts=('cellprofiler')
source=($pkgname::git+https://github.com/CellProfiler/CellProfiler.git
        cellprofiler.sh
        cp.patch)
md5sums=('SKIP'
         '47ec4b5ab623e48b9feb802ce3d2f6d1'
         '37c1361f69a319afadeb665023dc4106')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$pkgname"
  patch -p0 --strip 1 < ../cp.patch
  if [ $CARCH == 'i686' ]; then
    sed -i "s/'amd64'/'i386'/" cellprofiler/utilities/setup.py
  fi
}

build() {
  cd "$srcdir/$pkgname"

  cython2 cellprofiler/utilities/javabridge.pyx
  python2 setup.py build
  python2 CellProfiler.py --build-and-exit
}

package() {
  pydir=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`
  mkdir -p "$pkgdir/$pydir"
  cp -r "$srcdir/$pkgname" "$pkgdir/$pydir/cellprofiler"

  install -Dm755 "$srcdir/cellprofiler.sh" "$pkgdir/usr/bin/cellprofiler"

  cd "$pkgdir/$pydir/cellprofiler"
  rm -rf `find "$pkgdir" -name "tests" -type d` windows_setup.py
  python2 -m compileall -x tutorial "$pkgdir/$pydir/cellprofiler/"
}

