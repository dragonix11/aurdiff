# Original Author: Henner Zeller <h.zeller at acm dot org>
# Maintainer: Drew DeVore <w.drew.devore at gmail dot com>

pkgname=gmrender-resurrect-git
_gitname=gmrender-resurrect
pkgver=20130725
pkgrel=1
pkgdesc="Application to stream music from a UPnP server using gstreamer."
arch=('any')
url="https://github.com/hzeller/gmrender-resurrect"
license=('GPL2')
conflicts=('gmediarender')
depends=('gnome-icon-theme' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ugly-plugins')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'libxml2' 'libupnp')
install='gmrender-resurrect.install'
source=('git+https://github.com/hzeller/gmrender-resurrect.git'
'gmediarender.service'
'gmediarender')
md5sums=('SKIP'
'd997492bfb6ccd46018a20ee20b79a39'
'979798ff9cac610930f13fb922ca95d4'
)


pkgver() {
	cd $_gitname
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd $_gitname
	./autogen.sh
	./configure
	make
}

package() {
	cd $_gitname
	make DESTDIR=$pkgdir install
	install -D -m 644 $srcdir/gmediarender.service "$pkgdir/usr/lib/systemd/system/gmediarender.service"
	install -D -m 644 $srcdir/gmediarender "$pkgdir/etc/conf.d/gmediarender"
}
