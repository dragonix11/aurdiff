# Contributor: megadriver <megadriver at gmx dot com>
# Based on gnumeric from [extra]

pkgname=gnumeric-minimal
_pkgname=gnumeric
pkgver=1.12.5
pkgrel=1
pkgdesc="A GNOME-less spreadsheet program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice-light' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
install=gnumeric-minimal.install
conflicts=('gnumeric')
provides=('gnumeric')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz
        revert-warnings.patch)
sha256sums=('7ff36dff134157ce6919c4cebc1a419192e529b00e42c52d9cebbd15e8dd5871'
            'bcafca016b809000c2a5bf911e2e3dfa4de28f9e541d9964574cac5c7ce09e53')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i ../revert-warnings.patch

  # Remove invalid mime type. (FS#26338)
  sed -i -e 's/zz-application\/zz-winassoc-xls;//' gnumeric.desktop.in

  autoreconf -fi
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install --disable-ssindex \
              --without-gnome --without-psiconv --without-perl \
              --without-python --without-gda
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/etc/gconf"
  rm -rf "${pkgdir}/usr/share/gnome"
}
