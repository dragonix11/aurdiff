# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=luma-git
pkgver=0.r1615.g90ad5cc
pkgrel=2
pkgdesc="A feature-full LDAP browser/editor using PyQT - git checkout"
conflicts=('luma' 'luma-svn')
provides=('luma')
url="http://luma.sourceforge.net/"
license=("GPL")
depends=('python2-ldap' 'python2-pyqt')
makedepends=('git')
arch=('i686' 'x86_64')
source=('git://git.code.sf.net/p/luma/luma')

pkgver() {
	cd luma/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd luma/

	mkdir ${pkgdir}/usr
	python2 setup.py install --prefix=${pkgdir}/usr
}

sha256sums=('SKIP')
