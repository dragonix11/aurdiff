# Author: Christoph Jaeger <christophjaeger@linux.com>
# Maintener: Marc Rozanc <marc@rozanc.fr>

pkgbase="clevo-wmi"
_modname="clevo-wmi"
pkgname="${pkgbase}-dkms"
pkgver="0.9.6"
pkgrel=1
pkgdesc='A reverse engineering driver for the Clevo P150EM/P170EM/P150SM/P157SM/P170SM/P177SM backlight keyboard, by Christoph Jaegeri (DKMS version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.linux-onlineshop.de/forum/index.php?page=Thread&threadID=26&s=82130b57e71bd5b6ea569abc1424025c6f8d412d'
depends=('dkms' 'linux-headers')
provides=("clevo_wmi")
conflicts=("clevo_wmi-git" "clevo-wmi")
source=("clevo-wmi-${pkgver}.src.tar.gz::http://www.linux-onlineshop.de/forum/index.php?page=Attachment&attachmentID=71&h=03137ea150d545c2f30e1bfd3bc36a166a2bb653"
        "dkms.conf.in")
md5sums=('b4c4046799dcf4497b75b794659fcae3'
         '885768a16582199937509d456b3475af')
install='clevo-wmi-dkms.install'


build() {
    cd "${srcdir}/clevo-wmi-${pkgver}/src"
    make
}

package() {
    cd "${srcdir}/clevo-wmi-${pkgver}/src"

    install -dm755 "${pkgdir}/usr/src/${_modname}-${pkgver}/"
    for i in "${srcdir}/${_modname}-${pkgver}/src/"{Makefile,clevo-wmi.c}; do
        install -D -m644 "${i}" "${pkgdir}/usr/src/${_modname}-${pkgver}/"
    done
    sed "s/#MODULE_VERSION#/${pkgver}/" "${srcdir}/dkms.conf.in" > "${pkgdir}/usr/src/${_modname}-${pkgver}/dkms.conf"
}

