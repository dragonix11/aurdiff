# Maintainer: PelPix <kylebloss[at]pelpix.info>
# Contributor: DrZaius <lou[at]fakeoutdoorsman.com>

pkgname=libfdk-aac-git
_gitname="libfdk-aac"
pkgver=v0.1.1.7.g226601a
pkgrel=3
pkgdesc="Fraunhofer FDK AAC codec library"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/opencore-amr/"
license=('custom')
depends=('glibc')
provides=('libfdk-aac')
makedepends=('git')
conflicts=('libfdk-aac')
options=('!libtool')
source=('libfdk-aac::git://github.com/mstorsjo/fdk-aac.git')
sha1sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$_gitname"
  autoreconf -fiv
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir" install
  install -Dm644 NOTICE "$pkgdir/usr/share/licenses/$pkgname/NOTICE"
}
