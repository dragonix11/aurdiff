# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: St√©phane Marguet (Stemp) <smarguet@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=eclipse-gef
pkgver=3.9.0
_reldate=201305060205
pkgrel=1
pkgdesc="GEF framework for the Eclipse platform"
arch=(any)
url=http://www.eclipse.org/gef/
license=(EPL)
depends=(eclipse)
changelog=ChangeLog
_mirror="http://www.eclipse.org/downloads/download.php?r=1&file="
source=($_mirror/tools/gef/downloads/drops/$pkgver/R$_reldate/GEF-ALL-$pkgver.zip)
sha256sums=('7861f6f573808251a468d4b05b87f0fab03e728786e886c4b627303fe0333f23')
sha512sums=('ac700e689ecad04c0659c4091b7e456a3ae0849b02bbb898fe748a2c9c5560193b7a13fa2685c91e56255d251350d8df72ea3a7d72d6e038f1d9950f3e9d3054')

package() {
    _dest="$pkgdir"/usr/share/eclipse/dropins/${pkgname#*-}/eclipse
    
    cd eclipse/
    # Features
    find features -type f | while read _feature ; do
        if [[ $_feature =~ (.*\.jar$) ]] ; then
            install -d "${_dest}"/${_feature%*.jar}
            cd "$_dest"/${_feature/.jar}
            jar xf "${srcdir}/${_feature}"
        else
            install -Dm644 $_feature "$_dest/$_feature"
        fi
    done

    # Plugins
    find plugins -type f -exec install -Dm644 '{}' "$_dest"/'{}' \;
}
