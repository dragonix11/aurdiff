# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Dennis Craven <dcraven@gmail.com>
# Contributor: CRT <crt.011@gmail.com>

pkgname=cursetheweather
pkgver=0.6.1
pkgrel=1
pkgdesc="An ncurses client for retrieving weather forecasts"
arch=('any')
url="https://github.com/tdy/ctw/"
license=('GPL')
depends=('ncurses' 'python3')
source=($pkgname-$pkgver.tgz::https://github.com/tdy/ctw/archive/v$pkgver.tar.gz)
sha256sums=('1a134768b418f7c883bdcda98da90e1842b693b458f3793a54bfa5099b188d06')

build() {
  cd "$srcdir/ctw-$pkgver"
  python3 setup.py build
}

package() {
  cd "$srcdir/ctw-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  ln -sf ctw "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
