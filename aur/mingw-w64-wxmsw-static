# Maintainer: Никола Вукосављевић <hauzer@gmx.com>

pkgname=mingw-w64-wxmsw-static
pkgver=3.0.0.rc1
pkgrel=2
pkgdesc="MSW implementation of wxWidgets API for GUI (mingw-w64, static)"
arch=("i686" "x86_64")
url="http://wxwidgets.org"
license=("custom:wxWindows")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt
mingw-w64-libjpeg-turbo
mingw-w64-libpng
mingw-w64-expat
mingw-w64-libtiff)
options=(!libtool !strip !buildflags staticlibs)
conflicts=(mingw-w64-wxmsw2.9 mingw-w64-wxmsw)
provides=(mingw-w64-wxmsw2.9 mingw-w64-wxmsw)
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver%.rc1}-rc1.tar.bz2")
md5sums=('a4f74ef439b7a95d76c699b3a198097c')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}/wxWidgets-3.0.0-rc1/configure" \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --with-msw \
      --disable-mslu \
      --enable-static \
      --disable-shared \
      --enable-iniconf \
      --enable-std_string_conv_in_wxstring
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/wx"{-config,rc-3.0}
    ln -s "/usr/${_arch}/lib/wx/config/${_arch}-msw-unicode-static-3.0" "$pkgdir/usr/${_arch}/bin/wx-config"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}

