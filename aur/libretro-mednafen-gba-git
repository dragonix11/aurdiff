# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-mednafen-gba-git
pkgver=334.01fdcee
pkgrel=2
pkgdesc="libretro implementation of Mednafen VBA/GBA. (Game Boy Advance)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mednafen-libretro"
license=('GPL')
makedepends=('git')

_gitname=mednafen-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mednafen_gba_libretro.info")
md5sums=('SKIP'
         'f0e623e73cb8291fc2a8765669b3f05d')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make core=gba
}

package() {
  install -Dm644 "${_gitname}/mednafen_gba_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mednafen-gba.so"
  install -Dm644 "mednafen_gba_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mednafen-gba.info"
}
