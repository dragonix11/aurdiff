# Maintainer: Chun Yang <x@cyang.info>
pkgname=ghost
pkgver=0.3.2
pkgrel=1
pkgdesc="Free, open, simple blogging platform"
arch=('i686' 'x86_64')
url="http://ghost.org"
license=('MIT')
depends=('nodejs>0.10' 'nodejs<0.11.4')
backup=('srv/ghost/Gruntfile.js' 'srv/ghost/index.js' 'srv/ghost/package.json')
install=ghost.install
source=(https://en.ghost.org/zip/$pkgname-$pkgver.zip
        ghost.service)
sha256sums=('30681e393b1e2b5aa0f9c543b7c345b6b06b4a6365ab8463f1375000f65c1f85'
            'c1f7bc0a3b3c4aa11a68934e6e39e3e83a2d6ac7bf8591d18521df0d0a389c3c')

# Note: You may need to log into ghost.org and download the zip file manually
# and place it inside the same directory as the PKGBUILD

package() {
    install -dm755 "$pkgdir/srv/ghost"
    cd "$pkgdir/srv/ghost"

    install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/ghost/LICENSE.txt"

    install -Dm644 "$srcdir/ghost.service" "${pkgdir}/usr/lib/systemd/system/ghost.service"

    unzip "$srcdir/$pkgname-$pkgver.zip"
    npm install --production

    chmod -R g=u "$pkgdir/srv/ghost"
    find "$pkgdir/srv/ghost" -type d -exec chmod g+s {} \;

    cat <<-EOF

	Upgrading Ghost involves replacing old files with the new files, and
	restarting the server. However, as the database, image uploads and
	custom themes are stored alongside Ghost in the content directory,
	care should be taken to only replace the necessary files as explained at
	http://docs.ghost.org/installation/upgrading/ .

	It is highly recommended that you make a backup of your data before
	upgrading. This can be done by logging into your Ghost install and going
	to /ghost/debug/. Press the export button to download a JSON file
	containing all of your data.

	EOF
}
