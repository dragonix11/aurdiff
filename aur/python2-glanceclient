# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-glanceclient
pkgver=0.11.0
pkgrel=3
pkgdesc="OpenStack image service (client libs)"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-anyjson python2-crypto python2-eventlet python2-greenlet python2-httplib2
    python2-iso8601 python2-kombu python2-lxml python2-migrate python2-netaddr python2-passlib
    python2-paste python2-paste-deploy python2-routes python2-sendfile python2-sqlalchemy
    python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-setuptools)
source=(https://pypi.python.org/packages/source/${pkgname:0:1}/${pkgname//2}/${pkgname//2}-$pkgver.tar.gz)
sha256sums=('48d54b00050ada51561db09ffa455c4707b7db8853f6eacb716167a0f10bb43e')
sha512sums=('5e0c1c894abef90cf52dc11b35d637882df9f595ddaeb05716f34c9db8382d064c642260c57c079a4562a0967677304786f4ccae1a5ba67cf214d6f3bfb7da4b')

prepare() {
    find ${pkgname//2}-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py install --root="$pkgdir"
}
