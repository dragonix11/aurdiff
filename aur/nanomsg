 # Maintainer: KokaKiwi <admin@kokaelkiwi.eu>

_srcdir=nanomsg-0.1-alpha
pkgname=nanomsg
pkgver=0.1.alpha
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=('MIT')
source=(http://download.nanomsg.org/nanomsg-0.1-alpha.tar.gz)
md5sums=('f752352b36ff639906b7a68806b0c6ec')
arch=('i686' 'x86_64')
depends=('glibc')

build() {
  cd "${srcdir}/${_srcdir}"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr ..
  make
}

check() {
  cd "${srcdir}/${_srcdir}/build"
  make check
}

package() {
  cd "${srcdir}/${_srcdir}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
