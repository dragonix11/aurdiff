# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=shotcut
pkgver=131005
pkgrel=1
pkgdesc="A free, open source, cross-platform video editor."
arch=(i686 x86_64)
url="http://www.mltframework.org/bin/view/Shotcut/WebHome"
license=('GPL3')
depends=('qt5-svg' 'qt5-webkit' 'qt5-x11extras' 'mlt' 'libexif')
install="$pkgname.install"
source=("http://d1av856udzjaks.cloudfront.net/shotcut/$pkgname-src-$pkgver.tar.bz2"
        "start_shotcut.sh")
md5sums=('4a81f8f0921d95380a1502a4eee1bb2f'
         'fa0e1d7bdceb81f98918727446558d8d')

build() {
  # mlt qt5 module
  pushd src/mlt
  ./configure --prefix=/usr \
              --enable-gpl \
              --qimage-libdir=/usr/lib \
              --qimage-includedir=/usr/include/qt
  pushd src/modules/qimage
  make
  popd && popd

  # shotcut
  cd src/shotcut
  qmake PREFIX=/usr
  make
}

package() {
  # mlt qt5 module
  pushd src/mlt/src/modules
  install -Dm755 libmltqimage.so \
    "$pkgdir/opt/mlt/libmltqimage.so"

  # link all mlt modules in /usr/lib/mlt into /opt/mlt
  for _m in avformat avsync core decklink dgraft \
            dv effectv frei0r gtk2 jackrack kdenlive \
            kino linsys motion_est normalize oldfilm plus \
            resample rotoscoping rtaudio sdl sox videostab vmfx vorbis xine xml; do
    ln -s /usr/lib/mlt/libmlt$_m.so "$pkgdir/opt/mlt"
  done;

  popd

  pushd src/shotcut
  make INSTALL_ROOT="$pkgdir/" install
  popd

  # launcher
  install -Dm755 ${source[1]} \
    ${pkgdir}/usr/bin/${source[1]/.sh}
}

# vim:set ts=2 sw=2 et:
