# Maintainer: Leif Warner <abimelech@gmail.com>
pkgname=idris
pkgver=0.9.9.3
pkgrel=1
pkgdesc="Functional Programming Language with Dependent Types"
url="http://www.idris-lang.org"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-cabal' 'haskell-binary' 'haskell-bytestring' 'haskell-containers' 'haskell-directory' 'haskell-filepath' 'haskell-haskeline' 'haskell-mtl' 'haskell-parsec' 'haskell-pretty' 'haskell-process' 'haskell-transformers' 'haskell-split' 'haskell-libffi' 'haskell-language-java' 'haskell-llvm-general<3.3.9' 'haskell-vector-binary-instances' 'gc' 'haskell-trifecta>=1.1' 'haskell-ansi-terminal')
depends=('gmp' 'libffi' 'gcc')
optdepends=(
  'gc: dynamically linked from output of the llvm backend'
  'maven: downloads runtime when using Java backend'
)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('3ff7a34f24237faae87ad6c5d25a91ef')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
    runhaskell Setup build
}
package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
    rmdir ${pkgdir}/usr/share/doc/${pkgname}
}
