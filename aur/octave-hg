# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>

pkgname=octave-hg
pkgver=17839.2ff47214de72
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')
# Some of these may be optional, e.g. arpack, lapack, qhull, but if they
# are installed, octave will be linked against them.
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' 'gl2ps' 
  'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick' 'java-environment')
makedepends=('pcre' 'mercurial' 'gcc-fortran' 'gperf' 'perl' 'rsync')
# For building the documentation, epstool and fig2dev are searched during
# build. The build works without them, but you may want to install
# transfig from [extra] and epstool from AUR
optdepends=('texinfo: for help-support in octave'
  'gnuplot: alternative plotting')
conflicts=('octave' 'octave3' 'octave-suitsparse')
install=octave.install
options=('!emptydirs')
source=(hg+http://hg.savannah.gnu.org/hgweb/octave)
md5sums=('SKIP')
_hgrepo=octave
_appver=3.7.7+
provides=("octave=$_appver")

pkgver() {
  cd $srcdir/${_hgrepo}
  hg update > /dev/null
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd $srcdir/${_hgrepo}
  export LC_NUMERIC=C 
  ./bootstrap
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib --enable-shared --enable-jit \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lrt" \
    --enable-qhull --enable-java MOC=moc-qt4 UIC=uic-qt4 \
    --with-java-homedir=/usr/lib/jvm/java-7-openjdk \
    --with-java-includedir=/usr/lib/jvm/java-7-openjdk/include \
    --with-java-libdir=//usr/lib/jvm/java-7-openjdk/lib
  CXXFLAGS+="-fexceptions `llvm-config --cxxflags`" make
}

package() {
  cd $srcdir/${_hgrepo}
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_hgrepo}/${_appver}" > "${pkgdir}/etc/ld.so.conf.d/${_hgrepo}.conf"
}
