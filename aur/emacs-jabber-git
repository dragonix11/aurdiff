# Contributor: Anthony Garcia <lagg@[moltenrock]bit.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=emacs-jabber-git
pkgver=0.8.92.9.g993b1d7
pkgrel=1
pkgdesc="A Jabber client written in emacs lisp"
arch=('any')
url="http://emacs-jabber.sourceforge.net/"
license=('GPL')
depends=('emacs')
makedepends=('git')
provides=('emacs-jabber')
conflicts=('emacs-jabber')
install=$pkgname.install
source=('git://emacs-jabber.git.sourceforge.net/gitroot/emacs-jabber/emacs-jabber')
md5sums=('SKIP')
_gitname=emacs-jabber

pkgver() {
  cd $_gitname
  git describe --tags | sed 's|-|.|g'
}
  
build() {
  cd $srcdir/$_gitname
  autoreconf -i
  ./configure --prefix=/usr --with-gconf-schema-file-dir=/etc/gconf/schemas \
      --libexecdir=/usr/lib/emacs-jabber
  make 
}

package() {
  cd $srcdir/$_gitname
  make DESTDIR="$pkgdir/" install
} 
