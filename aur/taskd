# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=taskd
pkgver=1.0.0.beta1
pkgrel=1
pkgdesc="A lightweight, secure server providing multi-user, multi-client access to task data"
url='http://tasktools.org/projects/taskd.html'
license=('MIT')
arch=('i686' 'x86_64')
depends=('task' 'gnutls')
makedepends=('cmake')
source=("git://tasktools.org/taskd.git#tag=v1.0.0.beta1"
        'taskd.conf' 'taskd.service')
md5sums=('SKIP'
         'c677dde7fcd08d6f9c40434eb1c00e84'
         'e853d77d19f8b57b665b5d8dc99d87b4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/taskd.conf \
    "${pkgdir}"/etc/conf.d/taskd

  install -Dm644 "${srcdir}"/taskd.service \
    "${pkgdir}"/usr/lib/systemd/system/taskd.service

  install -d "${pkgdir}"/var/lib/taskd

  install -Dm644 ../${pkgname}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
