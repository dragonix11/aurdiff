# Author: mosra <mosra@centrum.cz>
pkgname=magnum-plugins-git
pkgver=20130819
pkgrel=1
pkgdesc="Plugins for Magnum graphics engine (Git version)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('magnum-git' 'libpng' 'libjpeg' 'freetype2')
makedepends=('cmake' 'git')
provides=('magnum-plugins')
conflicts=('magnum-plugins')

_gitroot="git://github.com/mosra/magnum-plugins.git"
_gitname="magnum-plugins"

pkgver() {
    date +%Y%m%d
}

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../$_gitname \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_PNGIMPORTER=ON \
        -DWITH_JPEGIMPORTER=ON \
        -DWITH_FREETYPEFONT=ON \
        -DWITH_WAVAUDIOIMPORTER=ON
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir/" install
}
