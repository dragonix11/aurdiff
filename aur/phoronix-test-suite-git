# Contributor: Jason Melton <jason.melton@gmail.com>
# Contributor: DuGi <dugi@irc.pl>

_pkgname=phoronix-test-suite
pkgname=phoronix-test-suite-git
pkgver=20110325
pkgrel=2
pkgdesc="The most comprehensive testing and benchmarking platform available for Linux"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.phoronix-test-suite.com/"
depends=('php')
makedepends=('git')
optdepends=('php-gtk'
            'php-curl'
            'php-gd')
conflicts=('phoronix-test-suite')
provides=('phoronix-test-suite')
install=${pkgname}.install
source=("git+http://www.phorogit.com/repo/phoronix-test-suite.git")
md5sums=('SKIP')


pkgver() {
    cd $_pkgname
    git describe --always | sed -e 's|-|.|g'
}

package() {
    cd ${srcdir}/$_pkgname
    mkdir -p ${pkgdir}/usr/bin
    ./install-sh ${pkgdir}/usr 
}

