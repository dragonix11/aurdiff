# Maintainer: WorMzy Tykasti <wormzy.tykashi@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor:  C.Coutinho <kikijump[at]gmail[dot]com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>, TDY <tdy@gmx.com>

pkgname=tmux-git
_gitname=tmux-code
pkgver=2013.10.06.f3ec869
pkgrel=1
pkgdesc="A terminal multiplexer"
url="http://tmux.sourceforge.net/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent')
makedepends=('git')
provides=('tmux')
conflicts=('tmux')
source=('git://git.code.sf.net/p/tmux/tmux-code'
        'LICENSE')
md5sums=('SKIP'
         '71601bc37fa44e4395580b321963018e')

pkgver() {
  cd "$_gitname"
  git log -1 --format="%cd.%h" --date=short | sed 's:-:.:g'
}

build() {
  cd "$_gitname"
  ./autogen.sh && ./configure
  make
}

package() {
  cd "$_gitname"
  install -Dm755 tmux "$pkgdir/usr/bin/tmux"
  install -Dm644 tmux.1 "$pkgdir/usr/share/man/man1/tmux.1"
  install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"
  install -Dm644 $srcdir/LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"
  install -dm755 "$pkgdir/usr/share/tmux/"
  install -m644 examples/* "$pkgdir/usr/share/tmux/"
}
