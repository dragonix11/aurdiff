# Maintainer: DrZaius <lou[at]fakeoutdoorsman.com>
# Contributor: zhuqin <zhuqin83[at]gmail>
# Contributor: pressh <pressh[at]gmail>

pkgname=x264-git
_gitname="x264"
pkgver=20131030
pkgrel=1
pkgdesc="H.264 video encoder and library"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc')
makedepends=('git' 'yasm')
url="http://www.videolan.org/developers/x264.html"
conflicts=("x264")
provides=("x264=$pkgver")
source=('git://git.videolan.org/x264.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd $_gitname

  ./configure --enable-shared
  make
}

package() {
  cd $_gitname

  make DESTDIR="$pkgdir" \
    bindir=/usr/bin \
    libdir=/usr/lib \
    includedir=/usr/include \
    install
}
