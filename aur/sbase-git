# Maintainer: Jens Staal <staal1978@gmail.com>
# Contributor: Sascha Biermmanns <saschakb at gmail dot com>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=sbase-git
pkgver=0.r205.gfd26256
pkgrel=1
pkgdesc="A suckless variant of the *nix core utilities - git checkout"
arch=('i686' 'x86_64')
license=('MIT')
url="http://git.suckless.org/sbase"
makedepends=('git')
source=('git://git.suckless.org/sbase')
sha256sums=('SKIP')

pkgver() {
	cd sbase/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd sbase/

	make
}

package() {
	cd sbase/
  
	make DESTDIR=${pkgdir} PREFIX=/opt/sbase MANPREFIX=/opt/sbase/man install 
	install -D -m0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}