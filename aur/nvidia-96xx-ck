# Maintainer: 3ED_0 <krzysztof1987 at gmail>
# Contributor: City-busz <AUR>
# Contributor: Maximilien Noal <noal dot maximilien at gmail dot com>
# Contributor: graysky <graysky AT archlnux.us>

pkgname=nvidia-96xx-ck
pkgver=96.43.23
pkgrel=15
_kver_ge="3.10"
_kver_lt="3.12"
pkgdesc="NVIDIA drivers, 96xx branch, for linux-ck."
url="http://www.nvidia.com/"
arch=('i686' 'x86_64')
depends=("linux-ck>=${_kver_ge}"
         "linux-ck<${_kver_lt}"
         "nvidia-96xx-utils")
makedepends=("linux-ck-headers>=${_kver_ge}"
             "linux-ck-headers<${_kver_lt}")
optdepends=('xorg-server1.12: latest compatible Xorg server')
provides=('nvidia-96xx')
conflicts=('nvidia'
           'nvidia-all'
           'nvidia-96xx-all'
           'nvidia-173xx-all'
           'nvidia-173xx'
           'nvidia-275xx'
           'nvidia-304xx'
           'nvidia-beta-all'
           'nvidia-beta')
license=('custom')
install='nvidia.install'
options=(!strip)

if [ "$CARCH" = "x86_64" ]; then
  _arch=x86_64
  _npkg=2
else
  _arch=x86
  _npkg=0
fi

source=("http://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}.run"
        "173.14.36-37.patch"
        "linux-3.10.patch"
        "linux-3.11.patch")

sha256sums=('51e6adf4ba73071622045fc37169f80db3ae3e784dff53b29d35146a04042142'
            'c8900d09528589f8f519aa424447e31e3e9a0ca4866fe49c37ac4ed4320a6728'
            '07103607296cbe8f365689fd2607f18393c5aca7539c1ae8fc2c893b8b39f465'
            '9726e9c193d8e4bc187447ec3c25fda5b4142db6b357c14576c1d3ffb6996bbf')

[ "$CARCH" = "x86_64" ] && sha256sums[0]='ce8d8253e7dab7da63fbdac449ebf190cd0c10cbfffea0f8d0165515f851bc64'

_extramodules=$(echo /usr/lib/modules/extramodules-[0-9.]*-ck/version|cut -d/ -f5)
_kernver=$(cat /usr/lib/modules/${_extramodules}/version)

prepare() {
  test ! -d NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg} \
    || rm -rf NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}

  sh NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}.run --extract-only

  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}/usr/src/nv
  patch -p1 -i "$srcdir"/173.14.36-37.patch
  patch -p4 -i "$srcdir"/linux-3.10.patch
  patch -p1 -i "$srcdir"/linux-3.11.patch
}

build() {
  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}/usr/src/nv
  make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
  cd NVIDIA-Linux-${_arch}-${pkgver}-pkg${_npkg}
  install -dm755 "$pkgdir/usr/share/licenses/${pkgname}/"
  install  -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/"

  cd usr/src/nv
  install -dm755 "$pkgdir/usr/lib/modules/${_extramodules}/"
  install  -m644 nvidia.ko "$pkgdir/usr/lib/modules/${_extramodules}/"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

  install -dm755 "$pkgdir/etc/modprobe.d"

  sed -i -e "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "$startdir/nvidia.install"
}
