# Maintainer: jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=at-spi2-core
pkgname=lib32-$_pkgbasename
pkgver=2.10.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
arch=(x86_64)
url="http://www.gnome.org"
license=('GPL2')
depends=(lib32-dbus lib32-glib2 lib32-libxtst $_pkgbasename)
makedepends=(gcc-multilib intltool) # 'gobject-introspection'
options=(!libtool)
source=(http://download.gnome.org/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('349b7ad5e6a8eebbf88aaefcb67a0f49b025dbcdf6ad646978faf2ca84650119')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbasename-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32/at-spi2-core \
    --enable-introspection=no \
    --disable-xevie
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}
