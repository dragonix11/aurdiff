# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs
#
# Contributor: Vasco Nunes <vascomfnunes at gmail dot com>

_pkgname="birdie"
pkgname="${_pkgname}-bzr"
pkgver=307
pkgrel=1
pkgdesc="Twitter client for elementary OS"
arch=('i686' 'x86_64')
url="http://launchpad.net/birdie"
license=('GPL3')
depends=('glib2' 'gtk3' 'libgee06' 'libwnck3' 'libx11' 'hicolor-icon-theme' 'cairo' 'pango' )
makedepends=('bzr' 'libpurple' 'dbus-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'libdbusmenu' 'intltool' 'vala' 'cmake' 'rest' 'json-glib' 'libnotify' 'libwnck3' 'libcanberra' 'webkitgtk3' 'sqlite' 'libxtst')
options=('!libtool')
conflicts=('birdie')
provides=('birdie')
install="${_pkgname}.install"
source=("${_pkgname}::bzr+http://bazaar.launchpad.net/~birdie-team/birdie/trunk/")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    bzr revno
}

build() {
    cd "${_pkgname}"
    cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DGSETTINGS_COMPILE=OFF 
    make ${MAKEFLAGS}
}

package() {
    cd "${_pkgname}"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install
}
