# Maintainer: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=wineasio
pkgver=0.9.2
pkgrel=3
pkgdesc="Steinberg ASIO driver implementation for Wine"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/wineasio/"
license=('LGPL')

if [[ ${CARCH} == i686 ]]; then
  depends=('ed' 'jack' 'wine')
else
  depends=('ed' 'jack' 'lib32-jack' 'wine' 'gcc-multilib')
fi    
makedepends=('steinberg-asio')

install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('aef9c22557f37c4bee393aab0232c63b')

prepare() {
  cd "${srcdir}/$pkgname"
  cp /usr/include/steinberg-asio/asio.h asio.h
  ./prepare_64bit_asio
}

build() {
  cd "${srcdir}/${pkgname}"

  if [[ "${CARCH}" = 'i686' ]]; then
    ed -s Makefile <<< $'g/lib32/s//lib/g\nw\nq'
    make
  else
    make
    mv ${pkgname}.dll.so ${pkgname}.dll.so.32
    make clean
    make -f Makefile64
  fi
}

package() {
  cd "${srcdir}/${pkgname}"

  if [[ "${CARCH}" = 'i686' ]]; then
    install -Dm755 "${pkgname}.dll.so" "${pkgdir}/usr/lib/wine/${pkgname}.dll.so"
  else
    install -Dm755 "${pkgname}.dll.so.32" "${pkgdir}/usr/lib32/wine/${pkgname}.dll.so"
    install -Dm755 "${pkgname}.dll.so" "${pkgdir}/usr/lib/wine/${pkgname}.dll.so"
  fi
}
# vim:set ts=2 sw=2 et:
