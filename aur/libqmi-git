# Contributor: Somebody <somebody[at]foo[dot]tld>
# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=libqmi
pkgname=$_pkgname-git
pkgver=1.4.0.82.gea008ff
pkgrel=1
_realver=1.7.0
pkgdesc="QMI modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libqmi/"
license=('GPL2')
depends=('glib2' 'bash')
makedepends=('git' 'gtk-doc' 'python')
options=('!libtool')
conflicts=('libqmi')
replaces=('libqmi')
provides=("libqmi=$_realver")
source=('git://anongit.freedesktop.org/libqmi')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe | sed 's/^v//;s/-/./g;s/_/./g;'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests --enable-gtk-doc
  make
}

check() {
  cd "$srcdir/$_pkgname"
  make check
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
