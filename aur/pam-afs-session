# Contributor: Ryan Corder <ryanc@greengrey.org>
# Contributor: Lee Burton <lburton@mrow.org>
# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=pam-afs-session
pkgver=2.5
pkgrel=1
pkgdesc="PAM module providing AFS session support"
url="http://www.eyrie.org/~eagle/software/pam-afs-session/"
arch=('i686' 'x86_64')
license=("custom")
depends=('krb5' 'pam')
source=("http://archives.eyrie.org/software/afs/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/afs/$pkgname-$pkgver.tar.gz.asc")
sha1sums=('5b2ee386b840ccecd38888230a69fe986a4150a9'
          'a3beaa312e13c12dde1a5df0a8350d54aa175c98')
options=(!libtool)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2:sw=2:et:ft=sh
