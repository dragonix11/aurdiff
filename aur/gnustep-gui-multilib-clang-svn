# Maintainer: X0rg

pkgname=gnustep-gui-multilib-clang-svn
_svnname=gnustep-gui
pkgver=37220
pkgrel=1
pkgdesc="The GNUstep GUI class library for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('GPL3' 'LGPL3')
groups=('gnustep-multilib-clang-svn')
depends=('libtiff'       'libjpeg-turbo'       'libpng'       'giflib'       'libcups'       'aspell' \
         'lib32-libtiff' 'lib32-libjpeg-turbo' 'lib32-libpng' 'lib32-giflib' 'lib32-libcups' 'lib32-aspell' 'gnustep-base-multilib-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-multilib-clang-svn' 'gnustep-base-multilib-clang-svn' 'audiofile' 'lib32-audiofile' 'portaudio>=19' 'lib32-portaudio>=19')
conflicts=('gnustep-gui' 'gnustep-gui-svn' 'gnustep-gui-clang-svn' 'lib32-gnustep-gui-clang-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/gui/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Patch 'NSBitmapImageRep+PNG.m' file..."
  sed -i 's|png_sizeof|sizeof|g' Source/NSBitmapImageRep+PNG.m
}

build() {
  cd "$_svnname"
  source "/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep32.conf"

  msg2 "Run 'configure' (32-bit)..."
  OBJCFLAGS="-fblocks" CC="clang -m32" CXX="clang++ -m32" LDFLAGS="-L/usr/share/GNUstep32/lib32 -L/usr/lib32" ./configure --prefix=/usr --libdir=/usr/lib32 --enable-libgif

  msg2 "Run 'make' (32-bit)..."
  make
}

_build64() {
  source "/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
  source "/usr/share/GNUstep/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config"
  export GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf"

  msg2 "Run 'configure' (64-bit)..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="-L/usr/share/GNUstep/lib -L/usr/lib" ./configure --prefix=/usr --libdir=/usr/lib --enable-libgif

  msg2 "Run 'make' (64-bit)..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
# }

package() {
  cd "$_svnname"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install

  make clean
  _build64
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install
}
